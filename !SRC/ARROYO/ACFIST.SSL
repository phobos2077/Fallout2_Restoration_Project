/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Standing Fist
        Location: Arroyo
        Description: He can train you in unarmed combat. (up to 40 or 55 depending on your current situation)

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "..\headers\define.h"
//#include "..\headers\<TownName.h>"

#define NAME                    SCRIPT_ACFIST
#define TOWN_REP_VAR            (GVAR_TOWN_REP_ARROYO)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node004a;
procedure Node004b;
procedure Node005;
procedure Node006;
procedure Node006a;
procedure Node007;
procedure Node007a;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Taught_Player              (7)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_ARROYO);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_ARROYO_WARRIOR);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

   if ( (REP_ENEMY_ARROYO) and (obj_can_see_obj(self_obj,dude_obj)) ) then begin //added and (obj_can_see_obj(self_obj,dude_obj)) by killap
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_global_var(GVAR_ENEMY_ARROYO,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_INTOLERANT;
   Karma_Perception:=KARMA_PERCEPTION5;

   CheckKarma;

   GetReaction;

   if (local_var(LVAR_Taught_Player) == 0) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node001;
       gSay_End;
       end_dialogue;
   end else if (local_var(LVAR_Taught_Player) == 1) then begin
       call Node014;
   end else begin
       call Node015;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(102);

   NOption(103,Node002,004);
   NOption(104,Node002,004);
   NLowOption(105,Node003);
   NOption(106,Node999,004);
end

procedure Node002 begin
   Reply(107);

   NOption(108,Node004,004);
   NOption(109,Node005,004);
end

procedure Node003 begin
   Reply(110);

   NLowOption(111,Node999);
end

procedure Node004 begin
   Reply(112);

   NOption(113,Node004a,004);
//   NOption(114,Node004b,004);
end

procedure Node004a begin
   if (dude_agility < 6) then
       call Node008;
   else if (get_critter_stat(dude_obj,STAT_max_move_points) < 3) then
       call Node009;
   else if (has_skill(dude_obj,SKILL_UNARMED_COMBAT) > 54) then
       call Node007;
   else if (has_skill(dude_obj,SKILL_UNARMED_COMBAT) < 40) then begin
       if (get_critter_stat(dude_obj,STAT_max_move_points) > 3) then
           call Node011;
       else
           call Node008;
   end
   else
       call Node012;
end

procedure Node004b begin
   if (has_skill(dude_obj,SKILL_UNARMED_COMBAT) > 52) then
       call Node007;
   else
       call Node008;
end

procedure Node005 begin
   Reply(114);

   NOption(115,Node999,004);
end

procedure Node006 begin
   Reply(116);

   NOption(117,Node006a,004);
   NOption(118,Node005,004);
end

procedure Node006a begin
   set_local_var(LVAR_Taught_Player,1);

   game_time_advance(ONE_GAME_DAY);

   gfade_out(600);
   gfade_in(600);

   call Node005;
end

procedure Node007 begin
   Reply(119);
   //added by killap
   if (local_var(LVAR_Taught_Player) == 0) then
      set_local_var(LVAR_Taught_Player,2);
   //end added
   NOption(120,Node999,004);
end

procedure Node007a begin
   set_local_var(LVAR_Taught_Player,1);

   game_time_advance(ONE_GAME_DAY);

   gfade_out(600);
   gfade_in(600);

   call Node005;
end

procedure Node008 begin
   Reply(121);

   NOption(122,Node999,004);
end

procedure Node009 begin
   Reply(123);

   NOption(124,Node999,004);
end

procedure Node010 begin
   Reply(125);

   NOption(126,Node999,004);
end

procedure Node011 begin
   variable skill;

   skill:=40-has_skill(dude_obj,SKILL_UNARMED_COMBAT);
   // Inc Unarmed to 40
   critter_mod_skill(dude_obj,SKILL_UNARMED_COMBAT,skill);
   Reply(127);

   NOption(128,Node013,004);
end

procedure Node012 begin
   variable skill;

   skill:=55-has_skill(dude_obj,SKILL_UNARMED_COMBAT);
   // Inc Unarmed to 55
   critter_mod_skill(dude_obj,SKILL_UNARMED_COMBAT,skill);
   Reply(129);

   NOption(130,Node013,004);
end

procedure Node013 begin
   set_local_var(LVAR_Taught_Player,1);

   game_time_advance(ONE_GAME_DAY);

   gfade_out(600);
   gfade_in(600);

   Reply(131);

   NOption(132,Node999,004);
end

procedure Node014 begin
   floater(133);
end

//added by killap
procedure Node015 begin
   floater(119);
end
//end added

//xxxxxxxxxxxxxxxxxxxx