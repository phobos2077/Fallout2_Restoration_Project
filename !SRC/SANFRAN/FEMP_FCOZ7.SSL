/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: OZ-7
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "..\headers\define.h"
//#include "..\headers\<TownName.h>"

#define NAME                    SCRIPT_FCOZ7
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SAN_FRANCISCO)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"
#include "..\headers\sfelronb.h"

#include "..\headers\sanfran.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure combat_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
procedure Node023;
procedure Node024;
procedure Node025;
procedure Node026;
procedure Node027;
procedure Node028;
procedure Node029;
procedure Node030;
procedure Node031;
procedure Node032;
procedure Node033;
procedure Node034;
procedure Node035;
procedure Node036;
procedure Node037;
procedure Node038;
procedure Node039;

procedure doCallGuards;
procedure doForceFieldOff;
procedure doAdjust;
procedure doAdjustEnd;
procedure doAdjustLesson1;

#define TIMER_DOADJUST_LESSON1 1
#define TIMER_DOADJUST_END     2

#define ELRON_COND1 DUDE_ADJUSTED


//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Node22_Count               (7)
#define LVAR_Node16_Count               (8)
#define LVAR_Node2_Count                (9)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable begin
   Only_Once:=0;
   callGuards := 0;
   forceFieldOff := 0;
   adjust := 0;

   dude_last_tile := 0;
   dude_last_rot := 0;

   self_last_tile := 0;
   self_last_rot := 0;
   adjustLesson := 0;
end

procedure timed_event_p_proc begin
   if (fixed_param == TIMER_DOADJUST_LESSON1) then begin
      call doAdjustLesson1;
   end else if (fixed_param == TIMER_DOADJUST_END) then begin
      call doAdjustEnd;
   end
end

procedure start begin
end

procedure combat_p_proc begin
   if (not critter_is_fleeing(self_obj)) then begin
      critter_set_flee_state(self_obj, 1);
   end
end


/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_SAN_FRAN_ELRONS);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_SF_OZ7);
   if (not is_loading_game) then begin
      if (Only_Once == 0) then begin
         set_map_var(MVAR_Force_Fields_Lower, 0);
         Only_Once := 1;
      end
   end
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 1) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       self_attack_dude;
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
      set_san_fran_flag(SF_ELRON_ENEMY);

      set_local_var(LVAR_Personal_Enemy,1);
      set_map_var(MVAR_Force_Fields_Lower, 0);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */

procedure talk_p_proc begin
   Evil_Critter:=1;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION3;

   CheckKarma;

   GetReaction;

   //Temp
//   set_elron_flag(EL_SPOKEN);
   //Temp

   if (san_fran_flag(SF_ELRON_ENEMY)) then begin
      call Node001;
   end else if (elron_flag(EL_REJECTED)) then begin
      //if (not map_var(MVAR_Force_Fields_Lower)) then
      //   set_map_var(MVAR_Force_Fields_Lower, 1);
      //edit by killap
      if (map_var(MVAR_Force_Fields_Lower) == 1) then  
         set_map_var(MVAR_Force_Fields_Lower, 0);
      //end edit
      call Node039;
   end else if (elron_flag(EL_SPOKEN)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node013;
      gSay_End;
      end_dialogue;
   end else if (elron_flag(EL_ASKED)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node018;
      gSay_End;
      end_dialogue;
   end else if (elron_flag_any < EL_ACCEPTED) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node002;
      gSay_End;
      end_dialogue;
   end else if (elron_flag(EL_ACCEPTED)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node023;
      gSay_End;
      end_dialogue;
   end else if (elron_flag(EL_ACBADGEROFFER)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node027;
      gSay_End;
      end_dialogue;
   end else if (elron_flag(EL_ACBADGER)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node028;
      gSay_End;
      end_dialogue;
   end else if (san_fran_flag(SF_GAS_ELRONS)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node030;
      gSay_End;
      end_dialogue;
   end else if ((struggle_flag_any < STRUG_ELRECRUIT and not struggle_flag(STRUG_SHIDOUBLE)) and (elron_flag_any >= EL_ACCEPTED and not san_fran_flag(SF_ELRON_ENEMY))) then begin
      if (local_var(LVAR_Node22_Count) == 0) then begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node020;
         gSay_End;
         end_dialogue;
      end else begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node022;
         gSay_End;
         end_dialogue;
      end
   end else if (not struggle_flag(STRUG_DONE) and (struggle_flag_any >= STRUG_ELRECRUIT or struggle_flag(STRUG_SHIDOUBLE))) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node025;
      gSay_End;
      end_dialogue;
   end else if (dude_iq < 4) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node003;
      gSay_End;
      end_dialogue;
   end else begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node023;
      gSay_End;
      end_dialogue;
   end

   if (callGuards) then begin
      callGuards := 0;
      call doCallGuards;
   end

   if (forceFieldOff) then begin
      forceFieldOff := 0;
      call doForceFieldOff;
   end

   if (adjust) then begin
      adjust := 0;
      inc_general_rep(REP_BONUS_SAN_FRAN_ELRON_ADJUST);
      inc_san_fran_rep(REP_HERO_SAN_FRAN_ELRON_ADJUST);
      call doAdjust;
   end

   set_local_var(LVAR_Herebefore, 1);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_evil_critter

/* Set global_variable for Enemy status*/
   set_map_var(MVAR_Force_Fields_Lower, 0);

end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_mstr(101);
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end

procedure Node999a begin
   forceFieldOff := 1;
   call Node999;
end

procedure Node999b begin
   forceFieldOff := 1;
   UpReactMinor;
   call Node999;
end

procedure Node998a begin
   callGuards := 1;
   call Node998;
end

// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node0304 begin
   if (DUDE_ADJUSTED) then
      call Node004;
   else
      call Node003;
end

procedure Node001 begin
   float_msg(self_obj, mstr(103), FLOAT_MSG_RED);
   callGuards := 1;
end

procedure Node002 begin
   if (local_var(LVAR_Node2_Count) == 0) then begin
      inc_local_var(LVAR_Node2_Count);
      Reply(104);
   end else begin
      Reply(229);
   end

   NLowOption(105, Node0304);
   if (DUDE_LETTER) then
      NOption(106, Node007, 4);
   if (armor_flag(ARMOR_START)) then
      NOption(107, Node007, 4);
   NOption(108, Node008, 4);
   NOption(109, Node009, 4);
   NOption(110, Node010, 4);
   NOption(111, Node998a, 4);
end

procedure Node003 begin
   Reply(112);
   NOption(113, Node005, 1);
   NOption(114, Node006, 1);
end

procedure Node004 begin
   Reply(115);
   NOption(116, Node999a, 1);
end

procedure Node005 begin
   Reply(117);
   NOption(118, Node999a, 1);
end

procedure Node006 begin
   Reply(119);
   NOption(120, Node999, 1);
end

procedure Node007 begin
   Reply(121);
   NOption(122, Node999a, 4);
end

procedure Node008 begin
   Reply(123);
   NOption(124, Node999, 4);
end

procedure Node009 begin
   Reply(125);
   NOption(126, Node011, 4);
   NOption(127, Node012, 4);
end

procedure Node010 begin
   Reply(128);
   if (DUDE_LETTER) then
      NOption(129, Node007, 4);
   if (armor_flag(ARMOR_START)) then
      NOption(130, Node007, 4);
   NOption(131, Node008, 4);
   NOption(132, Node009, 4);
   NOption(133, Node998a, 4);
end

procedure Node011 begin
   Reply(134);
   NOption(135, Node012, 4);
   NOption(136, Node999, 4);
end

procedure Node012 begin
   Reply(137);
   NOption(138, Node999b, 4);
   NOption(139, Node999a, 4);
   NOption(140, Node999, 4);
end

procedure Node1436 begin
   if (DUDE_ADJUSTED) then begin
      set_elron_flag(EL_ACCEPTED);
      call Node014;
   end else begin
      call Node036;
   end
end

procedure Node015a begin
   set_elron_flag(EL_ASKED);
   call Node015;
end

procedure Node016a begin
   set_elron_flag(EL_REJECTED);
   call Node016;
end

procedure Node017a begin
   set_san_fran_flag(SF_ELRON_ENEMY);
   set_elron_flag(EL_REJECTED);
   call Node017;
end

procedure Node013 begin
   Reply(141);
   GOption(142, Node1436, 4);
   GOption(143, Node1436, 4);
   NOption(144, Node015a, 4);
   NOption(145, Node016a, 4);
   BOption(146, Node017a, 4);
end

procedure Node2026 begin
   if (badger_flag(BADGER_DEADSHI) or badger_flag(BADGER_DEADCBT)) then
      call Node020;
   else
      call Node026;
end

procedure Node014 begin
   Reply(147);
   NOption(148, Node2026, 4);
   NOption(149, Node021, 4);
end

procedure Node014a begin
   set_elron_flag(EL_ACCEPTED);
   call Node014;
end

procedure Node015 begin
   set_elron_flag(EL_ASKED);
   Reply(150);
   NOption(151, Node014a, 4);
   NOption(152, Node999, 4);
end

procedure Node016 begin
   set_elron_flag(EL_REJECTED);
   inc_local_var(LVAR_Node16_Count);
   Reply(153);
   NOption(154, Node999, 4);
   NOption(155, Node999, 4);
   if (local_var(LVAR_Node16_Count) == 0) then
      NOption(228, Node015, 4);
end

procedure Node017 begin
   Reply(156);
   NOption(157, Node998a, 1);
end

procedure Node018 begin
   Reply(158);
   NOption(159, Node1436, 4);
   NOption(160, Node016, 4);
   NOption(161, Node019, 4);
end

procedure Node019 begin
   Reply(162);
   NOption(163, Node1436, 4);
   NOption(164, Node016, 4);
end

procedure Node024a begin
   if (struggle_flag_any > 0 and struggle_flag_any < STRUG_SHIDOUBLE) then begin
      set_struggle_flag(STRUG_SHIDOUBLE);
      SHOW_KILL_EMP_QST;
   end else begin
      set_struggle_flag(STRUG_ELRECRUIT);
      SHOW_KILL_EMP_QST;
   end

   call Node024;
end

procedure Node021a begin
   set_local_var(LVAR_Node22_Count, 1);
   call Node021;
end

procedure Node020 begin
   set_local_var(LVAR_Node22_Count, 1);
   Reply(165);
   NOption(166, Node024a, 4);
   NOption(167, Node021a, 4);
end

procedure Node021 begin
   Reply(168);
   NOption(169, Node023, 4);
   NOption(170, NOde999, 4);
end

procedure Node022 begin
   Reply(171);
   NOption(172, Node021, 4);
   NOption(173, Node024a, 4);
end

procedure Node3132 begin
   if (san_fran_flag(SF_GAS_ELRONS)) then
      call Node031;
   else
      call Node032;
end

procedure Node023 begin
   Reply(174);
   if (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL)) then
      NOption(175, Node3132, 4);
   NOption(176, Node033, 4);
   NOption(177, Node034, 4);
   if (not armor_flag(0)) then
      NOption(178, Node035, 4);
   NOption(179, Node999, 4);
end

procedure Node024 begin
   Reply(180);
   NOption(181, Node023, 4);
   NOption(182, Node999, 4);
end

procedure Node029b begin
   set_struggle_flag(STRUG_DONE);
   COMPLETE_KILL_EMP_QST
   call Node029;
end

procedure Node025 begin
   Reply(183);
   NOption(184, Node024, 4);
   if (san_fran_flag(SF_EMPEROR_FORMATTED) or san_fran_flag(SF_LEE_DEAD)) then
      NOption(185, Node029b, 4);
end

procedure Node024b begin
   set_elron_flag(EL_ACBADGER);
   call Node024;
end

procedure Node021c begin
   set_elron_flag(EL_ACBADGEROFFER);
   call Node021;
end

procedure Node026 begin
   Reply(186);
   NOption(187, Node024b, 4);
   NOption(188, Node021c, 4);
end

procedure Node027 begin
   Reply(189);
   NOption(190, Node024b, 4);
   NOption(191, Node021, 4);
end

procedure Node029a begin
   set_elron_flag(EL_ACBADGDEAD);
   inc_general_rep(REP_BONUS_SAN_FRAN_ELRON_KILL_BADGER);
   inc_san_fran_rep(REP_HERO_SAN_FRAN_ELRON_KILL_BADGER);
   give_xp(EXP_ELRON_KILL_BADGER);
   call Node029;
end

procedure Node028 begin
   Reply(192);
   NOption(193, Node024, 4);
   if (badger_flag(BADGER_DEADSHI) or badger_flag(BADGER_DEADCBT)) then
      NOption(194, Node029a, 4);
   if (DUDE_LETTER) then
      NOption(106, Node007, 4);
end

procedure Node029 begin
   Reply(195);
   NOption(196, Node023, 4);
   if ((struggle_flag_any < STRUG_ELRECRUIT and not struggle_flag(STRUG_SHIDOUBLE)) and (elron_flag_any >= EL_ACCEPTED and not san_fran_flag(SF_ELRON_ENEMY))) then begin
      NOption(230, Node020, 4);
   end
   NOption(197, Node999, 4);
//   NOption(198, Node998a, 4);
end

procedure Node030 begin
   Reply(199);
   NOption(200, Node023, 4);
   NOption(201, Node999, 4);
end

procedure Node031 begin
   Reply(202);
   NOption(203, Node023, 4);
   NOption(204, node999, 4);
end

procedure Node032 begin
   Reply(205);
   NOption(206, Node023, 4);
   NOption(207, Node999, 4);
end

procedure Node033 begin
   Reply(208);
   NOption(209, Node023, 4);
   NOption(210, Node999, 4);
end

procedure Node034 begin
   Reply(211);
   NOption(212, Node023, 4);
   NOption(213, Node999, 4);
end

procedure Node035 begin
   Reply(214);
   NOption(215, Node023, 4);
   NOption(216, Node999, 4);
end

procedure NodeAdjust begin
   adjust := 1;
   call Node999;
end

procedure Node036 begin
   Reply(217);
   NOption(218, Node037, 4);
   NOption(219, NodeAdjust, 4);
end

procedure Node037 begin
   Reply(220);
   NOption(221, Node016, 4);
   NOption(222, NodeAdjust, 4);
end

procedure Node038 begin
   Reply(223);
   NOption(224, Node014a, 4);
   NOption(225, Node016a, 4);
   NOption(144, Node015, 4);
end

procedure Node039 begin
   float_msg(self_obj, mstr(231), FLOAT_MSG_RED);
end

procedure doCallGuards begin
   SET_GUARD_NOTIFY(dude_obj);
end

procedure doForceFieldOff begin
   set_map_var(MVAR_Force_Fields_Lower, 1);
end

//#define DUDE_ADJUST_TILE 9910
//#define DUDE_ADJUST_ROT  5
//#define OZ7_ADJUST_TILE  9308
//#define OZ7_ADJUST_ROT   2

//edit for FEMP - killap
#define DUDE_ADJUST_TILE 9910
#define DUDE_ADJUST_ROT  5
#define OZ7_ADJUST_TILE  9508
#define OZ7_ADJUST_ROT   2

procedure doAdjust begin
   fadeout(1);
   debug_msg("FCOZ7: disabling ui");
   game_ui_disable;

   dude_last_tile := dude_tile;
   dude_last_rot  := obj_get_rot(dude_obj);

   self_last_tile := self_tile;
   self_last_rot  := obj_get_rot(self_obj);

   move_to(dude_obj, DUDE_ADJUST_TILE, 0);
   move_to(self_obj, OZ7_ADJUST_TILE, 0);

   reg_anim_clear(dude_obj);
   reg_anim_begin();
   anim(dude_obj, ANIMATE_ROTATION, DUDE_ADJUST_ROT);
   reg_anim_end();

   reg_anim_clear(self_obj);
   reg_anim_begin();
   anim(self_obj, ANIMATE_ROTATION, OZ7_ADJUST_ROT);
   reg_anim_end();
   fadein(1);

   add_timer_event(self_obj, game_ticks(1), TIMER_DOADJUST_LESSON1);
end

procedure doAdjustLesson1 begin
   if (adjustLesson == 0) then begin
      adjustLesson += 1;
      float_msg(self_obj, mstr(226), FLOAT_MSG_GREEN);
      add_timer_event(self_obj, game_ticks(10), TIMER_DOADJUST_LESSON1); //changed from 5 -killap
   end else if (adjustLesson == 1) then begin
      adjustLesson += 1;
      float_msg(dude_obj, mstr(227), FLOAT_MSG_YELLOW);
      add_timer_event(self_obj, game_ticks(4), TIMER_DOADJUST_LESSON1); //changed from 2 -killap
   end else if (adjustLesson == 2) then begin
      debug_msg("FCOZ7: enabling ui");
      game_ui_enable;
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node038;
      gSay_End;
      end_dialogue;
      debug_msg("FCOZ7: disabling");
      game_ui_disable;
      add_timer_event(self_obj, 0, TIMER_DOADJUST_END);
   end
end

procedure doAdjustEnd begin
   fadeout(1);
   set_global_var(GVAR_NCR_ELRON_ADJUST, 1);

   if (Random(1, 100) <= 50) then begin
      set_critter_stat(dude_obj, STAT_lu, 1);
      set_critter_stat(dude_obj, STAT_iq, 1);
      debug_msg("FCOZ7: uhh made luck and iq");
   end else begin
      debug_msg("FCOZ7: uhh lost luck");
      set_critter_stat(dude_obj, STAT_lu, -1);
   end

//   move_to(dude_obj, dude_last_tile, 0);
   move_to(self_obj, self_last_tile, 0);

/*   reg_anim_clear(dude_obj);
   reg_anim_begin();
   anim(dude_obj, ANIMATE_ROTATION, dude_last_rot);
   reg_anim_end();*/

   reg_anim_clear(self_obj);
   reg_anim_begin();
   anim(self_obj, ANIMATE_ROTATION, self_last_rot);
   reg_anim_end();
   debug_msg("FCOZ7: enabling ui");
   game_ui_enable;
   fadein(1);
end
//xxxxxxxxxxxxxxxxxxxx