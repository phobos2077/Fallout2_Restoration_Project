/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Amanda
        Location: Vault City Courtyard
        Description: Woman whose husband has been taken as a Slave

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "..\headers\define.h"
//#include "..\headers\<TownName.h>"

#define NAME                    SCRIPT_VCAMANDA
#define TOWN_REP_VAR            (GVAR_TOWN_REP_VAULT_CITY)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure map_exit_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node010a;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node016a;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Looked_At_Before           (7)
#define LVAR_Amanda_Pissed              (8)
#define LVAR_Banged_Amanda              (9)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   // Joshua and family leave the Courtyard after the player collects reward.
   if( ( not( is_loading_game ) ) and ( global_var(GVAR_QUEST_JOSHUA) == JOSHUA_REWARDED ) ) then begin
      set_obj_visibility(self_obj, true);
      move_to( self_obj, 0, 0 );
   end
   else begin
      Only_Once:=0;
      critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_VAULT_CITIZENS);
      critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_COWARD);
      add_timer_event( self_obj, game_ticks( random(20, 30) ), 1 );
   end
end

procedure map_exit_p_proc begin
   if( global_var(GVAR_QUEST_JOSHUA) == JOSHUA_REWARDED ) then begin
      destroy_object( self_obj );
   end
end

procedure timed_event_p_proc begin
   if( fixed_param == 1 ) then begin
      if( not( combat_is_initialized ) ) then begin
         if( global_var(GVAR_QUEST_JOSHUA) < JOSHUA_FREED ) then
            call Node003;
         else
            call Node004;
      end

      add_timer_event( self_obj, game_ticks( random(20, 30) ), 1 );
   end
end

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( ( (local_var(LVAR_Hostile) == 2) or ( global_var( GVAR_ENEMY_VAULT_COURTYARD ) == 1 ) ) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_global_var( GVAR_ENEMY_VAULT_COURTYARD, 1 );
      set_global_var( GVAR_ENEMY_VAULT_CITY, 1 );
      set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_KICKED_OUT );
      set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_KICKED_OUT ) );
      set_global_var( GVAR_VAULT_CITIZENSHIP, 0 );
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if ( (global_var(GVAR_QUEST_JOSHUA) == JOSHUA_UNKNOWN) and (dude_iq < 4) ) then
      call Node005;
   else if( (global_var(GVAR_QUEST_JOSHUA) == JOSHUA_UNKNOWN) and
            ( (not(has_trait(TRAIT_PERK, dude_obj, PERK_cult_of_personality))) and ( (check_general_rep < 0)  or (has_rep_slaver) or (has_rep_childkiller) ) or ( local_var(LVAR_Amanda_Pissed) == 1 ) ) ) then //edit by killap - added (not(has_trait(TRAIT_PERK, dude_obj, PERK_cult_of_personality)))
      call Node006;
   else if( global_var(GVAR_QUEST_JOSHUA) == JOSHUA_UNKNOWN ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node007;
       gSay_End;
       end_dialogue;
   end
   else if( global_var(GVAR_QUEST_JOSHUA) == JOSHUA_ASSIGNED ) then
      call Node018;
   else if( ( global_var(GVAR_QUEST_JOSHUA) == JOSHUA_FREED ) and ( local_var( LVAR_Banged_Amanda ) == 0 ) ) then
      call Node019;
   else if( ( global_var(GVAR_QUEST_JOSHUA) == JOSHUA_FREED ) and ( local_var( LVAR_Banged_Amanda ) == 1 ) ) then
      call Node021;
   else if( ( global_var(GVAR_QUEST_JOSHUA) == JOSHUA_REWARDED ) and ( local_var( LVAR_Banged_Amanda ) == 0 )  ) then
      call Node020;
   else if( ( global_var(GVAR_QUEST_JOSHUA) == JOSHUA_REWARDED ) and ( local_var( LVAR_Banged_Amanda ) == 1 ) ) then
      call Node021;
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if( global_var(GVAR_QUEST_JOSHUA) < JOSHUA_FREED ) then begin
      if( local_var(LVAR_Looked_At_Before) == 0 ) then begin
         display_msg(mstr(101));
         set_local_var(LVAR_Looked_At_Before, 1);
      end
      else
         display_msg(mstr(100));
   end
   else
      display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   if( global_var(GVAR_QUEST_JOSHUA) < JOSHUA_FREED ) then
      display_msg(mstr(101));
   else
      display_msg(mstr(104));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node003 begin
   floater( random(107, 110) );
end

procedure Node004 begin
   floater(111);
end

procedure Node005 begin
   floater(112);
end

procedure Node006 begin
   floater(113);
end

procedure Node007 begin
   Reply(114);

   NOption(115, Node008, 004);
   NOption(116, Node999, 004);
end

procedure Node008 begin
   Reply(117);

   NOption(118, Node009, 004);
   BOption(119, Node010, 004);
   NOption(120, Node999, 004);
end

procedure Node009 begin
   Reply(121);

   NOption(122, Node011, 004);
   BOption(123, Node010, 004);
   NOption(124, Node999, 004);
end

procedure Node010 begin
   Reply(125);

   NOption(126, Node009, 004);
   BOption(127, Node010a, 004);
   NOption(128, Node999, 004);
end

procedure Node010a begin
   set_local_var( LVAR_Amanda_Pissed, 1 );
end

procedure Node011 begin
   Reply(129);

   NOption(130, Node012, 004);
   GOption(131, Node013, 004);
   NOption(132, Node014, 004);
   NOption(133, Node999, 004);
end

procedure Node012 begin
   Reply(134);

   GOption(135, Node013, 004);
   NOption(136, Node999, 004);
end

procedure Node013 begin
   set_global_var( GVAR_QUEST_JOSHUA, JOSHUA_ASSIGNED );

   Reply(137);

   GOption(138, Node999, 004);
end

procedure Node014 begin
   Reply(139);

   if( dude_is_male ) then
      BOption(140, Node015, 004);
   GOption(141, Node013, 004);
   NOption(142, Node999, 004);
end

procedure Node015 begin
   Reply(143);

   BOption(144, Node016, 004);
   GOption(145, Node013, 004);
   NOption(146, Node999, 004);
end

procedure Node016 begin
   Reply(147);

   BOption(148, Node016a, 004);
   GOption(149, Node013, 004);
   NOption(150, Node999, 004);
end

procedure Node016a begin
   gfade_out(600);
   set_global_var( GVAR_QUEST_JOSHUA, JOSHUA_ASSIGNED );
   set_local_var( LVAR_Banged_Amanda, 1 );
   inc_general_rep( REP_BONUS_AMANDA_SEX_PAYMENT );
   set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_SEXED_UP_AMANDA ) );
   do_default_sex;
   gfade_in(600);
   if( Myron_Ptr != 0 ) then
      call Node017;
end

procedure Node017 begin
   float_msg(Myron_Ptr, message_str(NAME,151), FLOAT_COLOR_NORMAL);
end

procedure Node018 begin
   floater(152);
end

procedure Node019 begin
   floater(153);

   if( global_var( GVAR_QUEST_JOSHUA ) == JOSHUA_FREED ) then begin
      set_global_var( GVAR_QUEST_JOSHUA, JOSHUA_REWARDED );
      inc_general_rep( REP_BONUS_FREED_JOSHUA );
      set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_RESCUED_JOSHUA_NO_SEX ) );
      item_caps_adjust( dude_obj, 32 );
   end
end

procedure Node020 begin
   floater(154);
end

procedure Node021 begin
   floater(155);

   if( global_var( GVAR_QUEST_JOSHUA ) == JOSHUA_FREED ) then begin
      set_global_var( GVAR_QUEST_JOSHUA, JOSHUA_REWARDED );
      inc_general_rep( REP_BONUS_FREED_JOSHUA_AFTER_SEX );
   end
end


//xxxxxxxxxxxxxxxxxxxx