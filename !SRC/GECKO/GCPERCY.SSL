/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Percy Crump - Merchant
        Location: Gecko
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/

//Overrides the pickup p proc.
#define NPC_REACTION_TYPE       REACTION_TC /* REACTION_TC REACTION_TG REACTION_C REACTION_G */
#define NPC_REACTION_VAR        7 /* same as thief variable */

#include "..\headers\define.h"
//#include "..\headers\<TownName.h>"

#define NAME                    SCRIPT_GCPERCY
#define TOWN_REP_VAR            (GVAR_TOWN_REP_GECKO)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node006a;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node014a;
procedure Node014b;
procedure Node015;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Caught_Thief               (7)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable gecko_percy_box;
import variable gecko_junkyard_temp_box;

#define move_inven_to_box              move_obj_inven_to_obj(self_obj, gecko_percy_box);
#define move_inven_from_box            move_obj_inven_to_obj(gecko_percy_box, self_obj);
#define move_inven_to_temp_box         move_obj_inven_to_obj(self_obj, gecko_junkyard_temp_box);
#define move_inven_from_temp_box       move_obj_inven_to_obj(gecko_junkyard_temp_box, self_obj);


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Been_Here:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_GECKO);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_GHOUL_MERCHANT);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( ((global_var(GVAR_GECKO_MELTDOWN) == 1) or (global_var(GVAR_ENEMY_GECKO)) ) and (obj_can_see_obj(self_obj,dude_obj))) then begin //edit by killap - added (global_var(GVAR_ENEMY_GECKO))
      set_local_var(LVAR_Hostile,1);
      attack(dude_obj);
   end
   else if ((local_var(LVAR_Hostile) != 0) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       self_attack_dude;
       //Macro made by Tom to keep the critter fleeing.
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (obj_in_party(source_obj)) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_global_var(GVAR_ENEMY_GECKO,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION0;

   CheckKarma;

   GetReaction;
   //added by killap
   
   if ( global_var( GVAR_GECKO_FIND_WOODY ) == WOODY_REWARDED ) or ( global_var( GVAR_GECKO_FIND_WOODY ) == WOODY_FOUND ) then
       gdialog_set_barter_mod(-50);
   //end added by killap
   
   move_inven_to_temp_box
   move_inven_from_box

   Been_Here := 0;
   if ( global_var( GVAR_GECKO_FIND_WOODY ) == WOODY_REWARDED ) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node004;
      gSay_End;
      end_dialogue;
   end
   if ( global_var( GVAR_GECKO_FIND_WOODY ) == WOODY_FOUND ) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
   end
   else if ( global_var( GVAR_GECKO_FIND_WOODY ) == WOODY_REFUSED ) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node002;
      gSay_End;
      end_dialogue;
   end
   else if ( global_var( GVAR_GECKO_FIND_WOODY ) == WOODY_ACCEPTED ) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node003;
      gSay_End;
      end_dialogue;
   end
   else if ( global_var( GVAR_GECKO_FIND_WOODY ) == WOODY_UNKNOWN ) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node004;
      gSay_End;
      end_dialogue;
   end
   set_local_var( LVAR_Herebefore, 1 );

   move_inven_to_box
   move_inven_from_temp_box
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter
/* inc_evil_critter */
/* inc_neutral_critter */

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_mstr(100);
   else
      display_mstr(101);
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_mstr(102);
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(103);

   GOption(104, Node007, 004);
   NOption(105, Node007, 004);
   BOption(106, Node007, 004);
end

procedure Node002 begin
   Reply(107);

   NOption(108, Node006, 004);
   NOption(109, Node008, 004);
   BOption(110, Node999, 004);
end

procedure Node003 begin
   Reply(111);

   NOption(112, Node006, 004);
   NOption(113, Node999, 004);
end

procedure Node004 begin
   if( global_var( GVAR_GECKO_FIND_WOODY ) == WOODY_REWARDED ) then
      Reply(158);
   else begin
      if( local_var( LVAR_Herebefore ) == 0 ) then
         Reply(114);
      else
         Reply(115);
   end

   if( global_var( GVAR_GECKO_FIND_WOODY ) == WOODY_UNKNOWN ) then
      NOption(116, Node005, 004);
   GOption(117, Node006, 004);
   NOption(118, Node999, 004);
   GLowOption(119, Node006);
   NLowOption(120, Node999);
end

procedure Node005 begin
   if( global_var( GVAR_VAULT_GECKO_PLANT ) == PLANT_OPTIMIZED ) then
      Reply(121);
   else if( ( global_var( GVAR_VAULT_GECKO_PLANT ) == PLANT_REPAIRED ) or
            ( ( global_var( GVAR_VAULT_GECKO_PLANT ) >= PLANT_REACTOR_DATA_RECEIVED ) and
              ( global_var( GVAR_VAULT_GECKO_PLANT ) <= PLANT_REACTOR_DATA_ANALYZED ) ) ) then
      Reply(122);
   else
      Reply(123);

   NOption(124, Node008, 004);
   BOption(125, Node999, 004);
end

procedure Node006 begin
   if( Been_Here == 0 ) then begin
      Been_Here := 1;
      Reply(126);

      GOption(129, Node006a, 004);
   end
   else begin
      Reply(127);

      GOption(130, Node006a, 004);
   end

   GLowOption(128, Node006a);
   NOption(131, Node999, 004);
   NLowOption(132, Node999);
end

procedure Node006a begin
   
   if ( global_var( GVAR_GECKO_FIND_WOODY ) == WOODY_REWARDED ) or ( global_var( GVAR_GECKO_FIND_WOODY ) == WOODY_FOUND ) then
      gdialog_mod_barter(-50);
   else
      gdialog_mod_barter(0);

   NMessage(g_mstr(203));
   call Node006;
end

procedure Node007 begin
   variable item;

   set_global_var( GVAR_GECKO_FIND_WOODY, WOODY_REWARDED );

   // Give RadX and Radaway to player.
   item := create_object( PID_RAD_X, 0, 0 );
   add_mult_objs_to_inven( dude_obj, item, 3 );

   item := create_object( PID_RADAWAY, 0, 0 );
   add_mult_objs_to_inven( dude_obj, item, 4 );

   Reply(133);

   NOption(134, Node999, 004);
   NOption(135, Node999, 004);
end

procedure Node008 begin
   Reply(136);

   NOption(137, Node009, 004);
end

procedure Node009 begin
   Reply(138);

   if( den_visible_on_map ) then
      NOption(139, Node011, 004);
   else
      NOption(140, Node010, 004);
end

procedure Node010 begin
   mark_den_on_map
   Reply(141);

   NOption(142, Node011, 004);
end

procedure Node011 begin
   Reply(143);

   NOption(144, Node012, 004);
end

procedure Node012 begin
   Reply(145);

   NOption(146, Node013, 004);
end

procedure Node013 begin
   Reply(147);

   NOption(148, Node014, 004);
end

procedure Node014 begin
   Reply(149);

   NOption(150, Node015, 004);
   NOption(151, Node014a, 004);
   NOption(152, Node014b, 004);
   NOption(153, Node999, 004);
end

procedure Node014a begin
   set_global_var( GVAR_GECKO_FIND_WOODY, WOODY_ACCEPTED );
end

procedure Node014b begin
   set_global_var( GVAR_GECKO_FIND_WOODY, WOODY_REFUSED );
end

procedure Node015 begin
   Reply(154);

   NOption(155, Node014a, 004);
   NOption(156, Node014b, 004);
   NOption(157, Node999, 004);
end

//xxxxxxxxxxxxxxxxxxxx