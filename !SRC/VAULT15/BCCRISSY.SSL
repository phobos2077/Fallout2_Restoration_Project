/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Crissy
        Location: Vault 15
        Description: Rebecca's daughter who is kidnapped

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define NPC_REACTION_TYPE       REACTION_C

#include "..\headers\define.h"
//#include "..\headers\v15ent.h"

#define NAME                    SCRIPT_BCCRISSY
#define TOWN_REP_VAR            GVAR_TOWN_REP_VAULT_15

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

#include "..\headers\v15.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure combat_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;

#define TIMER_RUN     1

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable i_girl_obj;

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Node5Rep;

procedure start begin
   i_girl_obj := self_obj;
end

procedure combat_p_proc begin
   if (cur_map_index == MAP_VAULT_15_EAST_ENTRANCE) then
      script_overrides;
   else
      CHECK_SQUATTERS_RUN
end

procedure timed_event_p_proc begin
   if (fixed_param == TIMER_RUN) then begin
      if (andV15Status(V15_RESCUED) == 0) then begin
         if (not anim_busy(self_obj)) then begin
            if (self_tile != CRISSY_EXIT_TILE) then begin
               reg_anim_clear(self_obj);
               reg_anim_begin();
               animate_run_to_tile(CRISSY_EXIT_TILE);
               reg_anim_end();
               add_timer_event(self_obj, 2, TIMER_RUN);
            end else begin
               i_girl_obj := -1;
               orV15Status(V15_RESCUED);
               destroy_object(self_obj);
            end
         end else
            add_timer_event(self_obj, 2, TIMER_RUN);
      end else begin
         if (self_tile != CRISSY_HOME_TILE) then begin
            reg_anim_clear(self_obj);
            reg_anim_begin();
            animate_run_to_tile(CRISSY_HOME_TILE);
            reg_anim_end();
         end
      end
   end
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   CHECK_LEVEL

   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_VAULT15);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_PEASANT);

   debug_msg("CUR_MAP_INDEX: " + cur_map_index);
   if (not is_loading_game) then begin

      if (not andV15Status(V15_FOUND) and cur_map_index != MAP_VAULT_15_EAST_ENTRANCE) then
         set_obj_visibility(self_obj, OBJ_INVIS);
      else if (andV15Status(V15_FOUND) and cur_map_index != MAP_VAULT_15_EAST_ENTRANCE) then begin
         if (andV15Status(V15_GIRL_DEAD)) then
            destroy_object(self_obj);
         else
            set_obj_visibility(self_obj, OBJ_VIS);
      end
   end
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       self_attack_dude;
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if (lvar(LVAR_Herebefore) == 0 and not andV15Status(V15_RESCUED)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
          call Node001;
      gSay_End;
      end_dialogue;
   end else if (andV15Status(V15_RESCUED)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
          call Node008;
      gSay_End;
      end_dialogue;
   end else if (andV15Status(V15_FOUND)) then begin
      call Node007;
   end

   debug_msg("BCCRISSY: andV15Status(V15_TRUST): " + andV15Status(V15_TRUST));
   if (not andV15Status(V15_TRUST) and not andV15Status(V15_RESCUED)) then begin
      orV15Status(V15_FOUND);
      add_timer_event(self_obj, 0, TIMER_RUN);
   end
   set_local_var(LVAR_Herebefore, 1);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
   i_girl_obj := -1;
   orV15Status(V15_GIRL_DEAD);
   set_gvar_bit_off(GVAR_V15_SEED_STATUS, V15_FOUND);

   if (lvar(LVAR_Personal_Enemy)) then begin
       orV15Status(V15_KILLED_SQUATTERS);
   end

end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   if (dude_is_male) then
      Reply(103);
   else
      Reply(104);
   if (dude_is_male) then
      NOption(105, Node002, 1);
//   NLowOption(106, Node004);
   NLowOption(107, Node005);
//   NOption(108, Node004, 4);
   NOption(109, Node005, 4);
end

procedure Node002 begin
   Reply(110);
   NLowOption(111, Node003);
   NOption(112, Node003, 4);
end

procedure Node003 begin
   Reply(113);
//   NLowOption(114, Node004);
   NLowOption(115, Node005);
//   NOption(116, Node004, 4);
   NOption(117, Node005, 4);
end

procedure Node004 begin
   Reply(118);
   NOption(119, Node999, 1);
end

procedure Node005 begin
   Node5Rep += 1;
   if (Node5Rep == 1) then begin
      Reply(120);
      MOREOPTION(Node005);
   end else if (Node5Rep == 2) then begin
      Reply(135);
      MOREOPTION(Node005);
   end else begin
      Reply(136);
      NLowOption(121, Node004);
      NOption(122, Node004, 4);
   end
end

procedure Node006 begin
   Reply(123);
//   NLowOption(124, Node004);
   NLowOption(125, Node005);
//   NOption(126, Node004, 4);
   NOption(127, Node005, 4);
end

procedure Node007 begin
   float_msg(self_obj, mstr(128), FLOAT_MSG_NORMAL);
end

procedure Node008 begin
   Reply(129);
   NLowOption(130, Node009);
   NOption(131, Node009, 4);
end

procedure Node009 begin
   Reply(132);
   NLowOption(133, Node999);
   NOption(134, Node999, 4);
end

//xxxxxxxxxxxxxxxxxxxx