/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Zeeke
        Location: Vault 15
        Description: He is the mayor like dude

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define NPC_REACTION_TYPE       REACTION_TC
#define NPC_REACTION_VAR        8 /* same as thief variable */
#define SCRIPT_REALNAME "bczeke"
#include "../headers/define.h"
#include "../headers/v15ent.h"

#define NAME                    SCRIPT_BCZEKE
#define TOWN_REP_VAR            GVAR_TOWN_REP_VAULT_15

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/v15.h"
#include "../headers/ncr.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;

#define ZEEK_WAITING     (gvar(GVAR_NCR_V15_DARION_DEAL) and gvar(GVAR_NCR_V15_DARION_DEAD) == 0)
#define ZEEK_DARION_DEAD (gvar(GVAR_NCR_V15_DARION_DEAD))
#define ZEEK_COMPLETE    (lvar(LVAR_Complete))

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Complete                   (7)
#define LVAR_Caught_Thief               (8)
#define LVAR_N4_Count                   (9)
#define LVAR_Offer                      (10)


/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable begin
   q1 := 0;
   q2 := 0;
   q3 := 0;
   q4 := 0;
end

procedure start begin
end

procedure combat_p_proc begin
   CHECK_SQUATTERS_RUN
end

procedure timed_event_p_proc begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   CHECK_LEVEL


   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_VAULT15);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_PEASANT);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_INTOLERANT;
   Karma_Perception:=KARMA_PERCEPTION0;

   CheckKarma;

   GetReaction;

   // Begin Seraph's Code
   if (ZEEK_DARION_DEAD and global_var(GVAR_V15_NCR_DEAL) == 2) then // Darion Dead and NCR Deal
      call Node016;
   else /* End Seraph's Code */ if (ZEEK_DARION_DEAD and global_var(GVAR_NCR_TANDI_JOB_ACCEPT) == TJOB_DONE) then begin // Darion Dead and Tandi Job Done
      float_msg(self_obj, mstr(163), FLOAT_MSG_YELLOW);
   end else begin
      if (lvar(LVAR_Hostile) == 1) then begin // You've been bad...
         call Node017;
      end else if (ZEEK_WAITING) then begin // Waiting for Darion to die
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node014;
         gSay_End;
         end_dialogue;
      end else if (ZEEK_DARION_DEAD and not ZEEK_COMPLETE) then begin // Thank you
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node015;
         gSay_End;
         end_dialogue;
      end else if (ZEEK_COMPLETE) then begin // Darion Dead and NCR Deal
         call Node016;
      end else if (andV15Status(V15_TRUST)) then begin // Friendly
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node004;
         gSay_End;
         end_dialogue;
      end else if (lvar(LVAR_Herebefore) == 0) then begin // First Meeting: Get Lost
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node001;
         gSay_End;
         end_dialogue;
      end else begin // Get Lost
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node003;
         gSay_End;
         end_dialogue;
      end
   end
   set_local_var(LVAR_Herebefore, 1);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

   if (lvar(LVAR_Personal_Enemy)) then begin
       orV15Status(V15_KILLED_SQUATTERS);
   end


/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(103);
   NLowOption(104, Node999);
   NLowOption(105, Node002);
   NOption(106, Node999, 4);
   NOption(107, Node002, 4);
end

procedure Node002 begin
   Reply(108);
   NOption(109, Node999, 1);
end

procedure Node003 begin
   Reply(110);
   NOption(111, Node999, 1);
end

procedure Node004 begin
   if (local_var(LVAR_N4_Count) == 0) then begin
      set_local_var(LVAR_N4_Count, 1);
      Reply(112);
   end else begin
      Reply(160);
   end

   NLowOption(113, Node005);
   NLowOption(114, Node999);
   NOption(115, Node005, 4);
   NOption(116, Node999, 4);
end

procedure Node006a begin
   q1 := 1;
   call Node006;
end

procedure Node007a begin
   q2 := 1;
   call Node007;
end

procedure Node008a begin
   q3 := 1;
   call Node008;
end

procedure Node009a begin
   q4 := 1;
   call Node009;
end

procedure Node999Card begin
   variable obj;
   obj := obj_carrying_pid_obj(self_obj, KEY_CARD_PID);
   if (obj) then begin
      rm_obj_from_inven(self_obj, obj);
      add_obj_to_inven(dude_obj, obj);
      call Node018;
   end
   call NOde999;
end

procedure Node005 begin
   Reply(117);
   if (q1 == 0) then begin
      NLowOption(118, Node006a);
      NOption(123, Node006a, 4);
   end
   if (q2 == 0) then begin
      NLowOption(119, Node007a);
      NOption(124, Node007a, 4);
   end
   if (q3 == 0) then begin
      NLowOption(120, Node008a);
      NOption(125, Node008a, 4);
   end
   if (q4 == 0) then begin
      NLowOption(121, Node009a);
      NOption(126, Node009a, 4);
   end
   NLowOption(122, Node999Card);
   NOption(127, Node999Card, 4);
end

procedure Node006 begin
   Reply(128);
   NLowOption(129, Node005);
   NOption(130, Node005, 4);
end

procedure Node007 begin
   Reply(131);
   if (local_var(LVAR_Offer) == 0 and global_var(GVAR_NCR_TANDI_JOB_ACCEPT) != TJOB_DONE and global_var(GVAR_NCR_TANDI_JOB_ACCEPT) != 0) then begin
      set_local_var(LVAR_Offer, 1);
      NLowOption(138, Node010);
      NOption(139, Node010, 4);
   end else begin
      NLowOption(132, Node005);
      NOption(133, Node005, 4);
   end
end

procedure giveCard begin
   variable obj;

   obj := obj_carrying_pid_obj(self_obj, KEY_CARD_PID);
   if (obj) then begin
      rm_obj_from_inven(self_obj, obj);
      add_obj_to_inven(dude_obj, obj);
   end
end

procedure Node008 begin
   call giveCard;
   Reply(134);
   NLowOption(135, Node005);
   NOption(136, Node005, 4);
end

procedure Node009 begin
   mark_new_CA_republic_on_map
   if (local_var(LVAR_Offer)) then begin
      Reply(161);
   end else begin
      Reply(137);
   end

   ndebug("" + global_var(GVAR_NCR_TANDI_JOB_ACCEPT));
   if (local_var(LVAR_Offer) or global_var(GVAR_NCR_TANDI_JOB_ACCEPT) != TJOB_ACCEPT) then begin
      NOption(129, Node005, 1);
   end else begin
      set_local_var(LVAR_Offer, 1);
      NLowOption(138, Node010);
      NOption(139, Node010, 4);
   end
end

procedure Node010 begin
   Reply(140);
   NLowOption(141, Node011);
   NOption(142, Node011, 4);
end

procedure Node011 begin
   set_v15_status(V15_WANT_DEAL);
   set_global_var(GVAR_V15_KILL_DARION, 1);
   set_global_var(GVAR_V15_NCR_DEAL, 1);
   Reply(143);
   NLowOption(144, Node012);
   NLowOption(145, Node013);
   NOption(146, Node012, 4);
   NOption(147, Node013, 4);
end

procedure Node012 begin
   Reply(148);
   //149 Need to go back to questions to get the key card
   NOption(151, Node005, 1);
end

procedure Node013 begin
   Reply(150);
   //Need to go back to questions to get the key card
   NOption(151, Node005, 1);
end

procedure Node014 begin
   Reply(152);
   NOption(153, Node999, 1);
end

procedure Node015 begin
   slvar(LVAR_Complete, 1);
   set_global_var(GVAR_NCR_V15_DARION_DEAL, 1);
   if (local_var(LVAR_Offer)) then
      Reply(154);
   else
      Reply(162);
   NOption(155, Node999, 1);
end

procedure Node016 begin
   float_msg(self_obj, mstr(156), FLOAT_MSG_NORMAL);
end

procedure Node017 begin
   float_msg(self_obj, mstr(157), FLOAT_MSG_RED);
end

procedure Node018 begin
//Already gave the card in Node999Card
   Reply(158);
   NOption(159, Node999, 1);
end

//xxxxxxxxxxxxxxxxxxxx