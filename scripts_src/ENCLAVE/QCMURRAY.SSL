/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "..\headers\define.h"
//#include "..\headers\<TownName.h>"

#define NAME                    SCRIPT_QCMURRAY
#define TOWN_REP_VAR            (GVAR_TOWN_REP_ENCLAVE)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;

procedure Node008a;
procedure Node008b;
procedure Node009a;
procedure Node010a;
procedure Node013a;
procedure Node014a;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Looked_Before              (7)
#define LVAR_Said_7                     (8)
#define LVAR_Said_6                     (9)
#define LVAR_Watch_Player               (10)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_ENCLAVE);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_PEASANT);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if (local_var(LVAR_Said_7) == 1) then begin
       call Node007;
   end

   else if (global_var(GVAR_ENCLAVE_COMPUTER) == COMPUTER_DESTROYED) then begin
       call Node001;
   end

   else if (global_var(GVAR_ENCLAVE_ALARM) > 0) then begin
       call Node002;
   end

   else if (dude_iq < 4) then begin
       call Node003;
   end

   //commented out unimplemented disguise code
   /*else if ((dude_wearing_power_armor) and (party_size == 1)) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node004;
       gSay_End;
       end_dialogue;
   end*/

   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node006;
       gSay_End;
       end_dialogue;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Looked_Before) == 0) then begin
       inc_local_var(LVAR_Looked_Before);
       display_msg(mstr(100));
   end
   else
       display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   floater(random(129,133));
   set_local_var(LVAR_Hostile,2);
   set_global_var(GVAR_ENCLAVE_ALARM,ALARM_INTRUDER);  //added by killap
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   floater(random(103,107));
end

procedure Node002 begin
   floater(random(108,112));
end

procedure Node003 begin
   floater(random(113,117));
end

procedure Node004 begin
   Reply(118);

   BOption(119,Node005,004);
   GOption(120,Node999,004);
end

procedure Node005 begin
   Reply(121);

   BOption(122,Node006,004);
   NOption(123,Node999,004);
end

procedure Node006 begin
   if (local_var(LVAR_Said_6) == 0) then begin
       inc_local_var(LVAR_Said_6);
       Reply(124);
   end
   else
       Reply(125);

   BOption(126,Node008,004);
   NOption(127,Node014,004);
   BOption(128,Node998,004);
end

procedure Node007 begin
   floater(random(129,133));

   call Node998;
end

procedure Node008 begin
   Reply(134);

   BOption(135,Node008a,004);
   GOption(136,Node014,004);
   GOption(137,Node008b,004);
end

procedure Node008a begin
   if (skill_success(dude_obj,SKILL_CONVERSANT,0)) then
       call Node009;
   else
       call Node010;
end

procedure Node008b begin
   set_local_var(LVAR_Watch_Player,1);
end

procedure Node009 begin
   Reply(138);

   BOption(139,Node009a,004);
   GOption(140,Node008b,004);
end

procedure Node009a begin
   if (skill_success(dude_obj,SKILL_CONVERSANT,0)) then
       call Node011;
   else
       call Node012;
end

procedure Node010 begin
   Reply(141);

   BOption(142,Node010a,004);
   BOption(143,Node998,004); //edit by killap: was NOption
   NOption(144,Node014,004);
   GOption(145,Node008b,004);
end

procedure Node010a begin
   if (skill_success(dude_obj,SKILL_CONVERSANT,-50)) then
       call Node011;
   else
       call Node012;
end

procedure Node011 begin
   Reply(146);

   BOption(147,Node013,004);
   GOption(148,Node008b,004);
end

procedure Node012 begin
   Reply(149);

   BOption(150,Node998,004);
   NOption(151,Node014,004);
   NOption(152,Node008b,004);
end

procedure Node013 begin
   set_global_var(GVAR_ENCLAVE_REACTOR,ENCLAVE_REACTOR_OFF);
   if (global_var(GVAR_ENCLAVE_COUNTDOWN) == 0) then begin
       set_global_var(GVAR_ENCLAVE_COUNTDOWN,game_time);
       give_xp(EXP_DESTROY_REACTOR_TALK);
   end
   set_global_var(GVAR_ENCLAVE_ALARM,ALARM_REACTOR);
   set_global_var(GVAR_ENCLAVE_COMPUTER,COMPUTER_DESTROYED); //added by killap

   Reply(153);

   GOption(154,Node013a,004);
   BOption(155,Node998,004);
   NOption(156,Node008b,004);
end 

procedure Node013a begin
   if (skill_success(dude_obj,SKILL_CONVERSANT,0)) then
       call Node999;
   else
       call Node008b; //edit by killap - was Node008a
end

procedure Node014 begin
   Reply(157);

   NOption(158,Node014a,004);
end

procedure Node014a begin
   set_global_var(GVAR_ENCLAVE_POWER_PLANT,PLANT_KNOWN);
   call Node015;
end

procedure Node015 begin
   Reply(159);

   NOption(160,Node016,004);
end

procedure Node016 begin
   Reply(161);

   NOption(162,Node006,004);
   GOption(163,Node008b,004);
   BOption(164,Node007,004);
end

//xxxxxxxxxxxxxxxxxxxx