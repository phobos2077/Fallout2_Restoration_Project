/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "qcmartin"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_QCMARTIN
#define TOWN_REP_VAR            (GVAR_TOWN_REP_ENCLAVE)

#include "../headers/command.h"
#include "../headers/modreact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Looked_Before              (7)
#define LVAR_Said_3                     (8)
#define LVAR_Said_13                    (9)
#define LVAR_In_Timed_Event             (10)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_PRISONERS);
   set_self_ai(AI_TOUGH_CITIZEN);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

   if ((self_can_see_dude) and (local_var(LVAR_In_Timed_Event) == 0)) then begin
       inc_local_var(LVAR_In_Timed_Event);
       add_timer_event(self_obj,game_ticks(random(8,15)),1);
   end

end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
       set_local_var(LVAR_In_Timed_Event,0);
       call Node002;
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   set_global_var(GVAR_ENCLAVE_MARTIN,1);

   if (dude_iq < 4) then
       call Node001;

   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node003;
       gSay_End;
       end_dialogue;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Looked_Before) == 0) then begin
       inc_local_var(LVAR_Looked_Before);
       display_msg(mstr(100));
   end
   else
       display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   floater(random(103,112));
end

procedure Node002 begin
   floater(random(113,124));
end

procedure Node003 begin
   if (local_var(LVAR_Said_3) == 0) then begin
       inc_local_var(LVAR_Said_3);
       Reply(125);
   end
   else
       Reply(126);

   if (vault_13_visible_on_map) then
       NOption(127,Node007,004);
   NOption(128,Node005,004);
   NOption(129,Node017,004);
   NOption(130,Node008,004);
   BOption(131,Node004,004);
end

procedure Node004 begin
   Reply(132);

   BOption(133,Node012,004);
   GOption(134,Node013,004);
   NOption(135,Node005,004);
end

procedure Node005 begin
   Reply(136);

   NOption(137,Node006,004);
   NOption(138,Node003,004);
   BOption(139,Node999,004);
end

procedure Node006 begin
   Reply(140);

   NOption(141,Node007,004);
end

procedure Node007 begin
   Reply(142);

   NOption(143,Node008,004);
end

procedure Node008 begin
   Reply(144);

   NOption(145,Node003,004);
   NOption(146,Node009,004);
end

procedure Node009 begin
   Reply(147);

   if (vault_13_visible_on_map) then
       NOption(148,Node010,004);
   if (dude_item_count(PID_GECK)) then
       NOption(149,Node010,004);
   else
       NOption(150,Node017,004);
end

procedure Node010 begin
   Reply(151);

   NOption(152,Node011,004);
   if (dude_item_count(PID_GECK)) then
       NOption(153,Node003,004);
end

procedure Node011 begin
   Reply(154);

   GOption(155,Node012,004);
   NOption(156,Node003,004);
end

procedure Node012 begin
   Reply(157);

   if (global_var(GVAR_ENCLAVE_POWER_PLANT) == PLANT_KNOWN) then begin
      NOption(158,Node013,004);
   end else begin
       NOption(159,Node014,004);
   end
   BOption(160,Node999,004);
end

procedure Node013 begin
   if (local_var(LVAR_Said_13) == 0) then begin
       inc_local_var(LVAR_Said_13);
       Reply(161);
   end
   else
       Reply(162);

   NOption(163,Node003,004);
   GOption(164,Node999,004);
end

procedure Node014 begin
   Reply(165);

   NOption(166,Node015,004);
end

procedure Node015 begin
   set_global_var(GVAR_ENCLAVE_POWER_PLANT,PLANT_KNOWN);

   Reply(167);

   if ((dude_item_count(PID_DYNAMITE)) or (dude_item_count(PID_PLASTIC_EXPLOSIVES))) then begin
      GOption(168,Node013,004);
   end else begin
      NOption(169,Node016,004);
   end
end

procedure Node016 begin
   Reply(170);

   GOption(171,Node013,004);
end

procedure Node017 begin
   Reply(172);

   NOption(173,Node018,004);
end

procedure Node018 begin
   Reply(174);

   NOption(175,Node019,004);
end

procedure Node019 begin
   Reply(176);

   GOption(177,Node020,004);
   NOption(178,Node003,004);
   BOption(179,Node012,004);
end

procedure Node020 begin
   if (dude_is_male) then
       Reply(180);
   else
       Reply(181);

   //added check by killap
   if (global_var(GVAR_ENCLAVE_POWER_PLANT) == PLANT_KNOWN) then
       NOption(158,Node013,004);
   else
       NOption(182,Node014,004);
   BOption(183,Node012,004);
   NOption(184,Node003,004);
end

//xxxxxxxxxxxxxxxxxxxx
