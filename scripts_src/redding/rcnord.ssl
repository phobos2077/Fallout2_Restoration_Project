/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_RCNORD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_REDDING)

#include "../headers/command.h"
#include "../headers/modreact.h"
#include "../headers/caravan.h"

#define CARAVAN_GVAR            GVAR_CATTLE_DRIVE_CARAVAN

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node008a;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Looked_Before              (7)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_REDDING);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_STORE_OWNER);

   if ((not(is_loading_game)) and (global_var(GVAR_STANWELL_PAYOUT) == 1)) then begin
       destroy_object(self_obj);
   end
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((global_var(GVAR_ENEMY_REDDING) == 1) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       attack(dude_obj);
   end

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
//   set_global_var(GVAR_STANWELL_PAYOUT,1); //moved down by killap

   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;
   //check added to prevent infinite payoff - killap
   if (global_var(GVAR_STANWELL_PAYOUT) != 1) then begin
      if (dude_iq < 4) then begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node007;
         gSay_End;
         end_dialogue;
      end

      else begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node008;
         gSay_End;
         end_dialogue;
      end

      set_global_var(CARAVAN_GVAR,REDDING_CARAVAN_NONE);
      set_global_var(GVAR_STANWELL_PAYOUT,1);   //moved down by killap

   end else
       float_msg(self_obj,mstr(123),FLOAT_COLOR_NORMAL); //added by killap
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter
   set_global_var(GVAR_ENEMY_REDDING,1);

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Looked_Before) == 0) then begin
       display_msg(mstr(100));
       inc_local_var(LVAR_Looked_Before);
   end
   else
       display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   NMessage(103);
end

procedure Node002 begin
   Reply(104);

   NOption(105,Node999,004);
   BOption(106,Node999,004);
end

procedure Node003 begin
   Reply(107);

   BOption(108,Node999,004);
   NOption(109,Node999,004);
end

procedure Node004 begin
   item_caps_adjust(dude_obj,50);

   Reply(110);

   NOption(111,Node999,004);
   BOption(112,Node999,004);
end

procedure Node005 begin
   item_caps_adjust(dude_obj,100);

   Reply(113);

   NOption(114,Node999,004);
   NOption(115,Node999,004);
end

procedure Node006 begin
   item_caps_adjust(dude_obj,150);

   Reply(116);

   NOption(117,Node999,004);
   NOption(118,Node999,004);
end

procedure Node007 begin
   Reply(119);

   NOption(120,Node999,004);
end

procedure Node008 begin
   Reply(121);

   NOption(122,Node008a,004);
end

procedure Node008a begin
   if (global_var(CARAVAN_GVAR) == REDDING_CARAVAN_ABANDONED) then
       call Node001;
   else if (global_var(CARAVAN_GVAR) == REDDING_CARAVAN_THEFT) then
       call Node002;
   else if ((caravan_guards_left < (caravan_total_guards/4)) or //edit by killap was > in both checks
            (caravan_brahmin_left < ((caravan_total_brahmin*4)/10))) then
       call Node004;
   else if ((caravan_guards_left < (caravan_total_guards/2)) or //edit by killap was > in both checks
            (caravan_brahmin_left < ((caravan_total_brahmin*6)/10))) then
       call Node003;
   else if ((caravan_guards_left == caravan_total_guards) and
            (caravan_brahmin_left == caravan_total_brahmin)) then
       call Node006;
   else if ((caravan_guards_left >= (caravan_total_guards - 1)) and //edit by killap was <= in both checks
            (caravan_brahmin_left >= (caravan_total_brahmin - 2))) then
       call Node005;
   else
       call Node004;
end

//xxxxxxxxxxxxxxxxxxxx