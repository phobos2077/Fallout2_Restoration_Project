/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "../headers/define.h"
#include "../headers/reddown.h"

#define NAME                    SCRIPT_RCFANNIE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_REDDING)

#include "../headers/command.h"
#include "../headers/modreact.h"


#define COST_FOR_SEX            (60)
#define FREE_CASH               (200)
#define FINAL_MONEY             (150)

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure use_obj_on_p_proc;   // Seraph's Code
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node004a;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node007a;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node011a;
procedure Node012;
procedure Node012a;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node018a;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
// Begin Seraph's Code
procedure Node023;
procedure Node024a;
procedure Node024b;
procedure Node025;
procedure Node026;
procedure Node027;
// End Seraph's Code

procedure Node990; //added by killap - expansion

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Looked_At                  (7)
#define LVAR_Visits                     (8)
#define LVAR_Thankyou                   (9)  // Seraph's Code
#define LVAR_Gave_Money                 (10)
#define LVAR_Just_Gave_Money            (11) //added by killap - expansion
#define LVAR_Kill_Fannie                (12) //added by killap - expansion

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;

   //added by killap - expansion
   if (fannie == FANNIE_DEAD) then
      destroy_object(self_obj);
   //end added

   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_MALAMUTE_SALOON);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_REDDING_ADDICT);
end

//added by killap - expansion
procedure map_exit_p_proc begin
   if (local_var(LVAR_Kill_Fannie) == 1) then begin
      set_fannie(FANNIE_DEAD);
   end else begin
      set_local_var(LVAR_Just_Gave_Money,0);
   end
end
//end added

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((global_var(GVAR_ENEMY_REDDING) == 1) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       attack(dude_obj);
   end

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

// Begin Seraph's Code
procedure use_obj_on_p_proc begin
// comment out by killap
/*   if((obj_pid(obj_being_used_with) == PID_JET_ANTIDOTE) and (not rehab_set) and (not fannie_cured)) then begin
      display_mstr(200);
      script_overrides;
      set_rehab_time
   end*/
end
// End Seraph's Code

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
// Begin Seraph's Debug Code
   debug_msg("fannie = " + fannie);
   debug_msg("rehab_time = " + map_var(MVAR_Rehab_Time));
   debug_msg("game_time = " + game_time);
// End Seraph's Debug Code
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION0;

   CheckKarma;

   GetReaction;

   if (dude_iq < 4) then
      call Node003;

   // Begin Seraph's Code - expansion
   else if (not rehab_done and rehab_set) then
      call Node026;
   else if (fannie == FANNIE_CURED_LOU) then begin
      if (local_var(LVAR_Thankyou) == 0) then begin
         start_gdialog(NAME,self_obj,4,-1,-1);
            gSay_Start;
               call Node023;
            gSay_End;
         end_dialogue;
      end else begin
         start_gdialog(NAME,self_obj,4,-1,-1);
            gSay_Start;
               call Node025;
            gSay_End;
         end_dialogue;
      end
   end
  // End Seraph's Code

   else if ((fannie == FANNIE_SICK) or (fannie == FANNIE_SICKER)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
   end

   else if (fannie == FANNIE_MONEY_GIVEN) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node015;
      gSay_End;
      end_dialogue;
   end

   else if (fannie == FANNIE_MONEY_ASKED) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node019;
       gSay_End;
       end_dialogue;
   end

   else if (fannie == FANNIE_CUSTOMER) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node020;
       gSay_End;
       end_dialogue;
   end

   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node006;
       gSay_End;
       end_dialogue;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter
   set_global_var(GVAR_ENEMY_REDDING,1);

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Looked_At) == 0) then begin
       display_msg(mstr(100));
       inc_local_var(LVAR_Looked_At);
   end
   else
       display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   if (fannie == FANNIE_SICK) then
       if (dude_is_male) then
           Reply(103);
       else
           Reply(104);
   else if (dude_is_male) then
       Reply(105);
   else
       Reply(106);

   if (fannie == FANNIE_SICKER) then
       NOption(107,Node002,004);
   NOption(108,Node002,004);
   NOption(109,Node002,004);
   NOption(110,Node002,004);
end

procedure Node002 begin
   if (fannie == FANNIE_SICKER) then
       Reply(111);
   else
       Reply(112);

   if (obj_is_carrying_obj_pid(dude_obj,PID_JET_ANTIDOTE)) then
       NOption(113,Node004,004);
   BOption(114,Node999,004);
   BOption(115,Node999,004);

   NOption(210,Node990,004); //added by killap - expansion

end


//added by killap - expansion
procedure Node990 begin
   inc_local_var(LVAR_Visits);
   call Node007a;
end
//end added

procedure Node003 begin
   floater(random(116,120));
end

procedure Node004 begin
   Reply(121);

   if (obj_is_carrying_obj_pid(dude_obj,PID_JET)) then
       GOption(122,Node004a,004);
   BOption(123,Node999,004);
   NOption(124,Node999,004);
end

procedure Node004a begin
   variable item;
   variable junkit;

   if (obj_is_carrying_obj_pid(dude_obj,PID_JET)) then begin
       item:=obj_carrying_pid_obj(dude_obj,PID_JET);
       junkit:=rm_mult_objs_from_inven(dude_obj,item,1);
       add_mult_objs_to_inven(self_obj,item,1);
       //inc sickness
   end

   call Node005;

end

procedure Node005 begin
   Reply(125);

   NOption(126,Node999,004);
   NOption(127,Node999,004);
end

procedure Node006 begin
   if (dude_is_male) then
       Reply(128);
   else
       Reply(129);

   if (dude_is_male) then
       NOption(130,Node007,004);
   else
       NOption(131,Node021,004);
   NOption(132,Node999,004);
end

procedure Node007 begin
   if (local_var(LVAR_Visits) == 0) then
       Reply(133);
   else if (local_var(LVAR_Visits) == 1) then
       Reply(134);
   else
       Reply(135);

   inc_local_var(LVAR_Visits);

   if (item_caps_total(dude_obj) >= COST_FOR_SEX) then
       NOption(136,Node007a,004);
   NOption(137,Node999,004);
   NOption(138,Node999,004);
end

procedure Node007a begin
   item_caps_adjust(dude_obj,0-COST_FOR_SEX);
   item_caps_adjust(self_obj,COST_FOR_SEX);

   //added by killap
   if (fannie != FANNIE_MONEY_GIVEN) then
      set_fannie(FANNIE_CUSTOMER);
   //end added

   gfade_out(600);
   do_default_sex;
// need to move player to the back room
   move_to(dude_obj,12901,elevation(self_obj)); //added by killap
   gfade_in(600);

   call Node008;
end

procedure Node008 begin
   Reply(139);

   NOption(140,Node009,004);
   NOption(141,Node009,004);
end

procedure node009 begin
   if (not fannie_cured) then begin  // Seraph's Code
      Reply(142);

      NOption(143,Node010,004);
      if (fannie == FANNIE_MONEY_GIVEN) then
         NOption(144,Node017,004);
   // Begin Seraph's Code
   end else begin
      Reply(192);

      NOption(193,Node999,004);
   end
   // End Seraph's Code
end

procedure Node010 begin
   set_fannie(FANNIE_MONEY_ASKED);

   Reply(145);

   NOption(146,Node011,004);
   NOption(147,Node013,004);
   NOption(148,Node999,004);
end

procedure Node011 begin
   Reply(149);

   if (item_caps_total(dude_obj) >= FREE_CASH) then
       NOption(150,Node011a,004);
   NOption(151,Node999,004);
end

procedure Node011a begin
   item_caps_adjust(dude_obj,0-FREE_CASH);
   item_caps_adjust(self_obj,FREE_CASH);

   //added by killap - expansion
   inc_local_var(LVAR_Gave_Money);
   set_local_var(LVAR_Just_Gave_Money,1);

   if (local_var(LVAR_Gave_Money) == 4) then begin
      set_fannie(FANNIE_SICKER);
   end else if (local_var(LVAR_Gave_Money) == 2) then begin
      set_fannie(FANNIE_SICK);
   end
   //end added

   call Node012;
end

procedure Node012 begin
   set_fannie(FANNIE_MONEY_GIVEN);

   Reply(152);

   NOption(153,Node012a,004);
   NOption(154,Node999,004);
end

procedure Node012a begin
   set_fannie(FANNIE_MONEY_GIVEN);
   gfade_out(600);
   do_default_sex;
// need to move player to the back room
   move_to(dude_obj,12901,elevation(self_obj)); //added by killap
   gfade_in(600);

   call Node014;
end

procedure Node013 begin
   Reply(155);

   NOption(156,Node011,004);
end

procedure Node014 begin
   Reply(157);

   NOption(158,Node999,004);
   NOption(159,Node999,004);
end

procedure Node015 begin
   Reply(160);

   NOption(161,Node016,004);
   NOption(162,Node007,004);
end

procedure Node016 begin
   Reply(163);

   NOption(164,Node018,004);
   NOption(165,Node999,004);
end

procedure Node017 begin
   Reply(166);

   NOption(167,Node018,004);
   NOption(168,Node999,004);
end

procedure Node018 begin
   Reply(169);

   if (item_caps_total(dude_obj) >= FINAL_MONEY) then
       NOption(170,Node018a,004);
   NOption(171,Node999,004);
end

procedure Node018a begin
   item_caps_adjust(dude_obj,0-FINAL_MONEY);
   item_caps_adjust(self_obj,FINAL_MONEY);

   //added by killap - expansion
   inc_local_var(LVAR_Gave_Money);

   if ( (local_var(LVAR_Just_Gave_Money) == 0) and (local_var(LVAR_Gave_Money) > 1) ) then
      set_local_var(LVAR_Just_Gave_Money,1);

   if (local_var(LVAR_Just_Gave_Money) == 1) then begin
      set_local_var(LVAR_Kill_Fannie,1);
   end

   if (local_var(LVAR_Gave_Money) == 4) then begin
      set_fannie(FANNIE_SICKER);
   end else if (local_var(LVAR_Gave_Money) == 2) then begin
      set_fannie(FANNIE_SICK);
   end
   //end added

   call Node012;
end

procedure Node019 begin
   Reply(172);

   if (item_caps_total(dude_obj) >= COST_FOR_SEX) then
       NOption(173,Node007,004);
   NOption(174,Node011,004);
   NOption(175,Node999,004);
end

procedure Node020 begin
   Reply(176);

   NOption(177,Node007,004);
   NOption(178,Node999,004);
end

procedure Node021 begin
   if (dude_charisma > 5) then
       Reply(179);
   else
       Reply(180);

   NOption(181,Node022,004);
   NOption(182,Node999,004);
end

procedure Node022 begin
   Reply(183);

   NOption(184,Node999,004);
   NOption(185,Node999,004);
end

// Begin Seraph's Code
procedure Node023 begin
   set_local_var(LVAR_Thankyou,1);

   Reply(186);

   NOption(191,Node024a,004);
   NOption(187,Node999,004);
end

procedure Node024a begin
   gfade_out(600);
   do_default_sex;
// need to move player to the back room
   move_to(dude_obj,12901,elevation(self_obj)); //added by killap
   gfade_in(600);

   call Node024b;
end

procedure Node024b begin
   Reply(188);

   NOption(158,Node999,004);
end

procedure Node025 begin
   Reply(189);

   NOption(153,Node024a,004);
   NOption(175,Node999,004);
end

procedure Node026 begin
   floater(190);
end

procedure Node027 begin
   set_rehab_time
   display_msg(mstr(200));
end
// End Seraph's Code

//xxxxxxxxxxxxxxxxxxxx