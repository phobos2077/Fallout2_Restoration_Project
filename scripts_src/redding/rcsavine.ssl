/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "rcsavine"
#include "../headers/define.h"
#include "../headers/caravan.h"
#include "../headers/bhrnddst.h"

#define NAME                    SCRIPT_RCSAVINE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_REDDING)

#include "../headers/command.h"
#include "../headers/modreact.h"


#define CARAVAN_WADE            (1)
#define CARAVAN_SAVINELLI       (2)
#define CARAVAN_STANWELL        (3)

#define START_DAY               (21)
#define CURRENT_CARAVAN         (GVAR_JET_CARAVAN)
#define CURRENT_CARAVAN_LEADER  (CARAVAN_SAVINELLI)
#define TARGET_TOWN             (MAPSTR_NEWR1)

/* Standard Script Procedures */
procedure start;
procedure timed_event_p_proc; //added by killap
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure map_update_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node002a;
procedure Node003;
procedure Node003a;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node014a;
procedure Node015;
procedure Node016;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Looked_Before              (7)
#define LVAR_Finish_Caravan_Time        (8)
#define LVAR_Said_3                     (9)
#define LVAR_Said_5                     (10)
#define LVAR_Said_14                    (11)
#define LVAR_Said_1                     (12)
#define LVAR_Called_Clear               (13)
#define LVAR_No_More_Caravans           (14) //added by killap

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   if (global_var(GVAR_SAVINELLI_STATUS) == 1) then begin
       destroy_object(self_obj);
   end
   else if (not(is_loading_game)) then begin
       if (cur_map_index == MAP_BH_RND_DESERT) then
           critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_PLAYER);
       else
           critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_REDDING);
       critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_GENERIC_GUARDS);
       if ((get_day >= 17) and (get_day <= 21)) then begin
           if (cur_map_index == MAP_REDDING_MINE_ENT) then begin
               if ((get_month % 2) == 0) then begin
                   set_obj_visibility(self_obj,0);
               end
               else begin
                   set_obj_visibility(self_obj,1);
               end
           end
           else begin
               if ((get_month % 2) == 1) then begin
                   set_obj_visibility(self_obj,0);
               end
               else begin
                   set_obj_visibility(self_obj,1);
               end
           end
       end
       else begin
           set_obj_visibility(self_obj,1);
       end

       if (cur_map_index == MAP_BH_RND_DESERT) then
           set_obj_visibility(self_obj,0);

       else if ((global_var(CURRENT_CARAVAN) != REDDING_CARAVAN_NONE) and (local_var(LVAR_No_More_Caravans) == 0)) then //killap fix: was (global_var(CURRENT_CARAVAN) == REDDING_CARAVAN_AVERAGE_SUCCESS) then
           set_obj_visibility(self_obj,0);
   end
end

procedure map_update_p_proc begin
   if (not(obj_on_screen(self_obj))) then begin
       if ((get_day >= 17) and (get_day <= 21)) then begin
           if (cur_map_index == MAP_REDDING_MINE_ENT) then begin
               if ((get_month % 2) == 0) then begin
                   set_obj_visibility(self_obj,0);
               end
               else begin
                   set_obj_visibility(self_obj,1);
               end
           end
           else begin
               if ((get_month % 2) == 1) then begin
                   set_obj_visibility(self_obj,0);
               end
               else begin
                   set_obj_visibility(self_obj,1);
               end
           end
       end
       else begin
           set_obj_visibility(self_obj,1);
       end

       if (cur_map_index == MAP_BH_RND_DESERT) then
           set_obj_visibility(self_obj,0);

       else if (global_var(CURRENT_CARAVAN) == REDDING_CARAVAN_AVERAGE_SUCCESS) then
           set_obj_visibility(self_obj,0);
//       if (global_var(CURRENT_CARAVAN) == REDDING_CARAVAN_AVERAGE_SUCCESS) then begin
//           set_obj_visibility(self_obj,0);
//       end
   end

   if (not(is_loading_game)) then begin   //check added by killap - probably doesn't help, but doesn't hurt either.
      if (cur_map_index == MAP_BH_RND_DESERT) then begin
         if ((local_var(LVAR_Called_Clear) == 0) and (map_var(MVAR_Hostile_Total) <= 0)) then begin //edit by killap (incase you drop below zero, things will still run)
           set_local_var(LVAR_Called_Clear,1);
           add_timer_event( self_obj, game_ticks(10), 1 );
         end
      end
   end
end

//added by killap
procedure timed_event_p_proc begin
   if( fixed_param == 1 ) then begin
      float_msg(self_obj,g_mstr(450),FLOAT_COLOR_NORMAL);
      add_timer_event( self_obj, game_ticks(10), 1 );
   end
end
//end added

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((global_var(GVAR_ENEMY_REDDING) == 1) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       attack(dude_obj);
   end

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   //killap code
   if ((global_var(CURRENT_CARAVAN) != REDDING_CARAVAN_NONE) and (global_var(CURRENT_CARAVAN) != REDDING_CARAVAN_ABANDONED) and (global_var(CURRENT_CARAVAN) != REDDING_CARAVAN_THEFT) and (not(cur_map_index == MAP_BH_RND_DESERT))) then begin
      if ((caravan_guards_left < (caravan_total_guards/4)) or (caravan_brahmin_left < ((caravan_total_brahmin*4)/10))) then
         set_global_var(CURRENT_CARAVAN,REDDING_CARAVAN_TOTAL_FAILURE);
      else if ((caravan_guards_left < (caravan_total_guards/2)) or (caravan_brahmin_left < ((caravan_total_brahmin*6)/10))) then
         set_global_var(CURRENT_CARAVAN,REDDING_CARAVAN_FAILURE);
      else if ((caravan_guards_left == caravan_total_guards) and (caravan_brahmin_left == caravan_total_brahmin)) then
         set_global_var(CURRENT_CARAVAN,REDDING_CARAVAN_COMPLETE_SUCCESS);
      else if ((caravan_guards_left >= (caravan_total_guards - 1)) and (caravan_brahmin_left >= (caravan_total_brahmin - 2))) then
         set_global_var(CURRENT_CARAVAN,REDDING_CARAVAN_AVERAGE_SUCCESS);
      else
         set_global_var(CURRENT_CARAVAN,REDDING_CARAVAN_AVERAGE_SUCCESS);
   end
   //end killap code

   if ((global_var(CURRENT_CARAVAN) == REDDING_CARAVAN_ON_CARAVAN) or (cur_map_index == MAP_BH_RND_DESERT)) then begin
       call Node004;
   end

   else //if (game_time - (local_var(LVAR_Finish_Caravan_Time)) < ONE_GAME_DAY) then begin
       if (global_var(CURRENT_CARAVAN) == REDDING_CARAVAN_ABANDONED) then begin
           call Node005;
       end

       else if (global_var(CURRENT_CARAVAN) == REDDING_CARAVAN_THEFT) then begin
           start_gdialog(NAME,self_obj,4,-1,-1);
           gSay_Start;
               call Node006;
           gSay_End;
           end_dialogue;
       end

       else if (global_var(CURRENT_CARAVAN) == REDDING_CARAVAN_TOTAL_FAILURE) then begin
           start_gdialog(NAME,self_obj,4,-1,-1);
           gSay_Start;
               call Node007;
           gSay_End;
           end_dialogue;
       end

       else if (global_var(CURRENT_CARAVAN) == REDDING_CARAVAN_FAILURE) then begin
           start_gdialog(NAME,self_obj,4,-1,-1);
           gSay_Start;
               call Node008;
           gSay_End;
           end_dialogue;
       end

       else if (global_var(CURRENT_CARAVAN) == REDDING_CARAVAN_AVERAGE_SUCCESS) then begin
           start_gdialog(NAME,self_obj,4,-1,-1);
           gSay_Start;
               call Node009;
           gSay_End;
           end_dialogue;
       end

       else if (global_var(CURRENT_CARAVAN) == REDDING_CARAVAN_COMPLETE_SUCCESS) then begin
           start_gdialog(NAME,self_obj,4,-1,-1);
           gSay_Start;
               call Node010;
           gSay_End;
           end_dialogue;
       end

/*       else begin
           start_gdialog(NAME,self_obj,4,-1,-1);
           gSay_Start;
               call Node003;
           gSay_End;
           end_dialogue;
       end
   end
*/
   else if (global_var(CURRENT_CARAVAN) == REDDING_CARAVAN_ABANDONED) then begin
       call Node001;
   end

   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node003;
       gSay_End;
       end_dialogue;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

   if (global_var(CURRENT_CARAVAN) == REDDING_CARAVAN_ON_CARAVAN) then
       rm_caravan_masters(1);

   if (source_obj == dude_obj) then
       set_global_var(GVAR_ENEMY_REDDING,1);

   set_global_var(GVAR_SAVINELLI_STATUS,1);

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Looked_Before) == 0) then begin
       set_local_var(LVAR_Looked_Before,1);
       display_msg(mstr(100));
   end
   else
       display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   if (local_var(LVAR_Said_1) == 0) then begin
       floater(103);
       set_local_var(LVAR_Said_1,1);
   end
   else
       floater(104);
end

procedure Node002 begin
   Reply(105);

   NOption(106,Node002a,004);
end

procedure Node002a begin
   variable Caravan_Carts;
   variable Caravan_Guards;
   variable Caravan_Encounters;

   Caravan_Encounters:=random(0,4);

   set_global_var(CURRENT_CARAVAN,REDDING_CARAVAN_ON_CARAVAN);
   set_caravan_status(CARAVAN_STATUS_ON_JOB);
   set_caravan_encounters(Caravan_Encounters);

   if (cur_map_index == MAP_REDDING_MINE_ENT) then begin
       set_global_var(GVAR_CARAVAN_START,CARAVAN_REDDING);
       set_global_var(GVAR_CARAVAN_END,CARAVAN_NEW_RENO);
   end
   else begin
       set_global_var(GVAR_CARAVAN_START,CARAVAN_NEW_RENO);
       set_global_var(GVAR_CARAVAN_END,CARAVAN_REDDING);
   end

   if (global_var(GVAR_CARAVAN_ENCOUNTERS_TOTAL) == 0) then begin
       Caravan_Guards:=random(2,4);
       set_caravan_guards(Caravan_Guards);
       if (global_var(GVAR_CARAVAN_END) ==  CARAVAN_NEW_RENO) then begin
           game_time_advance(24*ONE_GAME_DAY);
           load_map(TARGET_TOWN,CURRENT_CARAVAN_LEADER);
           set_global_var(CURRENT_CARAVAN,REDDING_CARAVAN_AVERAGE_SUCCESS);
       end
       else begin
           game_time_advance(24*ONE_GAME_DAY);
           load_map(MAPSTR_REDMENT,CURRENT_CARAVAN_LEADER);
           set_global_var(CURRENT_CARAVAN,REDDING_CARAVAN_AVERAGE_SUCCESS);
       end
   end
   else begin
       Caravan_Carts:=random(1,3);
       Caravan_Guards:=random(2,4);
       set_caravan_masters(1);
       set_caravan_guards(Caravan_Guards);
       set_caravan_carts(Caravan_Carts);
       set_caravan_brahmin(2*Caravan_Carts);
       set_caravan_drivers(Caravan_Carts);

       load_map(MAPSTR_BHRNDDST,CURRENT_CARAVAN_LEADER);
   end

end

procedure Node003 begin
   if (local_var(LVAR_Said_3) == 0) then begin
       set_local_var(LVAR_Said_3,1);
       if (get_day == START_DAY) then
           Reply(107);
       else
           Reply(108);
   end
   else
       Reply(109);

   NLowOption(110,Node013);
   NOption(111,Node003a,004);
   NOption(112,Node014,004);
   NOption(113,Node011,004);
   NOption(114,Node999,004);
end

procedure Node003a begin
   if (get_day == START_DAY) then
       call Node015;
   else
       call Node012;
end

procedure Node004 begin
   floater(random(115,119));
end

procedure Node005 begin
   set_local_var(LVAR_No_More_Caravans,1);   //killap

   if (local_var(LVAR_Said_5) == 0) then begin
       inc_local_var(LVAR_Said_5);
       floater(120);
   end
   else
       floater(121);
end

procedure Node006 begin
   //   set_global_var(CURRENT_CARAVAN,REDDING_CARAVAN_NONE);  not needed, since when set you can go on another of his caravans - killap
   set_local_var(LVAR_No_More_Caravans,1); //added by killap

   Reply(122);

   NOption(123,Node999,004);
   BOption(124,Node999,004);
   BOption(200,Node998,004);
end

procedure Node007 begin
   set_global_var(CURRENT_CARAVAN,REDDING_CARAVAN_NONE);

   Reply(125);

   NOption(126,Node999,004);
   BOption(127,Node999,004);
end

procedure Node008 begin
   set_global_var(CURRENT_CARAVAN,REDDING_CARAVAN_NONE);
   item_caps_adjust(dude_obj,500);

   Reply(128);

   NOption(129,Node999,004);
   NOption(130,Node999,004);
end

procedure Node009 begin
   set_global_var(CURRENT_CARAVAN,REDDING_CARAVAN_NONE);
   item_caps_adjust(dude_obj,1500);

   Reply(131);

   NOption(132,Node999,004);
   NOption(133,Node999,004);
end

procedure Node010 begin
   set_global_var(CURRENT_CARAVAN,REDDING_CARAVAN_NONE);
   item_caps_adjust(dude_obj,1700);

   Reply(134);

   NOption(135,Node999,004);
   NOption(136,Node999,004);
end

procedure Node011 begin
   if (cur_map_index == MAP_REDDING_MINE_ENT) then
       Reply(137);
   else
       Reply(138);

   NOption(139,Node014,004);
   if (get_day == START_DAY) then
       NOption(140,Node015,004);
   NOption(141,Node999,004);
end

procedure Node012 begin
   Reply(142);

   NOption(143,Node011,004);
   NOption(144,Node014,004);
   NOption(145,Node999,004);
end

procedure Node013 begin
   Reply(146);

   NOption(147,Node999,004);
end

procedure Node014 begin
   Reply(148);

   if (local_var(LVAR_Said_14) == 0) then
       NOption(149,Node014a,004);
   NOption(150,Node003,004);
   NOption(151,Node999,004);
end

procedure Node014a begin
   set_local_var(LVAR_Said_14,1);

   ndebug("get_day == "+get_day+". START_DAY == "+START_DAY);

   if (get_day == START_DAY) then
       call Node015;
   else
       call Node012;
end

procedure Node015 begin
   if (party_size > 1) then
       Reply(152);
   else
       Reply(153);

   NOption(154,Node002,004);
   NOption(155,Node999,004);
end

procedure Node016 begin
   set_global_var(CURRENT_CARAVAN,REDDING_CARAVAN_NONE);

   Reply(156);

   NLowOption(157,Node999);
   NLowOption(158,Node999);
end

//xxxxxxxxxxxxxxxxxxxx