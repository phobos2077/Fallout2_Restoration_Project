/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "rcrooney"
#include "../headers/define.h"
#include "../headers/reddown.h"

#define NAME                    SCRIPT_RCROONEY
#define TOWN_REP_VAR            (GVAR_TOWN_REP_REDDING)

#include "../headers/command.h"
#include "../headers/modreact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure NOde013;
procedure Node014;

procedure Node010a;
procedure Node012a;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Looked_Before              (7)
#define LVAR_Said_2                     (8)
#define LVAR_Said_7                     (9)
#define LVAR_Said_11                    (10)
#define LVAR_Move_Tile                  (11)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_REDDING);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_PEASANT);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((global_var(GVAR_ENEMY_REDDING) == 1) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       attack(dude_obj);
   end

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

   if ((map_var(MVAR_Widow_Rooney) == WIDOW_KICKED_OUT) and (tile_num(self_obj) != 20506) and (local_var(LVAR_Move_Tile) == 0)) then begin
       animate_move_to_tile(20506);
   end

   if ((map_var(MVAR_Widow_Rooney) == WIDOW_KICKED_OUT) and (tile_num(self_obj) == 20506) and (local_var(LVAR_Move_Tile) == 0)) then begin
       set_local_var(LVAR_Move_Tile,1);
       animate_move_to_tile(15479);
   end

   if ((map_var(MVAR_Widow_Rooney) == WIDOW_KICKED_OUT) and (tile_num(self_obj) != 15479) and (local_var(LVAR_Move_Tile) == 1)) then begin
       animate_move_to_tile(15479);
   end

   if ((map_var(MVAR_Widow_Rooney) == WIDOW_KICKED_OUT) and (tile_num(self_obj) == 15479) and (local_var(LVAR_Move_Tile) == 1)) then begin
       set_local_var(LVAR_Move_Tile,2);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION3;

   CheckKarma;

   GetReaction;

   if (map_var(MVAR_Widow_Rooney) == WIDOW_KICKED_OUT) then begin
       call Node001;
   end

   else if (map_var(MVAR_Widow_Rooney) == WIDOW_PAID) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node002;
       gSay_End;
       end_dialogue;
   end

   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node005;
       gSay_End;
       end_dialogue;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter
   if (source_obj == dude_obj) then begin
       set_map_var(MVAR_Widow_Rooney,WIDOW_KILLED);
       set_global_var(GVAR_WIDOW_ROONEY,WIDOW_KILLED); //added by killap
   end
   set_global_var(GVAR_ENEMY_REDDING,1);

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Looked_Before) == 0) then begin
       display_msg(mstr(100));
       set_local_var(LVAR_Looked_Before,1);
   end
   else
       display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   floater(random(103,107));
end

procedure Node002 begin
   if (local_var(LVAR_Said_2) == 0) then begin
       set_local_var(LVAR_Said_2,1);
       Reply(108);
   end
   else
       Reply(109);

   NOption(110,Node003,004);
   NOption(111,Node999,004);
end

procedure Node003 begin
   Reply(112);

   NOption(113,Node004,004);
   NOption(114,Node999,004);
end

procedure Node004 begin
   Reply(115);

   NOption(116,Node999,004);
end

procedure Node005 begin
   if (dude_is_male) then
       Reply(117);
   else
       Reply(118);

   NLowOption(119,Node006);
   if ((map_var(MVAR_Widow_Rooney) == WIDOW_JOB_ACCEPTED) and (dude_level > 9)) then
       BOption(120,Node007,004);
   if ((map_var(MVAR_Widow_Rooney) == WIDOW_JOB_ACCEPTED) and (dude_level > 9)) then
       NOption(121,Node008,004);
   NOption(123,Node999,004);
end

procedure Node006 begin
   Reply(124);

   NLowOption(125,Node999);
end

procedure Node007 begin
   if (local_var(LVAR_Said_7) == 0) then begin
       inc_local_var(LVAR_Said_7);
       Reply(126);
   end
   else
       Reply(127);

   if (local_var(LVAR_Said_11) == 0) then
       NOption(128,Node011,004);
   NOption(129,Node010,004);
   NOption(130,Node009,004);
end

procedure Node008 begin
   Reply(131);

   NOption(132,Node009,004);
   NOption(133,Node999,004);
end

procedure Node009 begin
   Reply(134);

   if (map_var(MVAR_Widow_Rooney) == WIDOW_JOB_ACCEPTED) then
       NOption(135,Node007,004);
   if (map_var(MVAR_Widow_Rooney) == WIDOW_JOB_ACCEPTED) then
       NOption(136,Node011,004);
   if (map_var(MVAR_Widow_Rooney) == WIDOW_JOB_ACCEPTED) then
       NOption(137,Node012,004);
   NOption(138,Node999,004);
end

procedure Node010 begin
   Reply(139);

   NOption(140,Node010a,004);
   NOption(141,Node010a,004);
   NOption(142,Node010a,004);
end

procedure Node010a begin
   inc_general_rep(REP_BONUS_EVICTED_ROONEY);
   set_map_var(MVAR_Widow_Rooney,WIDOW_KICKED_OUT);
   display_msg(g_mstr(1008));
   give_xp(EXP_EVICTED_ROONEY);
   set_global_var(GVAR_WIDOW_ROONEY,WIDOW_KICKED_OUT); //added by killap
   inc_global_var_amt(GVAR_TOWN_REP_REDDING, -5); //added by killap
end

procedure Node011 begin
   set_local_var(LVAR_Said_11,1);

   Reply(143);

   NOption(144,Node007,004);
end

procedure Node012 begin
   if (dude_is_male) then
       Reply(145);
   else
       Reply(146);

   NOption(147,Node012a,004);
   NOption(148,Node999,004);
end

procedure Node012a begin
   if (item_caps_total(dude_obj) < 120) then
       call Node014;
   else begin
       item_caps_adjust(dude_obj,-120);
       set_map_var(MVAR_Widow_Rooney,WIDOW_PAID);
       set_global_var(GVAR_WIDOW_ROONEY,WIDOW_PAID); //added by killap
       inc_general_rep(REP_BONUS_HELPED_ROONEY);
       inc_global_var_amt(GVAR_TOWN_REP_REDDING, 5); //added by killap
       display_msg(g_mstr(1008));
       give_xp(EXP_HELPED_ROONEY);
   end
end

procedure NOde013 begin
   Reply(149);

   NOption(150,Node999,004);
   NOption(151,Node999,004);
end

procedure Node014 begin
   Reply(152);

   NOption(153,Node999,004);
   NOption(154,Node999,004);
end

//xxxxxxxxxxxxxxxxxxxx