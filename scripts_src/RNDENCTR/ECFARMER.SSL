/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/* Random Encounter scripts. These will be filled in more once they
   have been designed.
*/

procedure start;

#include "..\headers\define.h"

#define NAME                    SCRIPT_ECFARMER

#include "..\headers\command.h"
#include "..\headers\specenc.h"

procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure combat_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE
#define TAUNT_CHANCE 20

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;

procedure NodeFleeing;
procedure NodeFighting;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (0)
#define LVAR_Hostile                    (1)
#define LVAR_Personal_Enemy             (2)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

procedure combat_p_proc begin
   if (critter_is_fleeing(self_obj)) then begin
      if (Random(1, 100) <= TAUNT_CHANCE) then begin
         call NodeFleeing;
      end
   end else begin
      if (Random(1, 100) <= TAUNT_CHANCE) then begin
         call NodeFighting;
      end
   end
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_RND_FARMER);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_GENERIC_GUARDS);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   //call Node001 on some save condition
   //call Node002 after rewarding the save condition
   if (local_var(LVAR_Herebefore) == 0) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
      call Node003;
      gSay_End;
      end_dialogue;
   end else begin
      call Node006;
   end
   set_local_var(LVAR_Herebefore, 1);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then begin
      if (get_critter_stat(self_obj,STAT_gender) == GENDER_MALE) then
          display_msg(mstr(100));
      else
          display_msg(mstr(200));
   end else begin
      if (get_critter_stat(self_obj,STAT_gender) == GENDER_MALE) then
        display_msg(mstr(101));
      else
         display_msg(mstr(201));
   end
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   if (get_critter_stat(self_obj,STAT_gender) == GENDER_MALE) then
       display_msg(mstr(102));
   else
       display_msg(mstr(202));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure giveReward begin
   variable tmp;

   tmp := Random(1, 100);

   if (tmp >= 1 and tmp <= 5) then
      add_obj_to_inven(dude_obj, create_object(PID_STIMPAK, self_tile, 0));
   else if (tmp >= 6 and tmp <= 36) then
      add_obj_to_inven(dude_obj, create_object(PID_HEALING_POWDER, self_tile, 0));
   else if (tmp >= 37 and tmp <= 57) then
      add_obj_to_inven(dude_obj, create_object(PID_SHOTGUN_SHELLS, self_tile, 0));
   else if (tmp >= 58 and tmp <= 68) then
      add_obj_to_inven(dude_obj, create_object(PID_MUTATED_FRUIT, self_tile, 0));
   else if (tmp >= 69 and tmp <= 79) then
      add_obj_to_inven(dude_obj, create_object(PID_MEAT_JERKY, self_tile, 0));
   else if (tmp >= 80 and tmp <= 81) then
      add_obj_to_inven(dude_obj, create_object(PID_SHOTGUN, self_tile, 0));
   else if (tmp >= 82 and tmp <= 83) then
      add_obj_to_inven(dude_obj, create_object(PID_HUNTING_RIFLE, self_tile, 0));
   else begin
      add_obj_to_inven(dude_obj, create_object(PID_CROWBAR, self_tile, 0));
   end
end

procedure Node001 begin
   call giveReward;
   Reply(103);
   NOption(104, Node999, 4);
   NLowOption(105, Node999); // Seraph's Fix: NLowOption only takes 2 args. Thanks Rob!
end

procedure Node002 begin
   float_msg(self_obj, mstr(106), FLOAT_MSG_NORMAL);
end

procedure Node003 begin
   Reply(109);
   NOption(110, Node004, 4);
   NOption(111, Node999, 4);
   NLowOption(112, Node007);
end

procedure NodeBarter begin
   gdialog_barter;
   call Node005;
end

procedure Node004 begin
   Reply(113);
   NOption(114, NodeBarter, 1);
end

procedure Node005 begin
   Reply(115);
   ENDOPTION;
end

procedure Node006 begin
   float_msg(self_obj, mstr(117), FLOAT_MSG_NORMAL);
end

procedure Node007 begin
   Reply(119);
   NLowOption(120, Node004);
   NLowOption(121, Node008);
end

procedure Node008 begin
   Reply(122);
   ENDOPTION;
end

procedure NodeFleeing begin
   float_msg(self_obj, mstr(Random(124, 130)), FLOAT_MSG_YELLOW);
end

procedure NodeFighting begin
   //float_msg(self_obj, mstr(Random(131, 136)), FLOAT_MSG_RED);
   float_msg(self_obj, mstr(Random(131, (135 + critter_is_male))), FLOAT_MSG_RED); //edit by killap, added critter_is_male
end


//xxxxxxxxxxxxxxxxxxxx