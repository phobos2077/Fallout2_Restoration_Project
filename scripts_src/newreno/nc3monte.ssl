/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location: New Reno
        Description:

        Log:

           Created: May 6, 1998

           Updated:
*/

/* Include Files */
#define SCRIPT_REALNAME "nc3monte"
#include "../headers/define.h"
#define NAME                    SCRIPT_NC3MONTE
#define TOWN_REP_VAR            GVAR_TOWN_REP_NEW_RENO
#include "../headers/command.h"
#include "../headers/newreno.h"
#include "../headers/newr1.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

procedure Node001;
procedure Node001a;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node005a;
procedure Node005b;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node990;
procedure Node991;
procedure Node992;
procedure Node993;
procedure Node014;
procedure Node015;
procedure Node015a;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
procedure Node023;
procedure Node024;
procedure Node025;
procedure Node026;
procedure Node027;
procedure Node028;
procedure Node029;
procedure Node030;
procedure Node031;
procedure Node032;
procedure Node033;
procedure Node034;
procedure Node035;
procedure Node036;



/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */

#define LVAR_Flags                    (4)
#define LVAR_SWAP                     (5)
#define LVAR_NO_MONEY                 (6)
#define LVAR_NODE23_COUNT             (7)
#define LVAR_NODE24_COUNT             (8)
#define LVAR_NODE25_COUNT             (9)
#define LVAR_MESSAGE                  (10)
#define LVAR_WRIGHT_ASKED             (11)
#define LVAR_LLYOD_ASKED              (12)
#define LVAR_CAR_ASKED                (13)
#define LVAR_OTHER_ASKED              (14)
#define LVAR_MONEY                    (15)

#define saw_dude_bit                   bit_10

#define saw_dude                       (lvar_bit(LVAR_Flags, saw_dude_bit))
#define set_saw_dude                   set_lvar_bit_on(LVAR_Flags, saw_dude_bit)
#define off_saw_dude                   set_lvar_bit_off(LVAR_Flags, saw_dude_bit)

variable prev_node;
variable info_price;
variable rank;
variable suit;
variable tmp1;
variable tmp2;

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */

/* Local variables which do not need to be saved between map changes. */


procedure start begin
end

procedure timed_event_p_proc begin
   if (float_param) then begin
     if (saw_dude == false) then begin
         set_saw_dude;
         call Node001a;
     end else begin
         nr_add_timer_event_rand(self_obj, 15, 30, float_param);
         call Node001;
      end
   end
end

procedure map_enter_p_proc begin
   if (map_first_run) then
      set_local_var(LVAR_MONEY,200);

   if (is_loading_game == false) then begin
      if (map_first_run) then begin
         set_self_team(TEAM_NEW_RENO_MONTE);
         set_self_ai(AI_TOUGH_GUARD);
      end
   end
   nr_add_timer_event_rand(self_obj, 5, 30, float_param);
end

procedure critter_p_proc begin
   if ( (self_item_count(PID_DECK_OF_CARDS) == 0) and (self_item_count(PID_MARKED_DECK_OF_CARDS)) and (local_var(LVAR_MESSAGE) == 0))  then begin
      set_local_var(LVAR_MESSAGE,1);
      display_mstr(576);
      give_xp(100);
      set_local_var(LVAR_SWAP, 1);
   end

   if (self_can_see_dude) then begin
      if ((hostile)) then begin
         self_attack_dude;
      end else begin
         if (saw_dude == false) then begin
            nr_add_timer_event(self_obj, 1, float_param);
         end
      end
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_personal_enemy;
      set_map_var(MVAR_Monte_Attacked,1);
      call Node998;
   end
end

procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      call Node998;
   end
end

procedure talk_p_proc begin
   start_gdialog(NAME,self_obj,4,-1,-1);
   gSay_Start;
      if (local_var(LVAR_NO_MONEY) == 1) then
         call Node011;
      else if ( (self_item_count(PID_DECK_OF_CARDS)) or (self_item_count(PID_MARKED_DECK_OF_CARDS)) )  then
         call Node002;
      else
         call Node012;
   gSay_End;
   end_dialogue;

   prev_node := 0;
end

procedure destroy_p_proc begin
   inc_evil_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_mstr(100 + (herebefore));
end

procedure description_p_proc begin
   script_overrides;
   display_mstr(150 + (herebefore));
   set_examined;
end

procedure use_skill_on_p_proc begin

end

procedure Node990 begin
   Reply(573 + (dude_is_female));
   NOption(575,Node999,004);
   NOption(244,Node999,-003);
end

procedure Node991 begin

   info_price := 750;

   if (is_success(roll_vs_skill(dude_obj,SKILL_SPEECH,-20))) then begin
      if (prev_node == 23) then
         call Node029;
      else if (prev_node == 24) then
         call Node032;
      else if (prev_node == 25) then
         call Node034;
      else if ( (prev_node == 26) or (prev_node == 13) ) then begin
         info_price := 150;
         call Node036;
      end
   end else begin
      if (prev_node == 23) then
         call Node028;
      else if (prev_node == 24) then
         call Node031;
      else if (prev_node == 25) then
         call Node033;
      else if ( (prev_node == 26) or (prev_node == 13) ) then
         call Node035;
   end
end


procedure Node992 begin

   info_price := 500;

   if (is_success(roll_vs_skill(dude_obj,SKILL_SPEECH,-30))) then begin
      if (prev_node == 23) then
         call Node029;
      else if (prev_node == 24) then
         call Node032;
      else if (prev_node == 25) then
         call Node034;
      else if ( (prev_node == 26) or (prev_node == 13) ) then begin
         info_price := 150;
         call Node036;
      end
   end else begin
      if (prev_node == 23) then
         call Node028;
      else if (prev_node == 24) then
         call Node031;
      else if (prev_node == 25) then
         call Node033;
      else if ( (prev_node == 26) or (prev_node == 13) ) then
         call Node035;
   end
end

procedure Node993 begin

   info_price := 250;

   if (is_success(roll_vs_skill(dude_obj,SKILL_SPEECH,-40))) then begin
      if (prev_node == 23) then
         call Node029;
      else if (prev_node == 24) then
         call Node032;
      else if (prev_node == 25) then
         call Node034;
      else if ( (prev_node == 26) or (prev_node == 13) ) then begin
         info_price := 100;
         call Node036;
      end
   end else begin
      if (prev_node == 23) then
         call Node028;
      else if (prev_node == 24) then
         call Node031;
      else if (prev_node == 25) then
         call Node033;
      else if ( (prev_node == 26) or (prev_node == 13) ) then
         call Node035;
   end
end

procedure Node994 begin
   info_price := 200;
   call Node036;
end


procedure Node998 begin
   if (hostile == false) then begin
      set_hostile;
   end
end

procedure Node999 begin
end

procedure Node001a begin
   if (dude_is_male) then begin
      if ( (random(1,2)) == 1 ) then
         floater(220);
      else
         floater(222);
   end else begin
      if ( (random(1,2)) == 1 ) then
         floater(232);
      else
         floater(231);
   end

   nr_add_timer_event_rand(self_obj, 15, 30, float_param);
end

procedure Node001 begin
   if (Fallout2_enclave_destroyed) then begin
      floater(random(205,208));
   end else
   if (local_var(LVAR_NO_MONEY) == 1) then begin
         floater(random(200,203));
   end else begin
      if ( (self_item_count(PID_DECK_OF_CARDS)) or (self_item_count(PID_MARKED_DECK_OF_CARDS)) )  then begin
         if (dude_is_male) then
            floater(random(220,228));
         else
            floater(random(227,232));
      end else
         floater(random(215,216));
   end
end

procedure Node002 begin
   if (herebefore) then begin
      if (enclave_destroyed) then
         Reply(239);
      else
         Reply(237 + dude_is_female);
      NOption(245,Node005a,004); //added by killap
   end else begin
      Reply(240);
      //if (enclave_destroyed) then
      //   Reply(236);
      //else
      //   Reply(234 + dude_is_female);
      NOption(246,Node003,004);
      NOption(247,Node005a,004);  //play game
   end

   //NOption(241,Node003,-003); //you are a cheater
   NOption(242,Node005a,-003); //play game
   NOption(243,Node999,-003);
   NOption(244,Node999,-003);

   //NOption(245,Node999,004); //you are a cheater
   //NOption(249,Node999,004);
   NOption(391,Node019,004);  //info!!!
   NOption(250,Node999,004);
   //NOption(392,Node011,004);  //info!!!
   set_herebefore;
end

procedure Node003 begin
   Reply(251);
   NOption(256,Node004,004);
   NOption(257,Node005a,004);
   if (prev_node == 8) then
      NOption(255,Node999,004);
   NOption(258,Node999,004);
end

procedure Node004 begin
   Reply(mstr(252) + " " + mstr(253+dude_is_female));
   NOption(257,Node005a,004);  //play game
   NOption(258,Node999,004);
end

procedure Node005a begin
   if ((dude_caps) >= 20) then begin
      dude_caps_adjust(-20);
      //item_caps_adjust(self_obj, 20);
      set_local_var(LVAR_MONEY,((local_var(LVAR_MONEY)) + 20));
      call Node005b;
   end else
      call Node990;
end

procedure Node005b begin
   if (local_var(LVAR_SWAP) == 1) then //swapped cards
      call Node014;
   else
      call Node005;
end


//high perception check - this man is quick
procedure Node005 begin
   if ((dude_perception >= 7)) then
      call Node008;  //will notice cheating
   else
      call Node006;  //no notice of cheating
end

procedure Node006 begin //sees no cheating
   Reply(mstr(260) + " " + mstr(261 + dude_is_female));
   NOption(263,Node007,004);
   NOption(264,Node007,004);
   NOption(265,Node007,004);
   NOption(266,Node007,004);

   NOption(267,Node007,-003);
   NOption(268,Node007,-003);
   NOption(269,Node007,-003);
end

procedure Node007 begin //player lost
   tmp1 := random(1,4);

   if (tmp1 == 1) then
      suit := mstr(600);
   else if (tmp1 == 2) then
      suit := mstr(601);
   else if (tmp1 == 3) then
      suit := mstr(602);
   else if (tmp1 == 4) then
      suit := mstr(603);

   tmp2 := random(2,10);

   if (tmp2 == 2) then
      rank := mstr(604);
   else if (tmp2 == 3) then
      rank := mstr(605);
   else if (tmp2 == 4) then
      rank := mstr(606);
   else if (tmp2 == 5) then
      rank := mstr(607);
   else if (tmp2 == 6) then
      rank := mstr(608);
   else if (tmp2 == 7) then
      rank := mstr(609);
   else if (tmp2 == 8) then
      rank := mstr(610);
   else if (tmp2 == 9) then
      rank := mstr(611);
   else if (tmp2 == 10) then
      rank := mstr(612);

   Reply(mstr(280) + rank + mstr(613) + suit + ".)" + " " + mstr(281+dude_is_female) + " " + mstr(283));

   NOption(285,Node005a,-003);
   NOption(286,Node999,-003);
   if (prev_node == 8) then begin
      NOption(287,Node009,004);
      NOption(284,Node009,-003);
   end
   NOption(288,Node005a,004);
   NOption(289,Node999,004);
end

procedure Node008 begin //sees cheating
   Reply(mstr(300) + " " + mstr(301 + dude_is_female));

   BOption(307,Node009,004);  //you CHEATER!
   NOption(308,Node009,004); //the one in your sleeve

   BOption(303,Node009,-003);  //you CHEATER!
   NOption(304,Node007,-003);
   NOption(305,Node007,-003);
   NOption(306,Node007,-003);

   NOption(309,Node007,004);
   NOption(310,Node007,004);
   NOption(311,Node007,004);

   prev_node :=8;
end

procedure Node009 begin //monte caught cheating
   Reply(mstr(320 + dude_is_female) + " " + mstr(322));
   NOption(323,Node010,-003);  //yes!
   NOption(324,Node999,-003); //bye!
   NOption(325,Node010,004);  //yes!
   NOption(326,Node999,004); //bye!
end

procedure Node010 begin //monte caught cheating
   Reply(330);
   BOption(331,Node998,-003); //attack
   BOption(332,Node999,-003);
   BOption(333,Node998,004); //attack
   NOption(334,Node005a,004);
   NOption(335,Node999,004);
end

procedure Node011 begin //NO MONEY
   Reply(435);
   BOption(437,Node019,004);
   NOption(438,Node999,004);
end

procedure Node012 begin //Monte has no cards
   Reply(233);
   BOption(469,Node013,004);
   NOption(470,Node999,004);
end

procedure Node013 begin
   Reply(540);
   if (dude_caps >= 200) then
      NOption(541,Node994,004);
   if (dude_caps >= 150) then
      NOption(542,Node991,004);
   if (dude_caps >= 100) then
      NOption(543,Node993,004);
   NOption(545,Node999,004);

   prev_node := 13;
end

procedure Node014 begin
   Reply(mstr(360 + (prev_node==15)) + " " + mstr(362 + dude_is_female) + " " + mstr(364));
   NOption(365,Node015a,-003);
   NOption(366,Node015a,-003);
   NOption(367,Node015a,-003);
   NOption(368,Node015a,004);
   NOption(369,Node015a,004);
   NOption(370,Node015a,004);
end

procedure Node015 begin
   dude_caps_adjust(40);
   //item_caps_adjust(self_obj, -40);
   set_local_var(LVAR_MONEY,((local_var(LVAR_MONEY)) - 40));

   Reply(mstr(375) + " " + mstr(376 + dude_is_female) + " " + mstr(378));

   NOption(379,Node005a,-003); //again
   NOption(380,Node999,-003); //end
   NOption(381,Node005a,004); //again
   NOption(382,Node999,004); //end

   prev_node :=15;
end

procedure Node015a begin
   if ( local_var(LVAR_MONEY) >= 40 ) then
      call Node015;
   else
      call Node016;
end

procedure Node016 begin
   Reply(mstr(385) + " " + mstr(386 + dude_is_female) + " " + mstr(388));

   NOption(389,Node999,-003);
   NOption(391,Node017,004);
   NOption(392,Node999,004);

   set_local_var(LVAR_NO_MONEY,1);
end

procedure Node017 begin //INFO - paying debt
   info_price := 1000;

   Reply(404);

   if ((get_mystery_flag(MYSTERY_STATE_ASSIGNED)) and (know_scott == false) and (local_var(LVAR_WRIGHT_ASKED) != 1)) then
      NOption(405,Node020,004); //wright
   if ((get_track_lloyd(track_lloyd_assigned)) and (local_var(LVAR_LLYOD_ASKED) != 1)) then
      NOption(406,Node021,004); //llyod
   if ((get_stolen_car(stolen_car_stolen)) and (local_var(LVAR_CAR_ASKED) != 1)) then
      NOption(407,Node022,004); //missing car
   if ((local_var(LVAR_OTHER_ASKED) != 1)) then
      NOption(408,Node018,004); //other info
   NOption(409,Node999,004);
end

procedure Node018 begin //other info - paying debt
   Reply(430);
   NOption(431,Node999,004);
   set_local_var(LVAR_OTHER_ASKED,1);
end

procedure Node019 begin //INFO
   info_price := 1000;

   Reply(465);

   if ((get_mystery_flag(MYSTERY_STATE_ASSIGNED)) and (know_scott == false) and (local_var(LVAR_WRIGHT_ASKED) != 1)) then
      NOption(466,Node023,004); //wright
   if ((get_track_lloyd(track_lloyd_assigned)) and (local_var(LVAR_LLYOD_ASKED) != 1)) then
      NOption(467,Node024,004); //llyod
   if ((get_stolen_car(stolen_car_stolen)) and (local_var(LVAR_CAR_ASKED) != 1)) then
      NOption(468,Node025,004); //missing car
   if ((local_var(LVAR_OTHER_ASKED) != 1)) then
      NOption(469,Node026,004); //other info
   NOption(470,Node999,004);
end

procedure Node020 begin //wright - paying debt
   Reply(415);
   NOption(416,Node999,004);
   set_local_var(LVAR_WRIGHT_ASKED,1);
   suspect_renesco
end

procedure Node021 begin //llyod - paying debt
   Reply(420);
   NOption(421,Node999,004);
   set_local_var(LVAR_LLYOD_ASKED,1);
end

procedure Node022 begin //missing car - paying debt
   Reply(425);
   NOption(426,Node999,004);
   set_local_var(LVAR_CAR_ASKED,1);
   set_monte_fingered_jules;
end

procedure Node023 begin //wright
   Reply(475 + (local_var(LVAR_NODE23_COUNT)));
   NOption(478,Node029,004);
   NOption(479,Node991,004);
   NOption(480,Node992,004);
   NOption(481,Node993,004);
   NOption(482,Node027,004);
   NOption(483,Node019,004);
   NOption(484,Node999,004);

   if (local_var(LVAR_NODE23_COUNT) < 2) then
     inc_local_var(LVAR_NODE23_COUNT);

   prev_node := 23;

end

procedure Node024 begin //llyod
   Reply(500 + (local_var(LVAR_NODE24_COUNT)));
   NOption(502,Node032,004);
   NOption(503,Node991,004);
   NOption(504,Node992,004);
   NOption(505,Node993,004);
   NOption(506,Node019,004);
   NOption(507,Node999,004);

   if (local_var(LVAR_NODE24_COUNT) < 1) then
     inc_local_var(LVAR_NODE24_COUNT);

   prev_node := 24;

end

procedure Node025 begin //missing car
   Reply(520 + (local_var(LVAR_NODE25_COUNT)));
   NOption(522,Node034,004);
   NOption(523,Node991,004);
   NOption(524,Node992,004);
   NOption(525,Node993,004);
   NOption(526,Node019,004);
   NOption(527,Node999,004);

   if (local_var(LVAR_NODE25_COUNT) < 1) then
     inc_local_var(LVAR_NODE25_COUNT);

   prev_node := 25;

end

procedure Node026 begin //other info
   Reply(540);
   NOption(541,Node036,004);
   NOption(542,Node991,004);
   NOption(543,Node993,004);
   NOption(544,Node019,004);
   NOption(545,Node999,004);

   prev_node := 26;
end

procedure Node027 begin //golgotha?
   Reply(560);
   NOption(561,Node023,004);
   NOption(562,Node019,004);
   NOption(563,Node999,004);
   set_golgotha_known
end

procedure Node028 begin //wright - nope
   Reply(490);
   NOption(492,Node019,004);
   NOption(493,Node999,004);
end

procedure Node029 begin //wright - ya
   Reply(495);
   NOption(496,Node030,004);
end

procedure Node030 begin
   Reply(565);
   NOption(566,Node019,004);
   NOption(567,Node999,004);
   set_local_var(LVAR_WRIGHT_ASKED,1);
   dude_caps_adjust(-info_price);
   set_local_var(LVAR_MONEY,((local_var(LVAR_MONEY)) + info_price));
   //item_caps_adjust(self_obj, info_price);
   suspect_renesco
end

procedure Node031 begin //llyod - nope
   Reply(510);
   NOption(512,Node019,004);
   NOption(513,Node999,004);
end

procedure Node032 begin //llyod - ya
   Reply(515);
   NOption(516,Node019,004);
   NOption(517,Node999,004);
   set_local_var(LVAR_LLYOD_ASKED,1);
   dude_caps_adjust(-info_price);
   //item_caps_adjust(self_obj, info_price);
   set_local_var(LVAR_MONEY,((local_var(LVAR_MONEY)) + info_price));
end

procedure Node033 begin //missing car - nope
   Reply(530);
   NOption(532,Node019,004);
   NOption(533,Node999,004);
end

procedure Node034 begin //missing car - ya
   Reply(535);
   NOption(536,Node019,004);
   NOption(537,Node999,004);
   set_local_var(LVAR_CAR_ASKED,1);
   dude_caps_adjust(-info_price);
   //item_caps_adjust(self_obj, info_price);
   set_local_var(LVAR_MONEY,((local_var(LVAR_MONEY)) + info_price));
   set_monte_fingered_jules;
end

procedure Node035 begin //more info- nope
   Reply(550);
   NOption(551,Node036,004);
   if (not(prev_node == 13)) then
      NOption(552,Node019,004);
   NOption(553,Node999,004);
end

procedure Node036 begin //more info - ya
   Reply(555);
   if (not(prev_node == 13)) then
      NOption(556,Node019,004);
   NOption(557,Node999,004);
   set_local_var(LVAR_OTHER_ASKED,1);
   dude_caps_adjust(-info_price);
   //item_caps_adjust(self_obj, info_price);
   set_local_var(LVAR_MONEY,((local_var(LVAR_MONEY)) + info_price));
end
