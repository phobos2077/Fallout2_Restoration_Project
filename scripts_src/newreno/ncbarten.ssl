/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: FUCKING BARTENDER
        Location: New Reno
        Description:

        Log:

           Created: May 6, 1998

           Updated:
*/

/* Include Files */
#define SCRIPT_REALNAME "ncbarten"
#include "../headers/define.h"
#define NAME                    SCRIPT_NCBARTEN
#define TOWN_REP_VAR            GVAR_TOWN_REP_NEW_RENO
#include "../headers/command.h"
#include "../headers/newreno.h"
#include "../headers/modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure map_update_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Flags                    (4)
#define LVAR_Family                   (5)

#define hidden_for_fight_bit          bit_10
/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */

/* Local variables which do not need to be saved between map changes. */

import variable new_reno_temp_box;
import variable new_reno_salvatore_bar_box;
import variable new_reno_bishop_bar_box;
import variable new_reno_mordino_bar_box;

#define self_family                    (local_var(LVAR_Family))
#define move_inven_to_box              if (self_family == family_salvatore) then begin                      \
                                          move_obj_inven_to_obj(self_obj, new_reno_salvatore_bar_box);      \
                                       end else if (self_family == family_bishop) then begin                \
                                          move_obj_inven_to_obj(self_obj, new_reno_bishop_bar_box);         \
                                       end else if (self_family == family_mordino) then begin               \
                                          move_obj_inven_to_obj(self_obj, new_reno_mordino_bar_box);        \
                                       end
#define move_inven_from_box            if (self_family == family_salvatore) then begin                      \
                                          move_obj_inven_to_obj(new_reno_salvatore_bar_box, self_obj);      \
                                       end else if (self_family == family_bishop) then begin                \
                                          move_obj_inven_to_obj(new_reno_bishop_bar_box, self_obj);         \
                                       end else if (self_family == family_mordino) then begin               \
                                          move_obj_inven_to_obj(new_reno_mordino_bar_box, self_obj);        \
                                       end
#define move_inven_to_temp_box         move_obj_inven_to_obj(self_obj, new_reno_temp_box);
#define move_inven_from_temp_box       move_obj_inven_to_obj(new_reno_temp_box, self_obj);
#define do_bartender_barter            if ((dude_family==self_family) or (enclave_destroyed)) then begin    \
                                          gdialog_mod_barter(-75); /* Seraph's Fix: changed '75' to '-75' */ \
                                       end else if (dude_is_prizefighter) then begin                        \
                                          gdialog_mod_barter(-25); /* Seraph's Fix: changed '25' to '-25' */ \
                                       end else begin                                                       \
                                          gdialog_mod_barter(0);                                            \
                                       end                                                                  \
                                       Reply(270);                                                          \
                                       NOption(g_mstr(350), Node999, 001)


procedure start begin
end

procedure timed_event_p_proc begin
   if ((combat_is_initialized == false) and (self_visible)) then begin
      if (enclave_destroyed) then begin
         call Node001;
      end else if (dude_family == self_family) then begin
         call Node002;
      end else begin
         call Node003;
      end
   end
   nr_add_timer_event_rand(self_obj, 15, 30, float_param);
end

procedure map_enter_p_proc begin
variable temp;
   if (is_loading_game == false) then begin
      if (map_first_run) then begin
         get_and_set_family(temp);
         set_local_var(LVAR_Family, temp);
         critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_GENERIC_GUARDS);
         ndebug("self_family: " + self_family);
      end
      nr_add_timer_event_rand(self_obj, 25, 35, float_param);
   end
end
procedure map_update_p_proc begin
   check_hide_durring_boxing_fight
end

procedure critter_p_proc begin
   if ((hostile) and (self_can_see_dude)) then begin
      self_attack_dude;
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_personal_enemy;
      attack_family(self_family)
   end
end

procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      call Node998;
   end
end

procedure talk_p_proc begin
   script_overrides;

   //added by killap
   if ((dude_family==self_family) or (enclave_destroyed)) then
      gdialog_set_barter_mod(-75);
   else if (dude_is_prizefighter) then
      gdialog_set_barter_mod(-25);
   //end added by killap

   if ((dude_is_made_man) and (self_family != dude_family)) then begin
      call Node004;
   end else begin
      move_inven_to_temp_box
      move_inven_from_box
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node005;
      gSay_End;
      end_dialogue;
      move_inven_to_box
      move_inven_from_temp_box
   end
   nr_add_timer_event_rand(self_obj, 25, 35, float_param);
end

procedure destroy_p_proc begin
   inc_evil_critter
   move_inven_from_box
   dec_family_counter(self_family);
end

procedure look_at_p_proc begin
   script_overrides;
   display_mstr(100);
end

procedure description_p_proc begin
   script_overrides;
   display_mstr(150 - (50*(examined)));
   set_examined;
end

procedure use_skill_on_p_proc begin
end

procedure Node998 begin
   if (hostile == false) then begin
      set_hostile;
   end
   attack_family(self_family) //added by killap
end
procedure Node999 begin
end
procedure Node990 begin
   do_bartender_barter;
end

procedure Node001 begin
   float_msg(self_obj, mstr(200) + dude_name + mstr(201), FLOAT_COLOR_NORMAL);
end
procedure Node002 begin
   float_msg(self_obj, mstr(200) + made_man_name + mstr(206), FLOAT_COLOR_NORMAL);
end
procedure Node003 begin
   floater_rand(210,219);
end
procedure Node004 begin
   floater_rand(225,228);
end
procedure Node005 begin
   if (dude_family == self_family) then begin
      Reply(mstr(200) + made_man_name + mstr(236));
   end else if (dude_is_prizefighter) then begin
      Reply(237);
   end else begin
      Reply(238 + dude_is_female);
   end
   NOption(240, Node990, -003);
   NOption(241, Node999, -003);
   NOption(242, Node990, 004);
   NOption(243, Node990, 004);
   if ((mcgee_dead == false) and (mcgee_family == self_family) and not(mcgee_jackpot_settled)) then begin //edit by killap (expansion) - added not(mcgee_jackpot_settled)
      NOption(244, Node006, 004);
   end else if ((lil_jesus_dead == false) and (lil_jesus_family == self_family)) then begin
      NOption(245, Node007, 004);
   end
   NOption(246, Node999, 004);
end
procedure Node006 begin
   set_mcgee_known;
   Reply(250);
   NOption(242, Node990, 004);
   NOption(243, Node990, 004);
   NOption(246, Node999, 004);
end
procedure Node007 begin
   Reply(260);
   NOption(242, Node990, 004);
   NOption(243, Node990, 004);
   NOption(246, Node999, 004);
end