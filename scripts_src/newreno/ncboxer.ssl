/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Generic Boxer
        Location: New Reno
        Description:

        Log: // ### all boxers move around the gym, even special ones
             //     set this up so it's a generic macro in new reno.h

           Created: April 21, 1998

           Updated:
*/

/* Include Files */
#define SCRIPT_REALNAME "ncboxer"
#include "../headers/define.h"
#define NAME                    SCRIPT_NCBOXER
#define TOWN_REP_VAR            GVAR_TOWN_REP_NEW_RENO
#include "../headers/command.h"
#include "../headers/newreno.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

procedure Node001;

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Flags                    (0)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */

/* Local variables which do not need to be saved between map changes. */

#define boxer_is_black                    (false) // ###
#define boxer_is_white                    (false) // ###

import variable new_reno_generic_boxer_obj;

procedure start begin
end

procedure timed_event_p_proc begin
   if (fixed_param == float_param) then begin
      if (combat_is_initialized == false) then begin
         if (fight_is_on == false) then begin
            if (random(1, 4) == 1) then begin
               floater_rand(200,202);
            end
            if (random(1, 4) == 1) then begin
               reg_anim_clear(self_obj);
               reg_anim_begin();
                  reg_anim_animate(self_obj, ANIM_throw_punch, -1);
               reg_anim_end();
            end
         end
      end
      nr_add_timer_event_rand(self_obj, 25, 35, float_param);
   end
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
      if (map_first_run) then begin
         set_self_team(TEAM_NEW_RENO_BOXER);
         set_self_ai(AI_BOXER);
      end
      nr_add_timer_event_rand(self_obj, 25, 35, float_param);
   end
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      if (hostile) then begin
         self_attack_dude;
      end else set_new_reno_watcher(new_reno_generic_boxer_obj)
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_personal_enemy;
   end
end

procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      set_hostile;
   end
end

procedure talk_p_proc begin
   call Node001;
   nr_add_timer_event_rand(self_obj, 25, 35, float_param);
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_mstr(100);
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(400)); //edit by killap
   //display_mstr_rand(160-(boxer_is_black*10), 162+(boxer_is_white*6));
end

procedure use_skill_on_p_proc begin
   // ### give stealing bonus
end

procedure Node998 begin
   set_hostile;
end
procedure Node999 begin
end

procedure Node001 begin
   if ((dude_wearing_power_armor) or (dude_is_prizefighter)) then begin
      floater_rand(203,211);
   end else begin
      floater_rand(216-(dude_is_female*4), 219+(dude_is_female*8));
   end
end
