/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location: New Reno
        Description:

        Log:

           Created: May 6, 1998

           Updated:
*/

/* Include Files */
#include "../headers/define.h"
#define NAME                    SCRIPT_NCPITBOS
#define TOWN_REP_VAR            GVAR_TOWN_REP_NEW_RENO
#include "../headers/command.h"
#include "../headers/newreno.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
procedure Node023;
procedure Node023b;
procedure Node024;
procedure Node025;
procedure Node026;
procedure Node027;
procedure Node028;
procedure Node029;
procedure Node030;
procedure Node031;
procedure Node900;
procedure Node901;
procedure Node902;
procedure Node903;

procedure NodeGoodJokes;
procedure NodeBadJokes;

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */

import variable comedian_obj; //added by killap - expansion pack

#define LVAR_Flags                     (0)
#define LVAR_Family                    (1)
#define LVAR_Enclave_Reward            (2)
#define LVAR_Family                    (3)
#define LVAR_Work                      (4)
#define LVAR_No_Work                   (5)
#define LVAR_First_Routine             (6)
#define LVAR_Comic_Questioned          (7)
#define LVAR_Dance_Questioned          (8)
#define LVAR_Home_Tile                 (9)

#define self_family                    (local_var(LVAR_Family))
#define dude_has_important_rep            ((enclave_destroyed) or (dude_has_porn_star_rep) or (dude_is_prizefighter) or (dude_is_made_man))

#define player_tell_joke               (200)
#define player_tell_joke_cont          (300)
#define player_told_joke               (400)
#define player_start_dance             (500)
#define player_do_dance_walk           (600)
#define player_do_dance_walk_tmp       (700)
#define player_do_dance_walk2          (800)
#define player_do_dance_walk3          (900)
#define player_do_dance_walk4          (1000)
#define player_do_dance_walk5          (1100)
#define player_do_dance_walk6          (1200)
#define player_do_dance_walk7          (1300)
#define player_do_dance_walk8          (1400)
#define player_do_dance_walk9          (1500)
#define player_do_dance_walk10         (1600)
#define player_do_dance_walk11         (1700)
#define player_do_dance_walk12         (1800)
#define player_do_dance_walk13         (1900)
#define player_do_dance_walk14         (2000)
#define player_do_dance_walk15         (2100)
#define player_do_dance_walk16         (2200)
#define player_do_dance_walk17         (2300)
#define player_danced                  (2400)

#define casino_name                 (900 + local_var(LVAR_Family))

#define bishop_casino               2
#define mordino_casino              3


/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */

/* Local variables which do not need to be saved between map changes. */

variable tmp;
variable tell_funny := -1;
variable finished_routine :=0;
variable finished_dance :=0
variable Critter;
variable wore_leather :=0;

procedure start begin
end

procedure timed_event_p_proc begin
   variable tile;
   if (combat_is_initialized == false) then begin
      if (fixed_param == player_tell_joke) then begin
         if (tell_funny) then begin
            call NodeGoodJokes;
         end else begin
            call NodeBadJokes;
         end
      end else if (fixed_param == player_tell_joke_cont) then begin
         float_msg(dude_obj, message_str(NAME,(tmp+1000)), 8);
         add_timer_event(self_obj,game_ticks(8),player_told_joke);
      end else if (fixed_param == player_told_joke) then begin
         gfade_out(600);
         move_to(dude_obj, 11079, dude_elevation);
         set_obj_visibility(comedian_obj, false);
         game_ui_enable;
         party_no_follow_off;
         finished_routine :=1;
         display_msg(mstr(2500));
         game_time_advance(ONE_GAME_HOUR * 2);
         gfade_in(600);
      end else if (fixed_param == player_start_dance) then begin
         debug_msg("PITBOSS: tile_set_center(11087): " + tile_set_center(11087)); //screen will recenter on dancer
         gfade_in(600);
         add_timer_event(self_obj,game_ticks(2),player_do_dance_walk);
      end else if (fixed_param == player_do_dance_walk) then begin
          tmp := random(0,4);
          if (tmp == 0) then
            tile := 11085;
          else if (tmp == 1) then
            tile := 11292;
          else if (tmp == 2) then
            tile := 10690;
          else if (tmp == 3) then
            tile := 11484;
          else
            tile := 11687;
          reg_anim_clear(Critter);
          reg_anim_begin();
            animate_move_obj_to_tile(Critter,tile,ANIMATE_WALK);
          reg_anim_end();
          add_timer_event(self_obj,game_ticks(1),player_do_dance_walk_tmp);
      end else if (fixed_param == player_do_dance_walk_tmp) then begin
         if (anim_busy(Critter)) then begin
            add_timer_event(self_obj,game_ticks(1),player_do_dance_walk_tmp);
         end else begin
            add_timer_event(self_obj,game_ticks(1),player_do_dance_walk2);
         end
      end else if (fixed_param == player_do_dance_walk2) then begin
         reg_anim_clear(Critter);
         reg_anim_begin();
            anim(Critter, ANIMATE_ROTATION, 2);
         reg_anim_end();
         add_timer_event(self_obj, game_ticks(2), player_do_dance_walk3);
       end else if (fixed_param == player_do_dance_walk3) then begin
         reg_anim_clear(Critter);
         reg_anim_begin();
           animate_move_obj_to_tile(Critter, 10888, 0);
         reg_anim_end();
         add_timer_event(self_obj, game_ticks(1), player_do_dance_walk4);
       end else if (fixed_param == player_do_dance_walk4) then begin
         if (anim_busy(Critter)) then
           add_timer_event(self_obj, game_ticks(1), player_do_dance_walk4);
         else
           add_timer_event(self_obj, game_ticks(1), player_do_dance_walk5);
       end else if (fixed_param == player_do_dance_walk5) then begin
         reg_anim_clear(Critter);
         reg_anim_begin();
           anim(Critter, ANIMATE_ROTATION, 2);
         reg_anim_end();
         add_timer_event(self_obj, game_ticks(2), player_do_dance_walk6);
       end else if (fixed_param == player_do_dance_walk6) then begin
         reg_anim_clear(Critter);
         reg_anim_begin();
           reg_anim_animate(Critter, ANIM_dodge_anim, -1);
           reg_anim_animate(Critter, ANIM_dodge_anim, 10);
         reg_anim_end();
         add_timer_event(self_obj, game_ticks(2), player_do_dance_walk7);
       end else if (fixed_param == player_do_dance_walk7) then begin
         reg_anim_clear(Critter);
         reg_anim_begin();
           reg_anim_animate(Critter, ANIM_kick_leg, -1);
           reg_anim_animate(Critter, ANIM_kick_leg, 15);
         reg_anim_end();
         add_timer_event(self_obj, game_ticks(2), player_do_dance_walk8);
       end else if (fixed_param == player_do_dance_walk8) then begin
         reg_anim_clear(Critter);
         reg_anim_begin();
           reg_anim_animate(Critter, ANIM_dodge_anim, -1);
           reg_anim_animate(Critter, ANIM_dodge_anim, 10);
         reg_anim_end();
         add_timer_event(self_obj, game_ticks(2), player_do_dance_walk9);
       end else if (fixed_param == player_do_dance_walk9) then begin
         reg_anim_clear(Critter);
         reg_anim_begin();
           animate_move_obj_to_tile(Critter, 11488, 0);
         reg_anim_end();
         add_timer_event(self_obj, game_ticks(1), player_do_dance_walk10);
       end else if (fixed_param == player_do_dance_walk10) then begin
         if (anim_busy(Critter)) then
           add_timer_event(self_obj, game_ticks(1), player_do_dance_walk10);
         else
           add_timer_event(self_obj, game_ticks(1), player_do_dance_walk11);
       end else if (fixed_param == player_do_dance_walk11) then begin
         reg_anim_clear(Critter);
         reg_anim_begin();
           anim(Critter, ANIMATE_ROTATION, 5);
         reg_anim_end();
         add_timer_event(self_obj, game_ticks(2), player_do_dance_walk12);
       end else if (fixed_param == player_do_dance_walk12) then begin
         reg_anim_clear(Critter);
         reg_anim_begin();
           reg_anim_animate(Critter, ANIM_magic_hands_ground, -1);
           reg_anim_animate(Critter, ANIM_magic_hands_ground, 10);
         reg_anim_end();
         add_timer_event(self_obj, game_ticks(2), player_do_dance_walk13);
       end else if (fixed_param == player_do_dance_walk13) then begin
         reg_anim_clear(Critter);
         reg_anim_begin();
           anim(Critter, ANIMATE_ROTATION, 4);
         reg_anim_end();
         add_timer_event(self_obj, game_ticks(2), player_do_dance_walk14);
       end else if (fixed_param == player_do_dance_walk14) then begin
         reg_anim_clear(Critter);
         reg_anim_begin();
           reg_anim_animate(Critter, ANIM_kick_leg, -1);
           reg_anim_animate(Critter, ANIM_kick_leg, 15);
         reg_anim_end();
         add_timer_event(self_obj, game_ticks(2), player_do_dance_walk15);
       end else if (fixed_param == player_do_dance_walk15) then begin
         reg_anim_clear(Critter);
         reg_anim_begin();
           anim(Critter, ANIMATE_ROTATION, 0);
         reg_anim_end();
         add_timer_event(self_obj, game_ticks(2), player_do_dance_walk16);
       end else if (fixed_param == player_do_dance_walk16) then begin
         reg_anim_clear(Critter);
         reg_anim_begin();
           reg_anim_animate(Critter, ANIM_kick_leg, -1);
           reg_anim_animate(Critter, ANIM_kick_leg, 15);
         reg_anim_end();
         add_timer_event(self_obj, game_ticks(2), player_do_dance_walk17);
       end else if (fixed_param == player_do_dance_walk17) then begin
         reg_anim_clear(Critter);
         reg_anim_begin();
           anim(Critter, ANIMATE_ROTATION, 2);
         reg_anim_end();
         add_timer_event(self_obj,game_ticks(2),player_danced);
      end else if (fixed_param == player_danced) then begin
         gfade_out(600);
         destroy_object(Critter);
         set_obj_visibility(comedian_obj, false);
         set_obj_visibility(dude_obj, false);
         game_ui_enable;
         party_no_follow_off;
         finished_dance :=1;
         display_msg(mstr(2501));
         game_time_advance(ONE_GAME_HOUR * 1);
         debug_msg("PITBOSS: tile_set_center(11079): " + tile_set_center(11079)); //screen will recenter on player
         gfade_in(600);
      end else if (fixed_param == float_param) then begin
         if (self_distance_from_dude <= 3) then begin
            if (Fallout2_enclave_destroyed) then begin
               call Node012;
            end else if (dude_family == self_family) then begin
               if (dude_is_sneaking) then begin
                  call Node002;
               end else begin
                  call Node003;
               end
            end else if (dude_has_porn_star_rep) then begin
               if (dude_is_sneaking) then begin
                  if (dude_is_female) then begin
                     call Node007;
                  end
               end
            end else if (dude_is_sneaking) then begin
               call Node008;
            end else if (Myron_In_Party) then begin
               call Node009;
            end else if (Marcus_In_Party) then begin
               call Node010;
            end else if (Robobrain_In_Party) then begin
               call Node011;
            end
            nr_add_timer_event_rand(self_obj, 15, 30, float_param);
         end else begin
            call Node013;
            nr_add_timer_event_rand(self_obj, 15, 30, float_param);
         end
      end else if (fixed_param == wander_param) then begin
         if (self_family == mordino_casino) then begin
            tmp := random(1,2);
            if (self_tile != local_var(LVAR_Home_Tile)) then
               tile := local_var(LVAR_Home_Tile);
            else if (tmp == 1)  then
               tile := 13466;
            else
               tile := 14687;
         end else if (self_family == bishop_casino) then begin
            tmp := random(1,2);
            if (self_tile != local_var(LVAR_Home_Tile)) then
               tile := local_var(LVAR_Home_Tile);
            else if (tmp == 1)  then
               tile := 10878;
            else
               tile := 16673;
         end
      reg_anim_clear(self_obj);
      reg_anim_begin();
         animate_move_obj_to_tile(self_obj,tile,ANIMATE_WALK);
      reg_anim_end();
      nr_add_timer_event_rand(self_obj, 20, 40, wander_param);
      end
   end
end

procedure map_enter_p_proc begin
   variable temp_family;
   //destroy_object(self_obj);
   /*
   if (is_loading_game == false) then begin
   end
   if (map_first_run) then begin
   end
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NEW_RENO_);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_);
   */

   /* // added by killap - expansion
   if ( (self_family == family_salvatore and salvatore_dead and warned_attack) or
       (self_family == family_bishop and bishop_dead and warned_attack) or
       (self_family == family_mordino and big_jesus_dead and lil_jesus_dead and warned_attack) ) then begin
         destroy_object(self_obj);
         return;
   end
   // end added */

   if (map_first_run) then
      set_local_var(LVAR_Home_Tile,self_tile);

   get_and_set_family(temp_family);
   set_local_var(LVAR_Family, temp_family);

   nr_add_timer_event_rand(self_obj, 5, 30, float_param);
   nr_add_timer_event_rand(self_obj, 10, 40, wander_param);

end

procedure critter_p_proc begin
   if ((hostile) and (self_can_see_dude)) then begin

      self_attack_dude;
   end else if (finished_routine) then begin
      finished_routine :=0;
      if (tell_funny) then begin
         force_dialog_start(@Node028);
      end else begin
         force_dialog_start(@Node029);
      end
   end else if (finished_dance) then begin
      finished_dance := 0;
      force_dialog_start(@Node026);
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_personal_enemy;
   end
end

procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      call Node998;
   end
end

procedure talk_p_proc begin
   /*//added by killap - expansion
   if ( (self_family == family_salvatore and salvatore_dead and warned_attack) or
       (self_family == family_bishop and bishop_dead and warned_attack) or
       (self_family == family_mordino and big_jesus_dead and lil_jesus_dead and warned_attack) ) then begin
         float_msg(self_obj, message_str(SCRIPT_DCGAMBLE,200), FLOAT_COLOR_NORMAL);
   end else begin*/
      check_forced_dialog
      else begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node014;
         gSay_End;
         end_dialogue;
      end
   //end

   set_herebefore;
end

procedure destroy_p_proc begin
   // inc__critter ***
   dec_family_counter(self_family);
   inc_evil_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_mstr(100);
end

procedure description_p_proc begin
   script_overrides;
   display_mstr_rand(150,151);
   set_examined;
end

procedure use_skill_on_p_proc begin
end

procedure Node998 begin
   if (hostile == false) then begin
      set_hostile;
   end
end

procedure Node999 begin
end

#define is_insert_message(x)  ((x == 215) or (x == 216) or (x == 220) or (x == 221) or      \
                               (x == 225) or (x == 226) or (x == 230) or (x == 231) or      \
                               (x == 306))

procedure floater_rand_with_check_cust(variable x, variable y,  variable the_patch) begin
variable the_msg;
   the_msg := random(x, y);
   if (is_insert_message(the_msg)) then begin
      if ( (the_msg == 2030000) ) then begin
         the_msg := "" + dude_name + mstr(the_msg);
      end else
         the_msg := "" + mstr(the_msg) + the_patch + mstr(the_msg+1000);

      float_msg(self_obj, the_msg, FLOAT_COLOR_NORMAL);
   end else
      floater(the_msg);
end

procedure Node001 begin
   // floater_rand(, ); ***
end

procedure Node002 begin //mademan sneaking
    call floater_rand_with_check_cust(205 + (5*dude_is_female), 206 + (5*dude_is_female), made_man_name);
end

procedure Node003 begin //made man float dialogue night/day
    call floater_rand_with_check_cust(215 + (10*day) + (5*dude_is_female), 216 + (10*day) + (5*dude_is_female), made_man_name);
end

procedure Node004 begin //made man float wrong casino
    call floater_rand_with_check_cust(235, 237 + (dude_is_female), made_man_name);
end

procedure Node005 begin //prizefighter weapon
    call floater_rand_with_check_cust(245 + (5*dude_is_female), 245 + (5*dude_is_female), prizefighter_name);
end

procedure Node006 begin //pornstar weapon
    call floater_rand_with_check_cust(255 , 255, porn_star_name);
end

procedure Node007 begin //pornstar sneak
    call floater_rand_with_check_cust(260 , 260, porn_star_name);
end

procedure Node008 begin //no rep sneak
    call floater_rand_with_check_cust(270 , 270, dude_name);
end

procedure Node009 begin //Myon in party
    call floater_rand_with_check_cust(275 , 277, dude_name);
end

procedure Node010 begin //Marcus in party
    call floater_rand_with_check_cust(1285 , 1287, dude_name);
end

procedure Node011 begin //Robobrain in party
    call floater_rand_with_check_cust(295 , 297, dude_name);
end

procedure Node012 begin //Enclave destroyed
    call floater_rand_with_check_cust(305 , 306, dude_name);
end

procedure Node013 begin //radom work float
    floater_rand(310 + (10*night), 311 + (10*night));
end

procedure Node014 begin
   variable tmp;
   tmp := random(200,500);

   if ( (enclave_destroyed) and (local_var(LVAR_Enclave_Reward) == 0) ) then begin
      Reply("" + mstr(335) + dude_name + mstr(336 + dude_is_female));
      item_caps_adjust(dude_obj, tmp);
      set_local_var(LVAR_Enclave_Reward,1);
   end else if (dude_has_porn_star_rep) then begin
      Reply("" + mstr(360) + porn_star_name + mstr(361) + mstr(casino_name) + mstr(1361));
   end else if (dude_is_made_man) then begin
         Reply("" + made_man_name + mstr(341 - dude_is_male - (2*dude_has_important_rep)));
   end else if (dude_is_prizefighter) then begin
         Reply("" + prizefighter_name + mstr(341 - dude_is_male - (2*dude_has_important_rep)));
   end else begin
         Reply(341 - dude_is_male - (2*dude_has_important_rep));
   end

   NOption(342,Node015,-003);
   NOption(343,Node999,-003);
   NOption(344,Node019,004); //who are you?
   NOption(345,Node020,004); //what casino?
   NOption(346,Node021,004); //tell me about casino
   if ((self_family == mordino_casino) and (nikki_lloyd) and not(get_track_lloyd(track_lloyd_done)) ) then //edit by killap - added not(get_track_lloyd(track_lloyd_done))
      NOption(347,Node024,004); //Lloyd
   if ((self_family == bishop_casino) and (mcgee_dead == false) and not(mcgee_jackpot_settled) ) then //edit by killap - added not(mcgee_jackpot_settled)
      NOption(348,Node023,004); //who is old guy by machine?
   if ((dude_is_male) and (self_family == bishop_casino)) then begin
      if (local_var(LVAR_Comic_Questioned) == 0) then
         NOption(349,Node027,004); //comic is weak, mind if I try?
      else
         NOption(620,Node027,004); //I want more
   end else if (self_family == bishop_casino) then begin
      if (local_var(LVAR_Dance_Questioned) == 0) then
         NOption(350,Node900,004); //I wanna dance
      else
         NOption(720,Node025,004); //I wanna dance more
   end
   if (dude_has_important_rep) then
      NOption(353,Node999,004);
   else
      NOption(352,Node999,004);
end

procedure Node015 begin //dude retard
   Reply(302 - (dude_has_important_rep + dude_is_male));
   NOption(303,Node016,-003);
   NOption(304,Node999,-003);
end

procedure Node016 begin //dude retard cont.
   Reply(315);
   NOption(316,Node999,-003);
   NOption(317,Node999,-003);
end

procedure Node017 begin

end

procedure Node900 begin //dance skill check
   if ( (dude_agility >= 7) or (dude_charisma >= 7) or (dude_has_porn_star_rep) ) then
      call Node025;
   else
      call Node031;
end

procedure Node901 begin //comic skill check
   gfade_in(600);

   if ((dude_charisma > 7) or (has_skill(dude_obj,SKILL_CONVERSANT) > 75) or (dude_smooth_talker)) then
      tell_funny := 1;
   else
      tell_funny := 0;

   add_timer_event(self_obj,2,player_tell_joke);

end

procedure Node018 begin //I have some questions

   Reply(280);
   NOption(283,Node019,004); //who are you?
   NOption(284,Node020,004); //what casino?
   NOption(285,Node021,004); //tell me about casino
   if ((self_family == mordino_casino) and (nikki_lloyd)) then
      NOption(286,Node024,004); //Lloyd
   if ((self_family == bishop_casino) and (mcgee_dead == false) and not(mcgee_jackpot_settled) ) then //edit by killap - added not(mcgee_jackpot_settled)
      NOption(287,Node023,004); //who is old guy by machine?
   if ((dude_is_male) and (self_family == bishop_casino) ) then begin
      if (local_var(LVAR_Comic_Questioned) == 0) then
         NOption(288,Node027,004); //comic is weak, mind if I try?
      else
         NOption(620,Node027,004); //I want more
   end else if (self_family == bishop_casino) then begin
      if (local_var(LVAR_Dance_Questioned) == 0) then
         NOption(289,Node900,004); //I wanna dance
      else
         NOption(720,Node025,004); //I wanna dance more
   end
   if (dude_has_important_rep) then
      NOption(292,Node999,004);
   else
      NOption(291,Node999,004);
end

procedure Node019 begin //who are you?
   Reply(random(325,326));
   NOption(327,Node020,004);
   NOption(328,Node021,004);
   NOption(329,Node999,004);
end

procedure Node020 begin //what casino is this?
   variable tmp;
   tmp := random(1335,1336);

   Reply("" + mstr(tmp) + mstr(905 -(self_family == bishop_casino)) + mstr(tmp + 1000));
   NOption(1337,Node019,004);
   NOption(1338,Node021,004);
   NOption(1339,Node999,004);
end

procedure Node021 begin //tell me about casino
   Reply(random(1345,1346));
   NOption(1347,Node019,004);
   NOption(1348,Node022,004);
   NOption(1349,Node999,004);
end

procedure Node022 begin //who owns casino?
   variable tmp;
   tmp := random(355,356);

   Reply("" + mstr(tmp) + mstr(903 -(self_family == bishop_casino)) + mstr(tmp + 1000));
   NOption(357,Node019,004);
   NOption(358,Node021,004);
   NOption(359,Node999,004);
end

procedure Node023 begin //McGee
   //edit by killap
   if (mcgee_jackpot) then
      Reply(1366);
   else
      Reply(1365);
   //end edit
   NOption(1367,Node023b,004);
   NOption(1368,Node018,004);
   NOption(1369,Node999,004);

   set_mcgee_known;
end

procedure Node023b begin //McGee cont
   Reply(375);
   NOption(376,Node018,004);
   NOption(377,Node999,004);
end

procedure Node024 begin //Lloyd
   Reply(386);
   NOption(388,Node018,004);
   NOption(389,Node999,004);
   set_lloyd_locale_upstairs;
end

procedure Node025 begin //Dancer
   if ((game_time - (local_var(LVAR_Work))  >= ONE_GAME_WEEK)) then begin
      Reply(425 + (local_var(LVAR_Dance_Questioned) * 1000));
      NOption(426,Node903,004);
      NOption(427,Node018,004);
      NOption(428,Node999,004);
   end else begin
      Reply(721);
      NOption(722,Node018,004);
      NOption(723,Node999,004);
   end
end

procedure Node026 begin //Dancer performed
   Reply(445);
   NOption(448,Node030,004);
end

procedure Node027 begin //Comic
   if (local_var(LVAR_No_Work) == 1) then begin
      Reply(435);
      NOption(436,Node018,004);
      NOption(437,Node999,004);
   end else
   if ((game_time - (local_var(LVAR_Work))  >= ONE_GAME_WEEK)) then begin
      Reply(455 + (local_var(LVAR_Comic_Questioned) * 1000));
      NOption(456,Node902,004);
      NOption(457,Node999,004);
   end else begin
      Reply(621);
      NOption(622,Node018,004);
      NOption(623,Node999,004);
   end
end

procedure NodeGoodJokes begin

   tell_funny := 1;
   tmp := random(1000,1004);
   if (tmp == 1003) then begin
      add_timer_event(self_obj,game_ticks(2),player_tell_joke_cont);
   end
   else begin
      add_timer_event(self_obj,game_ticks(8),player_told_joke);
   end

   float_msg(dude_obj, message_str(NAME,tmp), 8);
end

procedure NodeBadJokes begin
   gfade_in(600);

   tell_funny := 0;
   tmp := random(1500,1504);
   if (tmp == 1502 or tmp == 1503 or tmp == 1504) then begin
      add_timer_event(self_obj,game_ticks(4),player_tell_joke_cont);
   end
   else begin
      add_timer_event(self_obj,game_ticks(7),player_told_joke);
   end

   float_msg(dude_obj, message_str(NAME,tmp), tmp);
end

procedure Node028 begin //good comic
   Reply(random(500,501));
   NOption(502,Node999,004);
   set_local_var(LVAR_Work,game_time);
   item_caps_adjust(dude_obj, 300);
   if (local_var(LVAR_First_Routine) == 0) then begin
      set_local_var(LVAR_First_Routine,1);
      display_msg(mstr(2000));
      give_xp(150);
   end
end

procedure Node029 begin //bad comic
   Reply(510);
   NOption(511,Node999,004);
   NOption(512,Node999,004);
   set_local_var(LVAR_No_Work,1);
   display_msg(mstr(2001));
end

procedure Node030 begin //Did dance
   variable tmp_msg;
   variable has_ball_gag := 0;

   if (random(1,2)) then
      tmp_msg := mstr(520);
   else
      tmp_msg := mstr(521);


   if ( ((random(1,4) == 1) and (obj_is_carrying_obj_pid(dude_obj, PID_BALL_GAG) > 0 )) and (wore_leather) ) then begin
      tmp_msg += mstr(446);
      tmp_msg += mstr(447);
      has_ball_gag := 1;
   end else if (wore_leather) then begin
         tmp_msg += mstr(446);
   end else if ( (random(1,4) == 1) and (obj_is_carrying_obj_pid(dude_obj, PID_BALL_GAG) > 0 ) ) then begin
         tmp_msg += mstr(447);
         has_ball_gag := 1;
   end

   tmp_msg += mstr(523);

   Reply(tmp_msg);
   NOption(522,Node999,004);
   set_local_var(LVAR_Work,game_time);
   item_caps_adjust(dude_obj, random(250,350));
   if (local_var(LVAR_First_Routine) == 0) then begin
      set_local_var(LVAR_First_Routine,1);
      display_msg(mstr(2010));
      if ( (wore_leather) or (has_ball_gag) ) then begin
         give_xp(300);
      end else begin
         give_xp(150);
      end
   end
end

procedure Node031 begin //refuse dance
   Reply(530);
   NOption(531,Node018,004);
   NOption(532,Node999,004);
end

procedure Node902 begin
   //fade out
   set_local_var(LVAR_Comic_Questioned,1);
   gfade_out(600);
   game_ui_disable;
   party_no_follow_on;
   //move_to(comedian_obj, 0, 0);
   set_obj_visibility(comedian_obj, true);
   move_to(dude_obj, 11087, dude_elevation);
   reg_anim_clear(dude_obj);
   reg_anim_begin();
         anim(dude_obj, ANIMATE_ROTATION, 2);
   reg_anim_end();
   call Node901;
   //disable party members
   //disable interface
   //disable usual comic
   //move player to stage
   //call skill check
end

procedure Node903 begin
   //fade out
   wore_leather := 0;
   set_local_var(LVAR_Dance_Questioned,1);
   gfade_out(600);
   game_ui_disable;
   party_no_follow_on;
   //move_to(comedian_obj, 0, 0);
   //set_obj_visibility(comedian_obj, true);
   //move_to(dude_obj, 11087, dude_elevation);
   set_obj_visibility(comedian_obj, true);
   set_obj_visibility(dude_obj, true);
   if ( (random(1,5) == 1) and (obj_is_carrying_obj_pid(dude_obj, PID_LEATHER_JACKET) > 0 ) ) then begin
      Critter:=create_object_sid(PID_HERO_LEATHER_JACKET_FEMALE,0,0,SCRIPT_ACVILLGR);
      wore_leather := 1;
   end else begin
      Critter:=create_object_sid(PID_HERO_PRIMITIVE_FEMALE,0,0,SCRIPT_ACVILLGR);
   end
   critter_attempt_placement(Critter,11087,0);
   reg_anim_clear(Critter);
   reg_anim_begin();
         anim(Critter, ANIMATE_ROTATION, 2);
   reg_anim_end();
   add_timer_event(self_obj,game_ticks(0),player_start_dance);
   //create frisky tribal woman
   //focus on her
   //fade out
   //2 seconds later fade back out
   //fade back in to dude in normal clothes and the forced dialog with pit boss
end