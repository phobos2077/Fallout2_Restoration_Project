/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Prostitute
        Location: New Reno
        Description:

        Log:

           Created: April 24, 1998

           Updated:
*/

/* Include Files */
#define SCRIPT_REALNAME "ncprosti"
#include "../headers/define.h"
#define NAME                    SCRIPT_NCPROSTI
#define TOWN_REP_VAR            GVAR_TOWN_REP_NEW_RENO
#include "../headers/command.h"
#include "../headers/newreno.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure map_update_p_proc;
procedure push_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Flags                  (0)
#define LVAR_Home_Tile              (1)

#define hidden_for_fight_bit        bit_10

#define message_type_float          (1)
#define message_type_click          (2)
#define message_is_float            (message_type == message_type_float)
#define message_is_click            (message_type == message_type_click)
#define set_message_float           message_type := message_type_float
#define set_message_click           message_type := message_type_click

#define is_cats_paw_whore           (self_team == TEAM_NEW_RENO_CATS_PAW)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */

/* Local variables which do not need to be saved between map changes. */

variable message_type;
import variable new_reno_cats_paw_obj;

procedure start begin
end

procedure push_p_proc begin
   if (self_tile == local_var(LVAR_Home_Tile)) then begin
      script_overrides;
   end
end

#define check_sidewalk(big_box_condition, sub_box_condition, the_tile)           \
         if ((big_box_condition) and (sub_box_condition == false)) then begin    \
            animate_move_to_tile(the_tile);                                      \
         end
procedure timed_event_p_proc begin
variable temp_tile;
   if ((combat_is_initialized == false) and (self_visible)) then begin
      if (no_whore_floats == false) then begin
         if ((self_can_see_dude) and (random(1,2) == 1)) then begin
            //if (message_str(SCRIPT_NCPERSON, 100) == "You see one of the Reno townsfolk.") then begin
               set_message_float;
               if (enclave_destroyed) then begin
                  call Node001;
               end else if (dude_wearing_power_armor) then begin
                  call Node002;
               end else if (dude_has_porn_star_rep) then begin
                  if (dude_is_female) then begin
                     call Node004;
                  end else begin
                     call Node003;
                  end
               end else if (dude_is_prizefighter) then begin
                  if (dude_is_female) then begin
                     call Node006;
                  end else begin
                     call Node005;
                  end
               end else if (dude_is_made_man) then begin
                  call Node007;
               end else if (has_rep_slaver) then begin
                  call Node008;
               end else if ((robobrain_is_dude_companion) or (marcus_is_dude_companion)) then begin
                  call Node009;
               end else if (myron_is_dude_companion) then begin
                  call Node010;
               end else if (sulik_is_dude_companion) then begin
                  call Node011;
               end else begin
                  call Node012;
               end
            //end else begin
            //   call Node021;
            //end
         end else if ((is_cats_paw_whore) and (obj_on_screen(self_obj))) then begin
            call Node020;
         end
      end
      // this should make the prostitutes walk around the sidewalks
      if (is_cats_paw_whore == false) then begin
         if ((anim_busy(self_obj) == false) and (random(1,5) == 1)) then begin
            if (self_tile != local_var(LVAR_Home_Tile)) then begin
               animate_move_to_tile(local_var(LVAR_Home_Tile));
            end else begin
               temp_tile := tile_num_in_direction(self_tile, random(0, 5), random(5,15));
               if (self_team == TEAM_NEW_RENO_PIMP_1) then begin
                  if (cur_map_index == MAP_NEW_RENO_1) then begin
                     check_sidewalk(tile_pimp_team_1_map_1(self_tile), tile_mordino_property(self_tile), temp_tile)
                  end else if (cur_map_index == MAP_NEW_RENO_2) then begin
                     check_sidewalk(tile_pimp_team_1_map_2(self_tile), tile_salvatore_property(self_tile), temp_tile)
                  end
               end else if (self_team == TEAM_NEW_RENO_PIMP_2) then begin
                  if (cur_map_index == MAP_NEW_RENO_1) then begin
                     check_sidewalk(tile_pimp_team_2_map_1(self_tile), tile_junkie_house_property(self_tile), temp_tile)
                  end else if (cur_map_index == MAP_NEW_RENO_2) then begin
                     check_sidewalk(tile_pimp_team_2_map_2(self_tile), tile_bishop_property(self_tile), temp_tile)
                  end
               end else if (self_team == TEAM_NEW_RENO_PIMP_3) then begin
                  check_sidewalk(tile_pimp_team_3(self_tile), tile_porn_studio_building(self_tile), temp_tile)
               end else if (self_team == TEAM_NEW_RENO_PIMP_4) then begin
                  check_sidewalk(tile_pimp_team_4(self_tile), tile_boxing_gym_building(self_tile), temp_tile)
               end
            end
         end
      end
   end
   nr_add_timer_event_rand(self_obj, 25, 35, float_param);
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
      if (map_first_run) then begin
         set_local_var(LVAR_Home_Tile, self_tile);
         if (obj_cats_paw_building(self_obj)) then begin
            critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NEW_RENO_CATS_PAW);
         end else begin
            set_team_pimp
         end
         critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_PROSTITUTE);
      end
      nr_add_timer_event_rand(self_obj, 25, 35, float_param);
   end
end

procedure map_update_p_proc begin
   check_hide_durring_boxing_fight
end
procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      if (hostile) then begin
         self_attack_dude;
      end else if (is_cats_paw_whore) then begin
         set_new_reno_watcher(new_reno_cats_paw_obj)
      end
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_personal_enemy;
   end
end

procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      set_hostile;
   end
end

procedure talk_p_proc begin
   //if (message_str(SCRIPT_NCPERSON, 100) == "You see one of the Reno townsfolk.") then begin
      set_message_click;
      ndebug("self_team == " + self_team);
      if (enclave_destroyed) then begin
         call Node013;
      end else if (dude_wearing_power_armor) then begin
         call Node014;
      end else if (dude_has_porn_star_rep) then begin
         if (dude_is_female) then begin
            call Node004;
         end else begin
            call Node003;
         end
      end else if (dude_is_prizefighter) then begin
         if (dude_is_female) then begin
            call Node015;
         end else begin
            call Node005;
         end
      end else if (dude_is_made_man) then begin
         call Node016;
      end else if (has_rep_slaver) then begin
         call Node017;
      end else if ((robobrain_is_dude_companion) or (marcus_is_dude_companion)) then begin
         call Node009;
      end else if (myron_is_dude_companion) then begin
         call Node010;
      end else if (sulik_is_dude_companion) then begin
         call Node018;
      end else begin
         call Node019;
      end
   //end else begin
   //   call Node021;
   //end
   nr_add_timer_event_rand(self_obj, 25, 35, float_param);
end

procedure destroy_p_proc begin
   inc_evil_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_mstr(100 + (3000*is_cats_paw_whore));
end

procedure description_p_proc begin
   script_overrides;
   if (examined) then begin
      if (is_cats_paw_whore) then begin
         display_mstr_rand(3151, 3158);
      end else begin
         display_mstr_rand(151, 158);
      end
   end else begin
      display_mstr(150 + (3000*is_cats_paw_whore));
   end
   set_examined;
end

procedure use_skill_on_p_proc begin
end

procedure Node998 begin
   set_hostile;
end
procedure Node999 begin
end

#define is_insert_message(x)  ((x == 175) or (x == 178) or        \
                               (x == 181) or (x == 195) or        \
                               (x == 197) or (x == 210) or        \
                               (x == 263) or (x == 264) or        \
                               (x == 268) or (x == 272) or        \
                               ((278 <= x) and (x <= 280)) or     \
                               ((292 <= x) and (x <= 295)) or     \
                               (x == 285) or (x == 855) or        \
                               (x == 858) or (x == 861) or        \
                               (x == 865) or (x == 867) or        \
                               (x == 868) or (x == 870) or        \
                               (x == 873))

procedure Node001 begin
   floater_rand_with_check(178-(3*dude_is_male), 180+(3*dude_is_female), dude_name, 2000);
end
procedure Node002 begin
   floater(190);
end
procedure Node003 begin
   floater_rand_with_check(197-(2*message_is_float), 200+(11*message_is_click), porn_star_name, 2000);
end
procedure Node004 begin
   floater_rand(225-(13*message_is_float), 238+(13*message_is_click));
end
procedure Node005 begin
   floater_rand_with_check(266-(14*message_is_float), 272+(5*message_is_click), prizefighter_name, 2000);
end
procedure Node006 begin
   floater_rand_with_check(278, 284, prizefighter_name, 2000);
end
procedure Node007 begin
   floater_rand_with_check(292-(7*dude_is_male), 294+(5*dude_is_female), made_man_name, 2000);
end
procedure Node008 begin
   floater_rand(336-(36*dude_is_male), 444+(15*dude_is_female));
end
procedure Node009 begin
   if (robobrain_is_dude_companion) then begin
      floater_rand(460, 464);
   end else if (marcus_is_dude_companion) then begin
      floater_rand(465, 470);
   end
end
procedure Node010 begin
   floater_rand(471, 482);
end
procedure Node011 begin
   floater_rand(524-(41*dude_is_male), 636+(18*dude_is_female));
end
procedure Node012 begin
   floater_rand(696-((696-655)*dude_is_male), 799+((817-799)*dude_is_female));
end
procedure Node013 begin
   floater_rand(818, 834);
end
procedure Node014 begin
   floater_rand(842-(7*dude_is_male), 851+(2*dude_is_female));
end
procedure Node015 begin
   floater_rand_with_check(854, 864, prizefighter_name, 2000);
end
procedure Node016 begin
   floater_rand_with_check(867-(2*dude_is_male), 872+(2*dude_is_female), made_man_name, 2000);
end
procedure Node017 begin
   floater_rand(918-((918-875)*dude_is_male), 1030+((1049-1030)*dude_is_female));
end
procedure Node018 begin
   floater_rand(1091-((1091-1050)*dude_is_male), 1204+((1222-1204)*dude_is_female));
end
procedure Node019 begin
   floater_rand(1264-((1264-1223)*dude_is_male), 1378+((1395-1378)*dude_is_female));
end
procedure Node020 begin
   floater_rand(3200, 3215);
end
procedure Node021 begin
   floater_rand(1223, 1260);
end