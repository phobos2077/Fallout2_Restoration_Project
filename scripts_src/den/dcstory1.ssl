/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location: Den
        Description:

        Log:

           Created: August 22, 1998

           Updated:
*/

/* Include Files */
#define SCRIPT_REALNAME "dcstory1"
#include "../headers/define.h"
#define NAME                    SCRIPT_DCSTORY1
#define TOWN_REPUTATION         GVAR_TOWN_REP_THE_DEN
#include "../headers/command.h"
#include "../headers/modreact.h"
#include "../headers/den.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure map_exit_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node004a;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node007a;
procedure Node008;
procedure Node009;
procedure Node009a;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node016a;
procedure Node017;

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Flags                        (4)
#define LVAR_Story_Status                 (5)

#define STORY_UNTOLD             (0)
#define STORY_MASTER_MENTIONED   (1)
#define STORY_DRINK_PURCHASED    (2)
#define STORY_TOLD               (3)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */

/* Local variables which do not need to be saved between map changes. */
variable chosen_one;

procedure start begin
end

procedure timed_event_p_proc begin
   if (fixed_param == timed_event_float) then begin
      if (combat_is_initialized == false) then begin
      end
      // ### flush_add_timer_event_sec(self_obj, random(, ), timed_event_float);
   end
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
      if (map_first_run) then begin
         critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_THE_DEN);
         critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_PEASANT);
      end
      // ### flush_add_timer_event_sec(self_obj, random(, ), timed_event_float);
   end
end

procedure map_exit_p_proc begin
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      if (hostile) then begin
         self_attack_dude;
      end
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      call Node998;
   end
end

procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      call Node998;
   end
end

procedure talk_p_proc begin
   GetReaction;

   chosen_one := 0;

   if( hostile ) then
      call Node001;
   else if( becky_dead ) then
      call Node002;
   else if( local_var( LVAR_Story_Status ) == STORY_TOLD ) then
      call Node003;
   else begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node004;
      gSay_End;
      end_dialogue;
   end
   set_herebefore;
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   if( not( herebefore ) ) then
      display_mstr(100);
   else
      display_mstr(101);
end

procedure description_p_proc begin
   script_overrides;
   display_mstr(102);
   set_examined;
end

procedure use_skill_on_p_proc begin
end

procedure Node998 begin
   set_hostile;
end
procedure Node999 begin
end

procedure Node001 begin
   floater( random( 103, 106 ) );
end

procedure Node002 begin
   floater( random( 107, 110 ) );
end

procedure Node003 begin
   floater( random( 111, 116 ) );
end

procedure Node004 begin
   if( not( herebefore ) ) then
      Reply(117);
   else
      Reply(118);

   NLowOption(123, Node006);
   NLowOption(124, Node999);
   if( local_var( LVAR_Story_Status ) >= STORY_MASTER_MENTIONED ) then
      NOption(119, Node004a, 004);
   else begin
      NOption(120, Node007, 004);
      // Removed next node since Node007 dialog doesn't sound like an appropriate response.
      //NOption(121, Node007, 004);
   end
end

procedure Node004a begin
   if( local_var( LVAR_Story_Status ) == STORY_DRINK_PURCHASED ) then
      call Node010;
   else
      call Node009;
end

procedure Node005 begin
   // Intentionally left Blank ;)
end

procedure Node006 begin
   Reply(131);

   NLowOption(132, Node999);
   NLowOption(133, Node999);
end

procedure Node007 begin
   if( not( herebefore ) ) then
      Reply(134);
   else
      Reply(135);

   NOption(136, Node007a, 004);
   GOption(137, Node008, 004);
   BOption(138, Node999, 004);
end

procedure Node007a begin
   chosen_one := 1;
   call Node008;
end

procedure Node008 begin
   set_local_var( LVAR_Story_Status, STORY_MASTER_MENTIONED );
   if( chosen_one == 1 ) then begin
      chosen_one := 0;
      Reply(139);
   end
   else
      Reply(140);

   GOption(141, Node009, 004);
   BOption(142, Node009, 004);
   NOption(143, Node999, 004);
end

procedure Node009 begin
   if( local_var( LVAR_Story_Status ) < STORY_DRINK_PURCHASED ) then begin
      if( dude_is_male ) then begin
         Reply(144);
      end
      else begin
         Reply(145);
      end
   end
   else
      Reply(146);

   if( local_var( LVAR_Story_Status ) < STORY_DRINK_PURCHASED ) then begin
      if( item_caps_total( dude_obj ) >= 5 ) then
         NOption(147, Node009a, 004);
      NOption(148, Node999, 004);
   end
   else begin
      NOption(147, Node010, 004);
      NOption(166, Node999, 004);
   end
end

procedure Node009a begin
   set_local_var( LVAR_Story_Status, STORY_DRINK_PURCHASED );
   item_caps_adjust( dude_obj, -5 );
   call Node010;
end

procedure Node010 begin
   Reply(149);

   NOption(150, Node011, 004);
end

procedure Node011 begin
   Reply(151);

   NOption(152, Node012, 004);
end

procedure Node012 begin
   Reply(153);

   NOption(154, Node013, 004);
end

procedure Node013 begin
   Reply(155);

   NOption(156, Node014, 004);
end

procedure Node014 begin
   Reply(157);

   NOption(158, Node015, 004);
end

procedure Node015 begin
   Reply(159);

   NOption(160, Node016, 004);
end

procedure Node016 begin
   Reply(161);

   GOption(162, Node017, 004);
   NOption(163, Node016a, 004);
end

procedure Node016a begin
   set_local_var( LVAR_Story_Status, STORY_TOLD );
   display_msg( mstr(167) );
   give_xp( EXP_LEANNE_TOLD_STORY );
end

procedure Node017 begin
   set_local_var( LVAR_Story_Status, STORY_TOLD );
   display_msg( mstr(167) );
   give_xp( EXP_LEANNE_TOLD_STORY );
   NMessage(164);
end