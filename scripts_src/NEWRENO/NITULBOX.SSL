/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Poor Box in Father Tully's Church
        Location: New Reno
        Description:

        Log:

           Created: May 6, 1998

           Updated:
*/

/* Include Files */
#include "..\headers\define.h"
#define NAME                    SCRIPT_NITULBOX
#define TOWN_REP_VAR            GVAR_TOWN_REP_NEW_RENO
#include "..\headers\command.h"
#include "..\headers\NewReno.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;

#define LVAR_Restock_Time                 (0)
#define LVAR_Poor_Box_Caps                (1)
#define LVAR_Flags                        (2)
#define LVAR_Restock_Time_Fix             (3) //added by killap

#define donated_bit                       bit_10
#define stole_bit                         bit_11

#define donated                           lvar_bit(LVAR_Flags, donated_bit)
#define set_donated                       if (donated == false) then begin                      \
                                             inc_general_rep(REP_BONUS_POOR_BOX_DONATIONS);     \
                                             inc_reno_rep(REP_NEW_RENO_DONATE_POOR_BOX);        \
                                          end                                                   \
                                          set_lvar_bit_on(LVAR_Flags, donated_bit)

#define stole                             lvar_bit(LVAR_Flags, stole_bit)
#define set_stole                         if (stole == false) then begin                        \
                                             dec_general_rep(REP_BONUS_POOR_BOX_DONATIONS);     \
                                             inc_reno_rep(REP_NEW_RENO_STEAL_POOR_BOX);         \
                                          end                                                   \
                                          set_lvar_bit_on(LVAR_Flags, stole_bit)

import variable new_reno_poor_box_obj;

procedure start begin
   new_reno_poor_box_obj := self_obj;
end

procedure map_enter_p_proc begin
   new_reno_poor_box_obj := self_obj;
   if (is_loading_game == false) then begin
      if (tully_dead == false) then begin

         //added by killap - fix for game_time going negative after 7 years
         if (((game_time) < 0) and (local_var(LVAR_Restock_Time_Fix) == 0)) then begin
            set_local_var(LVAR_Restock_Time, game_time - 1);
            set_local_var(LVAR_Restock_Time_Fix, 1);
         end
         //end added

         if (local_var(LVAR_Restock_Time) < game_time) then begin
            item_caps_adjust(self_obj, random(1,50) - self_caps);
            set_local_var(LVAR_Poor_Box_Caps, self_caps);
            set_local_var(LVAR_Restock_Time, (7 * ONE_GAME_DAY) + game_time);
         end
      end
   end
end

procedure map_update_p_proc begin
   if (local_var(LVAR_Poor_Box_Caps) > self_caps) then begin
      set_local_var(LVAR_Poor_Box_Caps, self_caps);
      set_stole;
   end else if (local_var(LVAR_Poor_Box_Caps) < self_caps) then begin
      set_local_var(LVAR_Poor_Box_Caps, self_caps);
      set_donated;
   end
end