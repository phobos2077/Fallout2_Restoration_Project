/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Mason
        Location: New Reno
        Description:

        Log:

           Created: May 6, 1998

           Updated:
*/

/* Include Files */
#include "..\headers\define.h"
#define NAME                    SCRIPT_NCMASON
#define TOWN_REP_VAR            GVAR_TOWN_REP_NEW_RENO
#include "..\headers\command.h"
#include "..\headers\NewReno.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
procedure Node023;
procedure Node024;
procedure Node026;
procedure Node027;
procedure Node028;
procedure Node029;
procedure Node030;
procedure Node031;
procedure Node032;
procedure Node033;
procedure Node034;
procedure Node035;
procedure Node036;
procedure Node037;
procedure Node038;
procedure Node039;
procedure Node040;
procedure Node041;
procedure Node042;
procedure Node043;
procedure Node044;
procedure Node045;
procedure Node046;
procedure Node047;
procedure Node048;
procedure Node049;
procedure Node050;
procedure Node051;
procedure Node052;
procedure Node053;
procedure Node054;
procedure Node055;
procedure Node056;
procedure Node057;
procedure Node058;
procedure Node059;
procedure Node060;
procedure Node061;
procedure Node062;
procedure Node063;
procedure Node064;

procedure Node065; //added by killap - expansion pack

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */

#define LVAR_Flags                        (0)
#define LVAR_Guard_Counter                (1)
#define LVAR_Patience                     (2)
#define LVAR_Node026                      (3)
#define LVAR_Node034                      (4)
#define LVAR_Home_Tile                    (5)
#define LVAR_Home_Rotation                (6)

#define gave_made_man_goods_bit           bit_10
#define mason_pissed_bit                  bit_11
#define taught_bit                        bit_12
#define first_sight_bit                   bit_13

#define mason_pissed                      lvar_bit(LVAR_Flags, mason_pissed_bit)
#define set_mason_pissed                  set_lvar_bit_on(LVAR_Flags, mason_pissed_bit)
#define off_mason_pissed                  set_lvar_bit_off(LVAR_Flags, mason_pissed_bit)

#define gave_made_man_goods               lvar_bit(LVAR_Flags, gave_made_man_goods_bit)
#define set_gave_made_man_goods           set_lvar_bit_on(LVAR_Flags, gave_made_man_goods_bit)

#define taught                            lvar_bit(LVAR_Flags, taught_bit)
#define set_taught                        set_lvar_bit_on(LVAR_Flags, taught_bit)

#define first_sight                       lvar_bit(LVAR_Flags, first_sight_bit)
#define set_first_sight                   set_lvar_bit_on(LVAR_Flags, first_sight_bit)

#define guard_time_up                     ((game_time > local_var(LVAR_Guard_Counter)) and (local_var(LVAR_Guard_Counter) != 0))
#define guard_time_ok                     ((game_time_hour <= local_var(LVAR_Guard_Counter)) and (local_var(LVAR_Guard_Counter) != 0))
#define set_guard_time                    if (local_var(LVAR_Guard_Counter) != 0) then set_local_var(LVAR_Guard_Counter, game_time_hour + 24)

#define self_family                       family_salvatore

#define dec_patience                      dec_local_var(LVAR_Patience)
#define inc_patience                      inc_local_var(LVAR_Patience)
#define has_patience                      (local_var(LVAR_Patience) > 0)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */

/* Local variables which do not need to be saved between map changes. */
import variable new_reno_mason_obj;
variable warning_result;
variable prev_node;

procedure start begin
   new_reno_mason_obj := self_obj;
end

procedure timed_event_p_proc begin
   if (fixed_param == tamper_door_param) then begin
      if (warned_door) then begin
         set_warning_on(warning_attack);
      end else begin
         wield_best_new_reno_weapon
         set_warning_on(warning_door);
         call Node003;
      end
   end else if (fixed_param == door_use_param) then begin
      call Node004;
   end else if (random(0,1)) then begin
      if (combat_is_initialized == false) then begin
         if (self_can_see_dude) then begin
            if (dude_family == self_family) then begin
               call Node055;
            end else if ((get_track_lloyd(track_lloyd_assigned)) or (get_tribute(tribute_assigned)) or (get_tribute(tribute_refused)) or (get_tribute(tribute_collected))) then begin
               call Node031;
            end else if (true_party_size > 0) then begin
               call Node002;
            end else begin
               call Node001;
            end
         end
      end
   end
   nr_add_timer_event_rand(self_obj, 25, 35, float_param);
end

procedure map_enter_p_proc begin
   new_reno_mason_obj := self_obj;
   if (is_loading_game == false) then begin
      if (map_first_run) then begin
         set_local_var(LVAR_Patience, 5);
         nr_add_timer_event_rand(self_obj, 5, 5 + (self_can_see_dude*20), float_param);
         critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NEW_RENO_SALVATORE);
         critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_GENERIC_GUARDS);
         set_local_var(LVAR_Home_Tile, self_tile);
         set_local_var(LVAR_Home_Rotation, self_cur_rot);
      end
      nr_add_timer_event_rand(self_obj, 25, 35, float_param);
   end
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      if ((hostile) or (warned_attack) or ((salvatore_dead) and (salvatore_assassinated == false)) or ((dude_is_made_man) and (self_family != dude_family))) then begin
         self_attack_dude;
      end else if (first_sight == false) then begin
         set_first_sight;
         Face_Critter(dude_obj, self_obj);
         nr_add_timer_event(self_obj, 0, float_param);
      end
   end else if (salvatore_called_guards) then begin
      if (self_elevation == dude_elevation) then begin
         if (self_is_armed == false) then begin
            wield_best_new_reno_weapon
         end
         Follow_Dude(1,1)
      end
   end else if (self_tile != local_var(LVAR_Home_Tile)) then begin
      animate_move_to_tile(local_var(LVAR_Home_Tile));
   end else if (self_cur_rot != local_var(LVAR_Home_Rotation)) then begin
      animate_rotation(local_var(LVAR_Home_Rotation));
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_personal_enemy;
      attack_family(self_family)
   end
end

procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      call Node998;
   end
end

procedure talk_p_proc begin
   if (salvatore_assassinated) then begin
      call Node060;
   end else if (dude_family == self_family) then begin
      if (gave_made_man_goods) then begin
         call Node055;
      end else begin
         set_gave_made_man_goods;
         start_dialog_at_node(Node053);
      end
   end else if (get_track_lloyd(track_lloyd_mason)) then begin
      start_dialog_at_node(Node017);
   end else if (get_track_lloyd(track_lloyd_assigned)) then begin
      start_dialog_at_node(Node026);
   end else if ((get_track_lloyd(track_lloyd_salvatore)) or (get_tribute(tribute_salvatore))) then begin
      call Node032;
   end else if (get_tribute(tribute_mason)) then begin
      if (renesco_dead) then begin
         start_dialog_at_node(Node061);
      end else begin
         start_dialog_at_node(Node033);
      end
   end else if (get_tribute(tribute_failed)) then begin
      start_dialog_at_node(Node041);
   end else if ((get_tribute(tribute_assigned)) or (get_tribute(tribute_refused)) or (get_tribute(tribute_collected))) then begin
      if (renesco_dead) then begin
         start_dialog_at_node(Node041);
      end else begin
         start_dialog_at_node(Node034);
      end
   end else if (get_guard_assignment(guard_assignment_mason)) then begin
      if (secret_trade_disrupted) then begin
         start_dialog_at_node(Node063);
      end else begin
         start_dialog_at_node(Node044);
      end
   end else if (get_guard_assignment(guard_assignment_success)) then begin
      set_salvatore_permission
      call Node064;
   end else if (get_guard_assignment(guard_assignment_assigned)) then begin
      set_guard_time;
      if (secret_trade_disrupted) then begin
         start_dialog_at_node(Node063);
      end else if (guard_time_ok) then begin
         start_dialog_at_node(Node049);
      end else begin
         start_dialog_at_node(Node050);
      end
   end else if ((get_guard_assignment(guard_assignment_failed)) or (salvatore_done)) then begin
      call Node043;
   end else if (salvatore_permission) then begin
      call Node004;
   end else begin
      start_dialog_at_node(Node005);
   end
   nr_add_timer_event_rand(self_obj, 25, 35, float_param);
   set_herebefore;
   if (get_guard_assignment(guard_assignment_assigned)) then begin
      set_guard_time;
   end
end

procedure destroy_p_proc begin
   inc_evil_critter
   dec_family_counter_amt(self_family, 5);
   new_reno_mason_obj := 0;
end

procedure look_at_p_proc begin
   script_overrides;
   display_mstr(100);
end

procedure description_p_proc begin
   script_overrides;
   display_mstr(150 + (examined));
   set_examined;
end

procedure use_skill_on_p_proc begin
end

procedure Node998 begin
   if (hostile == false) then begin
      set_hostile;
   end
   attack_family(self_family) //added by killap
end
procedure Node999 begin
end

procedure Node990 begin
   dec_patience;
   if (has_patience == false) then begin
      call Node016;
   end else begin
      call Node011;
   end
end
procedure Node989 begin
   dec_patience;
   if (has_patience == false) then begin
      call Node016;
   end else if ((dude_smooth_talker) or (is_success(roll_vs_skill(dude_obj,SKILL_CONVERSANT,0)))) then begin
      call Node009;
   end else begin
      call Node008;
   end
end
procedure Node988 begin
   dec_patience;
   if (has_patience == false) then begin
      call Node016;
   end else begin
      call Node014;
   end
end
procedure Node987 begin
   dec_patience;
   if (has_patience == false) then begin
      call Node016;
   end else begin
      call Node023;
   end
end
procedure Node986 begin
   if (mason_pissed) then begin
      call Node027;
   end
end
procedure Node985 begin
   dec_patience;
   if (has_patience == false) then begin
      call Node016;
   end else if (mason_pissed) then begin
      call Node027;
   end
end
procedure Node984 begin
   dec_patience;
   if (has_patience == false) then begin
      call Node016;
   end else if (mason_pissed) then begin
      call Node035;
   end
end
procedure Node983 begin
   dec_patience;
   if (has_patience == false) then begin
      call Node016;
   end else if (mason_pissed) then begin
      call Node038;
   end
end
procedure Node982 begin
   dec_patience;
   if (has_patience == false) then begin
      call Node016;
   end else if (mason_pissed) then begin
      call Node036;
   end
end
procedure Node981 begin
   set_guard_assignment(guard_assignment_assigned);
   load_map(MAP_NEW_RENO_VB, 0);
end
procedure Node980 begin
   dec_patience;
   if (has_patience == false) then begin
      call Node016;
   end else begin
      call Node013;
   end
end
procedure Node979 begin
   dec_patience;
   if (has_patience == false) then begin
      call Node016;
   end else begin
      call Node015;
   end
end
procedure Node978 begin
   dec_patience;
   if (has_patience == false) then begin
      call Node016;
   end
end

procedure Node001 begin
   floater_bad(200);
end
procedure Node002 begin
   floater_bad(205);
end
procedure Node003 begin
   floater_bad(210);
end
procedure Node004 begin
   floater(215);
end
procedure Node005 begin
   if (dude_is_pornstar) then begin
      Reply(221);
   end else if (dude_is_prizefighter) then begin
      Reply(222);
   end else begin
      Reply(220);
   end
   BOption(223, Node006, -003);
   BOption(224, Node006, -003);
   NOption(225, Node999, -003);
   BOption(226 + salvatore_known, Node007, 004);
   if ((know_lasers) and (get_salvatore_pistol(salvatore_pistol_none))) then begin
      BOption(228, Node990, 004);
   end
   if (know_scott) then begin
      BOption(229, Node062, 004);
   end
   NOption(230, Node999, 004);
   prev_node := 5;
end
procedure Node006 begin
   Reply(240);
   BOption(241, Node012, -003);
   NOption(225, Node999, -003);
   prev_node := 6;
end
procedure Node007 begin
   Reply(250 + (salvatore_known == false));
   set_salvatore_known
   BOption(252, Node010, 004);
   if ((dude_charisma > 5) or (dude_iq > 5) or (has_skill(dude_obj, SKILL_CONVERSANT) > 75) or (dude_smooth_talker)) then begin
      BOption(253, Node989, 004);
   end
   if ((know_lasers) and (get_salvatore_pistol(salvatore_pistol_none))) then begin
      BOption(254, Node990, 004);
   end
   if (know_scott) then begin
      BOption(229, Node062, 004);
   end
   NOption(230, Node999, 004);
   prev_node := 7;
end
procedure Node008 begin
   Reply(mstr(265) + mstr(266 + dude_is_female));
   BOption(268, Node012, 004);
   BOption(230, Node012, 004);
   NOption(270, Node999, 004);
   prev_node := 8;
end
procedure Node009 begin
   Reply(mstr(275) + mstr(276 + dude_is_female));
   NOption(278, Node056, 004);
   BOption(279, Node998, 004); //edit by killap: was NOption
   prev_node := 9;
end
procedure Node010 begin
   Reply(285);
   BOption(268, Node012, 004);
   if ((know_lasers) and (get_salvatore_pistol(salvatore_pistol_none))) then begin
      BOption(254, Node990, 004);
   end
   if (know_scott) then begin
      BOption(229, Node062, 004);
   end
   NOption(230, Node999, 004);
   prev_node := 10;
end
procedure Node011 begin
   Reply(295);
   BOption(226 + salvatore_known, Node007, 004);
   NOption(298, Node999, 004);
   NOption(299, Node999, 004);
   prev_node := 11;
end
procedure Node012 begin
   set_salvatore_permission
   Reply(mstr(305) + dude_name + mstr(1305));
   BOption(306, Node998, 004);
   BOption(307, Node980, 004);
   NOption(308, Node013, 004);
   prev_node := 12;
end
procedure Node013 begin
   Reply(mstr(315 + dude_is_female) + mstr(317));
   NOption(318, Node014, 004);
   BOption(319, Node988, 004);
   prev_node := 13;
end
procedure Node014 begin
   Reply(325);
   NOption(318, Node999, 004);
   BOption(327, Node978, 004);
   prev_node := 14;
end
procedure Node015 begin
   Reply(335);
   NOption(336, Node999, 004);
   BOption(337, Node979, 004);
   prev_node := 15;
end
procedure Node016 begin
   Reply(345);
   BOption(346, Node998, -003);
   BOption(347, Node998, 004);
   BOption(348, Node998, 004);
   prev_node := 16;
end
procedure Node017 begin
   set_track_lloyd(track_lloyd_assigned);
   off_salvatore_permission
   if (dude_is_pornstar == false) then begin
      Reply(mstr(355 + dude_is_female) + mstr(358));
   end else begin
      Reply(mstr(357) + mstr(358));
   end
   if (lloyd_dead) then //added by killap
      NOption(366, Node030, 004);
   NOption(359, Node019, 004);
   NOption(360, Node020, 004);
   NOption(361, Node022, 004);
   BOption(362, Node987, 004);
   NOption(364, Node999, 004);
   NOption(367, Node065, 004); //added by killap - expansion pack
   BOption(365, Node978, 004);
   prev_node := 17;
end
procedure Node018 begin
   Reply(375);
   if (lloyd_dead) then //added by killap
      NOption(366, Node030, 004);
   NOption(359, Node019, 004);
   NOption(360, Node020, 004);
   NOption(361, Node022, 004);
   BOption(362, Node023, 004);
   NOption(367, Node065, 004); //added by killap - expansion pack
   NOption(381, Node986, 004);
   BOption(382, Node985, 004);
   prev_node := 18;
end
procedure Node019 begin
   Reply(mstr(390 + dude_is_female) + mstr(392));
   NOption(393, Node018, 004);
   NOption(381, Node986, 004);
   BOption(382, Node985, 004);
   prev_node := 19;
end
procedure Node020 begin
   Reply(400);
   NOption(401, Node021, 008);
   NOption(393, Node018, 004);
   BOption(382, Node985, 004);
   NOption(381, Node986, 004);
   prev_node := 20;
end
procedure Node021 begin
   inc_patience;
   Reply(410);
   NOption(393, Node018, 004);
   BOption(382, Node985, 004);
   NOption(381, Node986, 004);
   prev_node := 21;
end
procedure Node022 begin
   set_thousand_caps;
   Reply(420);
   NOption(393, Node018, 004);
   BOption(382, Node985, 004);
   NOption(381, Node986, 004);
   prev_node := 22;
end
procedure Node023 begin
   Reply(430);
   BOption(431, Node057, 004);
   NOption(432, Node057, 004);
   NOption(433, Node057, 004);
   prev_node := 23;
end
procedure Node024 begin
   Reply(440);
   NOption(393, Node018, 004);
   BOption(442, Node986, 004);
   NOption(381, Node986, 004);
   prev_node := 24;
end
procedure Node026 begin
   set_mason_pissed;
   Reply(465 + local_var(LVAR_Node026));
//   inc_local_var(LVAR_Node026);
   if (lloyd_dead) then //added by killap
      NOption(470, Node030, 004);
   BOption(472, Node029, 004);
   if (local_var(LVAR_Node026) < 2) then begin //killap's fix: was < 3
      inc_local_var(LVAR_Node026); //moved by killap
      NOption(468, Node018, 004);
      BOption(473, Node027, 004);
   end else begin
      BOption(468, Node028, 004);
      BOption(473, Node028, 004);
   end
   prev_node := 26;
end
procedure Node027 begin
   Reply(480);
   NOption(481, Node999, 004);
   BOption(482, Node016, 004);
   prev_node := 27;
end
procedure Node028 begin
   Reply(490);
   BOption(491, Node998, 004);
   BOption(492, Node998, 004);
   BOption(493, Node998, 004);
   prev_node := 28;
end
procedure Node029 begin
   Reply(mstr(500) + mstr(501 + dude_is_female) + mstr(503));
   BOption(491, Node998, 004);
   BOption(492, Node998, 004);
   BOption(506, Node998, 004);
   prev_node := 29;
end
procedure Node030 begin
   set_track_lloyd(track_lloyd_salvatore);
   off_mason_pissed;
   set_salvatore_permission
   Reply(514 + (prev_node == 26));
   BOption(516, Node978, 004);
   BOption(517, Node999, 004);
   prev_node := 30;
end
procedure Node031 begin
   floater_bad(520);
end
procedure Node032 begin
   set_status_special(GVAR_NEW_RENO_SALVATORE)
   floater(525);
end
procedure Node033 begin
   set_tribute(tribute_assigned);
   off_salvatore_permission
   Reply(530);
   NOption(531, Node058, 004);
   prev_node := 33;
end
procedure Node034 begin
   set_mason_pissed;
   Reply(535 + local_var(LVAR_Node034));
//   inc_local_var(LVAR_Node034);
   if (dude_caps >= 1000) then begin
      NOption(542, Node042, 004);
   end else begin
      BOption(543, Node037, 004);
   end
   if (local_var(LVAR_Node034) < 2) then begin  //killap's fix: was < 3
      inc_local_var(LVAR_Node034);  //moved by killap
      BOption(539, Node984, 004);
      if (get_tribute(tribute_refused)) then begin
         BOption(540, Node983, 004);
      end
      BOption(544, Node982, 004);
   end else begin
      BOption(539, Node037, 004);
      if (get_tribute(tribute_refused)) then begin
         BOption(540, Node037, 004);
      end
      BOption(544, Node037, 004);
   end
   BOption(546, Node998, 004);
   prev_node := 34;
end
procedure Node035 begin
   Reply(555);
   BOption(556, Node982, 004);
   NOption(381, Node036, 004);
   prev_node := 35;
end
procedure Node036 begin
   Reply(565);
   NOption(481, Node999, 004);
   BOption(482, Node016, 004);
   prev_node := 36;
end
procedure Node037 begin
   Reply(575);
   BOption(491, Node998, 004);
   BOption(492, Node998, 004);
   BOption(493, Node998, 004);
   prev_node := 37;
end
procedure Node038 begin
   Reply(585);
   BOption(586, Node039, 004);
   BOption(587, Node040, 004);
   BOption(544, Node036, 004);
   BOption(546, Node998, 004);
   prev_node := 38;
end
procedure Node039 begin
   Reply(595);
   BOption(596, Node040, 004);
   BOption(587, Node040, 004);
   BOption(598, Node036, 004);
   BOption(546, Node998, 004);
   prev_node := 39;
end
procedure Node040 begin
   Reply(605);
   NOption(606, Node059, 004);
   prev_node := 40;
end
procedure Node041 begin
   Reply(mstr(615) + mstr(617));
   BOption(618, Node998, 004);
   BOption(619, Node998, 004);
   prev_node := 41;
end
procedure Node042 begin
   set_salvatore_permission
   set_tribute(tribute_salvatore);
   Reply(625);
   BOption(516, Node978, 004);
   NOption(517, Node999, 004);
   prev_node := 42;
end
procedure Node043 begin
   floater_bad(635);
end
procedure Node044 begin
   off_salvatore_permission
   set_guard_assignment(guard_assignment_assigned);
   Reply(640);
   if (get_salvatore_pistol(salvatore_pistol_mason)) then begin
      BOption(642, Node045, 004);
   end
   BOption(643, Node046, 004);
   NOption(644, Node981, 004);
   BOption(645, Node048, 004);
   prev_node := 44;
end
procedure Node045 begin
   set_salvatore_pistol(salvatore_pistol_received);
   give_pid_qty(dude_obj, PID_LASER_PISTOL, 1)
   Reply(655);
   if ( (taught == false) and (has_skill(dude_obj, SKILL_ENERGY_WEAPONS) <= 295) ) then begin //added (has_skill(dude_obj, SKILL_ENERGY_WEAPONS) <= 295) - killap
      BOption(656, Node052, 004);
   end
   BOption(643, Node046, 004);
   NOption(658, Node047, 004);
   BOption(645, Node048, 004);
   prev_node := 45;
end
procedure Node046 begin
   Reply(665);
   if (get_salvatore_pistol(salvatore_pistol_mason)) then begin
      BOption(642, Node045, 004);
   end
   NOption(644, Node047, 004);
   BOption(645, Node048, 004);
   prev_node := 46;
end
procedure Node047 begin
   Reply(675);
   NOption(676, Node981, 001);
   prev_node := 47;
end
procedure Node048 begin
   //   set_guard_time; // Commented (c) Avega
   set_local_var(LVAR_Guard_Counter, game_time_hour + 24); //(c) Avega 
   Reply(685);
   NOption(686, Node999, 004);
   prev_node := 48;
end
procedure Node049 begin
   Reply(695);
   BOption(696, Node046, 004);
   NOption(697, Node047, 004);
   BOption(698, Node048, 004);
   if (get_salvatore_pistol(salvatore_pistol_mason)) then begin
      BOption(642, Node045, 004);
   end else if ((get_salvatore_pistol(salvatore_pistol_received)) and (obj_is_carrying_obj_pid(dude_obj, PID_LASER_PISTOL)>1) and (taught == false) and (has_skill(dude_obj, SKILL_ENERGY_WEAPONS) <= 295) ) then begin //added (has_skill(dude_obj, SKILL_ENERGY_WEAPONS) <= 295) - killap
      BOption(699, Node052, 004);
   end
   prev_node := 49;
end
procedure Node050 begin
   Reply(705);
   BOption(706, Node998, 004);
   BOption(707, Node998, 004);
   prev_node := 50;
end
procedure Node051 begin
   Reply(715);
   BOption(696, Node046, 004);
   NOption(697, Node047, 004);
   if ((get_salvatore_pistol(salvatore_pistol_received)) and (obj_is_carrying_obj_pid(dude_obj, PID_LASER_PISTOL)>1) and (taught == false) and (has_skill(dude_obj, SKILL_ENERGY_WEAPONS) <= 295) ) then begin //added (has_skill(dude_obj, SKILL_ENERGY_WEAPONS) <= 295) - killap
      BOption(699, Node052, 004);
   end
   BOption(698, Node048, 004);
   prev_node := 51;
end
procedure Node052 begin
   gfade_out(5);
   Reply(725);
   set_taught;
   game_time_advance(ONE_GAME_HOUR);
   critter_mod_skill(dude_obj, SKILL_ENERGY_WEAPONS, 5);
   gfade_in(5);
   BOption(726, Node051, 006);
   BOption(643, Node046, 004);
   NOption(728, Node047, 004);
   NOption(729, Node048, 004);
   prev_node := 52;
end
procedure Node053 begin
   set_gave_made_man_goods;
   if (get_salvatore_pistol(salvatore_pistol_received) == false) then begin
      give_pid_qty(dude_obj, PID_LASER_PISTOL, 1)
   end
   if (salvatore_respect >= 15) then begin //edit by killap - was only possible to get to 15 without any dialogue exploiting
      if (local_var(LVAR_Patience) > 4) then begin
         give_pid_qty(dude_obj, PID_METAL_ARMOR_MK_II, 1)
         give_pid_qty(dude_obj, PID_SMALL_ENERGY_CELL, 2)
         item_caps_adjust(dude_obj, 300);
      end else begin
         give_pid_qty(dude_obj, PID_METAL_ARMOR, 1)
         item_caps_adjust(dude_obj, 150);
      end
   end else begin
      item_caps_adjust(dude_obj, 50);
   end
   Reply(mstr(735) + made_man_name + mstr(5735));
   NOption(736, Node054, 001);
   prev_node := 53;
end
procedure Node054 begin
   Reply(745);
   NOption(746, Node999, 004);
   NOption(747 + dude_is_female, Node999, 004);
   prev_node := 54;
end
procedure Node055 begin
   floater(755 + dude_is_female);
end
procedure Node056 begin
   set_salvatore_permission
   Reply(mstr(765) + dude_name + mstr(5765));
   NOption(766, Node980, 004);
   NOption(440, Node013, 004);
   prev_node := 56;
end
procedure Node057 begin
   Reply(775);
   BOption(776, Node016, 004);
   NOption(777, Node024, 004);
   BOption(778, Node986, 004);
   prev_node := 57;
end
procedure Node058 begin
   Reply(785);
   BOption(786, Node978, 004);
   NOption(381, Node999, 004);
   prev_node := 58;
end
procedure Node059 begin
   Reply(795 + dude_is_female);
   BOption(797, Node982, 004);
   NOption(798, Node036, 004);
   BOption(799, Node998, 004);
   prev_node := 59;
end
procedure Node060 begin
   floater_rand(805, 807);
end
procedure Node061 begin
   Reply(815);
   BOption(491, Node998, 004);
   BOption(492, Node998, 004);
   BOption(818, Node998, 004);
   prev_node := 61;
end
procedure Node062 begin
   Reply(825);
   BOption(826, Node998, 004);
   prev_node := 62;
end
procedure Node063 begin
   floater_bad(835);
end
procedure Node064 begin
   floater(840);
end

//added by killap - expansion pack
procedure Node065 begin
   Reply(900);
   NOption(901, Node018, 004);
   set_nikki_lloyd;
end
//end added