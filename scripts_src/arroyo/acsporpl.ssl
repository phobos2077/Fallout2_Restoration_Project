/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Arroyo spore plant.
        Location: Arroyo
        Description: The evil spore plant that the shamen sends the player after.  Killing one of these
                     plants should increment the map variable MVAR_Kill_Evil_Plants.  When this MVAR == 2
                     the player has killed both plants.  Killing both of these plants will give the player
                     some experience defined in EXP_KILL_EVIL_PLANTS as well as a reputation bonus
                     defined in REP_BONUS_KILL_EVIL_PLANTS.  It also sets the GVAR_KILL_EVIL_PLANTS so that
                     other scripts can see if our hero has killed the plants.


           Created: December 02, 1997

           Updated:
*/

/* Include Files */

#define SCRIPT_REALNAME "acsporpl"
#include "../headers/define.h"
#include "../headers/arvillag.h"

#define NAME                    SCRIPT_ACSPORPL

#include "../headers/command.h"
//#include "../headers/modreact.h"

#define KILLED_ALL_EVIL_PLANTS (map_var(MVAR_Kill_Evil_Plants) == 2)

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Local variables which do not need to be saved between map changes. */
variable pissed:=false;


procedure start begin
end

procedure critter_p_proc begin
   if ((self_can_see_dude) and (pissed == false)) then begin
      pissed := true;
      attack(dude_obj);
   end else
      pissed := false;
end

procedure destroy_p_proc begin
   inc_map_var(MVAR_Kill_Evil_Plants);
   if (KILLED_ALL_EVIL_PLANTS) then begin
      display_msg(mstr(150));
      set_global_var(GVAR_KILL_EVIL_PLANTS,QUEST_COMPLETED);
      inc_general_rep(REP_BONUS_KILL_EVIL_PLANTS);
      give_xp(EXP_KILL_EVIL_PLANTS);
//      terminate_combat;
   end
end

procedure damage_p_proc begin
   if (pissed == false) then begin
      pissed := true;
      attack(dude_obj);
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

//added by killap - expansion pack
procedure use_obj_on_p_proc begin
   if ((obj_pid(obj_being_used_with) == PID_PLANT_SPRAY)) then begin
      inc_map_var(MVAR_Kill_Evil_Plants);
      if (KILLED_ALL_EVIL_PLANTS) then begin
         display_msg(mstr(150));
         set_global_var(GVAR_KILL_EVIL_PLANTS,QUEST_COMPLETED);
         inc_general_rep(REP_BONUS_KILL_EVIL_PLANTS);
         give_xp(EXP_KILL_EVIL_PLANTS);
      end
      kill_critter(self_obj, ANIM_melted_to_nothing_sf);
   end
end
//end added by killap

procedure use_skill_on_p_proc begin
   if (action_being_used == SKILL_SCIENCE) then begin
      script_overrides;
      if (is_success(roll_vs_skill(dude_obj,action_being_used,0))) then
         display_msg(mstr(102));
      else
         display_msg(mstr(103));
   end else if (action_being_used == SKILL_STEAL) then
      script_overrides;
end

procedure map_enter_p_proc begin
   set_self_team(TEAM_SPORE_PLANT);
   set_self_ai(AI_SPORE_PLANT);
end
