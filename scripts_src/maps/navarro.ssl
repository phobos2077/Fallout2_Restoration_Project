/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: NAVARRO
        Location: NAVARRO
        Description: This is the Map Script for Navarro.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: December 04, 1997

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "../headers/define.h"
#include "../headers/navarro.h"
#include "../headers/updatmap.h"

#define NAME                    SCRIPT_NAVARRO

#include "../headers/command.h"

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;

export variable quincy_ptr;
export variable raul_ptr;
export variable commander_ptr;
export variable chris_ptr;
export variable Commander_Locker_Ptr;
export variable doctor_ptr;
export variable xarn_ptr;
export variable air_guard_obj; //added by killap - expansion
export variable guard1_obj; //added by killap - expansion
export variable guard2_obj; //added by killap - expansion
export variable guard3_obj; //added by killap - expansion

variable levtile;
variable lev;

procedure start begin
end

procedure map_enter_p_proc begin

   if (global_var(GVAR_LOAD_MAP_INDEX) == 10) then begin
      override_map_start_hex( 19517, 2, 2 );
      gfade_in(600);
   end else begin
      override_map_start_hex( 28894, 0, 0 ); //edit by killap - was 28695
   end

   if (global_var(GVAR_CAME_ON_VERTIBIRD) == 1) then begin
      game_ui_disable;
      override_map_start_hex( 19519, 2, 0 );
   end

   if ( (map_first_run) and (global_var(GVAR_LOAD_MAP_INDEX) != 10) ) then begin
      if (global_var(GVAR_CAME_ON_VERTIBIRD) == 1) then begin
         display_msg(mstr(101));
      end else begin
         display_msg(mstr(100));
      end
   end

   // Begin Seraph's Code - expansion
   if (map_var(MVAR_Doctor_Dead) == 1 and global_var(GVAR_SCHREBER_DEAD) > 1) then begin
      set_global_var(GVAR_SCHREBER_DEAD,1);
   end
   // End Seraph's Code


   //added by killap - prevent car from disappearing if the car runs out of gas while on the town circle
   if ((metarule(30, 0) == 21) and (cur_map_index != MAP_CAR_DESERT) and (car_out_of_fuel)) then begin
      set_global_var(GVAR_CAR_PLACED_TILE, 0);
      Create_Car(CAR_NAVARRO_HEX,CAR_NAVARRO_ELEV)
      if (tile_contains_pid_obj(CAR_NAVARRO_HEX, CAR_NAVARRO_ELEV, PID_DRIVABLE_CAR) != 0) then begin
         Create_Trunk(CAR_NAVARRO_HEX,CAR_NAVARRO_ELEV)
      end
   end else
      Check_Create_Car(CAR_NAVARRO_HEX, CAR_NAVARRO_ELEV)
   //end added

   //Check_Create_Car(CAR_NAVARRO_HEX, CAR_NAVARRO_ELEV)

   if (elevation(dude_obj) == 1) then begin
      Full_Light;
   end
   else begin
      Lighting;
   end

   set_global_var(GVAR_LOAD_MAP_INDEX,0);
end

procedure map_update_p_proc begin
   if ( (map_var(MVAR_Xarn_In_Party) == 1) and (elevation(xarn_ptr) != elevation(dude_obj)) ) then begin
      levtile:=tile_num_in_direction(tile_num(dude_obj),random(0,5),1);
      lev:=elevation(dude_obj);
      move_to(xarn_ptr,levtile,lev);
      critter_attempt_placement(xarn_ptr,levtile,lev);
   end
   if (elevation(dude_obj) == 1) then begin
      Full_Light;
   end
   else begin
      Lighting;
   end
end