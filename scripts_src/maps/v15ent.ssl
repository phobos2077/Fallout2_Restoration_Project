/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Map Script for Sierra Army Base, Outside area
*/

#define SCRIPT_REALNAME "v15ent"
#include "../headers/define.h"
#include "../headers/updatmap.h"
#include "../headers/v15ent.h"

#define NAME                    SCRIPT_V15ENT

#include "../headers/command.h"

#define CHECK_DUDE_V15ENT_LEVEL  (2)


#define place_on_inn(the_tile, the_elev, the_rot)                                \
         override_map_start_hex(the_tile, the_elev, the_rot);                    \
         set_map_start((the_tile % 200), (the_tile / 200), the_elev, the_rot)


export variable i_girl_obj;
variable checkLevel;

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;

procedure start begin
   i_girl_obj := -1;
end

//added by killap - expansion
procedure map_exit_p_proc begin
   variable Critter;
   if (global_var(GVAR_V15_DOC_FREE) == 1) then begin
      Critter:=create_object_sid(PID_DOC,19102,0 + (global_var(GVAR_V15_NCR_DEAL) == 2),SCRIPT_BCJONES);
      obj_rotate(Critter, 3);
      set_global_var(GVAR_V15_DOC_FREE,2);
   end
end
//end added

procedure map_enter_p_proc begin
   variable elv; //added by killap
   Lighting;
   ndebug("V15SENT: Darion_deal " + global_var(GVAR_V15_NCR_DEAL) + " diaron_dead " + global_var(GVAR_NCR_V15_DARION_DEAD));

   //added by killap
   if (map_first_run) then begin
      display_msg(mstr(100));
   end

   if (global_var(GVAR_V15_NCR_DEAL) == 2 and global_var(GVAR_NCR_V15_DARION_DEAD)) then
     elv := 1;
   else
     elv := 0;
   //end added by killap

   if (global_var(GVAR_V15_NCR_DEAL) == 2 and global_var(GVAR_NCR_V15_DARION_DEAD)) then begin
      place_on_inn(29525, elv, 0);
   end else begin
      override_map_start_hex(29525, elv, 0);
   end

   //added by killap
   if (global_var(GVAR_LOAD_MAP_INDEX) == 1) then begin
      override_map_start_hex(20276,elv,4);
   end else if (global_var(GVAR_LOAD_MAP_INDEX) == 2) then begin
      override_map_start_hex(18884,elv,4);
   end
   //end added by killap

   if (global_var(GVAR_V15_NCR_DEAL) == 2 and global_var(GVAR_NCR_V15_DARION_DEAD)) then begin
      if (global_var(GVAR_CAR_PLACED_TILE) == CAR_VAULT_15_HEX) then begin
         set_global_var(GVAR_CAR_PLACED_TILE, CAR_VAULT_15_DEAL_HEX);
      end

      if ((car_current_town == 21) and (cur_map_index != MAP_CAR_DESERT) and (car_out_of_fuel)) then begin
         set_global_var(GVAR_CAR_PLACED_TILE, 0);
         Create_Car(CAR_VAULT_15_DEAL_HEX,CAR_VAULT_15_DEAL_ELEV)
         if (tile_contains_pid_obj(CAR_VAULT_15_DEAL_HEX, CAR_VAULT_15_DEAL_ELEV, PID_DRIVABLE_CAR) != 0) then begin
            Create_Trunk(CAR_VAULT_15_DEAL_HEX,CAR_VAULT_15_DEAL_ELEV)
         end
      end else
         Check_Create_Car(CAR_VAULT_15_DEAL_HEX, CAR_VAULT_15_DEAL_ELEV)
   end else begin

      if ((car_current_town == 21) and (cur_map_index != MAP_CAR_DESERT) and (car_out_of_fuel)) then begin
         set_global_var(GVAR_CAR_PLACED_TILE, 0);
         Create_Car(CAR_VAULT_15_HEX,CAR_VAULT_15_ELEV)
         if (tile_contains_pid_obj(CAR_VAULT_15_HEX, CAR_VAULT_15_ELEV, PID_DRIVABLE_CAR) != 0) then begin
            Create_Trunk(CAR_VAULT_15_HEX,CAR_VAULT_15_ELEV)
         end
      end else
         Check_Create_Car(CAR_VAULT_15_HEX, CAR_VAULT_15_ELEV)
   end
   set_global_var(GVAR_LOAD_MAP_INDEX,0); //added by killap
end

procedure map_update_p_proc begin
   ndebug("V15ENT: cur_map_index: " + cur_map_index);
   Lighting;
end
