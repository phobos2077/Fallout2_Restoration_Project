/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/******************************************************************
   Filename: iiMine.ssl

   Purpose: This is for the mines in the Raiders Cave.

   Created: December 18, 1997   RWH2

   Updated:
        April 14, 1998          rwh2
                Made the template more generic for any weapon to be
                fired from any floor plate. All defines at the top
                are the only things that will need to be changed
                from now on
******************************************************************/


/* Include Files */
#define SCRIPT_REALNAME "iipit"
#include "../headers/define.h"

#define NAME                    SCRIPT_IIPIT
#define CUR_SCRIPT              SCRIPT_IIPIT

#include "../headers/command.h"
#include "../headers/newreno.h"

#define PID_BROKEN_FLOOR                    (33556185)

#define TRAP_BONUS                      (-25)
#define PERCEPTION_BONUS                (-4)
#define DIST_BONUS                      (2)

#define MIN_DMG      (2)
#define MAX_DMG      (10)

#define ART_TRAP_VISIBLE                (PID_BROKEN_FLOOR)
#define ART_TRAP_DISARMED               (PID_BROKEN_FLOOR)

procedure start;
procedure spatial_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure DumpPoorBastard;

#define LVAR_Trap_Found                 (0)
#define LVAR_Gave_Exp                   (1)
#define LVAR_Got_Perception_Roll        (2)
#define LVAR_Extended_Look              (3)

procedure start begin
end

procedure spatial_p_proc begin
   variable Range;
   variable Range_Mod;
   variable Detection_Roll;
   variable Trap;

    if (obj_type(source_obj) == 1) then begin
   //if ( source_obj == dude_obj ) then begin

       Range:=get_perception(source_obj) + PERCEPTION_BONUS;
       Range_Mod:=Range - (tile_distance_objs(self_obj,source_obj)*DIST_BONUS);

       if ((tile_distance_objs(self_obj,source_obj) <= Range) and
           (local_var(LVAR_Trap_Found) == 0)) then begin

           if (Range_Mod > PERCEPTION_BONUS) then
               Range_Mod:= PERCEPTION_BONUS;

           Detection_Roll:=do_check(source_obj,STAT_pe,Range_Mod);

           if (is_success(Detection_Roll)) then begin

               set_local_var(LVAR_Trap_Found,1);
               reg_anim_clear(source_obj);
               Trap:=create_object_sid(ART_TRAP_VISIBLE,self_tile,self_elevation,CUR_SCRIPT);

               if (is_critical(Detection_Roll)) then begin
                   if (source_obj == dude_obj) then
                       display_msg(mstr(101));
                   else if ((proto_data(obj_pid(source_obj),cr_body_type) == CR_BODY_BIPED) and (obj_can_see_obj(source_obj,dude_obj))) then
                       display_msg(obj_name(source_obj)+mstr(102));
               end

               else begin
                   if (source_obj == dude_obj) then
                       display_msg(mstr(101));
                   else if ((proto_data(obj_pid(source_obj),cr_body_type) == CR_BODY_BIPED) and (obj_can_see_obj(source_obj,dude_obj))) then
                       display_msg(obj_name(source_obj)+mstr(102));
               end
           end
       end

       if (tile_distance_objs(self_obj,source_obj) <= 1) then begin

           game_ui_disable;

           ndebug("Set off the Trap!");
           //Projectile:=create_object(PROJECTILE_WEAPON,INITIAL_TILE,elevation(self_obj));
           //Face_Critter(source_obj,Projectile);
           if (tile_contains_obj_pid(self_tile,self_elevation,ART_TRAP_VISIBLE)) then begin
               ndebug("Destroying Visible Trap");
               destroy_object(tile_contains_pid_obj(self_tile,self_elevation,ART_TRAP_VISIBLE));
               ndebug("Destroyed Visible Trap");
           end
           Trap:=create_object(ART_TRAP_VISIBLE,self_tile,self_elevation);

            display_msg( message_str( SCRIPT_IIPIT, 100 ) );
           critter_dmg(source_obj,random(MIN_DMG,MAX_DMG),(DMG_normal_dam BWOR DMG_BYPASS_ARMOR BWOR DMG_NOANIMATE));
           set_local_var(LVAR_Trap_Found,1);
            // Dump player in pits
            call DumpPoorBastard;
       end
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(103));
   ndebug("Looking At the Trap");
end

procedure description_p_proc begin
   variable Perception_Roll;

   script_overrides;

   if (local_var(LVAR_Got_Perception_Roll) == 0) then begin
       set_local_var(LVAR_Got_Perception_Roll,1);
       Perception_Roll:=do_check(dude_obj,STAT_pe,PERCEPTION_BONUS);
       set_local_var(LVAR_Extended_Look,Perception_Roll);
   end

   if (is_critical(local_var(LVAR_Extended_Look))) then begin
       if (is_success(local_var(LVAR_Extended_Look))) then
           display_msg(mstr(101));
       else
           display_msg(mstr(101));
   end
   else begin
       display_msg(mstr(101));
   end
end

procedure DumpPoorBastard begin
   variable PoorBastard;

   gfade_out( 10 );
   if( self_tile == 15692) then begin
      ndebug("attempt_pit_to_tile(" + 15692 + ", " + 15091 + ") passed initial check");
      PoorBastard := tile_get_next_critter( 15091, 1, 0 );
      if( PoorBastard > 0 ) then
         critter_attempt_placement( PoorBastard, 0, 1 );
      move_to( dude_obj, 15091, 1 );
      if( PoorBastard > 0 ) then
         critter_attempt_placement( PoorBastard, 15091, 1 );
   end
   else if( self_tile == 20504) then begin
      ndebug("attempt_pit_to_tile(" + 20504 + ", " + 19299 + ") passed initial check");
      PoorBastard := tile_get_next_critter( 19299, 1, 0 );
      if( PoorBastard > 0 ) then
         critter_attempt_placement( PoorBastard, 0, 1 );
      move_to( dude_obj, 19299, 1 );
      if( PoorBastard > 0 ) then
         critter_attempt_placement( PoorBastard, 19299, 1 );
   end
   else if( self_tile == 19084) then begin
      ndebug("attempt_pit_to_tile(" + 19084 + ", " + 20291 + ") passed initial check");
      PoorBastard := tile_get_next_critter( 20291, 1, 0 );
      if( PoorBastard > 0 ) then
         critter_attempt_placement( PoorBastard, 0, 1 );
      move_to( dude_obj, 20291, 1 );
      if( PoorBastard > 0 ) then
         critter_attempt_placement( PoorBastard, 20291, 1 );
   end
   else if( self_tile == 22879) then begin
      ndebug("attempt_pit_to_tile(" + 22879 + ", " + 23904 + ") passed initial check");
      PoorBastard := tile_get_next_critter( 23904, 1, 0 );
      if( PoorBastard > 0 ) then
         critter_attempt_placement( PoorBastard, 0, 1 );
      move_to( dude_obj, 23904, 1 );
      if( PoorBastard > 0 ) then
         critter_attempt_placement( PoorBastard, 23904, 1 );
   end
   gfade_in( 10 );
end
