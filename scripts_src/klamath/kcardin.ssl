/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Ardin Buckner
        Location: Klamath
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: Febuary 2, 1998

           Updated: April 27, 1998: Ardin no longer shares shop inventory with Maida
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "kcardin"
#include "../headers/define.h"
#include "../headers/klamath.h" //added by killap
#include "../headers/kladwtn.h"

#define NAME                    SCRIPT_KCARDIN
#define TOWN_REP_VAR            (GVAR_TOWN_REP_KLAMATH)
#define ROOM_ONE                21679
#define ROOM_TWO                21686

#include "../headers/command.h"
#include "../headers/modreact.h"

#define Vault_Known             (1)     // Temp Until get command from Jesse
#define Visited_City            (1)     // Temp Until get command from Jesse

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure push_p_proc;

/* Script Specific Procedure Calls */
procedure Node910;
procedure Node920;
procedure Node930;
procedure Node935;
procedure Node940;
procedure Node950;
procedure Node960;
procedure Node970;
procedure Node975;
procedure Node980;
procedure Node995;
procedure Node996;
procedure Node997;
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node013;
procedure Node13a;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node022;
procedure Node023;
procedure Node024;
procedure Node025;
procedure Node026;
procedure Node027;
procedure Node028;
procedure Node029;
procedure Node030;
procedure Node038;
procedure Node039;
procedure Node040;
procedure Node041;
procedure Node042;
procedure Node043;
procedure Node044;
procedure Node52a;
procedure Node52b;
procedure Node053;
procedure Node054;
procedure Node055;
//added by killap - expansion pack
procedure Node032;
procedure Node033;
procedure Node034;
procedure Node035;

procedure Node033a;
procedure Node007a;
procedure Node999a;

//end added

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
//#define LVAR_Torr_Search_Accept         (7)
//#define LVAR_Trapper_Quest_Accept       (8)
#define LVAR_torr_reward_given          (7)
#define LVAR_05_before                  (8)
#define LVAR_17_before                  (9)
#define LVAR_quest_smiley               (10) //need these because the globals to keep
#define LVAR_quest_torr                 (11) //track of quest accept change if they die
#define LVAR_Ardin_Angry                (12)
#define LVAR_Discovered_Rustlers        (13) //added by killap - expansion

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable item;
variable ardin_weapon;
variable k;

//import variable klam_bucknr_box_obj;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   ardin_weapon:=PID_KNIFE;
   set_self_team(TEAM_KLAMATH);
   set_self_ai(AI_STORE_OWNER);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

procedure combat_p_proc begin
   if (fixed_param == 4) then begin //4 is combat_sequencing_turn
      if (self_item_count(ardin_weapon) == 0) then begin
         item := create_object(ardin_weapon,0,0);
         add_obj_to_inven(self_obj, item);
      end
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_global_var(GVAR_ENEMY_KLAMATH,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   script_overrides;
   GetReaction;

   if ( (REP_ENEMY_KLAMATH) or (local_var(LVAR_Personal_Enemy) == 1) ) then begin
       call Node998;
   end
   else if (local_var(LVAR_Ardin_Angry) == 1) then begin
      call Node055;
   end
   else begin //(1)
      //move_obj_inven_to_obj(klam_bucknr_box_obj,self_obj);
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;

      if dude_low_iq then begin
         if (local_var(LVAR_quest_smiley) == 0) then
            call Node005;
         else
            call Node004; //was Node003 -edit by killap --RP;
      end else if (global_var(GVAR_TORR_HARMED) == 1) then begin
         call Node002;
      end else if ( (global_var(GVAR_TORR_MISSING) == 1) and (local_var(LVAR_17_before) == 0) ) then begin
         call Node017;
      end else if ( (global_var(GVAR_QUEST_RESCUE_TORR) == TORR_RESCUE_SUCCESS) and (local_var(LVAR_quest_torr) == 1 ) ) then begin
         call Node52a;
      end else if ( (global_var(GVAR_SMILEY_STATUS) == SMILEY_IS_HERE) and (local_var(LVAR_quest_smiley) == 1 ) ) then begin
         call Node52b;
      end else if ( (local_var(LVAR_quest_torr) == 1) or (local_var(LVAR_quest_smiley) == 1) ) then begin
         call Node053;
      end else if (local_var(LVAR_quest_smiley) == 0) then begin
         call Node005;
      end else begin
         call Node007;
      end

      set_local_var(LVAR_05_before,1); //so Ardin will not introduce herself if you have already spoken to her
      gSay_End;
      end_dialogue;
      //move_obj_inven_to_obj(self_obj,klam_bucknr_box_obj);
   end //(1)

end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
//  move_obj_inven_to_obj(klam_bucknr_box_obj,self_obj);
/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   //display_msg("SMILEY = "+ global_var(GVAR_SMILEY_STATUS));
   //ndebug("SMILEY = "+ global_var(GVAR_SMILEY_STATUS));
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

procedure push_p_proc begin
   script_overrides;
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

procedure Node910 begin
   if (dude_caps >= 25) then begin
      dude_caps_adjust(-25); //subtract 25 coins
      call Node920;
   end
   else begin
      call Node011;
   end
end

procedure Node920 begin
//need to move player and party to room
//need to heal rest of party
   critter_heal(dude_obj,(get_critter_stat(dude_obj,STAT_heal_rate)*4));//standard rate of healing/24 hours..Matt wants room to help healing
   gfade_out(600);
   if (random(0,1)) then
      move_to(dude_obj,ROOM_ONE,dude_elevation);
   else
      move_to(dude_obj,ROOM_TWO,dude_elevation);
   game_time_advance(game_ticks(28800)); //8 hours
   gfade_in(600);
end

/*
procedure Node910 begin
//if player has $25...subtract, fade out, advance clock
//8 hours, give PC and party 1 day of natural healing
//move pc and party to room in buckner house, fade in, end.
//if player doesn't have $25...then goto11.
call Node011;
end
*/

procedure Node930 begin
   //set_local_var(LVAR_Sulik_Freed, 1);
   //give_xp(EXP_KC_FREE_SULIK); //killap's fix: changed from give_exp_points - commented out aug 06
   inc_general_rep(REP_BONUS_KC_FREE_SULIK);
   //+200 EP end
end

procedure Node935 begin
   call Node930;
   //+200 EP end
   call Node007;
end

procedure Node940 begin
   set_local_var(LVAR_torr_reward_given,1);
   dude_caps_adjust(100); //add 100 coins
   //+$100 to PC inventory end
end

procedure Node950 begin
   set_global_var(GVAR_QUEST_RESCUE_TORR, TORR_RESCUE_ACCEPT);
   set_local_var(LVAR_quest_torr,1);
   //set_local_var(LVAR_Torr_Search_Accept, 1);
   //mark find torr on player quest log
   call Node020;
end

procedure Node960 begin
   set_global_var(GVAR_SMILEY_STATUS, SMILEY_QUEST_ACCEPTED);
   set_local_var(LVAR_quest_smiley,1);
   //set_local_var(LVAR_Trapper_Quest_Accept, 1);
   //mark search for smiley on player quest log
   call Node007;
end

procedure Node970 begin
   set_global_var(GVAR_SMILEY_STATUS, SMILEY_QUEST_ACCEPTED);
   set_local_var(LVAR_quest_smiley,1);
   //set_local_var(LVAR_Trapper_Quest_Accept, 1);
   //mark search for smiley on player quest log
   call Node999;
end

procedure Node975 begin
   if (local_var(LVAR_torr_reward_given) == 0) then
      call Node013;
   else
      call Node999;
end

procedure Node980 begin
   Reply(200);
   NLowOption(390,Node999);
end

procedure Node995 begin
   if (dude_charisma >= 7) then begin
       TopReact;
   end
   else begin
       BottomReact;
   end
end

procedure Node996 begin
   BottomReact;
end

procedure Node997 begin
   if (dude_charisma >= 8) then
       call Node004;
   else
       call Node005;
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end

// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

/*
procedure Node001 begin
   Reply(110);
   if ( (global_var(GVAR_ARDIN_FREEDOM) == 0) and (local_var(LVAR_Sulik_Freed) == 0) ) then begin
      NLowOption(111,Node014);
      GOption(112,Node014,004);
   end
   NOption(113,Node007,004);
   NOption(114,Node999,004);
end
*/

procedure Node002 begin
   set_local_var(LVAR_Ardin_Angry,1);
   NMessage(120);
end

procedure Node003 begin
   Reply(130);
   NLowOption(131,Node004);
   GLowOption(132,Node004);
end

procedure Node004 begin
   NMessage(140);
end

procedure Node005 begin
   set_global_var(GVAR_CAN_ASK_ARDIN_ABOUT_SMILEY,1);
   if (local_var(LVAR_05_before) == 0) then begin
      set_local_var(LVAR_05_before,1);
      Reply(150);
   end
   else begin
      Reply(510);
   end

   GLowOption(2000,Node008); //added by killap - RP

   GOption(151,Node008,004);
   NOption(152,Node007,004);
   NOption(153,Node999,004);
end

procedure Node006 begin
   Reply(160);
   NOption(161,Node007,004);
   NOption(162,Node999,004);
end

procedure Node007 begin
   if town_rep_worse_than(5) then
      Reply(170);
   else
      Reply(171);
   if ( (local_var(LVAR_quest_smiley) == 0) and (global_var(GVAR_CAN_ASK_ARDIN_ABOUT_SMILEY) == 1) ) then
      GOption(172,Node008,004);
   NOption(173,Node006,004);
   if (party_member_count(DONT_LIST_HIDDEN_MEMBERS) < 2) then
      NOption(174,Node010,004);
   else
      NOption(175,Node010,004);

   //NBK ADD THIS PLAYER REPLY
   //if (PC knows that Duntons are rustling Brahmin) then
   //   GOption(500,Node031,004); //set rustling to failure, ep+50, Karma+25, Town Rep+4
   //added by killap - RP
   if ( (global_var(GVAR_KLAMATH_FIND_RUSTLERS) >= FIND_RUSTLERS_ACCEPT) and  (global_var(GVAR_KLAMATH_FIND_RUSTLERS) < FIND_RUSTLERS_SUCCESS) ) then begin
      if (local_var(LVAR_Discovered_Rustlers) == 0) then
         GOption(500,Node032,004);
      else if (map_var(MVAR_Different_Brands) == 1) then
         GOption(540,Node034,004);
   end
   //end added by killap
   GOption(176,Node009,004);
   NOption(177,Node999,004);
end

procedure Node008 begin
   Reply(180);
   NOption(181,Node023,004);
   GLowOption(2001,Node023); //added by killap - RP
end

procedure Node009 begin
   Reply(190);
   NOption(191,Node007,004);
   NOption(192,Node999,004);
end

procedure Node010 begin
      Reply(201);
      NOption(202,Node910,004);
      NOption(203,Node007,004);
      NOption(204,Node999,004);
end

procedure Node011 begin
   Reply(210);
   NOption(211,Node007,004);
   NOption(212,Node999,004);
end
/*
procedure Node012 begin
   if (local_var(LVAR_torr_reward_given) == 0) then begin
      Reply(220);
   end
   else begin
      Reply(520);
   end
   GLowOption(221,Node975);
   GOption(222,Node975,004);
   if (local_var(LVAR_torr_reward_given) == 0) then begin
      NOption(223,Node013,004);
   end
   else begin
      NOption(530,Node005,004);
   end
end
*/
procedure Node013 begin
   //set_global_var(GVAR_ARDIN_FREEDOM,1);
   //if dude_low_iq then
   //   Reply(230);
   //else
   Reply(231);
   //if ( (global_var(GVAR_ARDIN_FREEDOM) == 0) and (local_var(LVAR_Sulik_Freed) == 0) ) then
   //   NLowOption(mstr(232) + dude_name + mstr(236),Node014);
   GOption(233,Node13a,004);
   NOption(234,Node015,004);
   if ( (global_var(GVAR_SULIK_FREE) == 0) and (global_var(GVAR_ARDIN_FREEDOM) == 0) ) then
      GOption(235,Node014,004);
end

procedure Node13a begin
   if ( (global_var(GVAR_SULIK_FREE) == 0) and (global_var(GVAR_ARDIN_FREEDOM) == 0) ) then
      call Node016;
   else
      call Node015;
end

procedure Node014 begin
   set_global_var(GVAR_ARDIN_FREEDOM,1);
   Reply(240);
   GLowOption(mstr(241) + dude_name + mstr(244),Node930);
   GOption(242,Node930,004);
   NOption(243,Node935,004);
end

procedure Node015 begin
   Reply(250);
   NOption(251,Node940,004);
   //NOption(252,Node013,004);
end

procedure Node016 begin
   set_global_var(GVAR_ARDIN_FREEDOM,1);
   Reply(260);
   GOption(261,Node930,004);
   //NOption(262,Node013,004);
end

procedure Node017 begin
   set_local_var(LVAR_17_before,1);
   Reply(270);
   GLowOption(271,Node018);
   GOption(272,Node018,004);
   BLowOption(273,Node007);
   NOption(274,Node999,004);
end

procedure Node018 begin
   Reply(280);
   NLowOption(281,Node019);
   if ( (global_var(GVAR_RUSTLE_REFUSE) == 1) or (global_var(GVAR_RUSTLE_ACCEPT)) ) then
      NOption(282,Node028,004);

   NOption(283,Node019,004);
end

procedure Node019 begin
   Reply(290);
   NOption(291,Node022,001);
end

procedure Node020 begin
   Reply(300);
   NOption(301,Node999,001);
end

/*
procedure Node021 begin
   Reply(310);
   NLowOption(311,Node999);
   NOption(312,Node007,004);
   GOption(313,Node999,004);
end
*/

procedure Node022 begin
   Reply(320);
   GLowOption(mstr(321) + dude_name + mstr(325),Node950);
   BLowOption(322,Node999);
   GOption(323,Node950,004);
   BOption(324,Node999,004);
end

procedure Node023 begin
   Reply(330);
   NOption(331,Node024,004); //edit by killap - was 001, which is again more evidence that this was to be open for dumb characters.

   GLowOption(2002,Node024); //added by killap - RP
end

procedure Node024 begin
   Reply(340);
   GOption(341,Node025,004);
   NOption(342,Node007,004);
   BOption(343,Node999,004);

   GLowOption(2003,Node025); //added by killap - RP
   GLowOption(2004,Node999); //added by killap - RP
end

procedure Node025 begin
mark_area_known(MARK_TYPE_TOWN, AREA_KLAMATH_TOXIC_CAVES, MARK_STATE_KNOWN);
   if not(dude_is_stupid) then
      Reply(350);
   else
      Reply(2005); //added by killap - RP
   GOption(351,Node960,004);
   GOption(352,Node970,004);

   GLowOption(2006,Node970); //added by killap - RP

   //BOption(353,Node999,004); - comment out by killap
end

procedure Node026 begin
   Reply(360);
   NOption(361,Node007,004);
   GOption(362,Node999,004);
end

procedure Node027 begin
   //if (local_var(LVAR_quest_torr) == 1) then
   Reply(370);
   if (local_var(LVAR_quest_torr) == 1) then
      GOption(372,Node007,004);
   if (local_var(LVAR_quest_torr) == 1) then
      BOption(373,Node999,004);
   NLowOption(374,Node999);
   NOption(375,Node007,004);
   NOption(376,Node999,004);

   //added by killap - 3 == dead
   set_local_var(LVAR_quest_torr,3);
   if (global_var(GVAR_QUEST_RESCUE_TORR) == TORR_RESCUE_ACCEPT) then
      set_global_var(GVAR_QUEST_RESCUE_TORR,3);
   //end added by killap
end

procedure Node028 begin
   Reply(380);
   NOption(381,Node019,004);
end

procedure Node029 begin
   Reply(400);
      BOption(401,Node007,004);
      BOption(402,Node040,004); //KARMA -10
      NOption(403,Node041,004); //KARMA -5
      NOption(404,Node999,004);
end

procedure Node030 begin
   Reply(410);
      NOption(411,Node042,004); //KARMA +5
      BOption(412,Node043,004); //KARMA -10
      NOption(413,Node044,004); //KARMA +5
end


procedure Node031 begin
   //added by killap
   //ep+50, Karma+25, Town Rep+4
   /*inc_general_rep(25);
   KC_REP_ADJUST(4)
   give_xp(50);
   //end added by killap

   Reply(420);
      NOption(421,Node007,004);
      NOption(422,Node999,004);
      //NOption(422,Node999,004);*/

   set_local_var(LVAR_Discovered_Rustlers,1);

   Reply(417);

   if (global_var(GVAR_SCARE_DUNTONS) == 1) then
      NOption(418,Node033,004);
   else
      NOption(419,Node033,004);
   //if (map_var(MVAR_Different_Brands) == 1) then
   //   NOption(422,Node033,004);
   //else
   //   NOption(421,Node999,004);

end

//added by killap - RP
procedure Node032 begin
   Reply(414);
   if ( (dude_item_count(PID_RADSCORPION_PARTS) > 0) ) then
      NOption(415,Node031,004);
   else
      NOption(416,Node999,004);
end

procedure Node033 begin
   if (global_var(GVAR_DUNTON_DEAD)) then
      Reply(421);
   else
      Reply(420);

   if (map_var(MVAR_Different_Brands) == 1) then
      NOption(423,Node035,004);
   else
      NOption(422,Node999,004);
end

procedure Node035 begin
   variable item;
   inc_general_rep(10);
   KC_REP_ADJUST(2)
   display_msg(mstr(1000));
   //give_xp(300);
   set_global_var(GVAR_KLAMATH_FIND_RUSTLERS,FIND_RUSTLERS_SUCCESS);

   if (dude_item_count(PID_RADSCORPION_PARTS) > 0) then begin
      item:=dude_item(PID_RADSCORPION_PARTS);
      rm_obj_from_inven(dude_obj,item);
      destroy_object(item);
   end

   if (global_var(GVAR_DUNTON_DEAD)) then begin
      Reply(425);

      NOption(1010,Node033a,004);
      NOption(426,Node007a,004);
      NOption(428,Node999a,004);
   end else begin
      Reply(424);

      NOption(1010,Node033a,004);
      NOption(426,Node007a,004);
      NOption(427,Node999a,004);
   end
end

procedure Node033a begin
   Reply(1011);
   dude_caps_adjust(150); //give 150 coins
   NOption(426,Node007,004);
   NOption(1012,Node999,004);
end

procedure Node007a begin
   give_xp(300);
   KC_REP_ADJUST(2)
   call Node007;
end

procedure Node999a begin
   give_xp(300);
   KC_REP_ADJUST(2)
end

procedure Node034 begin
   Reply(429);
   NOption(430,Node035,004);
end

//end added by killap

procedure Node038 begin
   set_local_var(LVAR_Ardin_Angry,1);
   inc_general_rep(-25);
   call Node999;
end

procedure Node039 begin
   set_local_var(LVAR_Ardin_Angry,1);
   call Node999;
end

procedure Node040 begin
   inc_general_rep(-10);
   call Node030;
end

procedure Node041 begin
   set_local_var(LVAR_Ardin_Angry,1);
   inc_general_rep(-5);
   call Node999;
end

procedure Node042 begin
   inc_general_rep(5);
   call Node007;
end

procedure Node043 begin
   set_local_var(LVAR_Ardin_Angry,1);
   inc_general_rep(10);
   call Node999;
end

procedure Node044 begin
   set_local_var(LVAR_Ardin_Angry,1);
   inc_general_rep(5);
   call Node999;
end

procedure Node52a begin
   set_local_var(LVAR_quest_torr,2);
   Reply(620);
   GOption(650,Node013,004);
end

procedure Node52b begin
   set_local_var(LVAR_quest_smiley,2);
   Reply(630);
   GOption(660,Node013,004);
end

procedure Node053 begin
   if ( (local_var(LVAR_quest_torr) == 1) and (local_var(LVAR_quest_smiley) == 1) ) then begin
      Reply(920);
      if (global_var(GVAR_TORR_DEAD) == 1) then
         BOption(700,Node027,004);
      if (global_var(GVAR_SMILEY_STATUS) == SMILEY_DEAD) then
         BOption(710,Node029,004);
      if ( (global_var(GVAR_TORR_DEAD) == 1) and (global_var(GVAR_SMILEY_STATUS) == SMILEY_DEAD) ) then begin
         NOption(720,Node007,004);
         BOption(730,Node054,004);
      end
   end
   else if (local_var(LVAR_quest_torr) == 1) then begin
      Reply(900);
      if (global_var(GVAR_TORR_DEAD) == 1) then
         BOption(700,Node027,004);
   end
   else if (local_var(LVAR_quest_smiley) == 1) then begin
      Reply(910);
      if (global_var(GVAR_SMILEY_STATUS) == SMILEY_DEAD) then
         BOption(710,Node029,004);
   end
   else if (global_var(GVAR_TORR_MISSING) == 1) then begin
      Reply(930);
      if (global_var(GVAR_TORR_DEAD) == 1) then begin
         BOption(700,Node027,004);
      end
      NOption(740,Node017,004);
   end

   if ( (global_var(GVAR_TORR_DEAD) == 1) or (global_var(GVAR_SMILEY_STATUS) == SMILEY_DEAD) ) then
      GOption(750,Node007,004);
   if ( (local_var(LVAR_quest_torr) == 1) and (local_var(LVAR_quest_smiley) == 1) ) then
      NOption(760,Node999,004);
   else if ( (local_var(LVAR_quest_torr) == 1) or (local_var(LVAR_quest_smiley) == 1) ) then
      NOption(770,Node999,004);
   if ( (global_var(GVAR_TORR_DEAD) == 1) and (global_var(GVAR_SMILEY_STATUS) == SMILEY_DEAD) ) then
      NOption(780,Node999,004);
   NOption(790,Node007,004);
   NOption(800,Node999,004);
   NLowOption(810,Node999);
end

procedure Node054 begin
   Reply(940);
   BOption(942,Node038,004);
   BOption(943,Node039,004);
end

procedure Node055 begin
   floater(random(950,959));
end
//xxxxxxxxxxxxxxxxxxxx
