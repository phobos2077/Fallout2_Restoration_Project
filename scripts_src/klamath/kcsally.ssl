/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Sally
        Location: Klamath
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: January 15, 1998
           Need code to move player and start up dialogue with jenny/attendent

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "../headers/define.h"
#include "../headers/kladwtn.h"

#define NAME                    SCRIPT_KCSALLY
#define TOWN_REP_VAR            (GVAR_TOWN_REP_KLAMATH)
#define BEST_SERVICE            18068
#define FAIR_SERVICE            16468
#define POOR_SERVICE            15469
#define KINKY_SERVICE           14269

#include "../headers/command.h"
#include "../headers/ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node910;
procedure Node911;
procedure Node912;
procedure Node913;
procedure Node914;
procedure Node915;
procedure Node916;
procedure Node917;
procedure Node918;
procedure Node919;
procedure Node995;
procedure Node996;
procedure Node997;
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
procedure Node023;
procedure Node024;
procedure Node025;
procedure Node026;
procedure Node027;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Personal_Enemy             (5)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Charisma_Roll;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_KLAMATH);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_CITIZEN);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Personal_Enemy) == 1) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       self_attack_dude;
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_global_var(GVAR_ENEMY_KLAMATH,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   GetReaction;

   // CHANGE -nbk
   if (global_var(GVAR_BATH_HOUSE_REJECT) == 1) then begin
       floater(400);
   end
   else if ((REP_ENEMY_KLAMATH) or (local_var(LVAR_Personal_Enemy) == 1)) then begin
       floater(400);
       call Node998;
   end

   //if Low INT
   else if ((get_critter_stat(dude_obj,STAT_iq) <= (0-LOW_IQ))) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node001;
       gSay_End;
       end_dialogue;
   end

   //add this mapvar -nbk
   else if ( (global_var(GVAR_RUSTLE_FAIL) == 1) and (global_var(GVAR_SCARE_DUNTONS) == 0) ) then begin //killap: added (global_var(GVAR_SCARE_DUNTONS) == 0) (expansion)
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
          call Node004;
       gSay_End;
       end_dialogue;
   end

   //need check to ignore dogmeat -nbk
   else if (party_member_count(DONT_LIST_HIDDEN_MEMBERS) > 1) then begin//this counts dude
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
          call Node027;
       gSay_End;
       end_dialogue;
   end

   else if (local_var(LVAR_Herebefore) == 0) then begin
//else if ((local_var(LVAR_Herebefore) == 0) and (get_critter_stat(dude_obj,STAT_iq) <= (0-LOW_IQ))) then begin
       set_local_var(LVAR_Herebefore,1);
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
          if (dude_is_male) then call Node005;
          else call Node006;
       gSay_End;
       end_dialogue;
   end

   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
          call Node025;
       gSay_End;
       end_dialogue;
   end


end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

procedure Node910 begin
   if (local_var(LVAR_Herebefore) == 0) then begin
      set_local_var(LVAR_Herebefore,1);
      if (dude_is_male) then call Node005;
      else call Node006;
   end
   else begin
      call Node025;
   end
end


procedure Node911 begin
   Charisma_Roll:=do_check(dude_obj,STAT_ch,0);
   if (is_success(Charisma_Roll)) then
      call Node012;
   else
      call Node013;
end

procedure Node912 begin
   Charisma_Roll:=do_check(dude_obj,STAT_ch,0);
   if (is_success(Charisma_Roll)) then
      call Node014;
   else
      call Node015;
end

procedure Node913 begin
   Charisma_Roll:=do_check(dude_obj,STAT_ch,0);
   if (is_success(Charisma_Roll)) then
      call Node016;
   else
      call Node017;
end

procedure Node914 begin
//money check
//[if <$25 in inventory then goto 22, else subtract $25 from PC and goto 18]
   if (item_caps_total(dude_obj) >= 25) then begin
      item_caps_adjust(dude_obj, -25);
      call Node018;
   end
   else begin
      call Node022;
   end
end

procedure Node915 begin
//money check
//[if <$35 in inventory then goto 22, else subtract $35 from PC and goto 18]
   if (item_caps_total(dude_obj) >= 35) then begin
      item_caps_adjust(dude_obj, -35);
      call Node018;
   end
   else begin
      call Node022;
   end
end

procedure Node916 begin
//money check
//[if <$55 in inventory then goto 23, else subtract $55 from PC and goto 19]
   if (item_caps_total(dude_obj) >= 55) then begin
      item_caps_adjust(dude_obj, -55);
      call Node019;
   end
   else begin
      call Node023;
   end
end

procedure Node917 begin
//money check
//[if <$70 in inventory then goto 23, else subtract $70 from PC and goto 19]
   if (item_caps_total(dude_obj) >= 70) then begin
      item_caps_adjust(dude_obj, -70);
      call Node019;
   end
   else begin
      call Node023;
   end
end

procedure Node918 begin
//money check
//[if <$125 in inventory then goto 23, else subtract $125 from PC and goto 20]
   if (item_caps_total(dude_obj) >= 125) then begin //killap's fix: was 124
      item_caps_adjust(dude_obj, -125);
      call Node020;
   end
   else begin
      call Node023;
   end
end

procedure Node919 begin
//money check
//[if <$185 in inventory then goto 23, else subtract $185 from PC and goto 20]
   if (item_caps_total(dude_obj) >= 185) then begin
      item_caps_adjust(dude_obj, -185);
      call Node020;
   end
   else begin
      call Node023;
   end
end


procedure Node995 begin
   if (get_critter_stat(dude_obj,STAT_ch) >= 7) then begin
       TopReact;
   end
   else begin
       BottomReact;
   end
end

procedure Node996 begin
   BottomReact;
end

procedure Node997 begin
   if (get_critter_stat(dude_obj,STAT_ch) >= 8) then
       call Node004;
   else
       call Node005;
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Personal_Enemy,1);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end

// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(200);
   NLowOption(201,Node002);
   NLowOption(202,Node003);
   NLowOption(203,Node002);
end


procedure Node002 begin
   NMessage(204);
end

procedure Node003 begin
   NMessage(205);
end

procedure Node004 begin
   NMessage(206);
end

procedure Node005 begin
   Reply(210);
   NOption(211,Node009,004);
   NOption(212,Node008,004);
   NOption(213,Node007,004);
end

procedure Node006 begin
   Reply(220);
   NOption(221,Node008,004);
   NOption(222,Node009,004);
   //added by killap
   if (global_var(GVAR_DUNTON_DEAD) == 0) then
     NOption(223,Node024,004);
   NOption(224,Node007,004);
end

procedure Node007 begin
   NMessage(225);
end

procedure Node008 begin
   Reply(230);
   NOption(231,Node999,004);
   NOption(232,Node009,004);
end

procedure Node009 begin
   Reply(240);
   NOption(241,Node010,004);
   NOption(242,Node007,004);
   NOption(243,Node010,007);
end

procedure Node010 begin
   Reply(250);
   NOption(251,Node911,004);
   NOption(252,Node912,004);
   NOption(253,Node913,004);
   NOption(254,Node007,004);
   NOption(255,Node011,006);
   NOption(256,Node026,006);
end

procedure Node011 begin
   Reply(260);
   NOption(261,Node010,006);
   NOption(262,Node007,006);
end

procedure Node012 begin
   Reply(270);
   NOption(271,Node914,004);
   NOption(272,Node010,004);
   NOption(273,Node007,004);
end

procedure Node013 begin
   Reply(280);
   NOption(281,Node915,004);
   NOption(282,Node010,004);
   NOption(283,Node007,004);
   //added by killap
   if (global_var(GVAR_DUNTON_DEAD) == 0) then
      NOption(284,Node021,004);
end

procedure Node014 begin
   Reply(290);
   NOption(291,Node916,004);
   NOption(292,Node010,004);
   NOption(293,Node007,004);
end

procedure Node015 begin
   Reply(300);
   NOption(301,Node917,004);
   NOption(302,Node010,004);
   NOption(303,Node007,004);
end

procedure Node016 begin
   Reply(310);
   NOption(311,Node918,004);
   NOption(312,Node010,004);
   NOption(313,Node007,004);
end

procedure Node017 begin
   Reply(320);
   NOption(321,Node919,004);
   NOption(322,Node010,004);
   NOption(323,Node007,004);
   NOption(324,Node007,004);
end

procedure Node018 begin
   debug_msg("Setting map var");
   // Commenting out because a handjob does not consitute sex, as per it's definition
   // and thus the player shouldn't lose the 'Virgin' title in this case. - killap (expansion)
   //up_sex_status;
   set_map_var(MVAR_Just_Had_Sex,1);
   NMessage(330);
   gfade_out(600);
   move_to(dude_obj,POOR_SERVICE,elevation(dude_obj));
   game_time_advance(game_ticks(1800));
   gfade_in(600);
//fade out 30 min ... attendent 1 float dialog
end

procedure Node019 begin
   debug_msg("Setting map var");
   up_sex_status;
   set_map_var(MVAR_Just_Had_Sex,1);
   NMessage(340);
   gfade_out(600);
   move_to(dude_obj,FAIR_SERVICE,elevation(dude_obj));
   game_time_advance(game_ticks(3600));
   gfade_in(600);
//fade out 1 hour ...  attendent 1 float dialog
end

procedure Node020 begin
   debug_msg("Setting map var");
   up_sex_status;
   set_map_var(MVAR_Just_Had_Sex,1);
   NMessage(350);
   gfade_out(600);
   move_to(dude_obj,BEST_SERVICE,elevation(dude_obj));
   game_time_advance(game_ticks(7200));
   gfade_in(600);
//fade out 2 hours ...  jenny float dialog
end

procedure Node021 begin
   NMessage(360);
end

procedure Node022 begin
   NMessage(361);
end

procedure Node023 begin
   NMessage(362);
end

procedure Node024 begin
   NMessage(363);
end

procedure Node025 begin
   Reply(370);
   NOption(371,Node007,004);
   NOption(372,Node010,004);
   NOption(373,Node026,006);
end

procedure Node026 begin
   Reply(380);
   NOption(381,Node007,004);
   NOption(382,Node010,004);
end

procedure Node027 begin
   Reply(390);
   NOption(391,Node910,004);
end
//xxxxxxxxxxxxxxxxxxxx