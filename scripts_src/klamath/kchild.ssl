/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Klamath Child
        Location: Klamath
        Description: Generic child in Klamath.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: January 13, 1998

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "kchild"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_KCHILD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_KLAMATH)

#include "../headers/command.h"
#include "../headers/kladwtn.h"
#include "../headers/modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure push_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node997;                                      // this Node handles the basic START CONDITIONS
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending
procedure Node901;                                      // Random dialogue 1
procedure Node902;                                      // Random dialogue 2
procedure Node903;                                      // Random dialogue 3

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Hostile                    (4)
#define LVAR_Personal_Enemy             (5)

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable pick;
variable In_Timed_Event:=0;
variable scarydude;
variable here;
variable cooldude;
variable playing:=1;
variable run_from_dude;
variable newtile;
variable pester_dude:=0;
variable dont_pester:=0;
variable spoke:=0;
variable prevhex:=0;
variable fix:=0;

// kids will like dude if town rep >= this
#define TOWN_REP_BREAKPOINT 6

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   //CHANGE THESE -nbk
   set_self_team(TEAM_KLAMATH);
   set_self_ai(AI_CHILD);
end

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin
   if (((local_var(LVAR_Hostile) == 2)or(local_var(LVAR_Personal_Enemy) == 1)) and (self_can_see_dude)) then begin
      set_local_var(LVAR_Hostile,1);
      Flee_From_Dude
   end
   if ((random(0,100) == 1) and (In_Timed_Event == 0)) then begin
       In_Timed_Event:=1;
       add_timer_event(self_obj,game_ticks(random(6,21)),1);
   end

   if town_rep_worse_than(TOWN_REP_BREAKPOINT) then begin
      scarydude:=dude_tile;
      here:=self_tile;
      if (run_from_dude == 0) then begin
         if (prevhex != scarydude) then begin //The dude has moved
            if ( (tile_distance(here,scarydude) < 2) and (self_can_see_dude) ) then begin
               playing:=0; //not playing ...gotta run from the dude
               run_from_dude:=1;
               prevhex:=scarydude; //save dudes last location so child
               //doesn't scream again if dude is not moving
            end
         end
         if (here == newtile) then begin //make child scream only at end of run
            newtile:=0;                  //if they don't run then they dont scream
            call Node901;
         end
      end
      else begin
         newtile:=tile_num_in_direction(here,random(0,5),random(3,7));
         if ( (tile_distance(newtile,scarydude)) > (tile_distance(here,scarydude)) ) then begin
            run_from_dude:=0; //child has run from the dude...dont need to run again
            playing:=1;
            self_run_to_tile(newtile);
         end
      end
   end

   else if (dont_pester < 1) then begin
      cooldude:=dude_tile;
      here:=self_tile;
      if (tile_distance(here,cooldude) < 5) then begin
         playing:=0; //not playing ...gonna pester dude for a while
         if (pester_dude < 1) then
            pester_dude:=random(3,6);
      end
      if (pester_dude > 1) then begin
         if ( (tile_distance(here,cooldude) < 6) and (spoke == 0) ) then begin
            spoke:=1;
            pester_dude:=pester_dude - 1;
            call Node903;
         end
         else if (tile_distance(here,cooldude) > 5) then begin
            spoke:=0;
            newtile:=tile_num_in_direction(cooldude,random(0,5),1);
            self_run_to_tile(newtile);
         end
      end //pester_dude > 1
      else if (pester_dude == 1) then begin //once pester becomes 1 we stop the pestering
         //fix:=0;
         pester_dude:=0;
         dont_pester:=3; //don't pester the dude for a while (maybe make this random)
         playing:=1;
      end
   end //good rep and dont pester is false
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_global_var(GVAR_ENEMY_KLAMATH,1);
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   if town_rep_worse_than(TOWN_REP_BREAKPOINT) then call Node901;
   else call Node902;
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
/* Increment the aligned critter counter*/
   //inc_childkiller
   inc_good_critter //edit by killap
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

procedure push_p_proc begin
   if town_rep_better_or_equal(TOWN_REP_BREAKPOINT) then begin
      pester_dude:=random(4,7);
      dont_pester:=0;
      playing:=0;
   end
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
   if (action_being_used == SKILL_STEAL) then begin
      if (is_success(roll_vs_skill(dude_obj,action_being_used,0))) then begin
        //CHANGE -nbk
        //inc_general_rep(REP_BONUS_ARROYO_MONEY_THEFT);
      end
   end
end

procedure timed_event_p_proc begin

   if (fixed_param == 1) then begin
       if (dont_pester > 0) then
          dont_pester:=dont_pester-1; //eventually child will pester hero again
       if (playing) then
          self_run_to_tile(tile_num_in_direction(self_tile,random(0,5),random(3,7)));
       In_Timed_Event:=0;
   end
   else if (fixed_param == 2) then begin
      pester_dude:=1; //force end to pestering...it has gone on too long
   end
end

procedure Node901 begin
   floater(random(150,168));
end
procedure Node902 begin
   floater(random(170,189));
end
procedure Node903 begin
   floater(random(190,223));
end
procedure Node997 begin
/*
   if (dude_iq <= (0-LOW_IQ)) then begin
      call Node007;
   end else if (local_var(LVAR_Herebefore) == 0) then begin
      call Node001;
   end else begin
      call Node001;
   end
*/
end
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end
/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end
