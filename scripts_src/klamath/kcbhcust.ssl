/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Klamath Buckner House Customer
        Location: Klamath
        Description: Same as the name.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: April 19, 1998

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "kcbhcust"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_KCBHCUST
#define TOWN_REP_VAR            (GVAR_TOWN_REP_KLAMATH)

#include "../headers/command.h"
#include "../headers/kladwtn.h"
#include "../headers/modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure saysomething;
procedure thanks;
procedure push_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending
procedure Node001;
procedure Node002;

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Hostile                    (4)
#define LVAR_Personal_Enemy             (5)

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable pick;
variable In_Timed_Event:=0;
variable k;
variable I_Am_In_Battle:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_KLAMATH);
   set_self_ai(AI_TOUGH_CITIZEN);
end

procedure combat_p_proc begin
   I_Am_In_Battle:=1;
end

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin
   I_Am_In_Battle:=0; //critter proc not run during combat so I must not be in combat
   if (((local_var(LVAR_Hostile) == 2)or(local_var(LVAR_Personal_Enemy) == 1)) and (self_can_see_dude)) then begin
      set_local_var(LVAR_Hostile,1);
      Flee_From_Dude
   end

   if (map_var(MVAR_Round_Of_Drinks) == 1) then begin
      set_map_var(MVAR_Round_Of_Drinks,0);
      call thanks;
   end
   else if ((random(0,200) == 1) and (In_Timed_Event == 0)) then begin
       In_Timed_Event:=1;
       add_timer_event(self_obj,game_ticks(random(17,37)),1);
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_global_var(GVAR_ENEMY_KLAMATH,1);
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Hostile,2);
   end
end

procedure talk_p_proc begin
   call saysomething;
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
   inc_good_critter
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   k:=random(100,104);
   script_overrides;
   display_msg(mstr(k));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   k:=random(106,110);
   script_overrides;
   display_msg(mstr(k));
end

procedure push_p_proc begin
   script_overrides;
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
   if (action_being_used == SKILL_STEAL) then begin
      if (is_success(roll_vs_skill(dude_obj,action_being_used,0))) then begin
        //CHANGE -nbk
        //inc_general_rep(REP_BONUS_ARROYO_MONEY_THEFT);
      end
   end
end

procedure timed_event_p_proc begin
   if (I_Am_In_Battle == 0) then begin //shouldn't say anything in battle
      if (fixed_param == 1) then begin
          if (tile_distance(dude_tile,self_tile) < 3) then begin
             call saysomething;
          end
          In_Timed_Event:=0;
      end
   end
   else begin
      In_Timed_Event:=0; //clear this timed event
   end
end

procedure thanks begin
   k:=random(201,220);
   if (k == 219) then
      float_msg(self_obj,mstr(k)+dude_name,FLOAT_MSG_RED);
   else
      floater(k);
end

procedure saysomething begin
   if town_rep_is_antipaty then begin
      call Node001;
   end
   else begin
      call Node002;
   end
end

procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end
/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end

procedure Node001 begin
   floater(random(120,144));
end
procedure Node002 begin
   floater(random(150,170));
end
