/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Yellow Dog
        Location: Klamath
        Description:

        Note: Currently this script cannot be loaded by the scripting tool.
              NEED NPC CODE SO DOG WILL FOLLOW. NEED DRYMEAT ITEM NO.
              SO DOG CAN DETERMINE WHEN YOU FEED IT DRYMEAT.
              NEED ITEM NO. FOR TRAPPER KEY WHICH THE DOG MUST DROP.
        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: Febuary 18, 1998

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "kcyellow"
#include "../headers/define.h"
#include "../headers/klamath.h"
#include "../headers/kladwtn.h"

#define NAME                    SCRIPT_KCYELLOW
#define TOWN_REP_VAR            (GVAR_TOWN_REP_KLAMATH)
#define floater(x)		float_msg(self_obj, message_str(NAME,x),FLOAT_MSG_GREEN) //edit by killap - was "_RED"

#include "../headers/command.h"
#include "../headers/modreact.h"

#define Vault_Known             (1)     // Temp Until get command from Jesse
#define Visited_City            (1)     // Temp Until get command from Jesse

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure push_p_proc;

/* Script Specific Procedure Calls */
procedure Node910;
procedure Node920;
procedure Node995;
procedure Node996;
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Befriend_YellowDog         (7)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable k;
variable item;
variable meat;
variable In_Timed_Event:=0;
variable dtile;
variable mytile;
variable newtile;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_DOG);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_GENERIC_DOG);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end
   if ((random(0,100) == 1) and (In_Timed_Event == 0)) then begin
       In_Timed_Event:=1;
       add_timer_event(self_obj,game_ticks(random(6,21)),1);
   end
   else if (local_var(LVAR_Befriend_YellowDog) == 1) then begin
     dtile:=tile_num(dude_obj);
     mytile:=tile_num(self_obj);
     if (tile_distance(mytile,dtile) > 6) then begin
        newtile:=tile_num_in_direction(dtile,random(0,5),1);
        animate_run_to_tile(newtile);
     end
  end//follow


end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
       animate_run_to_tile(tile_num_in_direction(tile_num(self_obj),random(0,5),random(3,7)));
       In_Timed_Event:=0;
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   GetReaction;

   if ((REP_ENEMY_KLAMATH) or (local_var(LVAR_Personal_Enemy) == 1)) then begin
       call Node998;
   end

   else if (local_var(LVAR_Befriend_YellowDog) == 1) then
      call Node005;

   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node001;
       gSay_End;
       end_dialogue;
   end

end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   KC_REP_ADJUST(-5)
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

procedure push_p_proc begin
   //can push the dog because he can trap you
   //script_overrides;
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

procedure Node910 begin
   if (is_success(do_check(dude_obj,STAT_lu,0))) then
      call Node004;
   else
      call Node998;
end

procedure Node920 begin
//if player has drymeat subtract one piece and goto 2, else 3
   if (obj_is_carrying_obj_pid(dude_obj, PID_MEAT_JERKY)) then begin
      meat:=obj_carrying_pid_obj(dude_obj, PID_MEAT_JERKY);
      rm_obj_from_inven(dude_obj,meat);
      destroy_object(meat); //added by killap
      call Node002;
   end
   else begin
      call Node003;
   end
end

procedure Node995 begin
   if (get_critter_stat(dude_obj,STAT_ch) >= 7) then begin
       TopReact;
   end
   else begin
       BottomReact;
   end
end

procedure Node996 begin
   BottomReact;
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin //may want to make dog attack here
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end

// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   ndebug("Node001");
   Reply(110);

   NLowOption(111,Node004);
   NOption(112,Node910,004);
   if (obj_is_carrying_obj_pid(dude_obj, PID_MEAT_JERKY)) then begin
   //if ( (has_skill(dude_obj, SKILL_OUTDOORSMAN) > 24) or (is_success(do_check(dude_obj,STAT_ch,0))) ) then
      NOption(113,Node004,004);
   //if (has_skill(dude_obj, SKILL_OUTDOORSMAN) > 50) then
      NOption(114,Node920,004);
   end
end

procedure Node002 begin
   ndebug("Node002");
   set_local_var(LVAR_Befriend_YellowDog, 1);
   KC_REP_ADJUST(3)
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_PLAYER);
  // if (is_success(do_check(dude_obj,STAT_pe,0))) then begin
      item:=create_object(PID_TRAPPER_TOWN_KEY,0,0);
      add_obj_to_inven(dude_obj,item);
      //place key in inventory
      Reply(121);
   //end
/*
   else begin
      item:=create_object(PID_KEY,0,0);
      drop_obj(item);
      //place key on ground nearby
      Reply(120);
   end
*/
   NLowOption(122,Node005);
   NOption(123,Node005,004);
end

procedure Node003 begin
   ndebug("Node003");
   Reply(130);

   NLowOption(131,Node004);
   NLowOption(132,Node999);
   NOption(133,Node999,004);
   BOption(134,Node998,004); //edit by killap: was NOption
end

procedure Node004 begin
   Reply(140);

   NLowOption(141,Node999);
   NOption(142,Node003,004);
   NOption(143,Node920,006);
end

procedure Node005 begin
   ndebug("Node004");
   k:=random(1,5);
   if (k == 1) then floater(201);
   else if (k == 2) then floater(202);
   else if (k == 3) then floater(203);
   else if (k == 4) then floater(204);
   else if (k == 5) then floater(205);
end
//xxxxxxxxxxxxxxxxxxxx