/*
        Name: abgrave1
        Location: Abbey
        Description: Grave stones.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: killap - 7/01/2006

           Updated:
*/



/* Include Files */
#define SCRIPT_REALNAME "abgrave1"
#include "../headers/define.h"

#define NAME                    SCRIPT_ABGRAVE1
#define TOWN_REP_VAR            (GVAR_TOWN_REP_ABBEY)

#include "../headers/command.h"

procedure start;
procedure use_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;

#define grv_mstr(x)                       message_str(SCRIPT_ABGRAVE1, x)
#define LVAR_Status                       (0)
#define set_headstone_generic             set_local_var(LVAR_Status, random(250,253))

#ifndef GIVE_HEADSTONE_NAME
#define GIVE_HEADSTONE_NAME               true
#endif

procedure start begin
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_mstr(100);
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(grv_mstr(101) + grv_mstr(local_var(LVAR_Status)));
end

procedure use_p_proc
begin
 script_overrides;
end

procedure num_to_bit(variable the_num) begin
variable temp := 1;
   the_num := (the_num % 32);
   while (the_num > 1) do begin
      temp := temp * 2;
      the_num -= 1;
   end
   return temp;
end

#define head_stone_top              10
procedure give_headstone_name begin
variable temp;
variable temp_bit;
   temp := random(0,head_stone_top);
   while ((local_var(LVAR_Status) == 0) and (temp <= head_stone_top)) do begin
      temp_bit := num_to_bit(temp);
      if ((global_var(GVAR_GRAVE_FLAGS_1 + (temp / 32)) bwand temp_bit) == false) then begin
         set_global_var(GVAR_GRAVE_FLAGS_1 + (temp / 32), global_var(GVAR_GRAVE_FLAGS_1 + (temp / 32)) bwor temp_bit);
         set_local_var(LVAR_Status, 102 + temp);
      end
      temp += 1;
   end

   if (local_var(LVAR_Status) == 0) then begin
      set_headstone_generic;
   end
end

procedure map_enter_p_proc begin
   if (map_first_run) then begin
      call give_headstone_name;
   end
end