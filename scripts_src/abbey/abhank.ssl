/*
  ***   Script conwerted to Interplay format by Fallout Script Edytor (by Jargo)  ***
*/

#define SCRIPT_REALNAME "abhank"
#include "../headers/define.h"
//#include "../headers/<TownMapName.h>"

#define NAME                    SCRIPT_ABHANK          //Script name from scripts.h
#define TOWN_REP_VAR            GVAR_TOWN_REP_ABBEY          //Global variable name for this Town

#include "../headers/command.h"
#include "../headers/modreact.h"
#include "../headers/partybkg.h"

procedure start;
procedure destroy_p_proc;
procedure use_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure use_obj_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure talk_p_proc;

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;

procedure Node015;
procedure Node016;

procedure Node005a;
procedure Node901;
procedure Node997;
procedure Node998;
procedure Node999;

/*****************************************************************
   Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_
*****************************************************************/
#define LVAR_Flags                      (0)
#define LVAR_borrow_light               (1)
#define LVAR_Herebefore                 (2)
#define LVAR_Abbey_Alcohol              (3)
#define LVAR_Home_Tile                  (4)
#define LVAR_Home_Rotation              (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Said_Thanks                (7)

/*******************************************************************
   Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_)
*******************************************************************/

/*******************************************************************
   Local variables which do not need to be saved between map changes.
*******************************************************************/
variable drinks_this_conversation;

/*******************************************************************
*******                         PROCEDURES                   *******
*******************************************************************/


/*******************************************************************
   The start procedure is the first procedure called when the map is
   first entered. Any initial information that needs to be set up
   should be placed in here.
*******************************************************************/

procedure start begin
end

procedure timed_event_p_proc begin
   if (fixed_param == 3) then begin
      if (local_var(LVAR_borrow_light) == 0) then begin
         floater(random(300,302));
         add_timer_event(self_obj,random(75,100),3);
      end
   end else if (fixed_param == 999) then begin
      if (drinks_this_conversation > 0) then begin
         drinks_this_conversation -= 1;
         use_obj_on_obj(create_object(PID_ABBEY_BOOZE,0,0), dude_obj);
         flush_add_timer_event_sec(self_obj, 1, 999);
      end
   end
end

/********************************************************************
********************************************************************/

procedure use_p_proc begin
end

procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      call Node998;
   end
end

/***************************************************************************
   This is cursory glance description that the player will receive should
   he just pass the Action Cursor over. Examines which give more information
   need to be in the description_p_proc procedure.
***************************************************************************/

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end


procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end



procedure use_skill_on_p_proc begin
end


procedure use_obj_on_p_proc begin
end


/******************************************************************************************
   IF it gets damaged
******************************************************************************************/

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_personal_enemy;
      if (global_var(GVAR_TOWN_REP_ABBEY) != -30) then
         set_global_var(GVAR_TOWN_REP_ABBEY, -30);
   end
end

/***************************************************************************************
   Whenever the map is first entered, this procedure will be called.
***************************************************************************************/

procedure map_enter_p_proc begin
   set_self_team(TEAM_ABBEY);
   add_timer_event(self_obj,random(25,50),3);
   set_local_var(LVAR_Home_Tile,self_tile);
   set_local_var(LVAR_Home_Rotation, self_cur_rot);
end


/**************************************************************************************
   This procedure gets called roughly every 30 seconds of real time.
**************************************************************************************/

procedure map_update_p_proc begin
end

procedure critter_p_proc begin
   if ( (global_var(GVAR_TOWN_REP_ABBEY) == -30) and (self_can_see_dude)) then begin
      attack(dude_obj);
   end else if (self_tile != local_var(LVAR_Home_Tile) and drinks_this_conversation == 0) then begin
      self_walk_to_tile(local_var(LVAR_Home_Tile));
   end else if (self_cur_rot != local_var(LVAR_Home_Rotation) and drinks_this_conversation == 0) then begin
      self_rotate(local_var(LVAR_Home_Rotation));
   end
end

/**************************************************************************************
   This is used for any dialogue that may need to occur with the player.
**************************************************************************************/

procedure talk_p_proc begin
   //if (local_var(LVAR_borrow_light) == 3) then begin
   //   call Node008;
   if (global_var(GVAR_ABBEY_LIGHTER) < 3) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_start;
         call Node001;
      gSay_end;
      end_dialogue;
   //end else begin
   //   floater(random(300,304));
   end else begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_start;
         call Node013;
      gSay_end;
      end_dialogue;
   end
end

procedure Node001 begin
   if (global_var(GVAR_ABBEY_LIGHTER) == 0) then
      Reply(200);
   else
      Reply(220);

   if (dude_item_count(PID_LIGHTER)) then begin
      GOption(201,Node002,004);
      NLowOption(800,Node015);
   end

   if (global_var(GVAR_ABBEY_LIGHTER) == 0) then
      NOption(212,Node010,004);
   else
      NOption(212,Node999,004);
end

procedure Node002 begin
   set_local_var(LVAR_borrow_light, 1);
   set_global_var(GVAR_ABBEY_LIGHTER,3);
   if (dude_item_count(PID_LIGHTER)) then
      remove_pid_qty(dude_obj, PID_LIGHTER, 1)
   Reply(202);
   NOption(203,Node009,004);
end

procedure Node003 begin
Reply(204);
   NOption(205,Node005,004);
   NOption(253,Node005a,004);
   NOption(206,Node999,004);
end

procedure Node004 begin
Reply(207);
   NOption(208,Node011,004);
   NOption(209,Node999,004);
end

procedure Node005 begin
   if (global_var(GVAR_ABBEY_BOOZE) == 0) then
      set_global_var(GVAR_ABBEY_BOOZE,1);

   inc_local_var( LVAR_Abbey_Alcohol );
   drinks_this_conversation += 1;

   if( (local_var( LVAR_Abbey_Alcohol ) > 3) and (dude_agility < 7) ) then begin
      call Node006;
   end else begin
      call Node004;
   end
end

procedure Node006 begin
set_global_var( GVAR_ALCOHOL_ADDICT, 1 );
Reply(210);
NOption(211,Node997,004);
end

procedure Node007 begin
Reply(213);
NOption(214,Node999,004);
end

procedure Node008 begin
floater(random(500, 502));
end

procedure Node997 begin
float_msg( dude_obj, mstr(400), FLOAT_MSG_GREEN);
 reg_anim_clear(dude_obj);
         reg_anim_begin();
            reg_anim_animate(dude_obj,ANIM_hit_from_front,-1);
            reg_anim_animate(dude_obj,ANIM_fall_back,5);
            reg_anim_animate(dude_obj,ANIM_fall_back_sf,-1);
         reg_anim_end();
end

procedure Node999 begin
   if (drinks_this_conversation > 0) then begin
      flush_add_timer_event_sec(self_obj, 0, 999);
   end
end

procedure Node998 begin
  set_global_var(GVAR_TOWN_REP_ABBEY, -30);
end

procedure destroy_p_proc
begin
   inc_good_critter
end

procedure Node009 begin
Reply(600);
NOption(601,Node003,004);
NOption(602,Node999,004);
end

procedure Node010 begin
   Reply(700);
   NOption(701,Node901,004);
   NOption(702,Node999,004);
end

procedure Node901 begin
   set_global_var(GVAR_ABBEY_LIGHTER,1);
end

procedure Node011 begin
   Reply(215);
   if (dude_caps >= 20) then
     NOption(216,Node012,004);
   else
     NOption(219,Node999,004);
   NOption(217,Node999,004);
end

procedure Node012 begin
   dude_caps_adjust( -20);
   Reply(218);
   NOption(205,Node005,004);
   NOption(253,Node005a,004);
end

procedure Node013 begin
   if (local_var(LVAR_Said_Thanks) == 0) then begin
      Reply(500);
      set_local_var(LVAR_Said_Thanks,1);
   end else
      Reply(501);

   NOption(250,Node014,004);
   NOption(251,Node999,004);
end

procedure Node014 begin
   Reply(252);
   if (dude_caps >= 20) then
     NOption(216,Node012,004);
   else
     NOption(219,Node999,004);
   NOption(206,Node999,004);
end

procedure Node015 begin
   Reply(810);
   NOption(811,Node016,001);
   NOption(812,Node999,001);
end

procedure Node016 begin

   set_local_var(LVAR_borrow_light, 1);
   set_global_var(GVAR_ABBEY_LIGHTER,3);
   if (dude_item_count(PID_LIGHTER)) then
      remove_pid_qty(dude_obj, PID_LIGHTER, 1)
   Reply(202);
   NOption(820,Node009,001);

end

procedure Node005a begin
   variable item;
   if (global_var(GVAR_ABBEY_BOOZE) == 0) then
      set_global_var(GVAR_ABBEY_BOOZE,1);
   item:=create_object(PID_ABBEY_BOOZE,0,0);
   add_obj_to_inven(dude_obj,item);

   if (global_var(GVAR_ABBEY_BOOZE) == 2) then
      set_global_var(GVAR_ABBEY_BOOZE,3);
end
