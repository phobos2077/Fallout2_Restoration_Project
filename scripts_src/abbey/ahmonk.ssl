/*
  ***   Script conwerted to Interplay format by Fallout Script Edytor (by Jargo)  ***
*/

#include "../headers/define.h"

#define NAME                    SCRIPT_AHMONK          //Script name from scripts.h
#define TOWN_REP_VAR            GVAR_TOWN_REP_ABBEY          //Global variable name for this Town

#include "../headers/command.h"
#include "../headers/modreact.h"

#define LVAR_Flags                      (0)
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_stolen_before              (7)
#define LVAR_has_book                   (8)
#define LVAR_Learn_Of_Founders          (9)
#define LVAR_Explained_Knowledge        (10)
#define LVAR_Find_Book                  (11)

procedure start;
procedure description_p_proc;
procedure destroy_p_proc;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure timed_event_p_proc;
procedure talk_p_proc;


procedure Node001a;
procedure Node001;
procedure Node002;
procedure Node003a;
procedure Node003b;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node006a;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node014a;
procedure Node014b;
procedure Node014c;
procedure Node015;
procedure Node016;
procedure Node016a;
procedure Node016b;
procedure Node016c;
procedure Node016d;
procedure Node017;
procedure Node017a;
procedure Node018;
procedure Node018a;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
procedure Node023;
procedure Node024;
procedure Node024b;
procedure Node025;

procedure Node026;
procedure Node027;
procedure Node028;
procedure Node029;
procedure Node030;

procedure Node997;  // Open door
procedure Node999; // Exit dialog

variable more_info := 0;

procedure start
begin
end

procedure critter_p_proc begin
   if ( (global_var(GVAR_TOWN_REP_ABBEY) == -30) and (obj_can_see_obj(self_obj,dude_obj))) then begin
      attack(dude_obj);
   end
end

procedure look_at_p_proc
begin
 script_overrides;
 display_msg(mstr(100));
end

procedure description_p_proc
begin
 script_overrides;
 display_msg(mstr(101));
end

procedure timed_event_p_proc
begin
 if (fixed_param == 1) and
    (not combat_is_initialized) and
    (self_can_see_dude) and
    (self_distance_from_dude <= 4) then
  call Node001;
 add_timer_event(self_obj, random(5,10)*ONE_GAME_SECOND, 1);
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_personal_enemy;
      if (global_var(GVAR_TOWN_REP_ABBEY) != -30) then
         set_global_var(GVAR_TOWN_REP_ABBEY, -30);
   end
end

procedure talk_p_proc begin
   more_info := 0;
   if (not dude_is_stupid) then begin
      //start_gdialog(NAME,self_obj,4,ABBEY_HEADMONK,BACKGROUND_CATHEDRAL);
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_start;
      call Node001a;
      gSay_end;
      end_dialogue;
   end else begin
      if (global_var(GVAR_ABBEY_BEAST) == 2) then begin
         //start_gdialog(NAME,self_obj,4,ABBEY_HEADMONK,BACKGROUND_CATHEDRAL);
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_start;
            call Node009;
         gSay_end;
         end_dialogue;
      end else if (global_var(GVAR_ABBEY_BEAST) == 3) then begin
         //start_gdialog(NAME,self_obj,4,ABBEY_HEADMONK,BACKGROUND_CATHEDRAL);
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_start;
            call Node028;
         gSay_end;
         end_dialogue;
      end else begin
         float_msg(self_obj, mstr(203), FLOAT_MSG_LIGHT_RED);
      end
   end
   set_local_var(LVAR_Herebefore,1);
end

procedure map_enter_p_proc
begin
 critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_ABBEY);
 add_timer_event(self_obj, random(5,10)*ONE_GAME_SECOND, 1);
end

procedure destroy_p_proc
begin
 inc_good_critter
end

procedure pickup_p_proc
begin
 if (source_obj == dude_obj) then begin
  if (local_var(LVAR_stolen_before) == 1) then begin
   set_global_var(GVAR_TOWN_REP_ABBEY, -30);
   attack(dude_obj);
  end else begin
   float_msg(self_obj, mstr(500), FLOAT_MSG_RED);
   set_local_var(LVAR_stolen_before, 1);
  end
 end
end

procedure fNode
begin
 floater_rand(200, 202);
end

procedure Node001a
begin
   if ((global_var(GVAR_ABBEY_BEAST) == 2) and (local_var(LVAR_Herebefore) == 1)) then
       call Node009;
   else
       call Node001;
end

procedure Node001
begin

 Reply(300 + (2700*more_info));

 if (global_var(GVAR_ABBEY_PUMP) == PUMP_FIXED) then
   NOption(600,Node016,4);
 NOption(301,Node002,4);
 NOption(302,Node003a,4);
 NOption(303,Node004,4);
 if (global_var(GVAR_ABBEY_PUMP) < PUMP_QUEST_GIVEN) then
   NOption(444,Node018,4);
 if (global_var(GVAR_ABBEY_PUMP) == PUMP_FOUND_PROBLEM) then
   NOption(445,Node020,4);
 NOption(304,Node999,4);
end

procedure Node002
begin
 Reply(400);
 NOption(430,Node015,4);
 NOption(431,Node014,4);
 NOption(432,Node001,4);

end

procedure Node003a
begin
   if (global_var(GVAR_ABBEY_BEAST) > 2) then
       call Node013;
   else
       call Node003b;
end

procedure Node003b
begin
 Reply(402);
 NOption(403,Node005,4);
end

procedure Node004
begin
 Reply(404);
 NOption(405,Node001,4);
end

procedure Node005
begin
 Reply(406);
 NOption(407,Node006,4);
end

procedure Node006
begin
 Reply(408);
 if (global_var(GVAR_ABBEY_BEAST) == 2) then
   NOption(1100,Node030,4);
 else
   NOption(4091,Node006a,4);
end

procedure Node006a begin
 Reply(4092);
 NOption(409,Node007,4);
 NOption(410,Node007,4);
 NOption(1411,Node999,4);
end

procedure Node007
begin
 Reply(411);
 GOption(412,Node008,4);
 NOption(413,Node999,4);
end

procedure Node008
begin
   set_global_var(GVAR_ABBEY_BEAST,1);
   set_local_var(LVAR_Find_Book,1);
   Reply(414);
   NOption(415,Node999,4);
end

procedure Node009 begin
   Reply(416);

   NLowOption(1000,Node026);

   if (local_var(LVAR_Find_Book)) then begin
      if ((obj_is_carrying_obj_pid(dude_obj,PID_ABBEY_MISSING_BOOK))) then
         NOption(417,Node010,4);
      else
         NOption(4177,Node017,4);
   end else begin
      NOption(1100,Node030,4);
   end
end

procedure Node010
begin
 Reply(418);
 GOption(419,Node011,4);
 BOption(420,Node012,4);
end

procedure Node011 begin
   variable item;
   more_info := 1;

   item:=obj_carrying_pid_obj(dude_obj, PID_ABBEY_MISSING_BOOK);
   rm_obj_from_inven(dude_obj,item);
   destroy_object(item);
   //give_pid_qty(dude_obj,140,1)
   set_global_var(GVAR_ABBEY_BEAST, 4);
   inc_global_var_amt(GVAR_TOWN_REP_ABBEY,10);
   inc_general_rep(5);

   Reply(421);
   NOption(422,Node997,4);
   NOption(1113,Node001,4);
end

procedure Node012 begin
   variable item;
   more_info := 1;

   item:=obj_carrying_pid_obj(dude_obj, PID_ABBEY_MISSING_BOOK);
   rm_obj_from_inven(dude_obj,item);
   give_pid_qty(dude_obj,41,250)
   //give_pid_qty(dude_obj,140,1)
   set_global_var(GVAR_ABBEY_BEAST, 4);
   inc_global_var_amt(GVAR_TOWN_REP_ABBEY,3);
   inc_general_rep(3);
   Reply(423);
   NOption(424,Node999,4);
   NOption(1113,Node001,4);
end

procedure Node013
begin
 Reply(425);
 NOption(426,Node999,4);
end

procedure Node014
begin
 Reply(434);
 NOption(435,Node014a,4);
 NOption(436,Node001,4);
end

procedure Node014a
begin
 Reply(437);
 NOption(438,Node014b,4);
end

procedure Node014b
begin
 Reply(439);
 NOption(440,Node002,4);
 if((dude_iq > 5)and(has_skill(dude_obj, SKILL_CONVERSANT) > 49)) then begin
 NOption(441,Node014c,4);
end
end

procedure Node014c
begin
 Reply(442);
 //if (local_var(LVAR_Learn_Of_Founders) == 0) then
 //  NOption(446,Node025,4);
 NOption(443,Node002,4);
end

procedure Node015
begin
 Reply(433);
 if ( (dude_iq >= 6) and (local_var(LVAR_Explained_Knowledge) == 0) ) then
   NOption(448,Node022,4);
 NOption(401,Node002,4);
end

procedure Node999
begin
end

procedure Node997
begin
   give_exp_points(200);
   display_msg(mstr(501));
end

procedure Node016
begin
   Reply(601);
   BOption(602,Node016b,4);
   BOption(6020,Node016b,4);
   NOption(603,Node016a,4);
   NOption(604,Node016c,4);

   set_global_var(GVAR_ABBEY_PUMP,PUMP_REPORTED);
end

procedure Node016a begin
   give_xp(50);
   inc_general_rep(5);
   inc_global_var_amt(GVAR_TOWN_REP_ABBEY,5);
   call Node002;
end

procedure Node016b begin
   Reply(605);
   BOption(606,Node016d,4);
end

procedure Node016c begin
   give_xp(50);
   inc_general_rep(2);
   inc_global_var_amt(GVAR_TOWN_REP_ABBEY,5);
end

procedure Node016d begin
   item_caps_adjust(dude_obj, 50);
   inc_general_rep(-2);
   inc_global_var_amt(GVAR_TOWN_REP_ABBEY,-5);
end

procedure Node017 begin
   Reply(4188);
   BOption(700,Node017a,4);
end

procedure Node017a begin
   //give_pid_qty(dude_obj,140,1)
   more_info := 1;
   set_global_var(GVAR_ABBEY_BEAST, 3);
   inc_global_var_amt(GVAR_TOWN_REP_ABBEY,5);
   inc_general_rep(3);
   give_xp(50);

   Reply(701);
   NOption(702,Node999,4);
   NOption(1113,Node001,4);

end

procedure Node018 begin
   Reply(800);
   NOption(801,Node018a,4);
   BOption(802,Node019,4);
   NOption(803,Node002,4);
end

procedure Node018a begin
    set_global_var(GVAR_ABBEY_PUMP,PUMP_QUEST_GIVEN);
end

procedure Node019 begin
   Reply(804);
   BOption(805,Node018a,4);
end

procedure Node020 begin
   Reply(806);
   NOption(807,Node021,4);
end

procedure Node021 begin
   Reply(808);
   NOption(809,Node999,4);
end

procedure Node022 begin
   Reply(449);
   NOption(450,Node023,4);
end

procedure Node023 begin
   Reply(451);
   if (has_skill(dude_obj, SKILL_CONVERSANT) > 90) then
      NOption(452,Node024,4);
   else
      NOption(453,Node024b,4);
end

procedure Node024 begin
   Reply(455);
   NOption(456,Node002,4);
   display_msg(mstr(900));
   give_xp(200);
   inc_general_rep(2);
   set_local_var(LVAR_Explained_Knowledge,1);
end

procedure Node024b begin
   Reply(454);
   NOption(457,Node002,4);
   set_local_var(LVAR_Explained_Knowledge,2);
end

procedure Node025 begin
   Reply(447);
   NOption(401,Node002,4);
   display_msg(mstr(901));
   give_xp(200);
   set_local_var(LVAR_Learn_Of_Founders,1);
end

procedure Node026 begin
   Reply(1010 + dude_is_female);
   NOption(1012,Node027,1);
end

procedure Node027 begin
   Reply(1020);
   if ((obj_is_carrying_obj_pid(dude_obj,PID_ABBEY_MISSING_BOOK))) then begin
      NOption(1021,Node029,1);
   end else begin
      set_global_var(GVAR_ABBEY_BEAST, 3);
      NOption(1022,Node999,1);
   end
end

procedure Node028 begin

   Reply(416);
   if ((obj_is_carrying_obj_pid(dude_obj,PID_ABBEY_MISSING_BOOK))) then
      NOption(1030,Node029,1);

end

procedure Node029 begin
variable item;
   item:=obj_carrying_pid_obj(dude_obj, PID_ABBEY_MISSING_BOOK);
   rm_obj_from_inven(dude_obj,item);
   destroy_object(item);

   //give_pid_qty(dude_obj,140,1)
   set_global_var(GVAR_ABBEY_BEAST, 4);
   inc_global_var_amt(GVAR_TOWN_REP_ABBEY,10);
   inc_general_rep(5);
   give_exp_points(200);
   display_msg(mstr(501));

   Reply(1031 + dude_is_female);
   NOption(1033,Node999,1);
end

procedure Node030 begin
   Reply(1110);
   if ((obj_is_carrying_obj_pid(dude_obj,PID_ABBEY_MISSING_BOOK))) then
      NOption(1112,Node010,4);
   else
      NOption(1111,Node017a,4);
end