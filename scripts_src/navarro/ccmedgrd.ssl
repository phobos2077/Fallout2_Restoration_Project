/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Med Lab Guard
        Location: Navarro
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: Sept 10, 1998
*/

/* Include Files */

#define SCRIPT_REALNAME "ccmedgrd"
#include "../headers/define.h"
//#include "../headers/navdef.h"
#include "../headers/navarro.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_CCMEDGRD
#define TOWN_REP_VAR            GVAR_TOWN_REP_NAVARRO

#include "../headers/command.h"
#include "../headers/modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_second_look                (7)

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable said_intruder:=0;
variable i_am_a_warrior:=1;
variable Force_Deathclaw := 0;

procedure start begin
end

procedure map_enter_p_proc begin
   Only_Once:=0;
   Force_Deathclaw := 0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NAVARRO);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_NAV_GUARD);
end

procedure critter_p_proc begin
   if (map_var(MVAR_Door) == 1) then begin
      set_map_var( MVAR_Door, 0 );
      Force_Deathclaw := 1;
      dialogue_system_enter;
   end
   else if (obj_can_see_obj(self_obj,dude_obj)) then begin
      if (local_var(LVAR_Hostile) == 2) then begin
         set_local_var(LVAR_Hostile,1);
         attack(dude_obj);
      end
      else if (global_var(GVAR_NAVARRO_BASE_ALERT) > 0) then begin
         set_local_var(LVAR_Hostile,1);
         attack(dude_obj);
      end
      else if( not_alone ) then begin
         if( said_intruder == 0 ) then begin
            said_intruder:=1;
            call Node003;
         end
      end
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      call Node998;
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      call Node998;
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   GetReaction;

   if( Force_Deathclaw == 1 ) then begin
      Force_Deathclaw := 0;
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node006;
      gSay_End;
      end_dialogue;
   end
   else if (global_var(GVAR_NAVARRO_BASE_ALERT) > 0) then begin
      call Node004;
      call Node998;
   end
   else if( not_alone ) then begin //dude is not alone
      call Node003;
   end
   else if( not_wearing_power_armor ) then begin //not wearing power armor
      call Node002;
   end
   else if (map_var(MVAR_Snookie) == 1) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node008;
      gSay_End;
      end_dialogue;
   end
   else begin
      call Node001;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
/* Increment the aligned critter counter*/
   set_map_var( MVAR_Med_Guard_Dead, 1 );
   inc_evil_critter
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_second_look) == 0) then begin
      set_local_var(LVAR_second_look,1);
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(101));
   end
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
   //if (action_being_used == SKILL_STEAL) then begin
      //if (is_success(roll_vs_skill(dude_obj,action_being_used,0))) then begin
      //end
   //end
end

procedure Node001 begin
   floater(random(103,105));
end

procedure Node002 begin
   floater(random(106,108));
end

procedure Node003 begin
   set_global_var(GVAR_NAVARRO_BASE_ALERT,1);
   floater(random(109,111));
end

procedure Node004 begin
   said_intruder:=1;
   floater(random(112,114));
end

procedure Node005 begin
   floater(random(115,117));
end

procedure Node006 begin
   Reply(118);

   if (map_var(MVAR_Exterminate) == 1) then begin
      NLowOption(119,Node007);
      NOption(121,Node007,004);
   end
   NLowOption(120,Node999);
   NOption(128,Node999,004);
end

procedure Node007 begin
   set_map_var( MVAR_Deathclaw_Allowed, 1 );
   Reply(122);

   NOption(123,Node999,001);
end

procedure Node008 begin
   Reply(124);

   NLowOption(125,Node009);
   NOption(126,Node009,004);
end

procedure Node009 begin
   Reply(127);
   NOption(128,Node999,001);
end

procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
   set_global_var(GVAR_NAVARRO_BASE_ALERT,1);
end
/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end