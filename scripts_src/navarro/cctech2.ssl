/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Com Tech 2
        Location: Navarro
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: Sept 10, 1998
*/

/* Include Files */

#define SCRIPT_REALNAME "cctech2"
#include "../headers/define.h"
//#include "../headers/navdef.h"
#include "../headers/navarro.h"

#define NAME                    SCRIPT_CCTECH2
#define TOWN_REP_VAR            GVAR_TOWN_REP_NAVARRO

#include "../headers/sanfran.h"
#include "../headers/command.h"
#include "../headers/modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_second_look                (7)
#define LVAR_02_before                  (8)
#define LVAR_q1                         (9)
#define LVAR_q2                         (10)
#define LVAR_q3                         (11)

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable said_intruder:=0;

procedure start begin
end

procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_NAVARRO);
   set_self_ai(AI_COWARD);
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      if (local_var(LVAR_Hostile) == 2) then begin
         call Node015;
         set_local_var(LVAR_Hostile,1);
         attack(dude_obj);
      end
      else if (global_var(GVAR_NAVARRO_BASE_ALERT) > 0) then begin
         if (said_intruder == 0) then
            call Node014;
         // This guy is a wimp. He's supposed to run.
         //set_local_var(LVAR_Hostile,1);
         //attack(dude_obj);
      end
      else if( not_alone ) then begin
         call Node013;
      end
   end
end

procedure damage_p_proc begin
   set_local_var(LVAR_Hostile,2);
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      call Node998;
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   GetReaction;

   if(global_var(GVAR_NAVARRO_BASE_ALERT) > 0) then begin
      call Node014;
   end
   else if( not_wearing_power_armor ) then begin
      call Node012;
   end
   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
          call Node001;
       gSay_End;
       end_dialogue;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
/* Increment the aligned critter counter*/
   inc_evil_critter
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_second_look) == 0) then begin
      set_local_var(LVAR_second_look,1);
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(101));
   end
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
   //if (action_being_used == SKILL_STEAL) then begin
      //if (is_success(roll_vs_skill(dude_obj,action_being_used,0))) then begin
      //end
   //end
end

procedure Node001 begin
   Reply(103);

   NLowOption(104,Node002);
   NLowOption(105,Node999);
   NOption(106,Node002,004);
   NOption(107,Node999,004);
end

procedure Node002 begin
   if (local_var(LVAR_02_before) == 0) then begin
      set_local_var(LVAR_02_before,1);
      Reply(108);
   end
   else begin
      Reply(109);
   end

   if (local_var(LVAR_q1) == 0) then
      NLowOption(110,Node003);
   if (local_var(LVAR_q2) == 0) then
      NLowOption(111,Node004);
   if (local_var(LVAR_q3) == 0) then
      NLowOption(112,Node005);
   if ( took_plans_quest ) then
      NLowOption(113,Node008);
   NLowOption(117,Node999);

   if (local_var(LVAR_q1) == 0) then
      NOption(118,Node003,004);
   if (local_var(LVAR_q2) == 0) then
      NOption(119,Node004,004);
   if (local_var(LVAR_q3) == 0) then
      NOption(120,Node005,004);
   if ( took_plans_quest ) then
      NOption(121,Node008,004);
   NOption(125,Node999,004);
end

procedure Node003 begin
   set_local_var(LVAR_q1,1);
   Reply(126);

   NLowOption(127,Node002);
   NOption(128,Node002,004);
end

procedure Node004 begin
   set_local_var(LVAR_q2,1);
   Reply(129);

   NLowOption(130,Node002);
   NOption(131,Node002,004);
end

procedure Node005 begin
   set_local_var(LVAR_q3,1);
   Reply(132);

   NLowOption(133,Node002);
   NOption(134,Node006,004);
   NOption(135,Node002,004);
end

procedure Node006 begin
   if( is_success( roll_vs_skill( dude_obj, SKILL_SPEECH, -20 ) ) ) then begin
      Reply(136);

      NOption(138, Node007, 004);
   end
   else begin
      Reply(137);

      NOption(139, Node002, 004);
   end
end

procedure Node007 begin
   Reply(140);

   NLowOption(141,Node002);
   NOption(142,Node002,004);
end

procedure Node008 begin
   //set plans
   set_map_var(MVAR_Plans,1);
   Reply(143);

   NLowOption(144,Node002);
   NOption(145,Node002,004);
end

procedure Node009 begin
   // Removed.
end

procedure Node010 begin
   // Removed.
end

procedure Node011 begin
   // Removed.
end

procedure Node012 begin
   floater(random(155,157));
end

procedure Node013 begin
   set_global_var(GVAR_NAVARRO_BASE_ALERT,1);
   floater(random(158,160));
end

procedure Node014 begin
   floater(random(161,163));
end

procedure Node015 begin
   floater(random(164,166));
end

procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
   set_global_var(GVAR_NAVARRO_BASE_ALERT,1);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end
