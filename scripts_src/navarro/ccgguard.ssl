/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Navarro Gate Guard (new)
        Location: Navarro
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: Sept 10, 1998
*/

/* Include Files */

#define SCRIPT_REALNAME "ccgguard"
#include "../headers/define.h"
//#include "../headers/navdef.h"
#include "../headers/navarro.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_CCGGUARD
#define TOWN_REP_VAR            GVAR_TOWN_REP_NAVARRO

#include "../headers/command.h"
#include "../headers/modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node007a;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;

procedure Node013; //added by killap

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_second_look                (7)
#define LVAR_Shoot                      (8)
#define LVAR_Pass                       (9)
#define LVAR_Groupbefore                (10)
#define LVAR_Gave_Pass                  (11) //added by killap

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable said_intruder:=0;
variable talk_to_dude:=0;

procedure start begin
end

procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_NAVARRO);
   set_self_ai(AI_NAV_GUARD);
end

procedure timed_event_p_proc begin
   if( fixed_param == 999 ) then
      floater( random( 200, 203 ) );
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      if (local_var(LVAR_Hostile) == 2) then begin
         set_local_var(LVAR_Hostile,1);
         attack(dude_obj);
      end
      else if (global_var(GVAR_NAVARRO_BASE_ALERT) > 0) then begin
         if (said_intruder == 0) then
            call Node012;
         set_local_var(LVAR_Hostile,1);
         attack(dude_obj);
      end
      else if (map_var(MVAR_Gate) == 1) then begin
         call Node998;
      end
      else if ( (local_var(LVAR_Herebefore) == 0) and (tile_distance(self_tile, dude_tile) < 5) )then begin
         talk_to_dude:=1;
         dialogue_system_enter;
      end
   end //can see dude
   //don't converge on player as I must watch the gate for other intruders.
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      call Node998;
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      call Node998;
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   variable background; // added by killap - RP

   GetReaction;

   //added by killap - RP
   if ((game_time_hour >= 2000) or (game_time_hour < 600)) then
      background := BACKGROUND_WASTELAND_NIGHT;
   else
      background := BACKGROUND_WASTELAND;
   //end added

   //added by killap
   if (local_var(LVAR_Gave_Pass) == 1) then begin
      call Node013;
   end //end added
   else if (global_var(GVAR_NAVARRO_BASE_ALERT) > 0) then begin
      call Node012;
      call Node998;
   end
   else if (talk_to_dude == 1) then begin
      talk_to_dude:=0;
         set_local_var(LVAR_Herebefore,1);
         start_gdialog(NAME,self_obj,4,HEAD_COM_OFFICER,background);
         gSay_Start;
            call Node001;
         gSay_End;
         end_dialogue;
   end
   else if (local_var(LVAR_Shoot) == 1) then begin
      start_gdialog(NAME,self_obj,4,HEAD_COM_OFFICER,background);
      gSay_Start;
         call Node011;
      gSay_End;
      end_dialogue;
   end
   else if (local_var(LVAR_Pass) == 1) then begin
      start_gdialog(NAME,self_obj,4,HEAD_COM_OFFICER,background);
      gSay_Start;
         call Node009;
      gSay_End;
      end_dialogue;
   end
   //Node 8 is only for player who is new recruit and has been here before with group
   else if (local_var(LVAR_Groupbefore) == 1) then begin
      start_gdialog(NAME,self_obj,4,HEAD_COM_OFFICER,background);
      gSay_Start;
         call Node008;
      gSay_End;
      end_dialogue;
   end
   //Cant branch on party_size because the two nodes which handle this
   //are only for new recruits and they give away the password.
   //
   //else if (party_size > 0) then begin //dude is not alone
   //   call Node003;
   //end
   //else if (worn == 0) then begin //not wearing power armor
   //   call Node002;
   //end
   else begin
      if (local_var(LVAR_Herebefore) == 0) then begin
         set_local_var(LVAR_Herebefore,1);
         start_gdialog(NAME,self_obj,4,HEAD_COM_OFFICER,background);
         gSay_Start;
            call Node001;
         gSay_End;
         end_dialogue;
      end
      else begin
         start_gdialog(NAME,self_obj,4,HEAD_COM_OFFICER,background);
         gSay_Start;
            call Node005;
         gSay_End;
         end_dialogue;
      end
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
/* Increment the aligned critter counter*/
   inc_evil_critter
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_second_look) == 0) then begin
      set_local_var(LVAR_second_look,1);
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(101));
   end
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
   //if (action_being_used == SKILL_STEAL) then begin
      //if (is_success(roll_vs_skill(dude_obj,action_being_used,0))) then begin
      //end
   //end
end

procedure Node001 begin
   Reply(103);

   NLowOption(104,Node002);
   NLowOption(105,Node004);
   if (map_var(MVAR_Replace) == 1) then
      NLowOption(106,Node006);
   NLowOption(107,Node999);

   NOption(108,Node004,004);
   if (map_var(MVAR_Replace) == 1) then
      NOption(109,Node006,004);
   NOption(110,Node999,004);
end

procedure Node002 begin
   Reply(111);

   NLowOption(112,Node003);
   NLowOption(113,Node999);
end

procedure Node003 begin
   Reply(114);

   BLowOption(115,Node998);
   NLowOption(116,Node999);
end

procedure Node004 begin
   set_local_var(LVAR_Pass,1);
   Reply(117);

   NLowOption(118,Node005);
   BLowOption(119,Node998);
   NOption(120,Node005,004);
   BOption(121,Node998,004);
end

procedure Node005 begin
   Reply(122);

   NOption(123,Node999,001);
end

procedure Node006 begin
   if( not_alone ) then begin //dude is not alone
      set_local_var(LVAR_Groupbefore,1);
      Reply(125);

      NOption(127,Node999,001);
   end
   else begin
      Reply(124);

      NOption(126,Node007,001);
   end
end

procedure Node007 begin
   set_local_var(LVAR_Gave_Pass,1); //added by killap
   set_map_var(MVAR_Gate,2); //turn off gate spatial alarm
   if (dude_iq < 4) then begin
      NMessage(128);
      call Node007a;
   end
   else begin
      Reply(128);

      NOption(130,Node999,001);
   end
end

procedure Node007a begin
   Reply(129);

   NOption(130,Node999,001);
end

procedure Node008 begin
   if( not_alone ) then begin //dude is not alone
      set_local_var(LVAR_Shoot,1);
      Reply(132);

      NOption(134,Node999,001);
   end
   else begin
      Reply(131);

      NOption(133,Node007,001);
   end
end

procedure Node009 begin
   Reply(135);

   NLowOption(136,Node010);
   BLowOption(137,Node998);
   if (map_var(MVAR_Replace) == 1) then
      NLowOption(133,Node007);
   NOption(138,Node010,004);
   BOption(139,Node998,004);
   if (map_var(MVAR_Replace) == 1) then
      NOption(133,Node007,004);
end

procedure Node010 begin
   set_local_var(LVAR_Shoot,1);
   Reply(140);

   NLowOption(141,Node999);
   BLowOption(142,Node998);
   if (map_var(MVAR_Replace) == 1) then
      NLowOption(133,Node007);
   NOption(143,Node999,004);
   BOption(144,Node998,004);
   if (map_var(MVAR_Replace) == 1) then
      NOption(133,Node007,004);
end

procedure Node011 begin
   Reply(145);

   BOption(146,Node998,001);
end

procedure Node012 begin
   said_intruder:=1;
   floater(random(147,149));
end

//added by killap
procedure Node013 begin
   floater(300);
end
//end added

procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
   set_global_var(GVAR_NAVARRO_BASE_ALERT,1);
end
/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end
