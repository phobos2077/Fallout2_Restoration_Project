/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Quartermaster
        Location: Navarro
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: Sept 10, 1998
*/

/* Include Files */

#define SCRIPT_REALNAME "ccqmstr"
#include "../headers/define.h"
//#include "../headers/navdef.h"
#include "../headers/navarro.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_CCQMSTR
#define TOWN_REP_VAR            GVAR_TOWN_REP_NAVARRO

#include "../headers/command.h"
#include "../headers/modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_second_look                (7)
#define LVAR_Said_Intruder              (8)
#define LVAR_Suited_Up                  (9)

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NAVARRO);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_NAV_GUARD);
end

procedure critter_p_proc begin
   if (obj_can_see_obj(self_obj,dude_obj)) then begin
      if (local_var(LVAR_Hostile) == 2) then begin
         set_local_var(LVAR_Hostile,1);
         attack(dude_obj);
      end
      else if (global_var(GVAR_NAVARRO_BASE_ALERT) > 0) then begin
         if (local_var(LVAR_Said_Intruder) == 0) then begin
            start_gdialog(NAME,self_obj,4,-1,-1);
            gSay_Start;
              call Node008;
            gSay_End;
            end_dialogue;
         end
         else begin
            set_local_var(LVAR_Hostile,1);
            attack(dude_obj);
         end
      end
   end //can see dude
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      call Node998;
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      call Node998;
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   GetReaction;

   if (global_var(GVAR_NAVARRO_BASE_ALERT) > 0) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
        call Node008;
      gSay_End;
      end_dialogue;
   end
   else if (map_var(MVAR_Get_Armor) == 1) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
        call Node007;
      gSay_End;
      end_dialogue;
   end
   else begin
      if (local_var(LVAR_Herebefore) == 0) then begin
         set_local_var(LVAR_Herebefore,1);
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
           call Node001;
         gSay_End;
         end_dialogue;
      end
      else begin
         call Node006;
      end
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
/* Increment the aligned critter counter*/
   inc_evil_critter
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_second_look) == 0) then begin
      set_local_var(LVAR_second_look,1);
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(101));
   end
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
   //if (action_being_used == SKILL_STEAL) then begin
      //if (is_success(roll_vs_skill(dude_obj,action_being_used,0))) then begin
      //end
   //end
end

procedure Node001 begin
   if( not_alone ) then begin //dude is not alone
      //set alarm
      Reply(103);

      BOption(106,Node998,001);
   end
   else begin
      if ( ( obj_pid( critter_inven_obj( dude_obj, INVEN_TYPE_WORN ) ) == PID_POWERED_ARMOR ) or
           ( obj_pid( critter_inven_obj( dude_obj, INVEN_TYPE_WORN ) ) == PID_HARDENED_POWER_ARMOR ) ) then begin
         Reply(104);

         NOption(107,Node004,001);
      end
      else if( ( obj_pid( critter_inven_obj( dude_obj, INVEN_TYPE_WORN ) ) != PID_ADVANCED_POWER_ARMOR ) and
               ( obj_pid( critter_inven_obj( dude_obj, INVEN_TYPE_WORN ) ) != PID_ADVANCED_POWER_ARMOR_MK2 ) ) then begin
         Reply(105); //Not in poper uniform as where to go's should have caught this

         NLowOption(108,Node002);
         if (map_var(MVAR_Replace) == 1) then
            NLowOption(109,Node004);
         BLowOption(110,Node998); // Seraph's Fix: BLowOption only takes 2 args. Thanks Rob!
         NOption(111,Node002,004);
         if (map_var(MVAR_Replace) == 1) then
            NOption(112,Node004,004);
         BOption(113,Node998,004);
      end
      else begin
         NMessage(124);
      end
   end
end

procedure Node002 begin
   Reply(114);

   NLowOption(115,Node003);
   NOption(116,Node003,004);
end

procedure Node003 begin
   if (dude_iq < 4) then begin
      Reply(117);
   end
   else begin
      Reply(118);
   end

   NLowOption(119,Node004);
   NOption(120,Node004,004);
end

procedure Node004 begin
   Reply(121);

   NOption(122,Node005,001);
end

procedure Node005 begin
   NMessage(123);
end

procedure Node006 begin
   floater(124);
end

procedure Node007 begin
   //set_local_var( LVAR_Suited_Up, 1 );
   if( ( local_var( LVAR_Suited_Up ) == 0 ) and
       ( obj_pid(critter_inven_obj(dude_obj,INVEN_TYPE_WORN)) != PID_ADVANCED_POWER_ARMOR ) and
       ( obj_pid(critter_inven_obj(dude_obj,INVEN_TYPE_WORN)) != PID_ADVANCED_POWER_ARMOR_MK2 ) ) then begin
      Reply(125);

      NOption(126,Node004,001);

      set_local_var( LVAR_Suited_Up, 1 ); //moved here by killap
   end
   else
      NMessage(124);
end

procedure Node008 begin
   set_local_var(LVAR_Said_Intruder,1);
   Reply(127);

   BLowOption(128,Node009);
   BLowOption(129,Node998);
   BOption(130,Node009,004);
   BOption(131,Node998,004);
end

procedure Node009 begin
   Reply(132);

   BOption(133,Node998,001);
end

procedure Node010 begin
end

procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
   set_global_var(GVAR_NAVARRO_BASE_ALERT,1);
end
/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end