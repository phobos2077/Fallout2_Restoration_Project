/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "..\headers\define.h"
//#include "..\headers\<TownName.h>"

#define NAME                    SCRIPT_ACNAGOR
#define TOWN_REP_VAR            (GVAR_TOWN_REP_ARROYO)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

#define RUNNING_TILE            (9114)

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node002a;
procedure Node003;
procedure Node003a;
procedure Node003b;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node006a;
procedure Node006b;
procedure Node007;
procedure Node007a;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Drop_Karma                 (7)
#define LVAR_Gave_Xp                    (8)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */

import variable Nagor_Tile;

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_ARROYO);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_ARROYO_VILLAGER);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

   if ( (REP_ENEMY_ARROYO) and (obj_can_see_obj(self_obj,dude_obj)) ) then begin //added and (obj_can_see_obj(self_obj,dude_obj)) by killap
       attack(dude_obj);
   end

   if (global_var(GVAR_ARROYO_DOG) == DOG_RUN_OFF) then begin
       animate_run_to_tile(RUNNING_TILE);
   end

   if (tile_num(self_obj) == RUNNING_TILE) then begin
       set_obj_visibility(self_obj,1);          // go invis
       move_to(self_obj,5000,2);
   end

   Nagor_Tile:=tile_num(self_obj);

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_global_var(GVAR_ENEMY_ARROYO,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_VERY_INTOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if (reached_tl_4) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node016;
       gSay_End;
       end_dialogue;
   end

   else if (reached_tl_3) then begin
       call Node015;
    end

   else if (reached_tl_2) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node014;
       gSay_End;
       end_dialogue;
   end

   else if (reached_tl_1) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node013;
       gSay_End;
       end_dialogue;
   end

   else if (dude_iq < 4) then begin //moved here by killap
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node012;
       gSay_End;
       end_dialogue;
   end

   else if (global_var(GVAR_ARROYO_DOG) == DOG_RUN_OFF) then begin
       floater(131);
   end

   else if (global_var(GVAR_ARROYO_DOG) == DOG_ACCEPTED) then begin
       set_local_var(LVAR_Herebefore,1);
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node005;
       gSay_End;
       end_dialogue;
   end

   else if (global_var(GVAR_ARROYO_DOG) == DOG_DEAD) then begin
       set_local_var(LVAR_Herebefore,1);
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node006;
       gSay_End;
       end_dialogue;
   end

   else if (global_var(GVAR_ARROYO_DOG) == DOG_RETURNED) then begin
       set_local_var(LVAR_Herebefore,1);
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node009;
       gSay_End;
       end_dialogue;
   end

   else if (global_var(GVAR_ARROYO_DOG) == DOG_FINISHED_GOOD) then begin
       call Node010;
   end

   else if (global_var(GVAR_ARROYO_DOG) == DOG_FINISHED_BAD) then begin
       call Node011;
   end

   else if (local_var(LVAR_Herebefore) == 0) then begin
       set_local_var(LVAR_Herebefore,1);
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node001;
       gSay_End;
       end_dialogue;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
   set_global_var(GVAR_ENEMY_ARROYO,1);
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if ((global_var(GVAR_ARROYO_DOG) >= DOG_FINISHED_GOOD) or (global_var(GVAR_ARROYO_DOG) == DOG_RETURNED)) then
       display_msg(mstr(300));
   else
       display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   if (dude_is_male) then
       Reply(102);
   else
       Reply(302);

   BOption(103,Node002,004);
   NOption(104,Node003,004);
end

procedure Node002 begin
   Reply(105);

   NOption(106,Node003,004);
   BOption(107,Node002a,004);
end

procedure Node002a begin
   set_global_var(GVAR_ARROYO_DOG,DOG_RUN_OFF);
   if (local_var(LVAR_Drop_Karma) == 0) then begin
       set_local_var(LVAR_Drop_Karma,1);
       inc_general_rep(REP_BONUS_ARROYO_LIED_ABOUT_DOG);
   end
end

procedure Node003 begin
   Reply(108);

   NOption(109,Node003a,004);
   NOption(110,Node003b,004);
   BOption(111,Node002,004);
end

procedure Node003a begin
   set_global_var(GVAR_ARROYO_DOG,DOG_ACCEPTED);
   call Node004;
end

procedure Node003b begin
   set_global_var(GVAR_ARROYO_DOG,DOG_ACCEPTED);
end

procedure Node004 begin
   Reply(112);

   NOption(113,Node999,004);
end

procedure Node005 begin
   Reply(114);

   NOption(115,Node004,004);
   NOption(116,Node999,004);
end

procedure Node006 begin
   Reply(117);

   BOption(118,Node006a,004);
   NOption(119,Node006b,004);
end

procedure Node006a begin
   if (local_var(LVAR_Drop_Karma) == 0) then begin
       set_local_var(LVAR_Drop_Karma,1);
       inc_general_rep(REP_BONUS_ARROYO_LIED_ABOUT_DOG);
   end
end

procedure Node006b begin
   set_global_var(GVAR_ARROYO_DOG,DOG_FINISHED_BAD);
   call Node007;
end

procedure Node007 begin
   Reply(120);

   NOption(121,Node008,004);
   BOption(122,Node007a,004);
end

procedure Node007a begin
   if (local_var(LVAR_Drop_Karma) == 0) then begin
       set_local_var(LVAR_Drop_Karma,1);
       inc_general_rep(REP_BONUS_ARROYO_LIED_ABOUT_DOG);
   end
   set_global_var(GVAR_ARROYO_DOG,DOG_RUN_OFF);
end

procedure Node008 begin
   Reply(123);

   NOption(124,Node999,004);
end

procedure Node009 begin
   set_global_var(GVAR_ARROYO_DOG,DOG_FINISHED_GOOD);
   if (local_var(LVAR_Drop_Karma) == 0) then begin
       set_local_var(LVAR_Drop_Karma,1);
       inc_general_rep(REP_BONUS_ARROYO_RETURNED_DOG);
       give_xp(EXP_RETURN_SMOKE);
   end

   Reply(125);

   NOption(126,Node999,004);
end

procedure Node010 begin
   floater(127);
end

procedure Node011 begin
   floater(128);
end

procedure Node012 begin
   Reply(129);

   NLowOption(130,Node999);
end

procedure Node013 begin
   if (dude_is_male) then
       Reply(200);
   else
       Reply(201);

   NLowOption(202,Node999);
   NOption(203,Node999,004);
end

procedure Node014 begin
   if (dude_is_male) then
       Reply(204);
   else
       Reply(205);

   NLowOption(206,Node999);
   NOption(207,Node999,004);
end

procedure Node015 begin
   if (dude_is_male) then
       floater(208);
   else
       floater(209);
end

procedure Node016 begin
   if (dude_is_male) then
       Reply(210);
   else
       Reply(211);

   NLowOption(212,Node017);
   NOption(213,Node017,004);
end

procedure Node017 begin
   if (dude_is_male) then
       NMessage(214);
   else
       NMessage(215);
end

//xxxxxxxxxxxxxxxxxxxx
