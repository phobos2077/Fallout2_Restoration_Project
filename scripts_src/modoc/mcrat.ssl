/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Garden Seed Rat Rat
        Location: Arroyo
        Description: Generic lesser rat when you need a small, not very strong rat.

        Log:

           Created: December 02, 1997

           Updated:
*/

/* Include Files */

#include "../headers/define.h"
#include "../headers/modgard.h"

#define NAME                    SCRIPT_MCRAT

#include "../headers/command.h"
#include "../headers/modoc.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure combat_p_proc;

#define POISON_MIN               (1)
#define POISON_MAX               (6)
#define RAD_MIN                  (2)
#define RAD_MAX                  (10)

#define is_mutated_rat           ((self_pid >= PID_MUTATED_RAT) and (self_pid <= PID_MUTATED_PIG_RAT))

/* Local variables which do not need to be saved between map changes. */

procedure start begin
end

procedure timed_event_p_proc begin
   if ((combat_is_initialized == false) and ((critter_state(self_obj) bwand DAM_KNOCKED_OUT) == FALSE)) then begin
      animate_move_to_tile(tile_num_in_direction(tile_num(self_obj),random(0,5),random(1,3)));
   end
   add_timer_event(self_obj,game_ticks(random(3,5)),0);
end

procedure combat_p_proc begin
   if (is_mutated_rat) then begin
      if (fixed_param == COMBAT_SUBTYPE_HIT_SUCCEEDED) then begin
         if (target_obj == dude_obj) then begin
            poison(target_obj,random(POISON_MIN,POISON_MAX));
            radiation_inc(target_obj,random(RAD_MIN,RAD_MAX));
         end
      end
   end
end

procedure critter_p_proc begin
   // the rat attacks if it sees the player
   if (self_can_see_dude) then begin
      if (critter_is_fleeing(self_obj)) then begin
         Flee_From_Dude
      end else begin
         self_attack_dude;
      end
   end
end

procedure destroy_p_proc begin
   dec_map_var(MVAR_Rat_Counter);
   debug_msg("\nMVAR_Rat_Counter == "+map_var(MVAR_Rat_Counter));
   if (map_var(MVAR_Rat_Counter) <= 0) then begin
      if (all_rats_killed == false) then begin
         debug_msg("setting ALL RATS KILLED");
         set_all_rats_killed;
         debug_msg("all_rats_killed == "+all_rats_killed);
         modoc_display_mstr(502); //added by killap - expansion
         give_xp(EXP_MODOC_KILL_ALL_RATS);
         if (vermin_hunter_seed_active) then begin
            modoc_dude_floater(500 + (dude_is_stupid)); //edit by killap - dud_is_stupid
         end
      end
   end
end

procedure damage_p_proc begin
   if (combat_is_initialized == false) then begin
      self_attack_dude;
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(message_str(SCRIPT_ZCLRAT,100));
end

procedure description_p_proc begin
end

procedure use_obj_on_p_proc begin
end

procedure use_skill_on_p_proc begin
   if (action_being_used == SKILL_STEAL) then
      script_overrides;
end

procedure map_enter_p_proc begin

   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_RAT);
   if (is_mutated_rat) then begin
      critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_MUTATED_RAT);
   end else begin
      critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_GENERIC_RAT);
   end
   if (map_first_run) then begin
      inc_map_var(MVAR_Rat_Counter);
      add_timer_event(self_obj,game_ticks(random(1,5)),0);
   end
end