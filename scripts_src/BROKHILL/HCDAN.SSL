/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "..\headers\define.h"
//#include "..\headers\<TownName.h>"

#define NAME                    SCRIPT_HCDAN
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BROKEN_HILLS)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node013a;
procedure Node014;
procedure Node014a;
procedure Node014b;
procedure Node015;
procedure Node015a;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Spouse_Status              (7)
#define LVAR_Down_Rep                   (8)

#define SPOUSE_UNKNOWN           (0)
#define SPOUSE_TOLD              (1)
#define SPOUSE_REJECTED          (2)
#define SPOUSE_TOLD_ABOUT_WIFE   (3)
#define SPOUSE_TOLD_ABOUT_LETTER (4)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_BROKEN_HILLS);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_COWARD);
   
   //added by killap
   if( local_var( LVAR_Spouse_Status ) == SPOUSE_TOLD_ABOUT_LETTER ) then begin
      destroy_object(self_obj);
   end
   //end added
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( ( ( global_var( GVAR_ENEMY_BROKEN_HILLS ) == 1 ) or (local_var(LVAR_Hostile) == 2) ) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
      set_global_var( GVAR_ENEMY_BROKEN_HILLS, 1 );
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if( ( global_var( GVAR_ENEMY_BROKEN_HILLS ) == 1 ) or
       ( local_var( LVAR_Spouse_Status ) == SPOUSE_REJECTED ) ) then
      call Node016;
   else if( local_var( LVAR_Spouse_Status ) == SPOUSE_TOLD_ABOUT_WIFE ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node015;
       gSay_End;
       end_dialogue;
   end
   else if( local_var( LVAR_Spouse_Status ) == SPOUSE_TOLD_ABOUT_LETTER ) then begin
      call Node019;
   end
   else if( ( local_var( LVAR_Herebefore ) == 1 ) and
            ( dude_iq <= 3 ) ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node002;
       gSay_End;
       end_dialogue;
   end
   else if( ( local_var( LVAR_Herebefore ) == 1 ) and
            ( local_var( LVAR_Spouse_Status ) == SPOUSE_TOLD ) ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node011;
       gSay_End;
       end_dialogue;
   end
   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node001;
       gSay_End;
       end_dialogue;
   end
   set_local_var( LVAR_Herebefore, 1 );
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if( local_var( LVAR_Herebefore ) == 0 ) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(103);

   NLowOption(104, Node002);
   NOption( mstr(105) + dude_name + mstr(157) , Node003, 004);
   NOption(106, Node003, 004);
   NOption(107, Node003, 004);
   NOption(108, Node999, 004);
end

procedure Node002 begin
   Reply(109);

   NLowOption(110, Node999);
end

procedure Node003 begin
   Reply(111);

   NOption(112, Node004, 004);
   NOption(113, Node004, 004);
   NOption(114, Node004, 004);
   NOption(115, Node004, 004);
end

procedure Node004 begin
   set_local_var( LVAR_Spouse_Status, SPOUSE_TOLD );
   Reply(116);

   NOption(117, Node005, 004);
   NOption(118, Node010, 004);
   NOption(119, Node007, 004);
end

procedure Node005 begin
   Reply(120);

   NOption(121, Node008, 004);
   NOption(122, Node007, 004);
end

procedure Node007 begin
   Reply(123);

   NOption(124, Node009, 004);
   NOption(125, Node008, 004); //edit by killap - was 999
end

procedure Node008 begin
   item_caps_adjust( dude_obj, 500 );

   Reply(126);

   NOption(127, Node010, 004);
   NOption(128, Node999, 004);
end

procedure Node009 begin
   set_local_var( LVAR_Spouse_Status, SPOUSE_REJECTED );
   set_global_var(TOWN_REP_VAR, (global_var(TOWN_REP_VAR) - 10 ) );
   inc_general_rep( REP_BONUS_REJECTED_DAN );

   Reply(129);

   NOption(130, Node999, 004);
end

procedure Node010 begin
   Reply(131);

   NOption(132, Node999, 004);
end

procedure Node011 begin
   Reply(133);

   NOption(134, Node012, 004);
   if( global_var( GVAR_BH_MISSING ) >= MISSING_DISCOVERED_CASH ) then
      NOption(135, Node013, 004);
   NOption(136, Node999, 004);
end

procedure Node012 begin
   Reply(137);

   NOption(138, Node999, 004);
   NOption(139, Node009, 004);
end

procedure Node013 begin
   Reply(140);

   BOption(141, Node014, 004);
   NOption(142, Node013a, 004);
end

procedure Node013a begin
   inc_general_rep( 5 );
   call Node015;
end

procedure Node014 begin
   Reply(143);

   BOption(144, Node014a, 004);
   GOption(145, Node014b, 004);
end

procedure Node014a begin
   inc_general_rep( REP_BONUS_MOCKED_DAN );
   set_global_var( TOWN_REP_VAR, (global_var(TOWN_REP_VAR) - 5 ) );
   call Node015;
end

procedure Node014b begin
   inc_general_rep( REP_BONUS_CONSOLED_DAN );
   call Node015;
end

procedure Node015 begin
   if( local_var( LVAR_Spouse_Status ) < SPOUSE_TOLD_ABOUT_WIFE ) then begin
      set_local_var( LVAR_Spouse_Status, SPOUSE_TOLD_ABOUT_WIFE );
      display_msg( mstr(156) );
      give_xp( EXP_TOLD_DAN_ABOUT_WIFE );
   end

   Reply(146);

   BOption(147, Node015a, 004);
   if( global_var( GVAR_READ_FRANCIS_NOTE ) == 1 ) then
      NOption(150, Node017, 004);
   NOption(148, Node999, 004);
end

procedure Node015a begin
   if( local_var( LVAR_Down_Rep ) == 0 ) then begin
      set_local_var( LVAR_Down_Rep, 1 );
      inc_general_rep( -5 );
   end
end

procedure Node016 begin
   floater(149);
end

procedure Node017 begin
   set_local_var( LVAR_Spouse_Status, SPOUSE_TOLD_ABOUT_LETTER );
   Reply(151);

   NOption(152, Node018, 004);
end

procedure Node018 begin
   Reply(153);

   NOption(154, Node999, 004);
end

procedure Node019 begin
   floater(155);
end

//xxxxxxxxxxxxxxxxxxxx