/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Lumpy the Car Accident Ghoul
        Location: Broken Hills
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/

//Overrides the pickup p proc.
#define NPC_REACTION_TYPE       REACTION_TC /* REACTION_TC REACTION_TG REACTION_C REACTION_G */
#define NPC_REACTION_VAR        7 /* same as thief variable */

#include "../headers/define.h"
#include "../headers/broken2.h"

#define NAME                    SCRIPT_HCLUMPY
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BROKEN_HILLS)

#include "../headers/command.h"
#include "../headers/modreact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node006a;
procedure Node007;
procedure Node008;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Caught_Thief               (7)
#define LVAR_Had_Accident               (8)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Perform_Extraction := 0;
variable Allow_Dialog := 1;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_BROKEN_HILLS);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_GHOUL_PEASANT);

   // Lumpy the accident victim shouldn't show up if the player is an enemy
   // of Broken Hills.  This is to avoid problems with combat and a forced dialog
   // both vying for priority.
   if( not( is_loading_game ) ) then begin
      if( ( car_current_town == AREA_BROKEN_HILLS ) and
          ( local_var( LVAR_Had_Accident ) == 0 ) and
          ( global_var( GVAR_ENEMY_BROKEN_HILLS ) == 0 ) and
          ( global_var( GVAR_CAME_FROM_BROKEN2 ) == 0 ) ) then begin
         set_local_var( LVAR_Had_Accident, 1 );
         if( not( self_visible ) ) then
            set_obj_visibility( self_obj, false );
         reg_anim_clear( self_obj );
         reg_anim_begin();
         reg_anim_animate( self_obj,ANIM_fall_back_sf,-1);
         reg_anim_end();

         Allow_Dialog := 1;
         add_timer_event( self_obj, 1, 0 );
         add_timer_event( self_obj, game_ticks(1), 1 );
      end
      else begin
         if( self_visible ) then
            set_obj_visibility( self_obj, true );
      end
   end
end

procedure timed_event_p_proc begin
   if( fixed_param == 0 ) then begin
      // Floaters
      floater( random( 103, 109 ) );
      add_timer_event( self_obj, game_ticks( random( 5, 10 ) ), 0 );
   end
   else if( fixed_param == 1 ) then begin
      // Player's response
      float_msg( dude_obj, mstr(200 + dude_is_stupid), FLOAT_COLOR_AFRAID ); //edit by killap - added dude_is_stupid
   end
   else if( fixed_param == 2 ) then begin
      animate_move_obj_to_tile( self_obj, 18367, -1 );
   end
end

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( (local_var(LVAR_Hostile) != 0) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       self_attack_dude;
       //Macro made by Tom to keep the critter fleeing.
   end
   else if( ( tile_num( self_obj ) == 18367 ) and ( anim_busy( self_obj ) == false ) ) then
      destroy_object( self_obj );
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (obj_in_party(source_obj)) then begin
       set_local_var(LVAR_Personal_Enemy,1);
      set_global_var( GVAR_ENEMY_BROKEN_HILLS, 1 );
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   Perform_Extraction := 0;

   if( Allow_Dialog == 1 ) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
   end

   if( Perform_Extraction == 1 ) then begin
      Perform_Extraction := 0;
      reg_anim_clear( self_obj );
      reg_anim_begin();
      reg_anim_animate( self_obj,ANIM_back_to_standing,-1);
      reg_anim_end();

      // Stop random floats
      rm_timer_event( self_obj );
      add_timer_event( self_obj, game_ticks(2), 2 );
      floater(139);
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter
/* inc_evil_critter */
/* inc_neutral_critter */

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_mstr(100);
   else
      display_mstr(101);
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_mstr(102);
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   if( dude_is_male ) then begin
      Reply( mstr(110) + " " + mstr(112) );
   end
   else begin
      Reply( mstr(111) + " " + mstr(112) );
   end

   NLowOption(113, Node002);
   NOption(114, Node002, 004);
   NOption(115, Node007, 001);
end

procedure Node002 begin
   Reply(116);

   NLowOption(117, Node003);
   NLowOption(118, Node999);
   NOption(119, Node003, 004);
   NOption(120, Node999, 004);
end

procedure Node003 begin
   Reply(121);

   NLowOption(122, Node004);
   NOption(123, Node004, 004);
end

procedure Node004 begin
   Reply(124);

   NLowOption(125, Node005);
   NOption(126, Node005, 004);
end

procedure Node005 begin
   Reply(127);

   NLowOption(128, Node006);
   NOption(129, Node006, 004);
end

procedure Node006 begin
   Reply(130);

   NLowOption(131, Node006a);
   NOption(132, Node006a, 004);
end

procedure Node006a begin
   Perform_Extraction := 1;
   Allow_Dialog := 0;
end

procedure Node007 begin
   Reply(133);

   NLowOption(134, Node008);
   NOption(135, Node008, 004);
end

procedure Node008 begin
   Reply(136);

   NLowOption(137, Node002);
   NOption(138, Node002, 004);
end

//xxxxxxxxxxxxxxxxxxxx