/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Manson
        Location: Broken Hills
        Description:

        Log:
            NEED TO SET A FLAG IF PLAYER KILLS MANSON...(this flag is checked by Aileen)
            NEED TO GIVE EXP FOR HELPING MANSON
            JAIL DOOR MUST BE LOCKED
            MANSON SHOULD NOT BE ON SAME TEAM AS MUTANTS (need an ANTI-MUTANT TEAM)

           Created: June 4, 1998

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "hcmanson"
#include "../headers/define.h"
#include "../headers/broken.h"
#include "../headers/broken1.h"

#define NAME                    SCRIPT_HCMANSON
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BROKEN_HILLS)

#include "../headers/command.h"
#include "../headers/modreact.h"

#define FREEDOM   16089
#define JAIL_CELL 10888

#define Vault_Known             (1)     // Temp Until get command from Jesse
#define Visited_City            (1)     // Temp Until get command from Jesse

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure timed_event_p_proc;
procedure combat;

procedure Node995;
procedure Node996;
procedure Node997;
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node005a;
procedure Node005b;
procedure Node006;
procedure Node006a;
procedure Node007;
procedure Node007a;
procedure Node008;
procedure Node009;
procedure Node009a;
procedure Node010;
procedure Node011;
procedure Node011a;
procedure Node011b;
procedure Node012;
procedure Node012a;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node016a;
procedure Node016b;
procedure Node016c;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
procedure Node023;
procedure Node024;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Freed                      (7)
#define LVAR_Gave_Dialog_XP             (8)
#define LVAR_Down_React_1               (9)
#define LVAR_Down_React_2               (10)
#define LVAR_Down_React_3               (11)
#define LVAR_Down_React_4               (12)
#define LVAR_Down_React_5               (13)
#define LVAR_Down_React_6               (14)
#define LVAR_Up_React_1                 (15)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable Manson_Ptr;
import variable Frank_Ptr;

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable stuff;
variable k;
variable dtile;
variable mytile;
variable newtile;

procedure start begin
   Manson_Ptr := self_obj;
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Manson_Ptr := self_obj;
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_RACIST);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_CITIZEN);

   if( ( global_var(GVAR_BH_CONSPIRACY) == CONSPIRACY_SUCCESSFUL ) or
       ( ( global_var(GVAR_BH_JAIL) == JAIL_FORCE ) or
         ( ( global_var(GVAR_BH_JAIL) == JAIL_CHARISMA ) and ( global_var(GVAR_JAIL_BREAK) > 0 ) ) or
         ( global_var(GVAR_BH_JAIL) == JAIL_STEALTH ) or
         ( global_var(GVAR_BH_JAIL) == JAIL_COINCIDENTAL ) and
         ( global_var(GVAR_BH_CONSPIRACY) != CONSPIRACY_BROKEN_DOUBLECROSS ) and
         ( global_var(GVAR_BH_CONSPIRACY) != CONSPIRACY_BROKEN_HERO ) ) ) then begin
      move_to( self_obj, 28879, 0 );
      critter_attempt_placement( self_obj, 28879, 0 );
   end
end

procedure map_update_p_proc begin
   Manson_Ptr := self_obj;
end

procedure timed_event_p_proc begin
   if( fixed_param == 1 ) then begin
      gfade_out(600);

      set_obj_visibility( self_obj, true );
      move_to( self_obj, 28879, 0 );
      set_obj_visibility( self_obj, false );
      critter_attempt_placement( self_obj, 28879, 0 );

      // Don't need to do anything with Frank here since if we're
      // calling this at all it means he's dead.

      // Move player.
      move_to( dude_obj, 28887, 0 );
      critter_attempt_placement( dude_obj, 28887, 0 );

      game_ui_enable;

      gfade_in(600);
   end
end

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( ( ( global_var( GVAR_ENEMY_CONSPIRATORS ) == 1 ) or (local_var(LVAR_Hostile) == 2) ) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end
   else if (global_var(GVAR_JAIL_BREAK) == JB_IN_PROGRESS) then begin
      if( map_var( MVAR_Frank_Dead ) == 1 ) then begin
         if (global_var(GVAR_JAIL_BREAK) != JB_SUCCESSFUL) then begin
            set_global_var(GVAR_JAIL_BREAK, JB_SUCCESSFUL);
            if ( (global_var(GVAR_BH_JAIL) == JAIL_ENLISTED) or
                 (global_var(GVAR_BH_JAIL) == JAIL_REJECTED) ) then begin
               //if not charisma or force then must have used stealth
               set_global_var(GVAR_BH_JAIL, JAIL_STEALTH);
            end
            else if( global_var(GVAR_BH_JAIL) == 0 ) then
               set_global_var(GVAR_BH_JAIL, JAIL_COINCIDENTAL);
         end

         display_msg( mstr(178) );
         give_xp( EXP_JAILBREAK_SUCCESS );
         set_global_var( GVAR_TOWN_REP_BROKEN_HILLS, (global_var(GVAR_TOWN_REP_BROKEN_HILLS) + REP_TOWN_JAILBREAK_SUCCESS ) );
         inc_general_rep( REP_BONUS_JAILBREAK_SUCCEEDS );
         game_ui_disable;
         floater(177);
         add_timer_event( self_obj, game_ticks(3), 1 );
      end
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin
/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_global_var( GVAR_ENEMY_CONSPIRATORS, 1 );
       set_global_var(GVAR_ENEMY_BROKEN_HILLS,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   GetReaction;
   ReactToLevel; //added by killap

   if (local_var(LVAR_Personal_Enemy) == 1) then begin
       call Node998;
   end
   else if (global_var(GVAR_JAIL_BREAK) == JB_IN_PROGRESS) then begin
      floater(174);
   end
   else if ( (global_var(GVAR_BH_CONSPIRACY) == CONSPIRACY_BROKEN_DOUBLECROSS) or
             (global_var(GVAR_BH_JAIL) == JAIL_DOUBLECROSS) ) then
      call Node020;
   else if (global_var(GVAR_BH_CONSPIRACY) == CONSPIRACY_REJECTED) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node019;
       gSay_End;
       end_dialogue;
   end
   // Don't respond to Jail Rejected. He's still in prison. How would he know?
   else if (global_var(GVAR_BH_CONSPIRACY) == CONSPIRACY_SUCCESSFUL) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node021;
       gSay_End;
       end_dialogue;
   end
   else if (global_var(GVAR_BH_CONSPIRACY) == CONSPIRACY_BROKEN_HERO) then begin
      call Node023;
   end
   else if (global_var(GVAR_BH_CONSPIRACY) == CONSPIRACY_ACCEPTED) then begin
      call Node024;
   end
/*
   else if (global_var(GVAR_BH_CONSPIRACY) == CONSPIRACY_FAILED) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node022;
       gSay_End;
       end_dialogue;
   end
*/
   else if ( ( global_var(GVAR_BH_JAIL) == JAIL_ENLISTED ) or
             ( ( global_var(GVAR_BH_JAIL) == JAIL_CHARISMA ) and ( global_var( GVAR_JAIL_BREAK ) == 0 ) ) or
             ( ( global_var(GVAR_BH_JAIL) == JAIL_FORCE ) and ( global_var( GVAR_JAIL_BREAK ) == 0 ) ) ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node013;
       gSay_End;
       end_dialogue;
   end
   else if ( (global_var(GVAR_BH_JAIL) == JAIL_FORCE) or
             (global_var(GVAR_BH_JAIL) == JAIL_STEALTH) or
             (global_var(GVAR_BH_JAIL) == JAIL_CHARISMA) or
             (global_var(GVAR_BH_JAIL) == JAIL_COINCIDENTAL) ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node014;
       gSay_End;
       end_dialogue;
   end
   else if (bad_critter_reaction) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node008;
       gSay_End;
       end_dialogue;
   end
   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
             if (local_var(LVAR_Herebefore) == 0) then begin
                call Node001;
             end
             else begin
                if (dude_iq <= 3) then begin
                   call Node002;
                end
                else begin
                   call Node012;
                end
             end
       gSay_End;
       end_dialogue;
      set_local_var( LVAR_Herebefore, 1 );
   end

end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
   set_map_var( MVAR_Manson_Dead, 1 );
/* Increment the aligned critter counter*/
   inc_evil_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if( local_var(LVAR_Herebefore) == 0 ) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

procedure combat begin
end

/*
procedure Node910 begin
//if player money < 5 goto 13 and set nogive
//else goto 5 subtract 5 coins and set give
   if (item_caps_total(dude_obj) > 4) then begin
      item_caps_adjust(dude_obj,-5); //subtract 5 coins
      set_local_var(LVAR_Give, 1);
      call Node005;
   end
   else begin
      //set_local_var(LVAR_Nogive,1);
      call Node013;
   end
end
*/

procedure Node995 begin
   if (get_critter_stat(dude_obj,STAT_ch) >= 7) then begin
       TopReact;
   end
   else begin
       BottomReact;
   end
end

procedure Node996 begin
   BottomReact;
end

procedure Node997 begin
   if (get_critter_stat(dude_obj,STAT_ch) >= 8) then
       call Node004;
   else
       call Node005;
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end

// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(103);

   if( dude_iq == 1 ) then
      NOption(104, Node002, 001);
   if( (dude_iq == 2) or (dude_iq == 3) ) then
      NOption(105,Node002,002);
   NOption(106,Node003,004);
   NOption(107,Node004,004);
   NOption(108,Node005,004);
   NOption(109,Node999,004);
end

procedure Node002 begin
   Reply(110);
   NLowOption(111,Node999);
end

procedure Node003 begin
   Reply(112);
   NOption(113,Node006,004);
   GOption(114,Node007,004);
   BOption(115,Node008,004);
   NOption(116,Node009,004);
   NOption(117,Node999,004);
end

procedure Node004 begin
   Reply(118);
   NOption(119,Node006,004);
   NOption(120,Node007,004);
end

procedure Node005 begin
   Reply(121);
   NOption(122,Node007,004);
   NOption(123,Node005a,004); //reaction down small
   NOption(124,Node005b,007); //give 100xp
   NOption(125,Node999,004);
end

procedure Node005a begin
   if( local_var(LVAR_Down_React_1) == 0 ) then begin
      set_local_var(LVAR_Down_React_1, 1);
      DownReactMinor;
   end
   call Node008;
end

procedure Node005b begin
   if( local_var(LVAR_Gave_Dialog_XP) == 0 ) then begin
      set_local_var( LVAR_Gave_Dialog_XP, 1 );
      display_msg( mstr(175) );
      give_xp(EXP_PREACHED_TO_MANSON);
      set_global_var( GVAR_TOWN_REP_BROKEN_HILLS, (global_var(GVAR_TOWN_REP_BROKEN_HILLS) + REP_TOWN_PREACHED_TO_MANSON ) );
   end
   call Node010;
end

procedure Node006 begin
   Reply(126);
   NOption(127,Node011,004); //reaction down small
   NOption(128,Node006a,004);
   NOption(129,Node999,004);
end

procedure Node006a begin
   if( local_var(LVAR_Down_React_2) == 0 ) then begin
      set_local_var(LVAR_Down_React_2, 1);
      DownReactModerate;
   end
   call Node008;
end

procedure Node007 begin
   Reply(130);
   BOption(131,Node007a,004); //reaction down small
   GOption(132,Node011,004);
   NOption(133,Node999,004);
end

procedure Node007a begin
   if( local_var(LVAR_Down_React_3) == 0 ) then begin
      set_local_var(LVAR_Down_React_3, 1);
      DownReactMinor;
   end
   call Node008;
end

procedure Node008 begin
   Reply(134);
   NOption(133,Node999,004);
end

procedure Node009 begin
   Reply(135);
   NOption(136,Node008,004);
   NOption(137,Node009a,004); //reaction down small
   NOption(138,Node999,004);
end

procedure Node009a begin
   if( local_var(LVAR_Down_React_4) == 0 ) then begin
      set_local_var(LVAR_Down_React_4, 1);
      DownReactMinor;
   end
   call Node008;
end

procedure Node010 begin
   Reply(139);
   NOption(140,Node012,004);
end

procedure Node011 begin
   Reply(141);
   BOption(142,Node011a,004); //react down med
   GOption(143,Node011b,004); //react up small
   NOption(144,Node012,004);
   NOption(145,Node999,004);
end

procedure Node011a begin
   if( local_var(LVAR_Down_React_5) == 0 ) then begin
      set_local_var(LVAR_Down_React_5, 1);
      DownReactModerate;
   end
   call Node008;
end

procedure Node011b begin
   if( local_var(LVAR_Up_React_1) == 0 ) then begin
      set_local_var(LVAR_Up_React_1, 1);
      UpReactMinor;
   end
   call Node012;
end

procedure Node012 begin
   Reply(146);
   NOption(147,Node003,004);
   NOption(148,Node004,004);
   NOption(149,Node005,004);
   NOption(150,Node011,004);
   NOption(151,Node009,004);
   NOption(152,Node012a,004); //reaction down small
   NOption(153,Node999,004);
end

procedure Node012a begin
   if( local_var(LVAR_Down_React_6) == 0 ) then begin
      set_local_var(LVAR_Down_React_6, 1);
      DownReactMinor;
   end
   call Node008;
end

procedure Node013 begin
   Reply(154);
   NOption(155,Node999,004);
end

procedure Node014 begin
   Reply(156);
   NOption(157,Node015,004);
end

procedure Node015 begin
   Reply(158);
   NOption(159,Node016,004);
   NOption(160,Node017,004);
   NOption(161,Node999,004);
end

procedure Node016 begin
   Reply(162);
   NOption(163,Node016a,004);
   NOption(164,Node016b,004);
   NOption(165,Node016c,004);
end

procedure Node016a begin
   set_global_var(GVAR_BH_CONSPIRACY, CONSPIRACY_QUESTIONED);
   call Node018;
end

procedure Node016b begin
   set_global_var(GVAR_BH_CONSPIRACY, CONSPIRACY_REJECTED);
   call Node019;
end

procedure Node016c begin
   set_global_var(GVAR_BH_CONSPIRACY, CONSPIRACY_QUESTIONED);
end

procedure Node017 begin
   Reply(166);
   NOption(167,Node015,004);
end

procedure Node018 begin
   Reply(168);
   NOption(169,Node999,004);
end

procedure Node019 begin
   Reply(170);
   NOption(133,Node999,004);
end

procedure Node020 begin
   //Reply(171);
   //NOption(133,Node999,004);
   floater(171);
end

procedure Node021 begin
   Reply(172);
   NOption(133,Node999,004);
end

procedure Node022 begin
   Reply(173);
   NOption(133,Node999,004);
end

procedure Node023 begin
   floater(176);
end

procedure Node024 begin
   floater(179);
end

//xxxxxxxxxxxxxxxxxxxx