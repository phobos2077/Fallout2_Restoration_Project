/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Husband and wife mutants
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "../headers/define.h"
#include "../headers/broken1.h"

#define NAME                    SCRIPT_HCHUSWIF
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BROKEN_HILLS)

#include "../headers/command.h"
#include "../headers/modreact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure map_update_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_I_Am_Male                  (7)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable Female_Mutant;

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Float_State := 0;
variable Current_Float := 0;

procedure start begin
end

procedure map_update_p_proc begin
   if( local_var( LVAR_I_Am_Male ) == 0 ) then
      Female_Mutant := self_obj;
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Float_State := 0;
   if( map_first_run ) then begin
      if( tile_num( self_obj ) == 15651 ) then
         set_local_var( LVAR_I_Am_Male, 1 );
      else begin
         set_local_var( LVAR_I_Am_Male, 0 );
         Female_Mutant := self_obj;
      end
   end

   if( local_var( LVAR_I_Am_Male ) == 0 ) then
      Female_Mutant := self_obj;

   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_BROKEN_HILLS);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_CITIZEN);

   if( ( global_var( GVAR_BH_CONSPIRACY ) == CONSPIRACY_SUCCESSFUL ) or ( global_var( GVAR_BH_FTM ) == FTM_SUCCESSFUL ) ) then begin
      if( self_visible ) then
         set_obj_visibility( self_obj, true );
   end
   else begin
      if( not( self_visible ) ) then
         set_obj_visibility( self_obj, false );
      // Let the male mutant control the back-and-forth floaters.
      if( local_var( LVAR_I_Am_Male ) == 1 ) then
         add_timer_event( self_obj, game_ticks(2), 1 );
   end
end

procedure timed_event_p_proc begin
   if (not combat_is_initialized) then begin //added check by killap
      if( fixed_param == 1 ) then begin
         if( map_var( MVAR_Female_Mutant_Dead ) == 0 ) then begin
            if( Float_State == 0 ) then begin
               // Man's turn
               Current_Float := random(0, 9);
               floater( 104 + Current_Float );
               Float_State := 1;
               add_timer_event( self_obj, game_ticks(3), 1 );
            end
            else begin
               // Woman's turn
               float_msg( Female_Mutant, mstr( 140 + Current_Float ), FLOAT_MSG_NORMAL );
               Float_State := 0;
               add_timer_event( self_obj, game_ticks(10), 1 );
            end
         end
      end
   end else begin
      add_timer_event( self_obj, game_ticks(10), 1 );
   end
end

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( ( ( global_var( GVAR_ENEMY_BROKEN_HILLS ) == 1 ) or (local_var(LVAR_Hostile) == 2) ) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
      set_global_var( GVAR_ENEMY_BROKEN_HILLS, 1 );
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if( global_var( GVAR_ENEMY_BROKEN_HILLS ) == 1 ) then
      call Node998;

   if( local_var( LVAR_I_Am_Male ) == 1 ) then
      floater(200);
   else
      floater(201);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

   if( local_var( LVAR_I_Am_Male ) == 0 ) then
      set_map_var( MVAR_Female_Mutant_Dead, 1 );

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if( local_var( LVAR_I_Am_Male ) == 1 ) then
      display_msg(mstr(100));
   else
      display_msg(mstr(102));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   if( local_var( LVAR_I_Am_Male ) == 1 ) then
      display_msg(mstr(101));
   else
      display_msg(mstr(103));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
end

//xxxxxxxxxxxxxxxxxxxx