/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Outfitter
        Location: Broken Hills
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: June 4, 1998

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "hcoutfit"
#include "../headers/define.h"
#include "../headers/broken.h"
#include "../headers/caravan.h"

#define NAME                    SCRIPT_HCOUTFIT
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BROKEN_HILLS)

//destinations for the caravan
#define V_CITY                  1
#define GKO                     2
#define NCR                     3

#define CURRENT_CARAVAN         ( GVAR_BH_CARAVAN )
#define CURRENT_CARAVAN_LEADER  ( CARAVAN_WADE )

#include "../headers/command.h"
#include "../headers/modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure combat;

procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node003a;
procedure Node004;
//procedure Node005;
procedure Node006;
procedure Node007;
procedure Node007a;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
//procedure Node016;
//procedure Node017;
//procedure Node018;
procedure Node019;
//procedure Node020;
//procedure Node021;
procedure Node022;
procedure Node023;
procedure Node024;
procedure Node025;
procedure Node026;
procedure Node027;
procedure Node028;
procedure Node029;
procedure Node030;
procedure Node031;
procedure Node032;
procedure Node033;

procedure StartCaravan;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Banned                     (7)
#define LVAR_Crap_Shoveled              (8)
#define LVAR_Caravan                    (9)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable broken_hills_outfitter_box;
import variable broken_hills_temp_box;

#define move_inven_to_box              move_obj_inven_to_obj(self_obj, broken_hills_outfitter_box);
#define move_inven_from_box            move_obj_inven_to_obj(broken_hills_outfitter_box, self_obj);
#define move_inven_to_temp_box         move_obj_inven_to_obj(self_obj, broken_hills_temp_box);
#define move_inven_from_temp_box       move_obj_inven_to_obj(broken_hills_temp_box, self_obj);

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable destination := 0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_BROKEN_HILLS);
   set_self_ai(AI_TOUGH_CITIZEN);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( ( ( global_var( GVAR_ENEMY_BROKEN_HILLS ) == 1 ) or (local_var(LVAR_Hostile) == 2) ) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_global_var(GVAR_ENEMY_BROKEN_HILLS,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   GetReaction;

   move_inven_to_temp_box
   move_inven_from_box

   if (local_var(LVAR_Personal_Enemy) == 1) then begin
       call Node998;
   end
   else if( global_var(GVAR_BH_CARAVAN) == CARAVAN_STATUS_FAILED ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node033;
       gSay_End;
       end_dialogue;
   end
   else if( ( local_var( LVAR_Banned ) == 1 ) or ( global_var(GVAR_ENEMY_BROKEN_HILLS) == 1 ) ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node022;
       gSay_End;
       end_dialogue;
   end
   else if( global_var(GVAR_BH_CONSPIRACY) == CONSPIRACY_SUCCESSFUL ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node032;
       gSay_End;
       end_dialogue;
   end
   else if( ( local_var( LVAR_Caravan ) == V_CITY ) and ( get_day == 21 ) and ( global_var( GVAR_CHAD_DEAD ) == 0 ) and ( (global_var(GVAR_BH_CHAD) != CHAD_ARRESTED) and (global_var( GVAR_BH_CHAD) != CHAD_FLED ) ) ) then begin // edits by killap - expansion
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node023;
       gSay_End;
       end_dialogue;
   end
   else if( ( local_var( LVAR_Caravan ) == GKO ) and ( get_day == 1 ) and ( global_var( GVAR_CHAD_DEAD ) == 0 ) and ( (global_var(GVAR_BH_CHAD) != CHAD_ARRESTED) and (global_var( GVAR_BH_CHAD) != CHAD_FLED ) ) ) then begin // edits by killap - expansion
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node025;
       gSay_End;
       end_dialogue;
   end
   else if( ( local_var( LVAR_Caravan ) == NCR ) and ( get_day == 11 ) and ( global_var(GVAR_BH_FTM) == FTM_EXPOSED ) and ( global_var( GVAR_CHAD_DEAD ) == 0 ) and ( (global_var(GVAR_BH_CHAD) != CHAD_ARRESTED) and (global_var( GVAR_BH_CHAD) != CHAD_FLED ) )) then begin // edits by killap - expansion
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node026;
       gSay_End;
       end_dialogue;
   end
   else if( ( local_var( LVAR_Caravan ) == NCR ) and ( get_day == 11 ) and ( global_var( GVAR_CHAD_DEAD ) == 0 ) and ( (global_var(GVAR_BH_CHAD) != CHAD_ARRESTED) and (global_var( GVAR_BH_CHAD) != CHAD_FLED ) ) ) then begin // edits by killap - expansion
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node024;
       gSay_End;
       end_dialogue;
   end
   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
             if (local_var(LVAR_Herebefore) == 0) then begin
                set_local_var(LVAR_Herebefore,1);
                call Node001;
             end
             else begin
                call Node012;
             end
       gSay_End;
       end_dialogue;
   end
   move_inven_to_box
   move_inven_from_temp_box
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if( local_var( LVAR_Herebefore ) == 0 ) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

procedure combat begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end

// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(103);

   if( dude_iq == 1 ) then
      NOption( 104, Node002, 001 );
   if( ( dude_iq == 2 ) or ( dude_iq == 3 ) ) then
      NOption(105,Node003,002);
   NOption(106,Node003,004);
   NOption(107,Node004,004);
/*
   if ( (global_var(GVAR_BH_CHAD) == CHAD_SUSPICIOUS) or (global_var(GVAR_BH_CHAD) == CHAD_RUMOR_GECKO) or (global_var(GVAR_BH_CHAD) == CHAD_RUMOR_BROKENHILLS) ) then
      NOption(108,Node005,004);
   if (global_var(GVAR_BH_CHAD) == CHAD_KILLED) then
      NOption(109,Node020,004);
   if ( (global_var(GVAR_BH_CHAD) == CHAD_KILLED) or (global_var(GVAR_BH_CHAD) == CHAD_CONFIRMED_GECKO) or (global_var(GVAR_BH_CHAD) == CHAD_CONFIRMED_BROKENHILLS) ) then
      NOption(110,Node021,004);
*/
   NOption(111,Node006,004);
end

procedure Node002 begin
   Reply(112);
   NLowOption(113,Node999);
end

procedure Node003 begin
   if( global_var( GVAR_CHAD_DEAD ) == 1 ) then begin
      Reply(212);
   //added by killap - expansion
   end else if ( (global_var(GVAR_BH_CHAD) == CHAD_ARRESTED) or (global_var( GVAR_BH_CHAD) == CHAD_FLED ) ) then begin
      Reply(214);
   end
   //end added
   else if( local_var( LVAR_Crap_Shoveled ) < 5 ) then begin
      if (dude_iq < 4) then
         Reply(114);
      else begin
         if( global_var(GVAR_BH_FTM) != FTM_EXPOSED ) then
            Reply(115);
         else
            Reply(192);
      end
   end
   else begin
      if (dude_iq < 4) then
         Reply(188);
      else begin
         if( global_var(GVAR_BH_FTM) != FTM_EXPOSED ) then
            Reply(189);
         else
            Reply(191);
      end
   end

   //edit by killap - added ( (global_var(GVAR_BH_CHAD) == CHAD_ARRESTED) or (global_var( GVAR_BH_CHAD) == CHAD_FLED ) ) - expansion
   if ( ( global_var( GVAR_CHAD_DEAD ) == 1 ) or (global_var(GVAR_BH_CHAD) == CHAD_ARRESTED) or (global_var( GVAR_BH_CHAD) == CHAD_FLED )  ) then begin
      NOption(213, Node999, 001);
   end
   else begin
      if( local_var( LVAR_Crap_Shoveled ) < 5 ) then begin
         NLowOption(116,Node007);
         NLowOption(117,Node008);
      end
      else
         NLowOption(190,Node999);
      if( local_var( LVAR_Crap_Shoveled ) < 5 ) then
         NOption(118,Node007,004);
      if (local_var(LVAR_Caravan) == 0) then begin
         NOption(119,Node009,004);
         NOption(121,Node011,004);
      end
      if ( (local_var(LVAR_Caravan) == 0) and (global_var(GVAR_BH_FTM) != FTM_EXPOSED) ) then
         NOption(120,Node010,004);
      if (local_var(LVAR_Caravan) != 0) then
         NOption(195, Node003a, 004);
      NOption(122,Node012,004);
      NOption(123,Node999,004);
   end
end

procedure Node003a begin
   if( local_var(LVAR_Caravan) == GKO ) then
      Reply(196);
   else if( local_var(LVAR_Caravan) == NCR ) then
      Reply(197);
   else if( local_var(LVAR_Caravan) == V_CITY ) then
      Reply(198);

   NOption(200, Node029, 004);
   NOption(201, Node999, 004);
end

procedure Node004 begin
   NMessage(124); //open barter screen, goto13

   gdialog_mod_barter(-50);

   Reply(210);
   NOption(211, Node013, 004);
end

/*
procedure Node005 begin
   Reply(125);
   NOption(126,Node016,004);
   NOption(127,Node017,004);
   NOption(128,Node018,004);
end
*/

procedure Node006 begin
   Reply(129);
   NOption(130,Node003,004);
   NOption(131,Node999,004);
end

procedure Node007 begin
   NMessage(132); //fade out set clock forward one day, goto14
   call Node007a;
end

procedure Node007a begin
   gfade_out(600);
   game_time_advance( ONE_GAME_DAY );
   inc_local_var( LVAR_Crap_Shoveled );
   if( local_var( LVAR_Crap_Shoveled ) == 5 ) then begin
      display_msg( mstr(193) );
      give_xp( EXP_SHOVELED_ALL_CRAP );
      set_global_var( GVAR_TOWN_REP_BROKEN_HILLS, (global_var(GVAR_TOWN_REP_BROKEN_HILLS) + REP_TOWN_SHOVELED_ALL_CRAP ) );
      critter_add_trait( dude_obj, TRAIT_PERK, PERK_expert_excrement_expediter_perk, 1 );
   end
   gfade_in(600);
   call Node014;
end

procedure Node008 begin
   Reply(133);

   NLowOption(134,Node999);
   NOption(135,Node012,004);
   NOption(136,Node999,004);
end

procedure Node009 begin
   set_local_var( LVAR_Caravan, GKO );
   set_global_var(GVAR_CARAVAN_END, CARAVAN_GECKO);
   if( get_day != 1 ) then
      Reply(137);
   else
      Reply(204);

   if( get_day != 1 ) then begin
      NOption(138,Node012,004);
      NOption(139,Node999,004);
   end
   else begin
      NOption(207,StartCaravan,004);
      NOption(208,Node999,004);
      NOption(209,Node012,004);
   end
end

procedure Node010 begin
   set_global_var(GVAR_CARAVAN_END, CARAVAN_NCR);
   set_local_var( LVAR_Caravan, NCR );
   if( get_day != 11 ) then
      Reply(140);
   else
      Reply(205);

   if( get_day != 11 ) then begin
      NOption(138,Node012,004);
      NOption(139,Node999,004);
   end
   else begin
      NOption(207,StartCaravan,004);
      NOption(208,Node999,004);
      NOption(209,Node012,004);
   end
end

procedure Node011 begin
   set_global_var(GVAR_CARAVAN_END, CARAVAN_VAULT_CITY);
   set_local_var( LVAR_Caravan, V_CITY );
   if( get_day != 21 ) then
      Reply(141);
   else
      Reply(206);

   if( get_day != 21 ) then begin
      NOption(138,Node012,004);
      NOption(139,Node999,004);
   end
   else begin
      NOption(207,StartCaravan,004);
      NOption(208,Node999,004);
      NOption(209,Node012,004);
   end
end

procedure Node012 begin
   Reply(142);
   NOption(143,Node003,004);
   NOption(144,Node004,004);
/*
   if (global_var(GVAR_BH_CHAD) == CHAD_RUMOR_GECKO) then
      NOption(145,Node005,004);
   if (global_var(GVAR_BH_CHAD) == CHAD_KILLED) then
      NOption(146,Node020,004);
   if ( (global_var(GVAR_BH_CHAD) == CHAD_ARRESTED) or (global_var(GVAR_BH_CHAD) == CHAD_CONFIRMED_GECKO) or (global_var(GVAR_BH_CHAD) == CHAD_CONFIRMED_BROKENHILLS) ) then
      NOption(147,Node021,004);
*/
   NOption(148,Node006,004);
end

procedure Node013 begin
   Reply(149);
   NOption(138,Node012,004);
   NOption(151,Node999,004);
end

procedure Node014 begin
   dude_caps_adjust( 100);
   NMessage(152);
end

procedure Node015 begin
   Reply(153);
   NOption(138,Node012,004);
   NOption(139,Node999,004);
end

/*
procedure Node016 begin
   Reply(154);
   NOption(155,Node005,004);
   NOption(156,Node012,004);
   NOption(157,Node999,004);
end

procedure Node017 begin
   Reply(158);
   NOption(159,Node005,004);
   NOption(160,Node012,004);
   NOption(161,Node999,004);
end

procedure Node018 begin
   Reply(162);
   NOption(163,Node005,004);
   NOption(164,Node012,004);
   NOption(165,Node999,004);
end
*/

procedure Node019 begin
   floater(166);
end

/*
procedure Node020 begin
   Reply(167); //set outfitter=banned
   NOption(158,Node999,004);
end

procedure Node021 begin
   Reply(169);
   NOption(138,Node012,004);
   NOption(139,Node999,004);
end
*/

procedure Node022 begin
   Reply(170);
   NOption(168,Node999,004);
end

procedure Node023 begin
   set_global_var(GVAR_CARAVAN_END, CARAVAN_VAULT_CITY);
   Reply(171);
   NOption(138,StartCaravan,004); //begin caravan to vault city
   NOption(139,Node027,004);
end

procedure Node024 begin
   set_global_var(GVAR_CARAVAN_END, CARAVAN_NCR);
   Reply(172);
   NOption(138,StartCaravan,004); //begin caravan to NCR
   NOption(139,Node027,004);
end

procedure Node025 begin
   set_global_var(GVAR_CARAVAN_END, CARAVAN_GECKO);
   Reply(173);
   NOption(138,StartCaravan,004); //begin caravan to GECKO
   NOption(139,Node027,004);
end

procedure Node026 begin
   set_local_var(LVAR_Caravan,0);
   Reply(174);
   NOption(138,Node012,004);
   NOption(139,Node999,004);
end

procedure Node027 begin
   Reply(175);
   NOption(176,Node028,004);
   NOption(177,Node029,004);
   NOption(138,Node030,004);
   NOption(139,Node031,004);
end

procedure Node028 begin
   set_local_var(LVAR_Caravan,0);
   Reply(178); //caravan=0
   NOption(168,Node999,004);
end

procedure Node029 begin
   set_local_var(LVAR_Caravan,0);
   Reply(179);
   NOption(180,Node010,004); //if FtM=Exposed goto 26,else10
   NOption(181,Node009,004);
   NOption(182,Node011,004);
   NOption(203,Node999,004);
end

procedure Node030 begin
   set_local_var(LVAR_Caravan,0);
   Reply(183);
   NOption(168,Node999,004);
end

procedure Node031 begin
   Reply(184);
   NOption(185,Node012,004);
   NOption(186,StartCaravan,004); //begin caravan
end

procedure Node032 begin
   Reply(187);
   NOption(168,Node999,004);
end

procedure Node033 begin
   set_global_var( GVAR_BROKEN_HILLS_CARAVAN_POOCH_SCREW, 1 );
   set_global_var( GVAR_BH_CARAVAN, CARAVAN_STATUS_NONE );
   set_local_var( LVAR_Banned, 1 );
   BMessage(202);
end

procedure StartCaravan begin
   variable Caravan_Carts;
   variable Caravan_Encounters;
   variable Caravan_Guards;

   set_local_var( LVAR_Caravan, 0 );
   set_global_var(CURRENT_CARAVAN,GVAR_BH_CARAVAN);
   set_global_var(GVAR_CARAVAN_START, CARAVAN_BROKEN_HILLS);
   set_global_var(GVAR_BH_CARAVAN,CARAVAN_STATUS_ON_JOB);

   set_caravan_status(CARAVAN_STATUS_ON_JOB);
   Caravan_Encounters := random( 0, 4 );
   set_caravan_encounters( Caravan_Encounters );

   //ndebug("ENCOUNTERS=" + k);

   if (global_var(GVAR_CARAVAN_ENCOUNTERS_TOTAL) == 0) then begin
      Caravan_Guards := random(2,4);
      set_caravan_guards( Caravan_Guards );
      if (global_var(GVAR_CARAVAN_END) == CARAVAN_VAULT_CITY) then begin
         game_time_advance(14*ONE_GAME_DAY);
         load_map(MAPSTR_VCTYCTYD,0);
      end
      else if (global_var(GVAR_CARAVAN_END) == CARAVAN_NCR) then begin
         game_time_advance(14*ONE_GAME_DAY);
         load_map(MAPSTR_NCRENT,0);
      end
      else if (global_var(GVAR_CARAVAN_END) == CARAVAN_GECKO) then begin
         game_time_advance(14*ONE_GAME_DAY);
         load_map(MAPSTR_GECKSETL,0);
      end
   end
   else begin
      Caravan_Carts:=random(1,3);
      Caravan_Guards:=random(2,4);

      set_caravan_masters(1);
      set_caravan_guards( Caravan_Guards );
      set_caravan_carts( Caravan_Carts );
      set_caravan_brahmin( 2 * Caravan_Carts );
      set_caravan_drivers( Caravan_Carts );

      load_map(MAPSTR_BHRNDDST,11); //<-robs map script ignores this index...must be above 10
                                    //however to put caravan in center of map
   end

////////
/*
   if (global_var(GVAR_CARAVAN_ENCOUNTERS_TOTAL) == 0) then begin
       Caravan_Guards:=random(2,4);
       set_caravan_guards(Caravan_Guards);
       if (global_var(GVAR_CARAVAN_END) ==  CARAVAN_VAULT_CITY) then begin
           game_time_advance(24*ONE_GAME_DAY);
           //load_map(TARGET_TOWN,CURRENT_CARAVAN_LEADER);
       end
       else begin
           game_time_advance(24*ONE_GAME_DAY);
           //load_map(MAPSTR_REDMENT,CURRENT_CARAVAN_LEADER);
       end
   end
   else begin
       Caravan_Carts:=random(1,3);
       Caravan_Guards:=random(2,4);

       set_caravan_masters(1);
       set_caravan_guards(Caravan_Guards);
       set_caravan_carts(Caravan_Carts);
       set_caravan_brahmin(2*Caravan_Carts);
       set_caravan_drivers(Caravan_Carts);

       load_map(MAPSTR_BHRNDDST,CURRENT_CARAVAN_LEADER);
   end
  */
end

//XXXXXXXXXXXXXXXXXX
