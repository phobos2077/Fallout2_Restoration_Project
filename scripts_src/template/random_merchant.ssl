#include "../headers/command.h"
#include "../headers/modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Personality;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   variable item;
   Only_Once:=0;
   set_self_team(TEAM_CARAVAN);
   set_self_ai(AI_STORE_OWNER);

   if (map_first_run) then begin
      restock;
   end
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;


   if (Personality > 0) then
       call Node014;

   else if (Personality < 0) then
       call Node015;

   else if ((CITY_START >= 0) and self_rep_negative) then begin
       call Node012;
   end

   else if ((CITY_START <  0) and self_rep_negative) then begin
       call Node009;
   end

   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           if ((CITY_START >= 0) and self_rep_positive) then
               call Node010;
           else
               call Node011;
       gSay_End;
       end_dialogue;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   #if CITY_START >= 0
      inc_good_critter
   #else
      inc_evil_critter
   #endif

   #ifdef custom_destroy_post
      custom_destroy_post
   #endif
/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   if (local_var(LVAR_Herebefore) == 0) then begin
       set_local_var(LVAR_Herebefore,1);
       Reply(101);
   end
   else
       Reply(102);

   NOption(103,Node002,004);
   NOption(104,Node003,004);
   NOption(105,Node999,004);
   NLowOption(106,Node017);
end

procedure Node002 begin
   variable line1;
   variable line2;

   line1:=CITY_LINE+CITY_START;
   line2:=CITY_LINE+CITY_END;

   Reply(mstr(107)+mstr(line1)+mstr(108)+mstr(line2)+mstr(109));

   NOption(110,Node003,004);
   NOption(111,Node999,004);
end

procedure Node003 begin
   variable line1;

   if (CITY_START == CITY_REDDING) then
       line1:=151;
   else if (CITY_START == CITY_VAULT_CITY) then
       line1:=150;
   else if (CITY_START == CITY_BROKEN_HILLS) then
       line1:=152;
   else if ((CITY_START == CITY_NCR) or (CITY_START == CITY_MODOC)) then
       line1:=153;
   else if (CITY_START == CITY_NEW_RENO) then
       line1:=154;
   else if (CITY_START == CITY_DEN) then
       line1:=155;
   else
       line1:=156;

   Reply(mstr(112)+mstr(line1));

   NOption(113,Node005,004);
   NOption(114,Node002,004);
   NOption(115,Node999,004);
end

procedure Node004 begin
   Reply(116);

   NOption(117,Node018,004);
   NOption(118,Node999,004);
end

procedure Node005 begin
   if not self_is_evil then
       call Node010;
   else
       call Node011;
end

procedure Node006 begin
   BMessage(119);
end

procedure Node007 begin
   Reply(120);

   NOption(121,Node018,004);
end

procedure Node008 begin
   BMessage(122);

   call Node998;
end

procedure Node009 begin
   Personality:=1;
   floater(123);
end

procedure Node010 begin
   Reply(124);

   NOption(125,Node018,004);
   NOption(126,Node999,004);
end

procedure Node011 begin
   Reply(127);

   NOption(128,Node018,004);
   NOption(129,Node999,004);
end

procedure Node012 begin
   Personality:=-1;
   floater(130);
end

procedure Node013 begin
   floater(131);
end

procedure Node014 begin
   floater(132);
end

procedure Node015 begin
   floater(133);
end

procedure Node016 begin
   Reply(134);

   NOption(135,Node002,004);
   NOption(136,Node003,004);
   NOption(137,Node999,004);
   NLowOption(138,Node017);
end

procedure Node017 begin
   NMessage(139);
end

procedure Node018 begin
   gdialog_mod_barter(0);

   NMessage(300);
end
