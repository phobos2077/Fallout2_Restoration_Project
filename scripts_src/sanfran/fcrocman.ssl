/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define NPC_REACTION_TYPE       REACTION_TC
#define NPC_REACTION_VAR        7 /* same as thief variable */
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_FCROCMAN
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SAN_FRANCISCO)

#include "../headers/command.h"
#include "../headers/modreact.h"
#include "../headers/sanfran.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;

procedure in_bitch_bounds;
procedure DoCallGuards;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Caught_Thief               (7)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Node2Count := 0;
variable callGuards := 0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_SAN_FRAN_ELRONS);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_SF_ELRONOLOGIST);
   Node2Count := 0;
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) != 0) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       self_attack_dude;
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (obj_in_party(source_obj)) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_san_fran_flag(SF_ELRON_ENEMY);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if (san_fran_flag(SF_ELRON_ENEMY)) then begin
      call Node001;
//   end else if (not elron_flag_any and in_bitch_bounds) then begin
//      call Node002;
   end else if (elron_flag_any < EL_ACCEPTED) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node003;
      gSay_End;
      end_dialogue;
   end else if (elron_flag_any >= EL_ACCEPTED and not san_fran_flag(SF_ELRON_ENEMY) and not san_fran_flag(SF_GAS_ELRONS)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node011;
      gSay_End;
      end_dialogue;
   end else if (elron_flag_any >= EL_ACCEPTED and not san_fran_flag(SF_ELRON_ENEMY) and san_fran_flag(SF_GAS_ELRONS)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node016;
      gSay_End;
      end_dialogue;
   end

   set_local_var(LVAR_Herebefore, 1);

   if (callGuards == 1) then begin
      callGuards := 0;
      call DoCallGuards;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_mstr(101);
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   float_msg(self_obj, mstr(103), FLOAT_MSG_RED);
   callGuards := 1;
end


procedure Node003 begin
   Reply(107);
   NLowOption(108, Node004);
   if (dude_item(PID_ELRON_FIELD_REP)) then
      NOption(109, Node005, 4);
   NOption(mstr(110) + " " + dude_name + mstr(161), Node006, 4);
   BOption(111, Node007, 4);
   NOption(112, Node010, 4);
   BOption(113, Node008, 4);
   NOption(114, Node999, 4);
end

procedure Node004 begin
   Reply(115);
   NOption(116, Node999, 4);
end

procedure Node005 begin
   Reply(117);
   NOption(118, Node999, 4);
end

procedure Node006 begin
   Reply(119);
   NOption(120, Node999, 4);
end

procedure Node007 begin
   Reply(121);
   NOption(mstr(122) + " " + dude_name + mstr(162), Node006, 4);
   BOption(123, Node009, 4);
   NOption(124, Node010, 4);
   BOption(125, Node008, 4);
   NOption(126, Node999, 4);
end

procedure Node008 begin
   Reply(127);
   NOption(mstr(128) + " " + dude_name + mstr(163), Node006, 4);
   BOption(129, Node009, 4);
   NOption(130, Node010, 4);
   NOption(131, Node999, 4);
end

procedure Node999a begin
   callGuards := 1;
   call Node999;
end

procedure Node009 begin
   Reply(132);
   NOption(133, Node999a, 4);
end

procedure Node010 begin
   Reply(134);
   NOption(135, Node999, 4);
end

procedure Node1718 begin
   if (not elron_whirly(EL_WH_GIVEN)) then
      call Node017;
   else
      call Node018;
end

procedure Node011 begin
   //added by killap -added in the event you give the plans and gas, but decide to siphon the gas to the Tanker
   if ( (elron_whirly(EL_WH_GIVEN)) and GASDONE ) then
      Reply(300);
   //end added
   else if (elron_whirly(EL_WH_GIVEN)) then
      Reply(171);
   else
      Reply(170);
   NOption(172, Node012, 4);
   NOption(173, Node013, 4);
   if (dude_item(PID_VERTIBIRD_PLANS) and elron_whirly(EL_WH_STEALSHI)) then
      NOption(174, Node014, 4);
   if (dude_item(PID_VERTIBIRD_PLANS) and (elron_whirly(EL_WH_REJECTED) or elron_whirly(EL_WH_ACCEPTED)) ) then
      NOption(175, Node014, 4);
   if (shi_whirly(SH_WH_GIVEN) and (elron_whirly(EL_WH_REJECTED) or elron_whirly(EL_WH_ACCEPTED))) then
      NOption(176, Node015, 4);
   if (not (elron_whirly(EL_WH_GIVEN) and GASDONE and shi_whirly(SH_WH_STEALELE))) then
      NOption(177, Node1718, 4);
   NOption(178, Node999, 4);
end

procedure Node999b begin
   DownReactMinor;
   call Node999;
end

procedure Node012 begin
   Reply(179);
   NOption(180, Node011, 4);
   NOption(181, Node999, 4);
end

procedure Node013 begin
   Reply(182);
   NOption(180, Node011, 4);
   NOption(181, Node999, 4);
end

procedure NodeEndTake begin
   variable obj;

   obj := dude_item(PID_VERTIBIRD_PLANS);
   if (obj) then begin
      rm_obj_from_inven(dude_obj, obj);
      destroy_object(obj);
   end
   call Node999;
end

procedure Node014 begin
   SHOW_ELRON_GAS_QUEST;

   if (elron_whirly(EL_WH_STEALSHI)) then begin
      COMPLETE_STEAL_VERTI_SHI;
   end else begin
      COMPLETE_VERTI_ELE;
   end

   set_elron_whirly(EL_WH_GIVEN);

   COMP_QUEST(REP_BONUS_SAN_FRAN_ELRON_VIRTIBIRD, REP_HERO_SAN_FRAN_ELRON_VERTIBIRD, EXP_ELRON_GET_VERTIBIRD)
   Reply(185);
   NOption(186, NodeEndTake, 4);
end

procedure Node999c begin
   set_elron_whirly(EL_WH_STEALSHI);
   SHOW_STEAL_VERT_SHI;

   call Node999;
end

procedure Node015 begin
   Reply(187);
   if (dude_item(PID_VERTIBIRD_PLANS)) then
      NOption(188, Node014, 4);
   NOption(189, Node999c, 4);
end

procedure Node016 begin
   Reply(190);
   NOption(191, Node999, 4);
end

procedure Node999d begin
   set_elron_whirly(EL_WH_REJECTED);
   call Node999;
end

procedure Node999e begin
   if (not elron_whirly(EL_WH_STEALSHI) or not elron_whirly(EL_WH_ACCEPTED)) then begin
      if not (shi_whirly(SH_WH_GIVEN)) then begin
         set_elron_whirly(EL_WH_ACCEPTED);
         mark_navarro_on_map
         SHOW_VERTI_ELE;
      end else if (shi_whirly(SH_WH_GIVEN)) then begin
         set_elron_whirly(EL_WH_STEALSHI);
         SHOW_STEAL_VERT_SHI;
      end
   end
   call Node999;
end

procedure Node017 begin
   if (elron_whirly_any == 0) then begin
      Reply(192);
   end else if (elron_whirly(EL_WH_ACCEPTED)) then begin
      Reply(193);
   end else if (elron_whirly(EL_WH_REJECTED)) then begin
      Reply(194);
   end else if (elron_whirly(EL_WH_STEALSHI)) then begin
      Reply(195);
   end

   if (dude_item(PID_VERTIBIRD_PLANS)) then
      NOption(196, Node014, 4);
   NOption(197, Node999d, 4);
   NOption(198, Node999e, 4);
end

procedure Node018 begin
   if (not san_fran_flag(SF_GAS_BEGUN) and not GASDONE) then begin
      set_san_fran_flag(SF_GAS_BEGUN);
      Reply(199);
   end else if (GASBEGUN) then begin
      Reply(200);
   end
   NOption(201, Node999, 4);
end

procedure in_bitch_bounds begin
   return 0;
end

procedure DoCallGuards begin
   SET_GUARD_NOTIFY(dude_obj);
end

//xxxxxxxxxxxxxxxxxxxx