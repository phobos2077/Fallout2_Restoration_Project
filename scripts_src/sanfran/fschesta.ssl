/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Chemistry Computer station.
        Location: San Fran China map
        Description: Uhh you can do stuff with it.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "..\headers\define.h"
//#include "..\headers\<TownName.h>"

#define NAME                    SCRIPT_FSCHESTA
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_SAN_FR)

#include "..\headers\command.h"
#include "..\headers\sanfran.h"


/* Standard Script Procedures */
procedure start;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure use_obj_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure use_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure doCallGuards; // added by killap

#define PASSWORD_PAPERS_PID PID_PASSWORD_PAPER

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Denied                     (7)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once   := 0;
variable callGuards  := 0;

procedure start begin
end

procedure timed_event_p_proc begin
end

procedure use_p_proc begin
   script_overrides;
   if (not san_fran_flag(SF_EMPEROR_FORMATTED)) then
      dialogue_system_enter;
   else
      display_msg(mstr(134));
end


/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
end

procedure use_obj_on_p_proc begin
   display_msg(mstr(135));
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   if (local_var(LVAR_Denied) == 1) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node003;
      gSay_End;
      end_dialogue;
   end else if (not san_fran_flag(SF_EMP_ROOM_CHEM)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node009;
      gSay_End;
      end_dialogue;
   end else begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
   end

   set_local_var(LVAR_Herebefore, 1);

   // added by killap
   if (callGuards) then begin
      callGuards := 0;
      call doCallGuards;
   end
   // end added

end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
   call use_p_proc;
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure NodeHack begin
   if (skill_success(dude_obj, SKILL_SCIENCE, -110)) then begin
      call Node002;
   end else if (skill_success(dude_obj, SKILL_SCIENCE, -100)) then begin //edit by killap - was 100
      set_shihacked_flag(SHI_TRUE);
      call Node002;
   end else begin
      call Node004;
   end
end

procedure NodeGuess begin
   if (stat_success(dude_obj, STAT_lu, -8)) then
      call Node010;
   else
      call Node003;
end

procedure Node001 begin
   set_san_fran_flag(SF_EMP_ROOM_CHEM);
   Reply(103);
   NLowOption(104, Node999);

   NOption(136, NodeGuess, 4);
   if (dude_item(PASSWORD_PAPERS_PID)) then begin
      NOption(105, Node002, 4);
   end
   NOption(106, NodeHack, 4);
   NOption(107, Node999, 4);
end

procedure Node009a begin
   unset_san_fran_flag(SF_EMP_ROOM_CHEM);
   call Node009;
end

procedure Node002 begin
   Reply(108);
   NOption(109, Node005, 1);
   NOption(110, Node006, 1);
   NOption(111, Node009a, 1);
   NOption(112, Node999, 1);
end

procedure Node003 begin
   Reply(113);
   NOption(114, NodeHack, 1);
   NOption(115, Node999, 1);
end

procedure Node004 begin
   set_local_var(LVAR_Denied, 1);
   set_shihacked_flag(SHI_TRUE);
   callGuards := 1; // added by killap
   Reply(116);
   NOption(117, Node999, 4);
end

procedure Node0708 begin
   COMPLETE_TANKER_FUEL;
   if (san_fran_flag(SF_LEE_OK_FUEL_TANKER)) then
      call Node007;
   else
      call Node008;
end

procedure Node007a begin
   COMPLETE_ELRON_GAS_QUEST;
   call Node007;
end

procedure Node005 begin
   if (skill_success(dude_obj, SKILL_SCIENCE, -100)) then begin
      game_time_advance(ONE_GAME_HOUR);
      Reply(118);
   end else begin
      game_time_advance(ONE_GAME_MINUTE * 15);
      Reply(119);
   end
   // Begin Seraph's Code
   if (not tanker_flag(TANK_FUEL) and not san_fran_flag(SF_GAS_ELRONS)) then begin
      if (tanker_flag(TANK_KNOWN)) then
         NOption(120, Node0708, 4);
      if (san_fran_flag(SF_GAS_BEGUN)) then
         NOption(121, Node007a, 4);
   end
   // End Seraph's Code

   /* Disabled by Seraph
   if (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL)) then
      NOption(120, Node0708, 4);
   if (GASBEGUN and not san_fran_flag(SF_GAS_ELRONS)) then
      NOption(121, Node007a, 4);
   /*/
   if (not (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL)) and not (GASBEGUN and not san_fran_flag(SF_GAS_ELRONS))) then
      NOption(137, Node011, 4);
   NOption(122, Node002, 4);
end

procedure Node006 begin
   game_time_advance(ONE_GAME_MINUTE * 15); //added by killap
   Reply(123);
   NOption(124, Node002, 1);
end

procedure Node999a begin
   set_shihacked_flag(SHI_TRUE);
   call Node999;
end

procedure Node007 begin
   Reply(125);
   NOption(126, Node999a, 1);
end

procedure Node008 begin
   Reply(127);
   NOption(128, Node999, 1);
end

procedure Node009 begin
   Reply(129);
   NOption(130, Node001, 1);
   NOption(131, Node999, 1);
end

procedure Node010 begin
   Reply(132);
   NOption(133, Node002, 1);
end

procedure Node011 begin
   Reply(138);
   NOption(124, Node002, 1);
end

//added by killap
procedure doCallGuards begin
   set_san_fran_flag(SF_SHI_ENEMY);
end
//end added