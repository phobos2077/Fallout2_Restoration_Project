/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Dave H
        Location: San Francisco
        Description: He can move gas around.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define NPC_REACTION_TYPE       REACTION_TC
#define NPC_REACTION_VAR        7 /* same as thief variable */

#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_FCDAVEH
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SAN_FRANCISCO)

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/sanfran.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
procedure Node023;
procedure Node024; //added by killap
procedure Node999a; //added by killap
//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Caught_Thief               (7)
#define LVAR_State                      (8)
#define LVAR_Gave_Plans                 (9) //added by killap

#define HIT_N4     bit_1
#define HIT_N5     bit_2

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

variable addNode13 := 0;

variable did_task; //added by killap

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_SAN_FRAN_ELRONS);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_CITIZEN);

   if (san_fran_flag(SF_ELRON_DRIVE_FORMAT)) then
      destroy_object(self_obj);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       self_attack_dude;
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=1;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   ReactToLevel; //added by killap
   did_task := 0;
   if (san_fran_flag(SF_ELRON_ENEMY)) then begin
      call Node001;
   end else if (dave_flag(DAVE_TOLD)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node015;
      gSay_End;
      end_dialogue;
   end else begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node002;
      gSay_End;
      end_dialogue;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_evil_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end

//added by killap
procedure Node999a begin
   set_san_fran_flag(SF_ELRON_DRIVE_FORMAT);
   COMP_QUEST(REP_BONUS_SAN_FRAN_FORMAT_ELRON, REP_HERO_SAN_FRAN_ELRON_FORMAT, 0)
   call Node999;
end
//end added

// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   float_msg(self_obj, mstr(103), FLOAT_MSG_NORMAL);
end

procedure Node009a begin
   inc_general_rep(-REP_BONUS_TOLD_DAVE_LOVE);
   set_dave_flag(DAVE_TOLD2);
   call Node009;
end

procedure Node014a begin
   inc_general_rep(REP_BONUS_TOLD_DAVE_LOVE);
   set_dave_flag(DAVE_TOLD);
   call Node014;
end

procedure Node002 begin
   if (dave_flag(DAVE_TOLD)) then begin
      Reply(mstr(105) + " " + mstr(106));
   end else if ((elron_flag_any >= EL_ACCEPTED) and (not (san_fran_flag(SF_ELRON_ENEMY)))) then begin
      Reply(104);
   end else if (not bad_critter_reaction) then begin
      Reply(106);
   end else begin
      Reply(107);
   end
   NLowOption(108, Node003);
   if (not lvar_bit(LVAR_State, HIT_N4)) then
      NOption(109, Node004, 4);
   if (not lvar_bit(LVAR_State, HIT_N5)) then
      NOption(110, Node005, 4);
   //added by killap
   if ((tanker_flag(TANK_KNOWN) and not GASDONE) and (lvar_bit(LVAR_State, HIT_N5))) then
      NOption(122, Node020, 4);
   //end added by killap
   if (dave_flag(DAVE_REJECTED)) then begin
      NOption(111, Node009a, 4);
      NOption(112, Node014a, 4);
   end
   if (dave_flag(DAVE_LOVE)) then
      NOption(113, Node019, 4);
   NOption(114, Node999, 4);
end

procedure Node003 begin
   Reply(115);
   NOption(116, Node999, 1);
end

procedure Node004 begin
   set_lvar_bit_on(LVAR_State, HIT_N4);
   Reply(117);
   NOption(118, Node005, 4);
   NOption(119, Node006, 4);
   NOption(120, Node999, 4);
end

procedure Node005 begin
   set_lvar_bit_on(LVAR_State, HIT_N5);
   Reply(121);
   if (tanker_flag(TANK_KNOWN) and not GASDONE) then
      NOption(122, Node020, 4);
   NOption(123, Node021, 4);
   NOption(124, Node002, 4);
   NOption(125, Node999, 4);
end

procedure Node006 begin
   Reply(126);
   NOption(127, Node007, 4);
   NOption(128, Node999, 4);
end

procedure Node008a begin
   DownReactMinor;
   call Node008;
end

procedure Node007 begin
   set_dave_flag(DAVE_LOVE);
   Reply(129);
   NOption(130, Node002, 4);
   BOption(131, Node008a, 4);
   NOption(132, Node008, 4);
end

procedure Node008 begin
   Reply(133);
   NOption(134, Node002, 4);
   NOption(135, Node999, 4);
end

procedure Node009 begin
   Reply(136);
   NOption(137, Node010, 4);
   NOption(138, Node002, 4);
   NOption(139, Node999, 4);
end

procedure Node011a begin
   add_obj_to_inven(dude_obj, create_object(PID_VERTIBIRD_PLANS, 0, 0));
   call Node011;
end

procedure Node011b begin
   if (not san_fran_flag(SF_EMPEROR_FORMATTED)) then begin  //added check by killap
      COMPLETE_TANKER_FUEL;
      unset_san_fran_flag(SF_GAS_ELRONS);
   //   set_badger_flag(BADGER_DONESHI);  //why is badger getting the blame? - killap
      set_shihacked_flag(SHI_TRUE);
      COMP_QUEST(REP_BONUS_SAN_FRAN_TANKER_FUEL_DAVE_SHI, REP_HERO_SAN_FRAN_TANKER_FUEL_DAVE_SHI, EXP_TANKER_FUEL_DAVE_SHI)
      call Node011;
   end else begin
      call Node024;
   end
end

procedure Node011c begin
   COMPLETE_TANKER_FUEL;
   unset_san_fran_flag(SF_GAS_ELRONS);
   COMP_QUEST(REP_BONUS_SAN_FRAN_TANKER_FUEL_DAVE_EL, REP_HERO_SAN_FRAN_TANKER_FUEL_DAVE_EL, EXP_TANKER_FUEL_DAVE_EL)
   call Node011;
end

procedure Node012a begin
   set_san_fran_flag(SF_ELRON_DRIVE_FORMAT);
   COMP_QUEST(REP_BONUS_SAN_FRAN_FORMAT_ELRON, REP_HERO_SAN_FRAN_ELRON_FORMAT, 0) //added by killap
   call Node012;
end

procedure Node010 begin
   Reply(140);
   //if (elron_whirly(EL_WH_GIVEN) and (SHI_WANT_PLANS or san_fran_flag(SF_BROTHER_PLANS)) and not dude_item(PID_VERTIBIRD_PLANS)) then
   if (elron_whirly(EL_WH_GIVEN) and (SHI_WANT_PLANS or san_fran_flag(SF_BROTHER_PLANS)) and not(local_var(LVAR_Gave_Plans))) then //edit by killap
      NOption(141, Node011a, 4);
   NOption(142, Node012a, 4);
   if (not san_fran_flag(SF_GAS_ELRONS) and (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL))) then
      NOption(143, Node011b, 4);
   if (san_fran_flag(SF_GAS_ELRONS) and (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL))) then
      NOption(144, Node011c, 4);
   NOption(145, Node999, 4);
end

procedure Node013a begin
   addNode13 := 1;
   inc_general_rep(-REP_BONUS_TOLD_DAVE_LOVE);
   call Node013;
end

procedure Node011 begin
   Reply(146);
   //if (elron_whirly(EL_WH_GIVEN) and (SHI_WANT_PLANS or san_fran_flag(SF_BROTHER_PLANS)) and not dude_item(PID_VERTIBIRD_PLANS)) then
   if (elron_whirly(EL_WH_GIVEN) and (SHI_WANT_PLANS or san_fran_flag(SF_BROTHER_PLANS)) and not(local_var(LVAR_Gave_Plans))) then //edit by killap
      NOption(147, Node011a, 4);
   if (not san_fran_flag(SF_GAS_ELRONS) and (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL))) then
      NOption(148, Node011b, 4);
   if (san_fran_flag(SF_GAS_ELRONS) and (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL))) then
      NOption(149, Node011c, 4);
   BOption(150, Node013a, 4);
   NOption(151, Node999a, 4);  //edit by killap - was Node999
end

procedure Node012 begin
   Reply(152);
   BOption(153, Node013a, 4);
   NOption(154, Node999, 4);
end

procedure Node013 begin
   if (addNode13) then begin
      Reply(mstr(155) + " " + mstr(156));
   end else begin
      Reply(156);
   end

   NOption(157, Node998, 4);
end

procedure Node015a begin
   did_task := 1;
   add_obj_to_inven(dude_obj, create_object(PID_VERTIBIRD_PLANS, 0, 0));
   set_local_var(LVAR_Gave_Plans,1); //added by killap
   call Node015;
end

procedure Node015b begin
   if (not san_fran_flag(SF_EMPEROR_FORMATTED)) then begin
      did_task := 1;
      unset_san_fran_flag(SF_GAS_ELRONS);
      COMPLETE_TANKER_FUEL;
//      set_badger_flag(BADGER_DONESHI);  //why is badger getting the blame? - killap
      set_shihacked_flag(SHI_TRUE);
      COMP_QUEST(REP_BONUS_SAN_FRAN_TANKER_FUEL_DAVE_SHI, REP_HERO_SAN_FRAN_TANKER_FUEL_DAVE_SHI, EXP_TANKER_FUEL_DAVE_SHI) //added by killap
      call Node015;
   end else begin
      call Node023;
   end
end

procedure Node015c begin
   did_task := 1;
   COMPLETE_TANKER_FUEL;
   unset_san_fran_flag(SF_GAS_ELRONS);
   COMP_QUEST(REP_BONUS_SAN_FRAN_TANKER_FUEL_DAVE_EL, REP_HERO_SAN_FRAN_TANKER_FUEL_DAVE_EL, EXP_TANKER_FUEL_DAVE_EL) //added by killap
   call Node015;
end

procedure Node014 begin
   Reply(158);
   //if (elron_whirly(EL_WH_GIVEN) and (SHI_WANT_PLANS or san_fran_flag(SF_BROTHER_PLANS)) and not dude_item(PID_VERTIBIRD_PLANS)) then
   if (elron_whirly(EL_WH_GIVEN) and (SHI_WANT_PLANS or san_fran_flag(SF_BROTHER_PLANS)) and not(local_var(LVAR_Gave_Plans))) then //edit by killap
      NOption(159, Node015a, 4);
   //added 'if' by killap
   if (not (san_fran_flag(SF_ELRON_DRIVE_FORMAT))) then
      NOption(160, Node016, 4);
   if (not san_fran_flag(SF_GAS_ELRONS) and (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL))) then
      NOption(161, Node015b, 4);
   if (san_fran_flag(SF_GAS_ELRONS) and (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL))) then
      NOption(162, Node015c, 4);
   NOption(163, Node999, 4);
end

procedure Node015 begin
   //if (not dave_flag(DAVE_TOLD)) then
   if (did_task) then //edit by killap
      Reply(164);
   else
      Reply(200);

   NLowOption(108, Node003);
   //if (elron_whirly(EL_WH_GIVEN) and (SHI_WANT_PLANS or san_fran_flag(SF_BROTHER_PLANS)) and not dude_item(PID_VERTIBIRD_PLANS)) then
   if (elron_whirly(EL_WH_GIVEN) and (SHI_WANT_PLANS or san_fran_flag(SF_BROTHER_PLANS)) and not(local_var(LVAR_Gave_Plans))) then //edit by killap
      NOption(165, Node015a, 4);
   //added 'if' by killap
   if (not (san_fran_flag(SF_ELRON_DRIVE_FORMAT))) then
      NOption(166, Node016, 4);
   if (san_fran_flag(SF_GAS_ELRONS) and (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL))) then
      NOption(167, Node015c, 4);
   if (not san_fran_flag(SF_GAS_ELRONS) and (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL))) then
      NOption(168, Node015b, 4);
   NOption(169, Node999, 4);
end

procedure Node1718 begin
   if (skill_success(dude_obj, SKILL_CONVERSANT, -20)) then begin
      call Node017;
   end else begin
      call Node018;
   end
end

procedure Node016 begin
   Reply(170);
   NOption(171, Node1718, 4);
   NOption(172, Node002, 4);
   NOption(173, Node999, 4);
end

procedure Node013b begin
   inc_general_rep(-REP_BONUS_FOOLED_DAVE);
   call Node013;
end

procedure Node017 begin
   set_san_fran_flag(SF_ELRON_DRIVE_FORMAT);
   COMP_QUEST(REP_BONUS_SAN_FRAN_FORMAT_ELRON, REP_HERO_SAN_FRAN_ELRON_FORMAT, 0)
   Reply(174);
   NOption(175, Node999, 4);
   NOption(176, Node013b, 4);
end

procedure Node018 begin
   Reply(177);
   NOption(178, Node002, 4);
   NOption(179, Node999, 4);
end

procedure Node019 begin
   set_dave_flag(DAVE_NOTALKLOVE);
   Reply(180);
   NOption(181, Node002, 4);
   NOption(182, Node999, 4);
end

procedure Node020 begin
   Reply(183);
   if (not GASDONE) then
      NOption(184, Node022, 4);
   NOption(185, Node999, 4);
end

procedure Node002a begin
   UpReactMinor;
   call Node002;
end

procedure Node021 begin
   Reply(mstr(186) + " " + mstr(Random(187, 192)) + " " + mstr(193));
   NOption(194, Node002a, 4);
   NOption(195, Node999, 4);
end

procedure Node022 begin
   Reply(196);
   NOption(197, Node002, 4);
   NOption(198, Node999, 4);
end

procedure Node023 begin
   Reply(199);
   //if (elron_whirly(EL_WH_GIVEN) and (SHI_WANT_PLANS or san_fran_flag(SF_BROTHER_PLANS)) and not dude_item(PID_VERTIBIRD_PLANS)) then
   if (elron_whirly(EL_WH_GIVEN) and (SHI_WANT_PLANS or san_fran_flag(SF_BROTHER_PLANS)) and not(local_var(LVAR_Gave_Plans))) then //edit by killap
      NOption(159, Node015a, 4);
   NOption(160, Node016, 4);
   if (not san_fran_flag(SF_GAS_ELRONS) and (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL))) then
      NOption(161, Node015b, 4);
   if (san_fran_flag(SF_GAS_ELRONS) and (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL))) then
      NOption(162, Node015c, 4);
   NOption(163, Node999, 4);
end

//added by killap
procedure Node024 begin
   Reply(201);
   if (elron_whirly(EL_WH_GIVEN) and (SHI_WANT_PLANS or san_fran_flag(SF_BROTHER_PLANS)) and not(local_var(LVAR_Gave_Plans))) then //edit by killap
      NOption(141, Node011a, 4);
   if (not san_fran_flag(SF_GAS_ELRONS) and (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL))) then
      NOption(143, Node011b, 4);
   if (san_fran_flag(SF_GAS_ELRONS) and (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL))) then
      NOption(144, Node011c, 4);
   NOption(145, Node999a, 4);
end
//end added

//xxxxxxxxxxxxxxxxxxxx