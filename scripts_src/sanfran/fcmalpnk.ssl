/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Male Punks
        Location: San Fran
        Description: They float shit

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "fcmalpnk"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_FCMALPNK
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SAN_FRANCISCO)

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/sanfran.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure push_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure chooseTile;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Next_Speek_Time            (7)


#define MIN_SPEEK_TIME   (game_ticks(140))
#define MAX_SPEEK_TIME   (game_ticks(300))
#define SET_NEXT_SPEEK   set_local_var(LVAR_Next_Speek_Time, game_time + Random(MIN_SPEEK_TIME, MAX_SPEEK_TIME))
#define TIMER_MOVE 1

procedure push_p_proc begin
end

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
procedure timed_event_p_proc begin
   if (fixed_param == TIMER_MOVE) then begin
      if (not anim_busy(self_obj)) then begin
         reg_anim_clear(self_obj);
         reg_anim_begin();
         animate_move_to_tile(chooseTile);
         reg_anim_end();
         add_timer_event(self_obj, game_ticks(1), TIMER_MOVE);
      end else begin
         add_timer_event(self_obj, game_ticks(Random(20, 30)), TIMER_MOVE);
      end
   end
end


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_SAN_FRAN_PUNKS);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_SF_PUNK);

   SET_NEXT_SPEEK;

   if (not is_loading_game) then
      add_timer_event(self_obj, game_ticks(Random(20, 30)), TIMER_MOVE);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) != 0) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       self_attack_dude;
   end

   if (not critter_is_fleeing(self_obj) and obj_can_see_obj(self_obj, dude_obj)) then begin
      if (game_time > local_var(LVAR_Next_Speek_Time)) then begin
         call talk_p_proc;
      end
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   if (elron_flag(EL_ACBADGDEAD)) then
      call Node002;
   else if (BADGER_DEAD) then
      call Node010;
   else if (tanker_flag(TANK_FUEL)) then
      call Node009;
   else if (badger_flag(BADGER_GFRIEND1)) then
      call Node008;
   else if (badger_flag(BADGER_GFRIEND2)) then
      call Node007;
   else if (spleen_flag(SP_REINSERTED)) then
      call Node006;
   else if (spleen_flag(SP_LOST)) then
      call Node005;
   else if (spleen_flag(SP_REJECTED)) then
      call Node004;
   else if (GIMPED) then
      call Node003;
   else
      call Node001;

   SET_NEXT_SPEEK;
   set_local_var(LVAR_Herebefore, 1);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   float_msg(self_obj, mstr(Random(103, 119)), FLOAT_MSG_NORMAL);
end

procedure Node002 begin
   float_msg(self_obj, mstr(Random(120, 126)), FLOAT_MSG_RED);
end

procedure Node003 begin
   float_msg(self_obj, mstr(Random(127, 133)), FLOAT_MSG_GREEN);
end

procedure Node004 begin
   float_msg(self_obj, mstr(Random(134, 144)), FLOAT_MSG_RED);
end

procedure Node005 begin
   float_msg(self_obj, mstr(Random(145, 149)), FLOAT_MSG_BLUE);
end

procedure Node006 begin
   float_msg(self_obj, mstr(Random(150, 156)), FLOAT_MSG_YELLOW);
end

procedure Node007 begin
   float_msg(self_obj, mstr(Random(157, 164)), FLOAT_MSG_GREEN);
end

procedure Node008 begin
   float_msg(self_obj, mstr(Random(165, 174)), FLOAT_MSG_BLUE);
end

procedure Node009 begin
   float_msg(self_obj, mstr(Random(175, 185)), FLOAT_MSG_YELLOW);
end

procedure Node010 begin
   float_msg(self_obj, mstr(Random(186, 194)), FLOAT_MSG_BLUE);
end

procedure chooseTile begin
   return tile_num_in_direction(self_tile, Random(0, 5), Random(1, 10));
end
//xxxxxxxxxxxxxxxxxxxx