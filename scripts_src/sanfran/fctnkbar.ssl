/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "fctnkbar"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_FCTNKBAR
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SAN_FRANCISCO)

#include "../headers/command.h"
#include "../headers/modreact.h"
#include "../headers/sanfran.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Restock_Time               (7) //added by killap
#define LVAR_Restock_Time_Fix           (8) //added by killap

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure timed_event_p_proc begin
end

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_SAN_FRAN_PUNKS);
   set_self_ai(AI_SF_PUNK);

   //added by killap
   if (is_loading_game == false) then begin
      restock_fix
      if (local_var(LVAR_Restock_Time) < game_time) then begin
         check_restock_item(PID_MUTATED_FRUIT, 1, 2, 100)
         check_restock_item(PID_BEER, 1, 5, 100)
         check_restock_item(PID_BOOZE, 2, 10, 100)
         check_restock_item(PID_GAMMA_GULP_BEER, 2, 10, 100)
         check_restock_item(PID_ROENTGEN_RUM, 1, 5, 100)
         check_restock_item(PID_NUKA_COLA, 1, 5, 100)
         set_local_var(LVAR_Restock_Time, (random(3,7) * ONE_GAME_DAY) + game_time);
      end
   end
   //end added by killap
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) != 0) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile, 1);
       self_attack_dude;
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (obj_in_party(source_obj)) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if (local_var(LVAR_Hostile) or san_fran_flag(SF_PUNKS_ENEMY)) then begin
      call Node008;
   end else if ((game_time_hour > 200) and (game_time_hour < 800)) then begin //killap's fix: should be "and" not "or"
      call Node007;
   end else if (local_var(LVAR_Herebefore) == 0) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
   end else begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node006;
      gSay_End;
      end_dialogue;
   end
//   set_local_var(LVAR_Herebefore, 1); //killap's fix: moved to Node001
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_mstr(100);
   else
      display_mstr(101);
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(103);

   set_local_var(LVAR_Herebefore, 1);  //killap's fix: moved from talk_p_pro

   NLowOption(104, Node002); // Seraph's Fix: NLowOption only takes 2 args. Thanks Rob!
   NLowOption(105, Node003); // Seraph's Fix: NLowOption only takes 2 args. Thanks Rob!
   NLowOption(106, Node999); // Seraph's Fix: NLowOption only takes 2 args. Thanks Rob!
   NOption(107, Node002, 4);
   NOption(108, Node003, 4);
   NOption(109, Node999, 4);
end

procedure Node002 begin
   Reply(110);
   NOption(111, Node003, 1);
   NOption(112, Node005, 1);
end

procedure NodeBarter begin
   gdialog_barter;
   call Node004;
end

procedure Node003 begin
   Reply(113);
   NOption(115, NodeBarter, 1);
end

procedure Node004 begin
   Reply(116);
   NOption(117, Node005, 1); //killap's fix: Node005 not 003
   NOption(118, Node003, 1); //killap's fix: Node003 not 005
end

procedure Node005 begin
   Reply(119);
   NOption(121, Node999, 1);
end

procedure Node006 begin
   Reply(122);
   NOption(123, Node003, 1);
   NOption(124, Node005, 1);
end

procedure Node007 begin
   float_msg(self_obj, mstr(125), FLOAT_MSG_RED);
//   Reply(124);
//   NOption(125, Node999, 1);
end

procedure Node008 begin
   float_msg(self_obj, mstr(128), FLOAT_MSG_RED);
//   Reply(126);
//   NOption(127, Node999, 1);
end


//xxxxxxxxxxxxxxxxxxxx
