/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Ron Meyers
        Location: San Fran Tanker
        Description: He is the captain

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define NPC_REACTION_TYPE       REACTION_TG
#define NPC_REACTION_VAR        7 /* same as thief variable */

#define SCRIPT_REALNAME "fcronmey"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_FCRONMEY
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SAN_FRANCISCO)

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/sanfran.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;

//added by killap - expansion
procedure Node015;
procedure Node016;
procedure Node017;
//end added

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Caught_Thief               (7)
#define LVAR_Spleen_Reaction            (8)
#define LVAR_Girl_Reaction              (9)
#define LVAR_Node6_Count                (10)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_SAN_FRAN_PUNKS);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_SF_PUNK);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) != 0) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       self_attack_dude;
   end

   if (san_fran_flag(SF_PUNKS_ENEMY) and (obj_can_see_obj(self_obj,dude_obj)) and not critter_is_fleeing(self_obj)) then begin
      call talk_p_proc;
   end


end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (obj_in_party(source_obj)) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_san_fran_flag(SF_PUNKS_ENEMY);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   ReactToLevel; //added by killap

   if (spleen_flag(SP_REINSERTED) and local_var(LVAR_Spleen_Reaction) == 0) then begin
      ndebug("should be setting good reaction 1");
      set_local_var(LVAR_Spleen_Reaction, 1);
      set_good_critter_reaction;
   end

   if ((badger_flag(BADGER_GFRIEND1) or badger_flag(BADGER_GFRIEND2)) and local_var(LVAR_Girl_Reaction) == 0) then begin
      ndebug("should be setting good reaction 2");
      set_local_var(LVAR_Girl_Reaction, 1);
      set_good_critter_reaction;
   end

   if (san_fran_flag(SF_PUNKS_ENEMY)) then begin
      call Node014;
   end else if ( /*(not good_critter_reaction) and */(not local_var(LVAR_Girl_Reaction)) and (not local_var(LVAR_Spleen_Reaction))) then begin
      call Node001;
   end else begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node002;
      gSay_End;
      end_dialogue;
      set_local_var(LVAR_Herebefore, 1);
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
   if (local_var(LVAR_Node6_Count) == 1 and not tanker_flag(TANK_NAVCOMP)) then begin
      inc_local_var(LVAR_Node6_Count);
      Reply(153);
      NOption(150, Node999, 1);
   end
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   float_msg(self_obj, mstr(103), FLOAT_MSG_NORMAL);
//   Reply(103);
//   NOption(104, Node999, 1);
end

procedure Node002 begin
   if (local_var(LVAR_Herebefore) == 0) then
      Reply(105);
   else
      Reply(154);
   NLowOption(106, Node003);
   NOption(107, Node004, 4);
   NOption(108, Node005, 4);
   NOption(109, Node006, 4);
   NOption(110, Node999, 4);
end

procedure Node003 begin
   Reply(111);
   NOption(112, Node999, 1);
end

procedure Node004 begin
   SHOW_TANKER_FOB_QST;

   Reply(113);
   if (sub_flag(0)) then
      NOption(200, Node015, 4); //added by killap - expansion
   NOption(114, Node005, 4);
   NOption(115, Node007, 4);
   NOption(116, Node006, 4);
   NOption(117, Node999, 4);
end

procedure Node005 begin
   Reply(118);
   NOption(119, Node008, 4);
   NOption(120, Node009, 4);
   NOption(121, Node006, 4);
   NOption(122, Node999, 4);
end

procedure Node006 begin
   set_tanker_flag(TANK_KNOWN);
   set_f2(f2_state_tanker_fix_known);
   SHOW_ALL_TANKER_QST;

   if (not local_var(LVAR_Node6_Count)) then
      set_local_var(LVAR_Node6_Count, 1);
   if (tanker_flag(TANK_FUEL)) then
      Reply(152);
   else
      Reply(123);
   if (sub_flag(0)) then
      NOption(200, Node015, 4); //added by killap - expansion
   NOption(124, Node005, 4);
   NOption(125, Node007, 4);
   NOption(126, Node011, 4);
   NOption(127, Node999, 4);
end

procedure Node007 begin
   Reply(128);
   NOption(129, Node005, 4);
   NOption(130, Node011, 4);
   NOption(131, Node999, 4);
end

procedure Node008 begin
   Reply(132);
   NOption(133, Node009, 4);
   NOption(134, Node010, 4);
end

procedure Node009 begin
   Reply(135);
   NOption(136, Node006, 4);
   NOption(137, Node011, 4);
end

procedure Node010 begin
   Reply(138);
   NOption(139, Node009, 4);
   NOption(140, Node999, 4);
end

procedure Node011 begin
// Begin Seraph's Code
   //if (f2_flag_any == f2_state_tribe_kidnapped) then
   if ((f2_flag_any >= f2_state_tribe_kidnapped) and (f2_flag_any < f2_state_enclave_destroyed)) then
      Reply(mstr(141) + mstr(155));
   else
      Reply(mstr(141) + mstr(156));
// End Seraph's Code
   NOption(142, Node012, 4);
   NOption(143, Node013, 4);
   NOption(144, Node999, 4);
end

procedure Node012 begin
   Reply(145);
   NOption(146, Node013, 4);
   NOption(147, Node999, 4);
end

procedure Node013 begin
   Reply(148);
   NOption(149, Node006, 4);
   NOption(150, Node999, 4);
end

procedure Node014 begin
   float_msg(self_obj, mstr(151), FLOAT_MSG_RED);
   call Node998;
end

//added by killap - RP
procedure Node015 begin
   Reply(210);
   NOption(126, Node011, 4);
   NOption(211, Node016, 4);
end

procedure Node016 begin
   Reply(220);
   NOption(221, Node017, 4);
   NOption(114, Node005, 4);
   NOption(150, Node999, 4);
end

procedure Node017 begin
   if (sub_flag(0)) then
      set_sub_flag(SHI_SUB_KNOWN);
   Reply(230);
   NOption(114, Node005, 4);
   NOption(150, Node999, 4);
end
//end added

//xxxxxxxxxxxxxxxxxxxx