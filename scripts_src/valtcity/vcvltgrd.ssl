/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "vcvltgrd"
#include "../headers/define.h"
#include "../headers/vctydwtn.h"

#define NAME                    SCRIPT_VCVLTGRD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_VAULT_CITY)

#include "../headers/command.h"
#include "../headers/modreact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node010a;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;

procedure JumpBack;
procedure ToCourt;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Talking_Guard              (7)
#define LVAR_Created                    (8)

#define VAULT_EXIT_HEX_1   (15855)
#define VAULT_EXIT_HEX_2   (16055)
#define VAULT_EXIT_HEX_3   (16255)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Jump_Back := 0;
variable To_Court := 0;
variable Float_Hello := 0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Float_Hello := 1;
   set_self_team(TEAM_VAULT_CITIZENS);
   set_self_ai(AI_VAULT_GUARD);

   if( local_var( LVAR_Created ) == 0 ) then begin
      if ( map_var( MVAR_Vault_Guard_Count ) == 0 ) then begin
         set_local_var( LVAR_Talking_Guard, 1 );
         inc_map_var( MVAR_Vault_Guard_Count );
      end
      else
         inc_map_var( MVAR_Vault_Guard_Count );
      set_local_var( LVAR_Created, 1 );
   end
end

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

   // Check to see if I'm the last of the 2 guards left alive.  If I am, be sure to set
   // myself as the Talking Guard or else nobody will stop the player from sneaking into
   // the Vault.
   if( map_var( MVAR_Vault_Guard_Count ) == 1 ) then
      set_local_var( LVAR_Talking_Guard, 1 );

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( ( (local_var(LVAR_Hostile) == 2) or ( global_var( GVAR_ENEMY_VAULT_CITY ) == 1 ) ) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
       //added by killap
       remove_blocking_hex(15856, 0);
       remove_blocking_hex(16056, 0);
       remove_blocking_hex(16256, 0);
       //end added
   end

   if( ( local_var( LVAR_Talking_Guard ) == 1 ) and
       ( self_can_see_dude ) or
       ( self_can_hear_dude ) and
       ( ( tile_distance( VAULT_EXIT_HEX_1, dude_tile ) <= 4 ) or
         ( tile_distance( VAULT_EXIT_HEX_2, dude_tile ) <= 4 ) or
         ( tile_distance( VAULT_EXIT_HEX_3, dude_tile ) <= 4 ) ) ) then begin
      if( Fallout2_enclave_destroyed ) then begin
         if( Float_Hello == 1 ) then
            call Node017;
      end
      else if( global_var(GVAR_VAULT_CITIZEN) == CITIZEN_CAPTAIN_GUARD ) then begin
         if( Float_Hello == 1 ) then
            call Node019;
      end
      else if( Marcus_Ptr != 0 ) then
         dialogue_system_enter;
      else if( Lenny_Ptr != 0 ) then
         dialogue_system_enter;
      else if( global_var(GVAR_VAULT_CITIZEN) == CITIZEN_REAL_CITIZEN ) then begin
         if( Float_Hello == 1 ) then
            call Node020;
      end
      else
         dialogue_system_enter;
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_global_var( GVAR_ENEMY_VAULT_CITY, 1 );
      set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_KICKED_OUT );
      set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_KICKED_OUT ) );
      set_global_var( GVAR_VAULT_CITIZENSHIP, 0 );
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_PROUD;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if( global_var(GVAR_VAULT_CITIZEN) == CITIZEN_CAPTAIN_GUARD ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node006;
       gSay_End;
       end_dialogue;
   end
   else if( Marcus_Ptr != 0 ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node013;
       gSay_End;
       end_dialogue;
   end
   else if( Lenny_Ptr != 0 ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node014;
       gSay_End;
       end_dialogue;
   end
   else if( global_var(GVAR_VAULT_CITIZEN) == CITIZEN_REAL_CITIZEN ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node006;
       gSay_End;
       end_dialogue;
   end
   else if ( global_var(GVAR_VAULT_CITIZEN) == CITIZEN_GUARD_BELIEVE_FAKE ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node010;
       gSay_End;
       end_dialogue;
   end
   else if( ( game_time_hour >= 1800 ) or ( game_time_hour < 600 ) ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node002;
       gSay_End;
       end_dialogue;
       //added by killap
       if (map_var(MVAR_Player_As_Slave) == 1) then begin
           destroy_object(tile_contains_pid_obj(28917, 0, PID_BLOCKING_HEX));    // remove blocking hex at gate
       end
       //end added
   end
   else if town_rep_worse_than(-20) or (dude_iq < 4) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node001;
       gSay_End;
       end_dialogue;
   end
   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node007;
       gSay_End;
       end_dialogue;
   end

   if( Jump_Back == 1 ) then begin
      Jump_Back := 0;
      gfade_out(600);
      move_to( dude_obj, 15664, 0 );
      critter_attempt_placement( dude_obj, 15664, 0 );
      gfade_in(600);
   end
   if( To_Court == 1 ) then begin
      To_Court := 0;
      load_map( MAP_VAULTCITY_COURTYARD, 0 );
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

   // Decrement the Vault Guard count
   dec_map_var( MVAR_Vault_Guard_Count );

/* Increment the aligned critter counter*/
   inc_good_critter

   //added by killap
   remove_blocking_hex(15856, 0);
   remove_blocking_hex(16056, 0);
   remove_blocking_hex(16256, 0);
   //end added

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end

procedure JumpBack begin
   Jump_Back := 1;
end

procedure ToCourt begin
   To_Court := 1;
end

// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(102);

   NLowOption(103,Node008);
   NOption(104,Node008,004);
   NOption(105,JumpBack,004);
end

procedure Node002 begin
   Reply(106);

   NLowOption(110,Node003);
   NOption(107,Node003,004);
   NOption(108,Node003,004);
   NOption(109,Node004,004);
end

procedure Node003 begin
   Reply(111);

   NLowOption(113,ToCourt);
   NOption(112,ToCourt,004);
   BOption(114,Node004,004);
end

procedure Node004 begin
   Reply(115);

   NOption(116,ToCourt,004);
   BOption(117,Node005,004);
end

procedure Node005 begin
   Reply(118);

   NOption(119,ToCourt,004);
   BOption(120,Node998,004);
end

procedure Node006 begin
   if( global_var(GVAR_VAULT_CITIZEN) == CITIZEN_CAPTAIN_GUARD ) then
      Reply(138);
   else
      Reply(121);

   NLowOption(123,Node009);
   NOption(122,Node009,004);
   NOption(124,JumpBack,004); //edit by killap - was Node999
end

procedure Node007 begin
   Reply(125);

   NLowOption(141, JumpBack);
   NOption(139, JumpBack, 004);
   NOption(140, JumpBack, 004);
end

procedure Node008 begin
   Reply(126);

   NLowOption(141, JumpBack);
   NOption(139, JumpBack, 004);
   NOption(140, JumpBack, 004);
end

procedure Node009 begin
   //added by killap
   remove_blocking_hex(15856, 0);
   remove_blocking_hex(16056, 0);
   remove_blocking_hex(16256, 0);
   //end added

   Reply(129);

   NLowOption(130,Node999);
   NOption(131,Node999,004);
end

procedure Node010 begin
   Reply(132);

   NLowOption(128, Node010a);
   NOption(133,Node011,004);
   NOption(134,JumpBack,004); //edit by killap - was Node999
end

procedure Node010a begin
   NMessage(142);
   call JumpBack;
end

procedure Node011 begin
   Reply(135);

   NOption(136,Node012,004);
end

procedure Node012 begin
   NMessage(137);
   call JumpBack;
end

procedure Node013 begin
   if( Lenny_Ptr != 0 ) then
      Reply(143);
   else
      Reply(144);

   NLowOption(145, Node015);
   if( Lenny_Ptr != 0 ) then
      NOption(146, Node015, 004);
   else
      NOption(147, Node015, 004);
   NOption(148, Node014, 004);
   NOption(149, Node016, 004);
end

procedure Node014 begin
   Reply(150);

   NOption(151, ToCourt, 004);
   BOption(152, Node016, 004);
end

procedure Node015 begin
   if( dude_is_stupid ) then
      Reply( mstr(153) + " " + mstr(155) );
   else
      Reply( mstr(154) + " " + mstr(155) );

   NLowOption(156, Node016);
   NLowOption(157, ToCourt);
   NOption(158, ToCourt, 004);
   NOption(159, Node016, 004);
end

procedure Node016 begin
   set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_KICKED_OUT );
   set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_KICKED_OUT ) );
   Reply(160);

   BLowOption(161, Node998);
   BOption(162, Node998, 004);
end

procedure Node017 begin
   Float_Hello := 0;
   floater( random(163, 165) );
end

procedure Node018 begin
   floater( random(166, 171) );
end

procedure Node019 begin
   Float_Hello := 0;
   floater( random(172, 174) );
   //added by killap
   remove_blocking_hex(15856, 0);
   remove_blocking_hex(16056, 0);
   remove_blocking_hex(16256, 0);
   //end added

end

procedure Node020 begin
   Float_Hello := 0;
   floater( random(175, 177) );
   //added by killap
   remove_blocking_hex(15856, 0);
   remove_blocking_hex(16056, 0);
   remove_blocking_hex(16256, 0);
   //end added

end

//xxxxxxxxxxxxxxxxxxxx
