/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Sub-Amenities Officer Farrell
        Location: Vault City Amenities Office A
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "vcdarrow"
#include "../headers/define.h"
#include "../headers/vctydwtn.h"

#define NAME                    SCRIPT_VCDARROW
#define TOWN_REP_VAR            (GVAR_TOWN_REP_VAULT_CITY)

#include "../headers/command.h"
#include "../headers/modreact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node009a;
procedure Node010;
procedure Node010a;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node013a;
procedure Node013b;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Looked_Before              (7)
#define LVAR_Steal_Warning              (8)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable vault_city_darrow_box;
import variable vault_city_downtown_temp_box;

#define move_inven_to_box              move_obj_inven_to_obj(self_obj, vault_city_darrow_box);
#define move_inven_from_box            move_obj_inven_to_obj(vault_city_darrow_box, self_obj);
#define move_inven_to_temp_box         move_obj_inven_to_obj(self_obj, vault_city_downtown_temp_box);
#define move_inven_from_temp_box       move_obj_inven_to_obj(vault_city_downtown_temp_box, self_obj);

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_VAULT_CITIZENS);
   set_self_ai(AI_COWARD);

   if( not( is_loading_game ) ) then begin
      if( ( game_time_hour > 700 ) and ( game_time_hour < 1800 ) ) then begin
         if( not( self_visible ) ) then begin
            set_obj_visibility(self_obj, false);
            add_timer_event( self_obj, game_ticks( random( 30, 40 )), 1 );
         end
      end
      else begin
         if( self_visible ) then
            set_obj_visibility(self_obj, true);
      end
   end
end

procedure timed_event_p_proc begin
   if( fixed_param == 0 ) then begin
      // The delay for reading the Kick Out floater text has passed.
      // Kick the player out of Vault City.
      kick_out_of_vault_city
   end
   else if( fixed_param == 1 ) then begin
      if( ( self_can_see_dude ) and
          ( not( combat_is_initialized ) ) ) then begin
         // This is the random floater text for Darrow.
         if( Fallout2_enclave_destroyed ) then begin
            floater( random( 103, 105 ) );
         end
         else if( day ) then begin
            if (global_var(GVAR_VAULT_CITIZEN) == CITIZEN_CAPTAIN_GUARD) then begin
               floater( random( 106, 107 ) );
            end
            else if( Marcus_Ptr != 0 ) then begin
               floater(110);
               set_map_var( MVAR_Guard_Alert, 1 );
            end
            else if (Lenny_Ptr != 0) then begin
               floater(111);
               set_map_var( MVAR_Guard_Alert, 1 );
            end
            else if (global_var(GVAR_VAULT_CITIZEN) == CITIZEN_REAL_CITIZEN) then begin
               floater( random( 108, 109 ) );
            end
            else begin
               floater( random( 112, 118 ) );
            end
         end
      end
      add_timer_event( self_obj, game_ticks( random( 30, 40 )), 1 );
   end
end

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( ( (local_var(LVAR_Hostile) == 2) or ( global_var( GVAR_ENEMY_VAULT_CITY ) == 1 ) ) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end
   // If the player is stealing from Darrow's stock, warn once, then
   // call the guards.
   else if( map_var( MVAR_Stealing_From_Darrow ) == 1 ) then begin
      if( local_var( LVAR_Steal_Warning ) == 0 ) then begin
         set_map_var( MVAR_Stealing_From_Darrow, 0 );
         set_local_var( LVAR_Steal_Warning, 1 );
         floater( 146 );
      end
      else begin
         set_map_var( MVAR_Stealing_From_Darrow, 0 );
         floater( 147 );
         // Delay for a few seconds so the player can read the floater message of
         // being kicked out.
         add_timer_event( self_obj, game_ticks(2), 0);
      end
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_global_var( GVAR_ENEMY_VAULT_CITY, 1 );
      set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_KICKED_OUT );
      set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_KICKED_OUT ) );
      set_global_var( GVAR_VAULT_CITIZENSHIP, 0 );
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if (global_var(GVAR_VAULT_CITIZENSHIP) != CITIZEN_TRUE) then //added by killap
      gdialog_set_barter_mod(50);

   if ( not(Fallout2_enclave_destroyed) and ( Marcus_Ptr != 0 ) and ( global_var(GVAR_VAULT_CITIZEN) != CITIZEN_CAPTAIN_GUARD ) ) then begin
       call Node005;
   end

   else if ( not(Fallout2_enclave_destroyed) and ( Lenny_Ptr != 0 ) and ( global_var(GVAR_VAULT_CITIZEN) != CITIZEN_CAPTAIN_GUARD ) ) then begin
       call Node006;
   end

   else if (night) then begin
       call Node008;
   end

   else begin
       move_inven_to_temp_box
       move_inven_from_box
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node009;
       gSay_End;
       end_dialogue;
       move_inven_to_box
       move_inven_from_temp_box
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Looked_Before) == 0) then begin
       inc_local_var(LVAR_Looked_Before);
       display_msg(mstr(100));
   end
   else
       display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
//   set_local_var(LVAR_Hostile,2);
        set_map_var( MVAR_Guard_Alert, 1 );
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node002 begin
   floater(random(103,105));
end

procedure Node003 begin
   floater(random(106,107));
end

procedure Node004 begin
   floater(random(108,109));
end

procedure Node005 begin
   floater(110);

   call Node998;
end

procedure Node006 begin
   floater(111);

   call Node998;
end

procedure Node007 begin
   floater(random(112,118));
end

procedure Node008 begin
   floater(random(119,121));
end

procedure Node009 begin
   if (global_var(GVAR_VAULT_CITIZEN) == CITIZEN_CAPTAIN_GUARD) then
       Reply(122);
   else if (global_var(GVAR_VAULT_CITIZEN) == CITIZEN_REAL_CITIZEN) then
       Reply(123);
   else
       Reply(124);

   NLowOption(125,Node010);
   NLowOption(126,Node999);
   NOption(127,Node013,004);
   NOption(128,Node009a,004);
   NOption(129,Node999,004);
end

procedure Node009a begin
   if (global_var(GVAR_VAULT_CITIZENSHIP) == CITIZEN_TRUE) then begin
       gdialog_mod_barter(0);

       Reply(148);
       NOption(149, Node012, 004);
   end
   else begin
       gdialog_mod_barter(50); // Seraph's Fix: changed '-50' to '50'
       Reply(148);
       NOption(149, Node011, 004);
   end
end

procedure Node010 begin
   Reply(130);

   NLowOption(131,Node998);
   NLowOption(132,Node010a);
end

procedure Node010a begin
   kick_out_of_vault_city
end

procedure Node011 begin
   Reply(133);

   NOption(134,Node999,004);
   BOption(135,Node999,004);
end

procedure Node012 begin
   if (global_var(GVAR_VAULT_CITIZEN) == CITIZEN_REAL_CITIZEN) then
       Reply(136);
   else
       Reply(137);

   NOption(138,Node999,004);
   NOption(139,Node999,004);
end

procedure Node013 begin
   if (global_var(GVAR_VAULT_CITIZENSHIP) == CITIZEN_TRUE) then begin
       Reply(140);

       NOption(142,Node009a,004);
       NOption(144,Node999,004);
   end
   else begin
       Reply(141);

       NOption(143,Node009a,004);
       NOption(145,Node999,004);
   end
end

procedure Node013a begin
end

procedure Node013b begin
end

//xxxxxxxxxxxxxxxxxxxx
