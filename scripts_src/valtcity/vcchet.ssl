/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Illicit Allocations Chief Chet
        Location: Vault City
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "vcchet"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_VCCHET
#define TOWN_REP_VAR            (GVAR_TOWN_REP_VAULT_CITY)

#include "../headers/command.h"
#include "../headers/modreact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node001a;
procedure Node001b;
procedure Node001c;
procedure Node001d;
procedure Node001e;
procedure Node002;
procedure Node002a;
procedure Node002b;
procedure Node002c;
procedure Node003;
procedure Node003a;
procedure Node003b;
procedure Node003c;
procedure Node003d;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   destroy_object(self_obj);
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_VAULT_CITIZENS);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_COWARD);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   start_gdialog(NAME,self_obj,4,-1,-1);
   gSay_Start;
       call Node001;
   gSay_End;
   end_dialogue;
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(104);

   NOption(106, Node001a, 001);
   NOption(107, Node001b, 001);
   NOption(108, Node001c, 001);
   NOption(109, Node001d, 001);
   NOption(110, Node001e, 001);
   NOption(111, Node002, 001);
   NOption(112, Node003, 001);
   NOption(113, Node999, 001);
end

procedure Node001a begin
   force_f2(f2_state_enclave_destroyed);
end

procedure Node001b begin
   set_global_var( GVAR_VAULT_CITIZENSHIP, TRUE );
   set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_CAPTAIN_GUARD );
end

procedure Node001c begin
   set_global_var( GVAR_VAULT_CITIZENSHIP, TRUE );
   set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_REAL_CITIZEN );
end

procedure Node001d begin
   variable papers;
   set_global_var( GVAR_VAULT_CITIZENSHIP, FALSE );
   set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_GUARD_BELIEVE_FAKE );
   papers := create_object( PID_FAKE_CITIZENSHIP, 0, 0 );
   add_obj_to_inven( dude_obj, papers );
end

procedure Node001e begin
   variable DayPass_KickOutObj                  :=0;
   variable DayPass_KickOutCount             :=0;
   variable CitizenPapers_KickOutObj            :=0;
   variable CitizenPapers_KickOutCount          :=0;
   variable KickOutTemp                   :=0;

   set_global_var(GVAR_VAULT_CITIZEN,CITIZEN_KICKED_OUT);
   set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_KICKED_OUT ) );
   set_global_var(GVAR_VAULT_CITIZENSHIP,0);
   DayPass_KickOutCount := obj_is_carrying_obj_pid(dude_obj, PID_DAY_PASS);
   if( DayPass_KickOutCount > 0 ) then begin
      DayPass_KickOutObj := obj_carrying_pid_obj(dude_obj, PID_DAY_PASS);
      KickOutTemp := rm_mult_objs_from_inven(dude_obj, DayPass_KickOutObj, DayPass_KickOutCount );
      destroy_object(DayPass_KickOutObj); //added by killap
   end
   CitizenPapers_KickOutCount := obj_is_carrying_obj_pid(dude_obj, PID_FAKE_CITIZENSHIP);
   if( CitizenPapers_KickOutCount > 0 ) then begin
      CitizenPapers_KickOutObj := obj_carrying_pid_obj(dude_obj, PID_FAKE_CITIZENSHIP);
      KickOutTemp := rm_mult_objs_from_inven(dude_obj, CitizenPapers_KickOutObj, CitizenPapers_KickOutCount );
      destroy_object(CitizenPapers_KickOutObj); //added by killap
   end
end

procedure Node002 begin
   Reply(114);

   NOption(115, Node002a, 001);
   NOption(116, Node002b, 001);
   NOption(117, Node002c, 001);
   NOption(118, Node001, 001);
   NOption(119, Node999, 001);
end

procedure Node002a begin
   set_global_var(GVAR_REPUTATION_SLAVER, 1);
end

procedure Node002b begin
   set_global_var(GVAR_CHILDKILLER_REPUTATION, 5);
end

procedure Node002c begin
   inc_general_rep(-100);
end

procedure Node003 begin
   Reply(120);

   NOption(121, Node003a, 001);
   NOption(122, Node003b, 001);
   NOption(123, Node003c, 001);
   NOption(124, Node003d, 001);
   NOption(125, Node001, 001);
   NOption(126, Node999, 001);
end

procedure Node003a begin
   variable party_member;

   party_member := create_object_sid(PID_MARCUS, 0, 0, SCRIPT_HCMARCUS );
   critter_attempt_placement( party_member, tile_num( self_obj ), 0 );
   party_add( party_member );
end

procedure Node003b begin
   variable party_member;

   party_member := create_object_sid(PID_LENNY, 0, 0, SCRIPT_GCLENNY );
   critter_attempt_placement( party_member, tile_num( self_obj ), 0 );
   party_add( party_member );
end

procedure Node003c begin
   variable party_member;

   party_member := create_object_sid(PID_VIC, 0, 0, SCRIPT_DCVIC );
   critter_attempt_placement( party_member, tile_num( self_obj ), 0 );
   party_add( party_member );
end

procedure Node003d begin
   variable party_member;

   party_member := create_object_sid(PID_MYRON, 0, 0, SCRIPT_NHMYRON );
   critter_attempt_placement( party_member, tile_num( self_obj ), 0 );
   party_add( party_member );
end
//xxxxxxxxxxxxxxxxxxxx