/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_VCRANDAL
#define TOWN_REP_VAR            (GVAR_TOWN_REP_VAULT_CITY)

#include "../headers/command.h"
#include "../headers/modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure map_update_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node015a;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node020a;
procedure Node021;
procedure Node022;
procedure Node022a;
procedure Node023;
procedure Node023a;
procedure Node024;
procedure Node025;
procedure Node026;
procedure Node027;
procedure Node028;
procedure Node029;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Randal_Status              (7)

#define RANDAL_UNKNOWN           (0)
#define RANDAL_KNOWN             (1)
#define RANDAL_CONVINCED         (2)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable vault_city_randal_box;
import variable vault_city_downtown_temp_box;
import variable Randal_Ptr;

#define move_inven_to_box              move_obj_inven_to_obj(self_obj, vault_city_randal_box);
#define move_inven_from_box            move_obj_inven_to_obj(vault_city_randal_box, self_obj);
#define move_inven_to_temp_box         move_obj_inven_to_obj(self_obj, vault_city_downtown_temp_box);
#define move_inven_from_temp_box       move_obj_inven_to_obj(vault_city_downtown_temp_box, self_obj);

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable From_WTG := 0;
variable From_19 := 0;


procedure start begin
   Randal_Ptr := self_obj;
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Randal_Ptr := self_obj;
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_VAULT_CITIZENS);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_COWARD);

   if( not( is_loading_game ) ) then begin
      if( ( game_time_hour > 700 ) and ( game_time_hour < 1800 ) ) then begin
         if( not( self_visible ) ) then begin
            set_obj_visibility(self_obj, false);
            add_timer_event( self_obj, game_ticks( random( 20, 40 )), 1 );
         end
      end
      else begin
         if( self_visible ) then
            set_obj_visibility(self_obj, true);
      end
   end
end

procedure map_update_p_proc begin
   Randal_Ptr := self_obj;
end

procedure timed_event_p_proc begin
   if( fixed_param == 1 ) then begin
      if( not( combat_is_initialized ) ) then begin
         if( Fallout2_enclave_destroyed ) then
            call Node001;
         else if ( day ) then begin
            if ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_CAPTAIN_GUARD  ) then
               call Node005;
            else if ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_KICKED_OUT  ) then
               call Node002;
            else if ( ( obj_can_see_obj(self_obj, party_member_obj(PID_MARCUS) ) ) and ( global_var( GVAR_VAULT_CITIZEN ) != CITIZEN_CAPTAIN_GUARD ) ) then
               call Node003;
            else if ( ( obj_can_see_obj(self_obj, party_member_obj(PID_LENNY) ) ) and ( global_var( GVAR_VAULT_CITIZEN ) != CITIZEN_CAPTAIN_GUARD ) ) then
               call Node004;
            else if ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_REAL_CITIZEN ) then
               call Node006;
            else
               call Node007;
         end
      end
      add_timer_event( self_obj, game_ticks( random( 20, 40 ) ), 1 );
   end
end

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if (obj_can_see_obj(self_obj,dude_obj)) then begin
      if ( (local_var(LVAR_Hostile) == 2) or ( global_var( GVAR_ENEMY_VAULT_CITY ) == 1 ) ) then begin
         debug_msg("LVAR_Hostile == " + local_var(LVAR_Hostile));
         debug_msg("GVAR_ENEMY_VAULT_CITY == " + global_var(GVAR_ENEMY_VAULT_CITY));
         set_local_var(LVAR_Hostile,1);
         attack(dude_obj);
      end
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_global_var( GVAR_ENEMY_VAULT_CITY, 1 );
      set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_KICKED_OUT );
      set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_KICKED_OUT ) );
      set_global_var( GVAR_VAULT_CITIZENSHIP, 0 );
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_PROUD;
   Karma_Perception:=KARMA_PERCEPTION3;

   CheckKarma;

   GetReaction;

   From_WTG := 1;
   From_19 := 0;

   gdialog_set_barter_mod(35); //added by killap

   move_inven_to_temp_box
   if( ( local_var( LVAR_Randal_Status ) == RANDAL_CONVINCED ) or
       ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_CAPTAIN_GUARD ) or
       ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_REAL_CITIZEN ) ) then
      move_inven_from_box

   if( Fallout2_enclave_destroyed ) then
      call Node001;
   else if( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_KICKED_OUT ) then
      call Node002;
   else if( ( obj_can_see_obj(self_obj, party_member_obj(PID_MARCUS) ) ) and ( global_var( GVAR_VAULT_CITIZEN ) != CITIZEN_CAPTAIN_GUARD ) ) then
      call Node003;
   else if( ( obj_can_see_obj(self_obj, party_member_obj(PID_LENNY) ) ) and ( global_var( GVAR_VAULT_CITIZEN ) != CITIZEN_CAPTAIN_GUARD ) ) then
      call Node004;
   else if( night ) then
      call Node008;
   //else if( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_CAPTAIN_GUARD ) then
//      call Node005;
//   else if( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_REAL_CITIZEN ) then
//      call Node006;
   else if( local_var( LVAR_Randal_Status ) > RANDAL_UNKNOWN ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node014;
       gSay_End;
       end_dialogue;
   end
   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node009;
       gSay_End;
       end_dialogue;
   end

   if( ( local_var( LVAR_Randal_Status ) == RANDAL_CONVINCED ) or
       ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_CAPTAIN_GUARD ) or
       ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_REAL_CITIZEN ) ) then
      move_inven_to_box
   move_inven_from_temp_box

   set_local_var( LVAR_Herebefore, 1 );

   //added by killap
   if ( local_var( LVAR_Randal_Status ) < RANDAL_KNOWN ) then begin
      set_local_var( LVAR_Randal_Status, RANDAL_KNOWN);
   end
   //end added

end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if( local_var( LVAR_Herebefore ) == 0 ) then
      display_msg(mstr(101));
   else
      display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   variable val;

   val := random(104, 106);
   if( val == 104 ) then
      //floater( mstr(104) + dude_name + mstr(225) );
      float_msg(self_obj,mstr(104) + dude_name + mstr(225),FLOAT_COLOR_NORMAL); //edit by killap
   else
      floater( val );
end

procedure Node002 begin
   floater(107);
   call Node998;
end

procedure Node003 begin
   floater(108);
   call Node998;
end

procedure Node004 begin
   floater(109);
   call Node998;
end

procedure Node005 begin
   floater( random( 110, 111 ) );
end

procedure Node006 begin
   floater( random( 112, 113 ) );
end

procedure Node007 begin
   floater( random( 114, 117 ) );
end

procedure Node008 begin
   floater( random( 118, 119 ) );
end

procedure Node009 begin
   From_WTG := 0;

   Reply(120);

   if( not( dude_found_geck ) ) then
      NLowOption(121, Node010);
   NLowOption(122, Node999);
   NLowOption(123, Node999);
   NOption(124, Node015, 004);
   NOption(125, Node999, 004);
end

procedure Node010 begin
   Reply(126);

   NLowOption(127, Node011);
   NLowOption(128, Node999);
   NOption(129, Node011, 004);
   NOption(130, Node014, 004);
   NOption(131, Node999, 004);
end

procedure Node011 begin
   Reply(132);

   NLowOption(133, Node012);
   NLowOption(134, Node999);
   NOption(135, Node012, 004);
   NOption(136, Node014, 004);
   NOption(137, Node999, 004);

   From_WTG := 1;

end

procedure Node012 begin
   Reply(138);

   NLowOption(139, Node013); // Seraph's Fix: NLowOption only takes 2 args. Thanks Rob!
   NLowOption(140, Node999); // Seraph's Fix: NLowOption only takes 2 args. Thanks Rob!
   NOption(141, Node013, 004);
   NOption(142, Node014, 004);
   NOption(143, Node999, 004);
end

procedure Node013 begin
   if( ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_CAPTAIN_GUARD ) or
       ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_REAL_CITIZEN ) ) then
      Reply(144);
   else
      Reply(145);

   NLowOption(146, Node999);
   NOption(147, Node014, 004);
   NOption(148, Node999, 004);

end

procedure Node014 begin
   if( From_WTG == 1 ) then begin
      From_WTG := 0;
      Reply(149);
   end else
      Reply(150);

   NLowOption(151, Node010);
   NLowOption(152, Node999);
   NOption(153, Node015, 004);
   if( not( dude_found_geck ) ) then
      NOption(154, Node010, 004);
   if( global_var( GVAR_VAULT_VAL_TOOLS ) == VAL_TOOLS_ASSIGNED ) then
      NOption(155, Node029, 004);
   if( global_var( GVAR_VAULT_GECKO_PLANT ) == PLANT_RANDAL_GIVE ) then begin
      if( dude_iq == 4 ) then
         NOption(156, Node023, 004);
      NOption(157, Node023, 005);
   end
   if( global_var( GVAR_VAULT_RAIDERS ) == RAIDERS_GIVE_REWARD ) then
      NOption(158, Node025, 004);
   NOption(159, Node999, 004);
end

procedure Node015 begin
   Reply(160);

   NOption(161, Node015a, 004);
   if( not( dude_found_geck ) ) then
      NOption(162, Node010, 004);
   if( global_var( GVAR_VAULT_VAL_TOOLS ) == VAL_TOOLS_ASSIGNED ) then
      NOption(163, Node029, 004);
   if( global_var( GVAR_VAULT_GECKO_PLANT ) == PLANT_RANDAL_GIVE ) then begin
      if( dude_iq == 4 ) then
         NOption(164, Node023, 004);
      NOption(165, Node023, 005);
   end
   if( global_var( GVAR_VAULT_RAIDERS ) == RAIDERS_GIVE_REWARD ) then
      NOption(166, Node025, 004);
   NOption(167, Node014, 004);
   NOption(168, Node999, 004);
end

procedure Node015a begin
   if( ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_CAPTAIN_GUARD ) or
       ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_REAL_CITIZEN ) or
       ( local_var( LVAR_Randal_Status ) == RANDAL_CONVINCED ) ) then begin
      gdialog_mod_barter(35); // Seraph's Fix: changed '-35' to '35'

      Reply(228);
      NOption(229, Node028, 004);
   end
   else
      call Node016;
end

procedure Node016 begin
   Reply(169);

   NOption(171, Node017, 004);
   NOption(172, Node019, 004);
   if( global_var( GVAR_VAULT_GECKO_PLANT ) == PLANT_RANDAL_GIVE ) then begin
      if( dude_iq == 4 ) then
         NOption(164, Node023, 004);
      NOption(174, Node023, 005);
   end
   if( global_var( GVAR_VAULT_RAIDERS ) == RAIDERS_GIVE_REWARD ) then
      NOption(175, Node025, 004);
   NOption(176, Node014, 004);
   NOption(177, Node999, 004);
end

procedure Node017 begin
   Reply(178);

   NOption(179, Node018, 004);
   NOption(180, Node014, 004);
   NOption(181, Node999, 004);
end

procedure Node018 begin
   Reply(182);

   NOption(183, Node014, 004);
   NOption(184, Node999, 004);
end

procedure Node019 begin
   From_19 := 1;
   Reply(185);

   NOption(186, Node021, 004);
   if( obj_is_carrying_obj_pid( dude_obj, PID_FAKE_CITIZENSHIP ) > 0 ) then
      NOption(187, Node020, 004);
   NOption(188, Node999, 004);

end

procedure Node020 begin
   Reply(189);

   BOption(190, Node021, 004);
   NOption(191, Node021, 004);
   NOption(192, Node020a, 004);
end

procedure Node020a begin
   gfade_out(600);
   gfade_in(600);
   call Node022;
end

procedure Node021 begin
   Reply(193);

   if( From_19 == 1 ) then begin
      From_19 := 0;
      NOption(194, Node014, 004);
   end
   NOption(195, Node999, 004);
end

procedure Node022 begin
   set_local_var( LVAR_Randal_Status, RANDAL_CONVINCED );
   // Since you can barter with him now, move shop inven to Randal.
   move_inven_from_box
   Reply(196);

   NOption(197, Node022a, 004);
   NOption(198, Node014, 004);
   NOption(199, Node999, 004);
end

procedure Node022a begin
   gdialog_mod_barter(35);   //killap's fix: was -35

   Reply(228);
   NOption(229, Node028, 004);
end

procedure Node023 begin
   if( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_REAL_CITIZEN ) then
      Reply( mstr(200) + dude_name + mstr(226) );
   else
      Reply( mstr(201) + dude_name + mstr(227) );

   NOption(202, Node023a, 004);
   NOption(203, Node999, 004);
end

procedure Node023a begin
   gfade_out(600);
   gfade_in(600);
   call Node024;
end

procedure Node024 begin
   variable item;
   item := create_object( PID_HY_MAG_PART, 0, 0 );
   add_obj_to_inven( dude_obj, item );

   set_global_var(GVAR_VAULT_GET_MCCLURE_PART,REWARD_OBTAINED);
   set_global_var(GVAR_VAULT_GECKO_PLANT,PLANT_HAS_PART);

   Reply(204);

   NOption(205, Node015a, 004);
   NOption(206, Node014, 004);
   NOption(207, Node999, 008);
   NOption(208, Node999, 004);
end

procedure Node025 begin
   if( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_CAPTAIN_GUARD ) then
      Reply(209);
   else
      Reply(210);

   NOption(211, Node026, 004);
   NOption(212, Node999, 004);
end

procedure Node026 begin
   variable item1;
   variable item2;

   // Give H&K G11 and 2 Frag Grenades to player.
   item1 := create_object( PID_HK_G11, 0, 0 );
   add_obj_to_inven( dude_obj, item1 );
   item2 := create_object( PID_FRAG_GRENADE, 0, 0 );
   add_mult_objs_to_inven( dude_obj, item2, 2 );
   set_global_var( GVAR_VAULT_RAIDERS, RAIDERS_DONE );
   Reply(213);

   NOption(214, Node027, 004);
end

procedure Node027 begin
   Reply(215);

   NOption(216, Node015a, 004);
   NOption(217, Node014, 004);
   NOption(218, Node999, 004);
end

procedure Node028 begin
   Reply(219);

   NOption(220, Node014, 004);
   NOption(221, Node999, 004);
end

procedure Node029 begin
   Reply(222);

   NOption(223, Node014, 004);
   NOption(224, Node999, 004);
end