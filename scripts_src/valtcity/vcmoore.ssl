/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "vcmoore"
#include "../headers/define.h"

#define NAME                    SCRIPT_VCMOORE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_VAULT_CITY)

#include "../headers/command.h"
#include "../headers/modreact.h"
#include "../headers/newreno.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
procedure Node023;
procedure Node024;
procedure Node025;
procedure Node026;
procedure Node027;
procedure Node028;
procedure Node029;
procedure Node030;
procedure Node031;
procedure Node032;
procedure Node033;

procedure Node001a;
procedure Node003a;
procedure Node004a;
procedure Node004b;
procedure Node005a;
procedure Node006a;
procedure Node007a;
procedure Node010a;
procedure Node010b;
procedure Node010c;
procedure Node016a;
procedure Node016b;
procedure Node016c;
procedure Node018a;
procedure Node024a;
procedure Node024b;
procedure Node025a;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Looked_Before              (7)
#define LVAR_Pissed                     (8)
#define LVAR_Said_27                    (9)
#define LVAR_In_Timed_Event             (10)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable From_Node_20_23:=0;
variable From_Node024:=0;
variable From_Node001:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_VAULT_CITIZENS);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_COWARD);

   set_local_var(LVAR_In_Timed_Event,0);

   if( not( is_loading_game ) ) then begin
      if( ( global_var( GVAR_MOORE_STATE ) == MOORE_EXILED ) or
          ( ( game_time_hour > 200 ) and
            ( game_time_hour < 700 ) ) ) then begin
         if( self_visible ) then
            set_obj_visibility(self_obj, true);
      end
      else begin
         if( not( self_visible ) ) then begin
            set_obj_visibility(self_obj, false);
         end
      end
   end
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( ( (local_var(LVAR_Hostile) == 2) or ( global_var( GVAR_ENEMY_VAULT_CITY ) == 1 ) ) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

   if ( ( global_var( GVAR_MOORE_STATE ) < MOORE_EXILED ) and ( local_var(LVAR_In_Timed_Event) == 0 ) and (self_visible) ) then begin //killap's fix: added (self_visible)
       set_local_var(LVAR_In_Timed_Event,1);
       add_timer_event(self_obj,game_ticks(random(15,25)),1);
   end

end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
       set_local_var(LVAR_In_Timed_Event,0);
       call Node033;
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_global_var( GVAR_ENEMY_VAULT_CITY, 1 );
      set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_KICKED_OUT );
      set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_KICKED_OUT ) );
      set_global_var( GVAR_VAULT_CITIZENSHIP, 0 );
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_VERY_INTOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;
   GetReaction;

   if (local_var(LVAR_Pissed) == 1) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node026;
       gSay_End;
       end_dialogue;
   end

   else if (global_var(GVAR_MOORE_ACCEPT_DELIVERY) == ACCEPT_DELIVERY_HAPPY) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node027;
       gSay_End;
       end_dialogue;
   end

   else if (global_var(GVAR_MOORE_ACCEPT_DELIVERY) == ACCEPT_DELIVERY_DEATH) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node019;
       gSay_End;
       end_dialogue;
   end

   else if (global_var(GVAR_MOORE_ACCEPT_DELIVERY) == ACCEPT_DELIVERY_ASSIGNED) then begin
      call Node031;
   end

   else if (global_var(GVAR_MOORE_REFUSE_DELIVERY) == 1) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node018;
       gSay_End;
       end_dialogue;
   end

   else if (has_rep_slaver) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node024;
       gSay_End;
       end_dialogue;
   end

   else if ((dude_iq < 4) or (local_var(LVAR_Herebefore) == 0)) then begin
       set_local_var(LVAR_Herebefore,1);
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node001;
       gSay_End;
       end_dialogue;
   end

   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node020;
       gSay_End;
       end_dialogue;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Looked_Before) == 0) then begin
       set_local_var(LVAR_Looked_Before,1);
       display_msg(mstr(100));
   end
   else
       display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   set_global_var(GVAR_MOORE_STATE,MOORE_KNOWN);

   Reply(103);

   NLowOption(104,Node001a);
   NLowOption(105,Node001a);
   NLowOption(106,Node001a);
   NLowOption(107,Node999);
   NOption(108,Node002,004);
   NOption(109,Node009,004);
   NOption(110,Node999,004);
end

procedure Node001a begin
   From_Node001:=1;
   call Node017;
end

procedure Node002 begin
   Reply(111);

   NOption(112,Node015,004);
   GOption(113,Node003,004);
   NOption(114,Node999,004);
   BOption(115,Node999,008);
end

procedure Node003 begin
   if (From_Node_20_23 == 1) then begin
      From_Node_20_23 := 0;
      Reply( mstr(116) + mstr(117) );
   end
   else
       Reply(117);

   GOption(118,Node003a,004);
   if (dude_charisma > 5) then
       GOption(119,Node004,004);
   NOption(120,Node999,004);
end

procedure Node003a begin
   if (skill_success(dude_obj,SKILL_CONVERSANT,0)) then
       call Node004;
   else
       call Node010;
end

procedure Node004 begin
   Reply(121);

   GOption(122,Node004a,004);
   if (dude_charisma > 5) then
       GOption(123,Node004b,006);
   NOption(124,Node999,004);
end

procedure Node004a begin
   if ( bishop_dead ) then
       call Node028;
   else
       call Node005;
end

procedure Node004b begin
   if ( bishop_dead ) then
       call Node028;
   else
       call Node007;
end

procedure Node005 begin
   Reply(125);

   BOption(126,Node005a,004);
   GOption(127,Node006,004);
   NOption(128,Node999,004);
end

procedure Node005a begin
   //if (skill_success(dude_obj,SKILL_CONVERSANT,-30)) then
       //call Node007;
   //else
       call Node011;
end

procedure Node006 begin
   Reply(129);

   if (dude_charisma > 5) then
       GOption(130,Node007,006);
   GOption(131,Node006a,004);
   NOption(132,Node007,004);
   NOption(133,Node999,004);
end

procedure Node006a begin
//   if (skill_success(dude_obj,SKILL_CONVERSANT,-30)) then
//       call Node007;
//   else
//       call Node012;
   call Node007;
end

procedure Node007 begin
   mark_new_reno_on_map

   Reply(134);

   BOption(135,Node014,004);
   GOption(136,Node008,004);
   BOption(137,Node007a,004);
end

procedure Node007a begin
   set_global_var(GVAR_MOORE_REFUSE_DELIVERY,1);
end

procedure Node008 begin
   variable item;

   item:=create_object(PID_MOORE_GOOD_BRIEFCASE,0,0);
   add_mult_objs_to_inven(dude_obj,item,1);

   set_global_var(GVAR_MOORE_ACCEPT_DELIVERY,ACCEPT_DELIVERY_ASSIGNED);

   Reply(138);

   NOption(139,Node999,004);
end

procedure Node009 begin
   set_global_var(GVAR_MOORE_STATE,MOORE_KNOWN);

   Reply(140);

   NOption(141,Node002,004);
   NOption(142,Node999,004);
end

procedure Node010 begin
   Reply(143);

   BOption(144,Node010a,004);
   NOption(145,Node010b,004);
   if (dude_charisma > 5) then
      GOption(146,Node010c,008);
   NOption(147,Node999,004);
end

procedure Node010a begin
   DownReactModerate;
   call Node011;
end

procedure Node010b begin
   if ( bishop_dead ) then
       call Node032;
   else if (skill_success(dude_obj,SKILL_CONVERSANT,-20)) then
       call Node007;
   else
       call Node012;
end

procedure Node010c begin
   if ( bishop_dead ) then
       call Node028;
   else
       call Node007;
end

procedure Node011 begin
   set_local_var(LVAR_Pissed,1);

   Reply(148);

   if( dude_iq == 4 ) then
      BOption(149,Node998,004);
   BOption(150,Node998,005);
   NOption(151,Node999,004);
end

procedure Node012 begin
   Reply(152);

   if( not( has_rep_slaver ) ) then
      GOption(153,Node028,004);
   NOption(154,Node999,004);
   NOption(155,Node999,004);
end

procedure Node014 begin
   Reply(156);

   BOption(157,Node016,004);
end

procedure Node015 begin
   Reply(158);

   NOption(159,Node009,004);
   GOption(160,Node002,004);
   if (dude_charisma > 5) then
       GOption(161,Node004,008);
   NOption(162,Node999,004);
end

procedure Node016 begin
   mark_new_reno_on_map

   Reply(163);

   BOption(164,Node016a,004);
   BOption(165,Node016b,004);
   NOption(166,Node016c,004);
end

procedure Node016a begin
   set_local_var( LVAR_Pissed, 1 );
   call Node011;
end

procedure Node016b begin
   set_local_var( LVAR_Pissed, 1 );
end

procedure Node016c begin
   variable item;
   variable item2;

   if (obj_is_carrying_obj_pid(dude_obj,PID_MOORE_GOOD_BRIEFCASE)) then begin
       item2:=obj_carrying_pid_obj(dude_obj,PID_MOORE_GOOD_BRIEFCASE);
       destroy_object(item2);
   end

   item:=create_object(PID_MOORE_BAD_BRIEFCASE,0,0);
   add_mult_objs_to_inven(dude_obj,item,1);

   inc_general_rep( REP_BONUS_BLACKMAIL_MOORE );
   set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_BLACKMAIL_MOORE ) );
   set_global_var(GVAR_MOORE_ACCEPT_DELIVERY,ACCEPT_DELIVERY_ASSIGNED);

end

procedure Node017 begin
   if (From_Node024 == 1) then
       Reply(167);
   else if (From_Node001 == 1) then
       Reply(168);

   NLowOption(169,Node999);
end

procedure Node018 begin
   Reply(170);

   BOption(171,Node014,004);
   GOption(172,Node018a,004);
   NOption(173,Node999,004);
end

procedure Node018a begin
   if ( bishop_dead ) then
       call Node032;
   else
      call Node007;
//   else if (skill_success(dude_obj,SKILL_CONVERSANT,-10)) then
//       call Node007;
//   else
//       call Node028;
end

procedure Node019 begin
   Reply(174);

   BOption(175,Node998,004);
   BOption(176,Node998,004);
   NOption(177,Node999,004);
end

procedure Node020 begin
   From_Node_20_23 := 1;
   Reply(178);

   if ( new_CA_republic_visible_on_map ) then
       NOption(179,Node021,004);
   NOption(180,Node023,004);
   GOption(181,Node002,004);
   if (dude_charisma > 5) then
       GOption(182,Node003,008);
   NOption(183,Node999,004);
end

procedure Node021 begin
   Reply(184);

   NOption(185,Node022,004);
   BOption(186,Node011,004);
   NOption(187,Node999,004);
end

procedure Node022 begin
   Reply(188);

   NOption(189,Node004,004);
   BOption(190,Node011,004);
   NOption(191,Node999,004);
end

procedure Node023 begin
   From_Node_20_23 := 1;
   Reply(192);

   BOption(193,Node011,004);
   if ((has_rep_slaver) and ((dude_charisma > 8) or (has_skill(dude_obj,SKILL_CONVERSANT) > 95))) then
       GOption(194,Node003,004);
   if (( not(has_rep_slaver) ) and (dude_charisma > 5)) then
       GOption(195,Node003,004);
   BOption(196,Node011,004);
   NOption(197,Node999,004);
end

procedure Node024 begin
   set_global_var(GVAR_MOORE_STATE,MOORE_KNOWN);

   Reply(198);

   NLowOption(199,Node024a);
   NLowOption(200,Node024a);
   NLowOption(201,Node024a);
   NLowOption(202,Node999);
   NOption(203,Node024b,004);
   NOption(204,Node023,004);
   NOption(205,Node012,004);
   if (dude_iq < 7) then
       NOption(206,Node999,004);
   else
       NOption(207,Node999,004);
end

procedure Node024a begin
   From_Node024:=1;

   call Node017;
end

procedure Node024b begin
   if (skill_success(dude_obj,SKILL_CONVERSANT,-20)) then
       call Node012;
   else
       call Node025;
end

procedure Node025 begin
   Reply(208);

   NOption(209,Node025a,004);
   BOption(210,Node998,004);
   NOption(211,Node999,004);
end

procedure Node025a begin
//   if (skill_success(dude_obj,SKILL_CONVERSANT,-20)) then
//       call Node012;
//   else
//       call Node011;
   call Node012;
end

procedure Node026 begin
   Reply(212);

   BOption(213,Node998,004);
   NOption(214,Node025a,004);
   NOption(215,Node012,008);
   NOption(216,Node999,004);
end

procedure Node027 begin
   if (local_var(LVAR_Said_27) == 0) then begin
       set_local_var(LVAR_Said_27,1);
       Reply(217);
   end
   else
       Reply(218);

   BOption(219,Node029,004);
   NOption(220,node029,004);
   NOption(221,Node029,004);
   NOption(222,Node999,004);
end

procedure Node028 begin
   set_local_var(LVAR_Pissed,0);

   Reply(223);

   NOption(224,Node999,004);
   NOption(225,Node999,004);
end

procedure Node029 begin
   Reply(226);

   BOption(227,Node011,004);
   BOption(228,Node030,004);
   BOption(229,Node011,008);
   NOption(230,Node999,004);
   NOption(231,Node999,004);
end

procedure Node030 begin
   Reply(232);

   BOption(233,Node998,004);
   BOption(234,Node011,004);
   NOption(235,Node999,004);
end

procedure Node031 begin
   floater(236);
end

procedure Node032 begin
   Reply(237);

   NOption(238,Node999,004);
end

procedure Node033 begin
   floater(random(239,244));
end

//xxxxxxxxxxxxxxxxxxxx