/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "..\headers\define.h"
//#include "..\headers\<TownName.h>"

#define NAME                    SCRIPT_VCGREG
#define TOWN_REP_VAR            (GVAR_TOWN_REP_VAULT_CITY)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
procedure Node022a;
procedure Node023;
procedure Node024;
procedure Node025;
procedure Node026;
procedure Node027;
procedure Node028;
procedure Node028a;
procedure Node029;
procedure Node030;
procedure Node031;
procedure Node031a;
procedure Node032;
procedure Node033;
procedure Node034;
procedure Node035;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Greg_Status                (7)
#define LVAR_Test_Status                (8)

#define GREG_UNKNOWN                    (0)
#define GREG_KNOWN                      (1)

#define TEST_NOTHING                    (0)
#define TEST_KNOWN                      (1)
#define TEST_TAKEN                      (2)
#define TEST_MUTANT                     (3)
#define TEST_PASSED                     (4)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable Greg_Ptr;

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Stupid_Float := 0;
variable From_WTG := 0;
variable From_32_or_33 := 0;

procedure start begin
   Greg_Ptr := self_obj;
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Greg_Ptr := self_obj;
   Only_Once:=0;
   Stupid_Float := 0;

   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_VAULT_CITIZENS);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_COWARD);

   if( not( is_loading_game ) ) then begin
      if( ( game_time_hour > 700 ) and ( game_time_hour < 1800 ) ) then begin
         if( not( self_visible ) ) then begin
            set_obj_visibility(self_obj, false);
            add_timer_event( self_obj, game_ticks( random( 20, 40 )), 1 );
         end
      end
      else begin
         if( self_visible ) then
            set_obj_visibility(self_obj, true);
      end
   end
end

procedure map_update_p_proc begin
   Greg_Ptr := self_obj;
end

procedure timed_event_p_proc begin
   if( fixed_param == 1 ) then begin
      if( not( combat_is_initialized ) ) then begin
         if( Fallout2_enclave_destroyed ) then
            call Node001;
         else if( ( day ) and
                  ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_CAPTAIN_GUARD ) and
                  ( local_var( LVAR_Test_Status ) == TEST_MUTANT ) ) then
            call Node011;
         else if( ( day ) and
                  ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_CAPTAIN_GUARD ) ) then
            call Node005;
         else if( ( day ) and
                  ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_KICKED_OUT ) ) then
            call Node002;
         else if( ( day ) and ( Marcus_Ptr != 0 ) ) then
            call Node003;
         else if( ( day ) and ( Lenny_Ptr != 0 ) ) then
            call Node004;
         else if( ( day ) and
                  ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_REAL_CITIZEN ) and
                  ( local_var( LVAR_Test_Status ) == TEST_MUTANT ) ) then
            call Node010;
         else if( ( day ) and
                  ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_REAL_CITIZEN ) ) then
            call Node006;
         else if( ( day ) and
                  ( local_var( LVAR_Test_Status ) == TEST_MUTANT ) ) then
            call Node009;
         else if( day ) then
            call Node007;
      end
      add_timer_event( self_obj, game_ticks( random(20, 40) ), 1 );
   end
end

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( ( (local_var(LVAR_Hostile) == 2) or ( global_var( GVAR_ENEMY_VAULT_CITY ) == 1 ) ) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_global_var( GVAR_ENEMY_VAULT_CITY, 1 );
      set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_KICKED_OUT );
      set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_KICKED_OUT ) );
      set_global_var( GVAR_VAULT_CITIZENSHIP, 0 );
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION0;

   CheckKarma;

   GetReaction;

   Stupid_Float := 0;
   From_WTG := 1;
   From_32_or_33 := 0;

   if( Fallout2_enclave_destroyed ) then
      call Node001;
   else if( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_KICKED_OUT ) then
      call Node002;
   else if( ( Marcus_Ptr != 0 ) and ( global_var( GVAR_VAULT_CITIZEN ) != CITIZEN_CAPTAIN_GUARD ) ) then
      call Node003;
   else if( ( Lenny_Ptr != 0 ) and ( global_var( GVAR_VAULT_CITIZEN ) != CITIZEN_CAPTAIN_GUARD ) ) then
      call Node004;
   else if( ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_CAPTAIN_GUARD ) and
            ( local_var( LVAR_Test_Status ) == TEST_MUTANT ) ) then
      call Node011;
   else if( ( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_REAL_CITIZEN ) and
            ( local_var( LVAR_Test_Status ) == TEST_MUTANT ) ) then
      call Node010;
   else if( night ) then
      call Node008;
   else if( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_CAPTAIN_GUARD ) then
      call Node005;
   else if( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_REAL_CITIZEN ) then
      call Node006;
   else if( local_var( LVAR_Greg_Status ) == GREG_KNOWN ) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node031;
       gSay_End;
       end_dialogue;
   end
   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node012;
       gSay_End;
       end_dialogue;
   end
   set_local_var( LVAR_Herebefore, 1 );

   if( Stupid_Float == 1 ) then begin
      Stupid_Float := 0;
      floater(129);
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if( local_var( LVAR_Herebefore ) == 0 ) then
      display_msg(mstr(101));
   else
      display_msg(mstr(102));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   floater(104);
end

procedure Node002 begin
   floater(105);
   call Node998;
end

procedure Node003 begin
   floater(106);
   call Node998;
end

procedure Node004 begin
   floater(107);
   call Node998;
end

procedure Node005 begin
   floater( random( 108, 109 ) );
end

procedure Node006 begin
   floater( random( 110, 111 ) );
end

procedure Node007 begin
   floater( random( 112, 115 ) );
end

procedure Node008 begin
   floater( random( 116, 117 ) );
end

procedure Node009 begin
   floater(118);
end

procedure Node010 begin
   floater(119);
end

procedure Node011 begin
   floater(120);
end

procedure Node012 begin
   From_WTG := 0;
   Reply(121);

   BLowOption(122, Node013);
   NLowOption(123, Node014);
   NOption(124, Node015, 004);
   NOption(125, Node999, 004);
end

procedure Node013 begin
   Reply(126);

   BLowOption(127, Node998);
   NLowOption(128, Node014);
end

procedure Node014 begin
   Stupid_Float := 1;
end

procedure Node015 begin
   set_local_var( LVAR_Greg_Status, GREG_KNOWN );
   Reply(130);

   if( check_general_rep < 0 ) then
      BOption(131, Node016, 004);
   else
      NOption(132, Node016, 004);
   NOption(133, Node999, 004);
end

procedure Node016 begin
   Reply(134);

   if( local_var( LVAR_Test_Status ) < TEST_TAKEN ) then
      NOption(135, Node017, 004);
   if( local_var( LVAR_Test_Status ) == TEST_TAKEN ) then
      NOption(136, Node022, 004);
   NOption(137, Node031, 004);
   NOption(138, Node999, 004);
end

procedure Node017 begin
   if( From_32_or_33 == 1 ) then begin
      From_32_or_33 := 0;
      Reply(139);
   end
   else
      Reply(140);

   if( local_var( LVAR_Test_Status ) < TEST_TAKEN ) then
      NOption(141, Node019, 004);
   if( local_var( LVAR_Test_Status ) == TEST_TAKEN ) then
      NOption(142, Node022, 004);
   NOption(143, Node018, 004);
   NOption(144, Node031, 004);
   NOption(145, Node999, 004);
end

procedure Node018 begin
   Reply(146);

   if( local_var( LVAR_Test_Status ) < TEST_TAKEN ) then
      NOption(147, Node019, 004);
   if( local_var( LVAR_Test_Status ) == TEST_TAKEN ) then
      NOption(148, Node022, 004);
   NOption(149, Node031, 004);
   NOption(150, Node999, 004);
end

procedure Node019 begin
   if( local_var( LVAR_Test_Status ) == TEST_KNOWN ) then
      set_local_var( LVAR_Test_Status, TEST_KNOWN );
   Reply(151);

   if( dude_iq == 4 ) then
      BOption(152, Node020, 004);
   if( dude_iq < 6 ) then
      NOption(153, Node021, 004);
   if( ( dude_iq == 6 ) or ( dude_iq == 7 ) ) then
      NOption(154, Node021, 006);
   if( ( dude_iq == 8 ) or ( dude_iq == 9 ) ) then
      NOption(155, Node021, 008);
   NOption(156, Node021, 010);
   NOption(157, Node031, 004);
   NOption(158, Node999, 004);
end

procedure Node020 begin
   if( ( global_var( GVAR_VAULT_GECKO_PLANT ) == PLANT_ACCEPTED_QUEST ) or
       ( global_var( GVAR_VAULT_GECKO_PLANT ) == PLANT_DECLINED_QUEST ) ) then
      Reply(159);
   else
      Reply(160);

   if( ( global_var( GVAR_VAULT_GECKO_PLANT ) == PLANT_ACCEPTED_QUEST ) or
       ( global_var( GVAR_VAULT_GECKO_PLANT ) == PLANT_DECLINED_QUEST ) ) then
      NOption(161, Node003, 004);
   if( local_var( LVAR_Test_Status ) < TEST_TAKEN ) then
      NOption(162, Node021, 004);
   if( local_var( LVAR_Test_Status ) == TEST_TAKEN ) then
      NOption(163, Node022, 004);
   NOption(164, Node031, 004);
   NOption(165, Node999, 004);
end

procedure Node021 begin
   if( dude_charisma > 5 ) then
      Reply(166);
   else
      Reply(167);

   if( dude_charisma > 5 ) then begin
      NOption(168, Node022, 004);
      NOption(169, Node031, 004);
      NOption(170, Node999, 004);
   end
   else begin
      NOption(171, Node022, 004);
      BOption(172, Node022, 004);
      NOption(173, Node031, 004);
      NOption(174, Node999, 004);
   end
end

procedure Node022 begin
   if( local_var( LVAR_Test_Status ) < TEST_TAKEN ) then
      Reply( mstr(175) + " " + mstr(177) );
   else
      Reply( mstr(176) + " " + mstr(177) );

   NOption(178, Node022a, 004);
end

procedure Node022a begin
   gfade_out(600);
   gfade_in(600);
   call Node023;
end

procedure Node023 begin
   if( ( dude_iq > 8 ) and ( dude_perception > 8 ) and ( dude_luck > 8 ) ) then
      Reply(179);
   else
      Reply(180);

   if( ( dude_iq > 8 ) and ( dude_perception > 8 ) and ( dude_luck > 8 ) ) then
      NOption(181, Node026, 004);
   else
      NOption(182, Node024, 004);
end

procedure Node024 begin
   if( dude_iq < 9 ) then
      Reply(183);
   else if( dude_perception < 9 ) then
      Reply(184);
   else if( dude_luck < 9 ) then
      Reply(185);

   NOption(186, Node025, 004);
end

procedure Node025 begin
   game_time_advance( 5 * ONE_GAME_HOUR );
   set_local_var( LVAR_Test_Status, TEST_TAKEN );
   if( dude_iq < 9 ) then
      Reply( mstr(187) + " " + mstr(188) );
   else if( dude_perception < 9 ) then
      Reply( mstr(187) + " " + mstr(189) );
   else if( dude_luck < 9 ) then
      Reply( mstr(187) + " " + mstr(190) );

   NOption(191, Node999, 004);
end

procedure Node026 begin
   Reply(192);

   NOption(193, Node027, 004);
end

procedure Node027 begin
   game_time_advance( 3 * ONE_GAME_HOUR );
   Reply(194);
   NOption(195, Node028, 004);
end

procedure Node028 begin
   Reply(196);

   NOption(197, Node028a, 004);
end

procedure Node028a begin
   if( global_var( GVAR_HAVE_MUTATED ) == 1 ) then
      call Node029;
   else
      call Node030;
end

procedure Node029 begin
   set_local_var( LVAR_Test_Status, TEST_MUTANT );
   Reply(198);

   NOption(199, Node998, 004);
   NOption(200, Node999, 004);
end

procedure Node030 begin
   display_msg( mstr(226) );
   give_xp( EXP_PASSED_VAULT_TEST );
   set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_PASSED_CITIZENSHIP_TEST ) );
   set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_REAL_CITIZEN );
   set_global_var( GVAR_VAULT_CITIZENSHIP, CITIZEN_TRUE );
   set_local_var( LVAR_Test_Status, TEST_PASSED );
   Reply(201);

   NOption(202, Node999, 004);
end

procedure Node031 begin
   if( From_WTG == 1 ) then begin
      From_WTG := 0;
      Reply(203);
   end
   else
      Reply(204);

   NOption(205, Node016, 004);
   if( local_var( LVAR_Test_Status ) == TEST_KNOWN ) then
      NOption(206, Node031a, 004);
   if( local_var( LVAR_Test_Status ) == TEST_TAKEN ) then
      NOption(207, Node022, 004);
   if( ( global_var( GVAR_VAULT_GECKO_PLANT ) == PLANT_ACCEPTED_QUEST ) or
       ( global_var( GVAR_VAULT_GECKO_PLANT ) == PLANT_DECLINED_QUEST ) ) then
      NOption(208, Node034, 004);
   NOption(209, Node999, 004);
end

procedure Node031a begin
   if( ( global_var( GVAR_VAULT_GECKO_PLANT ) == PLANT_ACCEPTED_QUEST ) or
       ( global_var( GVAR_VAULT_GECKO_PLANT ) == PLANT_DECLINED_QUEST ) ) then
      call Node021;
   else
      call Node032;
end

procedure Node032 begin
   From_32_or_33 := 1;
   Reply(210);

   NOption(211, Node017, 004);
   NOption(212, Node033, 004);
   NOption(213, Node031, 004);
   NOption(214, Node999, 004);
end

procedure Node033 begin
   From_32_or_33 := 1;
   Reply(215);

   NOption(216, Node017, 004);
   NOption(217, Node031, 004);
   NOption(218, Node999, 004);
end

procedure Node034 begin
   mark_gecko_on_map

   Reply(219);

   NOption(220, Node035, 004);
   NOption(221, Node031, 004);
   NOption(222, Node999, 004);
end

procedure Node035 begin
   Reply(223);

   NOption(224, Node031, 004);
   NOption(225, Node999, 004);
end