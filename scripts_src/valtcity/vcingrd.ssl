/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Inner Vault City Guard
        Location: Vault City
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "vcingrd"
#include "../headers/define.h"
#include "../headers/vctydwtn.h"

#define NAME                    SCRIPT_VCINGRD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_VAULT_CITY)

#include "../headers/command.h"
#include "../headers/modreact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable Locker_Watcher;
import variable Shack_Shelf_Watcher;
import variable Shack_Locker_Watcher;

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_VAULT_CITIZENS);
   set_self_ai(AI_VAULT_GUARD);

   add_timer_event( self_obj, game_ticks( random(20, 40) ), 1);
end

procedure timed_event_p_proc begin
   if( fixed_param == 1 ) then begin
      if( not( combat_is_initialized ) ) then begin
         if( self_can_see_dude ) then begin
            if( Fallout2_enclave_destroyed ) then
               call Node013;
            else if( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_CAPTAIN_GUARD ) then
               call Node008;
            else if( Marcus_Ptr != 0 ) then
               dialogue_system_enter;
            else if( Lenny_Ptr != 0 ) then
               dialogue_system_enter;
            else if( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_REAL_CITIZEN ) then
               call Node006;
            else if( ( game_time_hour >= 1800 ) or ( game_time_hour < 600 ) ) then
               dialogue_system_enter;
            else
               call Node007;
         end
      end
      add_timer_event( self_obj, game_ticks( random(20, 40) ), 1);
   end
end

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( ( (local_var(LVAR_Hostile) == 2) or ( global_var( GVAR_ENEMY_VAULT_CITY ) == 1 ) ) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end
   else if( cur_map_index == MAP_VAULTCITY_DOWNTOWN ) then begin
      if( map_var( MVAR_Guard_Alert ) == 1 ) then begin
         set_map_var( MVAR_Guard_Alert, 0 );
         set_local_var(LVAR_Hostile,1);
         anim_run_to_point_rate( dude_tile, 5 )
         attack(dude_obj);
      end
      else if( self_can_see_dude ) then
         set_watcher( Locker_Watcher )
   end
   else if( cur_map_index == MAP_VAULTCITY_COUNCIL ) then begin
      if( self_can_see_dude ) then begin
         set_watcher( Shack_Shelf_Watcher )
         set_watcher( Shack_Locker_Watcher )
      end
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_global_var( GVAR_ENEMY_VAULT_CITY, 1 );
      set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_KICKED_OUT );
      set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_KICKED_OUT ) );
      set_global_var( GVAR_VAULT_CITIZENSHIP, 0 );
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   variable Time;

   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION0;

   CheckKarma;

   GetReaction;

   if( Fallout2_enclave_destroyed ) then
      call Node014;
   else if( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_CAPTAIN_GUARD ) then
      call Node008;
   else if( Marcus_Ptr != 0 ) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node009;
      gSay_End;
      end_dialogue;
   end
   else if( Lenny_Ptr != 0 ) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node009;
      gSay_End;
      end_dialogue;
   end
   else if( global_var( GVAR_VAULT_CITIZEN ) == CITIZEN_REAL_CITIZEN ) then
      call Node006;
   else if( ( game_time_hour >= 1800 ) or ( game_time_hour < 600 ) ) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node002;
      gSay_End;
      end_dialogue;
   end
   else if( ( dude_charisma < 6 ) or ( dude_iq < 4 ) ) then begin
      call Node001;
   end
   else
      call Node007;
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(message_str(SCRIPT_VCCRTGRD,100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(message_str(SCRIPT_VCCRTGRD,101));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

procedure Node997 begin
   // Place the PC outside of the main gate
   // This is just a temp fix
   load_map(MAP_VAULTCITY_COURTYARD, 0);
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   floater(random(100,103));
end

procedure Node002 begin
   Reply(104);

   NOption(105,Node003,004);
   if( global_var(GVAR_VAULT_CITIZEN) == CITIZEN_GUARD_BELIEVE_FAKE ) then
      NOption(127,Node015,004);
   NOption(106,Node003,004);
   NOption(107,Node004,004);
   NLowOption(108,Node003);
end

procedure Node003 begin
   Reply(109);

   NOption(110,Node997,004);
   NOption(112,Node004,004);
   NLowOption(111,Node997);
end

procedure Node004 begin
   Reply(113);

   NOption(114,Node997,004);
   NOption(115,Node005,004);
end

procedure Node005 begin
   Reply(116);

   NOption(117,Node997,004);
   BOption(118,Node998,004);
end

procedure Node006 begin
   floater(random(119,122));
end

procedure Node007 begin
   floater(random(123,126));
end

procedure Node008 begin
   floater( random(128, 131) );
end

procedure Node009 begin
   if( Lenny_Ptr != 0 ) then
      Reply(132);
   else if( Marcus_Ptr != 0 ) then
      Reply(133);

   NLowOption(134, Node011);
   if( Lenny_Ptr != 0 ) then
      NOption(135, Node011, 004);
   if( Marcus_Ptr != 0 ) then
      NOption(136, Node011, 004);
   NOption(137, Node010, 004);
   NOption(138, Node012, 004);
end

procedure Node010 begin
   Reply(139);

   NOption(140, Node997, 004);
   BOption(141, Node012, 004);
end

procedure Node011 begin
   if( dude_iq < 4 ) then
      Reply( mstr(142) + " " + mstr(144) );
   else
      Reply( mstr(143) + " " + mstr(144) );

   BLowOption(145, Node012);
   NLowOption(146, Node997);
   NOption(147, Node997, 004);
   NOption(148, Node012, 004);
end

procedure Node012 begin
   set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_KICKED_OUT );
   set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_KICKED_OUT ) );
   set_global_var( GVAR_VAULT_CITIZENSHIP, 0 );
   set_global_var(GVAR_DAY_PASS_SHOWN, 0);
   Reply(149);

   BLowOption(150, Node998);
   BOption(151, Node998, 004);
end

procedure Node013 begin
   floater( random(152, 154) );
end

procedure Node014 begin
   floater( random(155, 160) );
end

procedure Node015 begin
   Reply(161);

   if( dude_item_count( PID_FAKE_CITIZENSHIP) > 0) then
      NOption(162, Node016, 004);
   NOption(163, Node005, 004);
end

procedure Node016 begin
   Reply(164);

   NOption(165, Node999, 004);
   BOption(166, Node999, 004);
end
//xxxxxxxxxxxxxxxxxxxx
