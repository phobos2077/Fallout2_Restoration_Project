/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Ed, the Brahmin Seller
        Location: Vault City Coutryard
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "vced"
#include "../headers/define.h"

// Include this so we can get the vic_is_trader flag.
#include "../headers/den.h"

#define NAME                    SCRIPT_VCED
#define TOWN_REP_VAR            (GVAR_TOWN_REP_VAULT_CITY)

#include "../headers/command.h"
#include "../headers/modreact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure map_update_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Ed_Memory                  (7)

#define ED_NOTHING                      (0)
#define ED_REMEMBERED                   (1)
#define ED_DONE                         (2)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable Ed_Ptr;

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Vic_Ed_Message:=0;

procedure start begin
   Ed_Ptr := self_obj;
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   Vic_Ed_Message:=0;
   Ed_Ptr := self_obj;

   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_VAULT_CITIZENS);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_CITIZEN);
   add_timer_event( self_obj, game_ticks(random(30, 40)), 1);
end

procedure map_update_p_proc begin
   Ed_Ptr := self_obj;
end

procedure timed_event_p_proc begin
   if( fixed_param == 1 ) then begin
      if( not( combat_is_initialized ) ) then begin
         floater( random(102, 105) );
      end
      add_timer_event( self_obj, game_ticks(random(30, 40)), 1);
   end
end

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( ( ( local_var(LVAR_Hostile) == 2 ) or ( ( global_var( GVAR_ENEMY_VAULT_COURTYARD ) == 1 ) ) ) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

   // If Vic gets within 12 hexes of Ed's place, spout out a message about it.
   if( (Vic_Ed_Message == 0) and (Vic_Ptr != 0) and (vic_trader) ) then begin
      if(tile_distance(19113, tile_num(Vic_Ptr) ) <= 12) then begin
         Vic_Ed_Message := 1;
         float_msg(Vic_Ptr, message_str(NAME,165), FLOAT_COLOR_NORMAL);
      end
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_global_var( GVAR_ENEMY_VAULT_COURTYARD, 1 );
      set_global_var( GVAR_ENEMY_VAULT_CITY, 1 );
      set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_KICKED_OUT );
      set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_KICKED_OUT ) );
      set_global_var( GVAR_VAULT_CITIZENSHIP, 0 );
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_INTOLERANT;
   Karma_Perception:=KARMA_PERCEPTION0;

   CheckKarma;

   GetReaction;

   if( dude_iq < 4 ) then
      call Node016;
   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node002;
       gSay_End;
       end_dialogue;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node002 begin
   if( local_var(LVAR_Herebefore) == 0 ) then begin
      set_global_var( GVAR_VC_MET_ED, ED_KNOWN );
      Reply(106);
   end
   else
      Reply(107);

   set_local_var(LVAR_Herebefore,1);

   if( (vic_trader) and (Vic_Ptr == 0) and (local_var(LVAR_Ed_Memory) == ED_NOTHING) and (obj_is_carrying_obj_pid( dude_obj, PID_VIC_WATER_FLASK ) > 0) ) then
      NOption(108, Node005, 004);
   if( (vic_trader) and (Vic_Ptr == 0) and (local_var(LVAR_Ed_Memory) == ED_NOTHING) and (obj_is_carrying_obj_pid( dude_obj, PID_VIC_WATER_FLASK ) == 0) ) then
      NOption(109, Node003, 004);
   if( (vic_trader) and (Vic_Ptr != 0) and (local_var(LVAR_Ed_Memory) == ED_NOTHING) ) then
      NOption(110, Node005, 004);
   if(local_var(LVAR_Ed_Memory) >= ED_REMEMBERED) then
      NOption(111, Node006, 004);
   NOption(112, Node013, 004);
   NOption(113, Node014, 004);
   NOption(114, Node015, 004);
   NOption(115, Node999, 004);
end

procedure Node003 begin
   Reply(116);

   NOption(117, Node004, 004);
   NOption(118, Node999, 004);
end

procedure Node004 begin
   Reply(119);

   NOption(120, Node012, 004);
   NOption(121, Node999, 004);
end

procedure Node005 begin
   if( Vic_Ptr != 0 ) then
      Reply(122);
   else
      Reply(123);

   if( obj_is_carrying_obj_pid( dude_obj, PID_VIC_WATER_FLASK ) > 0 ) then
      NOption(124, Node006, 004);
   if( obj_is_carrying_obj_pid( dude_obj, PID_VIC_WATER_FLASK ) == 0 ) then
      NOption(125, Node004, 004);
   NOption(126, Node999, 004);

end

procedure Node006 begin
   if( local_var( LVAR_Ed_Memory ) == ED_NOTHING ) then
      set_local_var( LVAR_Ed_Memory, ED_REMEMBERED );

   Reply(127);

   NOption(128, Node007, 004);
   NOption(129, Node999, 004);
end

procedure Node007 begin
   Reply(130);

   NOption(131, Node008, 004);
   NOption(132, Node999, 004);
end

procedure Node008 begin
   mark_broken_hills_on_map
   mark_new_reno_on_map
   mark_redding_on_map
   mark_den_on_map
   mark_modoc_on_map

   Reply(133);

   NOption(134, Node009, 004);
   NOption(135, Node999, 004);
end

procedure Node009 begin
   Reply(136);

   NOption(137, Node010, 004);
   NOption(138, Node999, 004);
end

procedure Node010 begin
   Reply(139);

   NOption(140, Node011, 004);
   NOption(141, Node999, 004);
end

procedure Node011 begin
   if( local_var( LVAR_Ed_Memory ) == ED_REMEMBERED ) then begin
      set_local_var( LVAR_Ed_Memory, ED_DONE );
      display_msg( mstr(166) );
      give_xp(EXP_ED_REMEMBERED);
   end

   Reply(142);

   NOption(143, Node012, 004);
   NOption(144, Node999, 004);
end

procedure Node012 begin
   Reply(145);

   if( (vic_trader) and (Vic_Ptr == 0) and (local_var(LVAR_Ed_Memory) == ED_NOTHING) and (obj_is_carrying_obj_pid( dude_obj, PID_VIC_WATER_FLASK ) > 0) ) then
      NOption(146, Node005, 004);
   if( (vic_trader) and (Vic_Ptr == 0) and (local_var(LVAR_Ed_Memory) == ED_NOTHING) and (obj_is_carrying_obj_pid( dude_obj, PID_VIC_WATER_FLASK ) == 0) ) then
      NOption(147, Node003, 004);
   if( (vic_trader) and (Vic_Ptr != 0) and (local_var(LVAR_Ed_Memory) == ED_NOTHING) ) then
      NOption(148, Node005, 004);
   if(local_var(LVAR_Ed_Memory) >= ED_REMEMBERED) then
      NOption(149, Node006, 004);
   NOption(150, Node013, 004);
   NOption(151, Node014, 004);
   NOption(152, Node015, 004);
   NOption(153, Node999, 004);
end

procedure Node013 begin
   if( local_var( LVAR_Ed_Memory ) == ED_DONE ) then
      Reply(154);
   else
      Reply(155);

   NOption(156, Node012, 004);
   NOption(157, Node999, 004);
end

procedure Node014 begin
   Reply(158);

   NOption(159, Node012, 004);
   NOption(160, Node999, 004);
end

procedure Node015 begin
   Reply(161);

   NOption(162, Node012, 004);
   NOption(163, Node999, 004);
end

procedure Node016 begin
   floater(164);
end


//xxxxxxxxxxxxxxxxxxxx