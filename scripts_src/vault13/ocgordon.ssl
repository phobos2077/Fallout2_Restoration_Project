/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Gordon Vault13
        Location: Vault 13
        Description: He is the Shrine Keeper

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define NPC_REACTION_TYPE       REACTION_TC
#define NPC_REACTION_VAR        8 /* same as thief variable */

#define SCRIPT_REALNAME "ocgordon"
#include "../headers/define.h"
#include "../headers/vault13.h"

#define NAME                    SCRIPT_OCGORDON
#define TOWN_REP_VAR            GVAR_TOWN_REP_VAULT_13

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/v13.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure push_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;

#define TO_WARN   1
#define TO_GUARDS 2
#define TO_OUT    3

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Thrown_Out                 (7)
#define LVAR_Caught_Thief               (8)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable begin
   Node3Rep := 0;
   q1 := 0;
   q2 := 0;
   q3 := 0;
   q4 := 0;
   warpOutPlayer := 0;
   moveGuards := 0;
end

procedure start begin
   if (VAULT13_SEQ) then begin
      DO_KILL_SELF
   end
end

procedure push_p_proc begin
end

procedure timed_event_p_proc begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_VAULT13);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_CITIZEN);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if (((local_var(LVAR_Hostile) == 2) or map_var(MVAR_Vault13_Enemy)) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       set_map_var(MVAR_Vault13_Enemy, 1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_map_var(MVAR_Vault13_Enemy, 1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if (lvar(LVAR_Hostile) == 1) then begin
      call Node010;
   end else if (lvar(LVAR_Thrown_Out) == TO_OUT) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node011;
       gSay_End;
       end_dialogue;
   end else if (local_var(LVAR_Herebefore) == 0) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
   end else begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node012;
      gSay_End;
      end_dialogue;
   end
   set_local_var(LVAR_Herebefore, 1);

   if (warpOutPlayer == 1) then begin
      warpOutPlayer := 0;
      fadeout(1);
      move_to(dude_obj, GORDON_WARP_PLAYER_POS, elevation(self_obj));
      fadein(1);
   end

   if (moveGuards == 1) then begin
      moveGuards := 0;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(103);
   NLowOption(104, Node002);
   NLowOption(105, Node003);
   NLowOption(106, Node999);

   GOption(mstr(107) + " " + dude_name + mstr(177), Node003, 4);
   BOption(108, Node009, 4);
   NOption(109, Node999, 4);
end

procedure Node002 begin
   Reply(110);
   NLowOption(111, Node003);
   NLowOption(112, Node999);
end

procedure Node004a begin
   q1 := 1;
   call Node004;
end
procedure Node005a begin
   q2 := 1;
   call Node005;
end
procedure Node007a begin
   q3 := 1;
   call Node007;
end
procedure Node008a begin
   q4 := 1;
   call Node008;
end

procedure Node003 begin
   Node3Rep += 1;
   if (Node3Rep == 1) then
      Reply(113);
   else
      Reply(114);

   if (q1 == 0) then begin
      NLowOption(115, Node004a);
      NOption(120, Node004a, 4);
   end
   if (q2 == 0) then begin
      NLowOption(116, Node005a);
      NOption(121, Node005a, 4);
   end
   if (q3 == 0) then begin
      NLowOption(117, Node007a);
      NOption(122, Node007a, 4);
   end
   if (q4 == 0) then begin
      NLowOption(118, Node008a);
      NOption(123, Node008a, 4);
   end
   NLowOption(119, Node999);
   NOption(124, Node999, 4);
end

procedure Node004 begin
   Reply(125);
   NLowOption(126, Node003);
   NLowOption(127, Node999);
   BOption(128, Node009, 4);
   NOption(129, Node003, 4);
   GOption(130, Node003, 4);
end

procedure Node005 begin
   Reply(131);
   NLowOption(132, Node006);
   NLowOption(133, Node003);
   NLowOption(134, Node999);
   GOption(135, Node003, 4);
   NOption(136, Node003, 4);
   BOption(137, Node009, 4);
   NOption(138, Node006, 4);
end

procedure Node006 begin
   set_global_var(GVAR_V13_GORIS_QST, 1);  //added by killap
   Reply(139);
   NLowOption(140, Node003);
   NLowOption(141, Node999);
   NOption(142, Node003, 4);
   BOption(143, Node009, 4);
   GOption(144, Node003, 4);
end

procedure Node007 begin
   Reply(145);
   NLowOption(146, Node003);
   NLowOption(147, Node999);
   GOption(148, Node003, 4);
   BOption(149, Node009, 4);
   NOption(150, Node003, 4);
end

procedure Node008 begin
   Reply(151);
   NLowOption(152, Node003);
   NLowOption(153, Node999);
   NOption(154, Node003, 4);
   BOption(155, NOde009, 4);
   GOption(156, Node003, 4);
end

//#define TO_WARN   1
//#define TO_GUARDS 2
//#define TO_OUT    3

procedure Node009 begin
   if (lvar(LVAR_Thrown_Out) == 0) then begin
      slvar(LVAR_Thrown_Out, TO_WARN);
      Reply(157);
      BOption(160, Node998, 4);
      NOption(161, Node003, 4);
      GOption(162, Node003, 4);
   end else if (lvar(LVAR_Thrown_Out) == TO_WARN) then begin
      slvar(LVAR_Thrown_Out, TO_GUARDS);
      moveGuards := 1;
      Reply(158);
      GOption(163, Node003, 4);
      NOption(164, Node003, 4);
      BOption(165, Node998, 4);
   end else if (lvar(LVAR_Thrown_Out) == TO_GUARDS) then begin
      slvar(LVAR_Thrown_Out, TO_OUT);
      warpOutPlayer := 1;
      Reply(159);
   end
end

procedure Node010 begin
   float_msg(self_obj, mstr(166), FLOAT_MSG_NORMAL);
end

procedure Node011 begin
   warpOutPlayer := 1;
   Reply(169);
   ENDOPTION;
end

procedure Node012 begin
   Reply(172);
   NLowOption(173, Node003);
   NLowOption(174, Node999);
   NOption(175, Node003, 4);
   NOption(176, Node999, 4);
end



//xxxxxxxxxxxxxxxxxxxx