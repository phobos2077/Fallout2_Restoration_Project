/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Ariel Vault13
        Location: Vault 13
        Description: She is an NPC you can talk too.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define NPC_REACTION_TYPE       REACTION_TC
#define NPC_REACTION_VAR        9 /* same as thief variable */

#include "../headers/define.h"
#include "../headers/vault13.h"

#define NAME                    SCRIPT_OCARIEL
#define TOWN_REP_VAR            GVAR_TOWN_REP_VAULT_13

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/v13.h"
#include "../vault13/waypnt.ssl"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure push_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;

procedure check_load_pos;
procedure tile_cb_func;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Said_Raid                  (7)
#define LVAR_Cur_Local                  (8)
#define LVAR_Caught_Thief               (9)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable i_ariel_obj;

#define TIMER_ATTACK_PLAYER 3


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Node5Rep  := 0;
variable q1        := 0;
variable q2        := 0;
variable q3        := 0;
variable q4        := 0;
variable q5        := 0;
variable story     := 0;

procedure start begin
   if (VAULT13_SEQ) then begin
      DO_KILL_SELF
   end else begin
      i_ariel_obj := self_obj;
   end
end

procedure push_p_proc begin
end

procedure timed_event_p_proc begin
   if (tvarProcess(fixed_param) == 0) then begin
      if (fixed_param == TIMER_ATTACK_PLAYER) then begin
         if (not combat_is_initialized) then begin
            set_local_var(LVAR_Hostile, 2);
         end
      end
   end
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_VAULT13);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_PEASANT);

   tvarSetNum(2);
   tvarCBack(@tile_cb_func);
   tvarTile(0, ARIEL_COMMON_POS);
   tvarTile(1, ARIEL_LIB_POS);
   tvarTime(0, ONE_GAME_HOUR * 12);
   tvarTime(1, ONE_GAME_HOUR * 12);

   if (not is_loading_game) then
      call check_load_pos;
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if (((local_var(LVAR_Hostile) == 2) or map_var(MVAR_Vault13_Enemy)) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       set_map_var(MVAR_Vault13_Enemy, 1);
       self_attack_dude;
   end


end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (obj_in_party(source_obj)) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_map_var(MVAR_Vault13_Enemy, 1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   debug_msg("calling old pickup for some reason: source_obj:" + source_obj + "self_obj: " + self_obj+ " dude_obj: " + dude_obj);
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_VERY_INTOLERANT;
   Karma_Perception:=KARMA_PERCEPTION4;

   CheckKarma;

   GetReaction;

   if (local_var(LVAR_Hostile) == 1) then begin
      call Node012;
   end
   else if (local_var(LVAR_Herebefore) == 0) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node001;
       gSay_End;
       end_dialogue;
   end else begin
       call Node011;
   end
   set_local_var(LVAR_Herebefore, 1);

end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
   i_ariel_obj := -1;
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(103);
   NLowOption(mstr(104) + " " + dude_name + mstr(178), Node002);
   NLowOption(105, Node003); //edit by killap, was - NLowOption(105, Node002);
   NLowOption(106, Node999);
   GOption(mstr(107) + " " + dude_name + mstr(179), Node002, 4);
   NOption(108, Node003, 4);
   BOption(109, Node004, 4);
   NOption(110, Node999, 4);
end

procedure Node002 begin
   Reply(111);
   NLowOption(112, Node005);
   NLowOption(113, Node999);
   BOption(114, Node999, 4);
   NOption(115, Node005, 4);
   GOption(116, Node005, 4);
end

procedure Node003 begin
   Reply(117);
   GLowOption(118, Node009);
   NLowOption(119, Node005);
   GOption(120, Node009, 4);
   NOption(121, Node005, 4);
   BOption(122, Node004, 4);
end

procedure Node004 begin
   Reply(123);
   ENDOPTION;
end

procedure Node005 begin
   Node5Rep += 1;
   if (Node5Rep == 1) then
      Reply(126);
   else
      Reply(127);
   if (q1 == 0) then begin
      NLowOption(128, Node006);
      NOption(134, Node006, 4);
   end
   if (q2 == 0) then begin
      NLowOption(129, Node007);
      NOption(135, Node007, 4);
   end
   if (q3 == 0) then begin
      NLowOption(130, Node008);
      NOption(136, Node008, 4);
   end
   if (q4 == 0) then begin
      NLowOption(131, Node009);
      NOption(137, Node009, 4);
   end
   if (q5 == 0) then begin
      NLowOption(132, Node010);
      NOption(138, Node010, 4);
   end
   NLowOption(133, Node999);
   NOption(139, Node999, 4);
end

procedure Node006 begin
   q1 := 1;
   Reply(140);
   NLowOption(141, Node005);
   NLowOption(142, Node999);
   NOption(143, Node005, 4);
   BOption(144, Node004, 4);
   GOption(145, Node005, 4);
end

procedure Node007 begin
   q2 := 1;
   Reply(146);
   NLowOption(147, Node999);
   NLowOption(148, Node005);
   GOption(149, Node005, 4);
   NOption(150, Node005, 4);
   BOption(151, Node004, 4);
end

procedure Node008 begin
   q3 := 1;
   Reply(152);
   NLowOption(153, Node999);
   NLowOption(154, Node005);
   BOption(155, Node004, 4);
   NOption(156, Node999, 4);
   GOption(157, Node005, 4);
end

procedure Node009 begin
   q4 := 1;
   Reply(158);
   GLowOption(159, Node010);
   NLowOption(160, Node999);
   GOption(161, Node010, 4);
   NOption(162, Node999, 4);
   BOption(163, Node004, 4);
end

procedure Node010story begin
   slvar(LVAR_Said_Raid, 1);
   story += 1;
   if (story == 1) then begin
      Reply(165);
      MOREOPTION(Node010story);
   end
   else if (story == 2) then begin
      Reply(180);
      NLowOption(166, Node005);
      GLowOption(167, Node005);
      NLowOption(168, Node999);
      NOption(169, Node005, 4);
      BOption(170, Node999, 4);
      GOption(171, Node005, 4);
   end
end

procedure Node010 begin
   q5 := 1;

   if (lvar(LVAR_Said_Raid)) then begin
      Reply(164);
      NLowOption(166, Node005);
      GLowOption(167, Node005);
      NLowOption(168, Node999);
      NOption(169, Node005, 4);
      BOption(170, Node999, 4);
      GOption(171, Node005, 4);
   end
   else begin
      call Node010story;
   end
end

procedure Node011 begin
   float_msg(self_obj, mstr(172), FLOAT_MSG_NORMAL);
end

procedure Node012 begin
   if (dude_is_male) then
      float_msg(self_obj, mstr(175), FLOAT_MSG_NORMAL);
   else
      float_msg(self_obj, mstr(176), FLOAT_MSG_NORMAL);
end

procedure check_load_pos begin
   if (getTimeBit >= bit_12) then
      tvarWarpToPos(1, ONE_GAME_HOUR * 12);
   else
      tvarWarpToPos(0, ONE_GAME_HOUR * 12);
   tvarSetTimer(ONE_GAME_HOUR * 12);
end

procedure tile_cb_func begin
   tvarSetTimer(ONE_GAME_HOUR * 12);
end

//xxxxxxxxxxxxxxxxxxxx