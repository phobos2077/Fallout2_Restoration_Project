/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Dalia
        Location: Vault 13
        Description: She is an NPC you can talk too. This character is also on the
        vault15 map I am not to sure when she is supposed to be here and not there though.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define NPC_REACTION_TYPE       REACTION_TC
#define NPC_REACTION_VAR        8 /* same as thief variable */

#include "..\headers\define.h"
#include "..\headers\vault13.h"

#define NAME                    SCRIPT_OCDALIA
#define TOWN_REP_VAR            GVAR_TOWN_REP_VAULT_13

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

#include "..\headers\v13.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure push_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;

procedure check_dalia_state;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Safe                       (7)
#define LVAR_Caught_Thief               (8)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable begin
   q1 := 0;
   q2 := 0;
   q3 := 0;
   q4 := 0;
   q5 := 0;
   Node7Rep := 0;
end

procedure start begin
   if (VAULT13_SEQ) then begin
      //added by killap
      if (not dalia_state(DALIA_ON_15)) then begin
         destroy_object(self_obj);
      //end added
      end else begin
         DO_KILL_SELF
      end
   end
end

procedure push_p_proc begin
end

procedure timed_event_p_proc begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_VAULT13);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_GUARD);

   if (not is_loading_game) then begin
      call check_dalia_state;
   end
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if (obj_is_visible_flag(self_obj)) then begin
      if (((local_var(LVAR_Hostile) == 2) or map_var(MVAR_Vault13_Enemy)) and (obj_can_see_obj(self_obj,dude_obj))) then begin
          set_local_var(LVAR_Hostile,1);
         attack(dude_obj);
      end
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_map_var(MVAR_Vault13_Enemy, 1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION0;

   CheckKarma;

   GetReaction;

   if (local_var(LVAR_Hostile) == 1) then begin
      call Node012;
   end else if (dalia_state(DALIA_KNOWN)) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node001;
       gSay_End;
       end_dialogue;
   end else if (lvar(LVAR_Herebefore) == 0) then begin
      call Node010;
   end else begin
      call Node011;
   end
   set_local_var(LVAR_Herebefore, 1);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
   set_dalia_state(DALIA_DEAD);
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(mstr(103) + " " + dude_name + mstr(153));
   NLowOption(104, Node002);
   NLowOption(105, Node999);
   GOption(106, Node002, 4);
   NOption(107, Node999, 4);
end

procedure Node002 begin
   Reply(108);
   NLowOption(109, Node003);
   NLowOption(110, Node999);
   NOption(111, Node003, 4);
   NOption(112, Node999, 4);
end

procedure Node004a begin
   q1 := 1;
   call Node004;
end
procedure Node005a begin
   q2 := 1;
   call Node005;
end
procedure Node006a begin
   q3 := 1;
   call Node006;
end
procedure Node007a begin
   q4 := 1;
   call Node007;
end
procedure Node008a begin
   q5 := 1;
   call Node008;
end

procedure Node003 begin
   Reply(113);

   if (q1 == 0) then begin
      NLowOption(114, Node004a);
      NOption(120, Node004a, 4);
   end
   if (q2 == 0) then begin
      NLowOption(115, Node005a);
      NOption(121, Node005a, 4);
   end
   if (q3 == 0) then begin
      NLowOption(116, Node006a);
      NOption(122, Node006a, 4);
   end
   if (q4 == 0) then begin
      NLowOption(117, Node007a);
      NOption(123, Node007a, 4);
   end
   if (q5 == 0 and lvar(LVAR_Safe)) then begin
      NLowOption(118, Node008a);
      NOption(124, Node008a, 4);
   end
   NLowOption(119, Node999);
   NOption(125, Node999, 4);
end

procedure Node004 begin
   slvar(LVAR_Safe, 1);
   Reply(126);
   NLowOption(127, Node003);
   NLowOption(128, Node999);
   NOption(129, Node003, 4);
   NOption(130, Node999, 4);
end

procedure Node005 begin
   Reply(131);
   NLowOption(132, Node003);
   NOption(133, Node003, 4);
end

procedure Node006 begin
   Reply(134);
   NLowOption(135, Node003);
   NLowOption(136, Node003);
   NOption(137, Node003, 4);
   NOption(138, Node003, 4);
end

procedure Node007 begin
   Node7Rep += 1;
   if (Node7Rep == 1) then begin
      Reply(139);
      MOREOPTION(Node007);
   end else begin
      Reply(154);
      NLowOption(140, Node003);
      NOption(141, Node003, 4);
   end
end

procedure Node008 begin
   Reply(142);
   NLowOption(143, Node009);
   NOption(144, Node009, 4);
end

procedure Node009 begin
   Reply(mstr(145) + " " + dude_name + mstr(155));
   NLowOption(146, Node003);
   NLowOption(147, Node999);
   NOption(148, Node003, 4);
   NOption(149, Node999, 4);
end

procedure Node010 begin
   float_msg(self_obj, mstr(150), FLOAT_MSG_NORMAL);
end

procedure Node011 begin
   float_msg(self_obj, mstr(151), FLOAT_MSG_NORMAL);
end

procedure Node012 begin
   float_msg(self_obj, mstr(152), FLOAT_MSG_NORMAL);
end

procedure check_dalia_state begin
   if (dalia_state(DALIA_DEAD)) then begin
      destroy_object(self_obj);
   end else if (dalia_state(DALIA_ON_15)) then begin
      set_obj_visibility(self_obj, OBJ_VIS);
   end else begin
      set_obj_visibility(self_obj, OBJ_INVIS);
   end
end
//xxxxxxxxxxxxxxxxxxxx