/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Matt he is Vault13 captive
        Location: Vault 13
        Description: He is an NPC you can escape with.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define NPC_REACTION_TYPE       REACTION_TC
#define NPC_REACTION_VAR        8 /* same as thief variable */

#include "..\headers\define.h"
#include "..\headers\vault13.h"

#define NAME                    SCRIPT_OCMATT
#define TOWN_REP_VAR            GVAR_TOWN_REP_VAULT_13

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

#include "..\headers\v13.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure push_p_proc;
procedure timed_event_p_proc;
procedure map_update_p_proc; //added by killap

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;

#define E_TOLD  1
#define E_DOIT  2
#define E_DOING 3
#define E_ESCAPED 4

#define TIMER_ESCAPE 1

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Escape                     (7)
#define LVAR_Caught_Thief               (8)
#define LVAR_Did_Sabotage               (9)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable i_matt_obj;


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable begin
   q1 := 0;
   q2 := 0;
end

procedure start begin
   if (VAULT13_SEQ) then begin
      DO_KILL_SELF
   end else begin
      i_matt_obj := self_obj;
   end
end

procedure push_p_proc begin
end

procedure timed_event_p_proc begin
   if (fixed_param == TIMER_ESCAPE) then begin
      if (not combat_is_initialized) then begin
         reg_anim_clear(self_obj);
         reg_anim_begin();
         animate_run_to_tile(MATT_RUN_POS);
         reg_anim_end();
      end
      add_timer_event(self_obj, game_ticks(1), TIMER_ESCAPE);
   end
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_LOANER);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_GUARD);

end

//added by killap
procedure map_update_p_proc begin
   i_matt_obj := self_obj;
end
//end added


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

   if (self_tile == MATT_RUN_POS) then begin
      slvar(LVAR_Escape, E_ESCAPED);
      i_matt_obj := -1;
      destroy_object(self_obj);
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=1;
   Slavery_Tolerant:=SLAVE_PROUD;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if (lvar(LVAR_Hostile) == 1) then
      call Node009;
   else if (lvar(LVAR_Escape) == E_TOLD) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node008;
      gSay_End;
      end_dialogue;
   end else if (lvar(LVAR_Escape) == E_DOING) then begin
      // Possibly tell John about this case
   end else begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
   end
   set_local_var(LVAR_Herebefore, 1);
   if (lvar(LVAR_Escape) == E_DOIT) then begin
      slvar(LVAR_Escape, E_DOING);
      fadeout(1);
      move_to(self_obj, MATT_ESCAPE_POS, 0);
      move_to(dude_obj, DUDE_ESCAPE_POS, 0);
      add_timer_event(self_obj, game_ticks(2), TIMER_ESCAPE);
      fadein(1);
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_evil_critter

/* Set global_variable for Enemy status*/
   i_matt_obj := -1;
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(103);
   NLowOption(104, Node002);
   NLowOption(105, Node999);
   NOption(106, Node002, 4);
   NOption(107, Node999, 4);
end

procedure Node003a begin
   q1 := 1;
   call Node003;
end
procedure Node004a begin
   q2 := 1;
   call Node004;
end

procedure Node002 begin
   Reply(108);
   if (q1 == 0) then begin
      NLowOption(109, Node003);
      NOption(112, Node003a, 4);
   end
   if (q2 == 0) then begin
      NLowOption(110, Node004);
      NOption(113, Node004a, 4);
   end
   NLowOption(111, Node999);
   NOption(114, Node999, 4);
end

procedure Node003 begin
   Reply(115);
   NLowOption(116, Node002);
   NLowOption(117, NOde999);
   NOption(118, Node999, 4);
   NOption(119, Node002, 4);
end

procedure Node004 begin
   Reply(120);
   NLowOption(121, Node005);
   NLowOption(122, Node002);
   NLowOption(123, Node999);
   NOption(124, Node005, 4);
   NOption(125, Node002, 4);
   NOption(126, Node999, 4);
end

procedure Node005 begin
   Reply(127);
   NLowOption(128, Node006);
   NLowOption(129, Node007);
   NOption(130, Node007, 4);
   NOption(131, Node006, 4);
end

procedure NodeMakeBreak begin
   slvar(LVAR_Escape, E_DOIT);
   call Node999;
end

procedure Node006 begin
   Reply(132);
   NOption(133, NodeMakeBreak, 1);
   NOption(134, Node007, 1);
end

procedure Node007 begin
   slvar(LVAR_Escape, E_TOLD);
   Reply(135);
   NOption(136, Node999, 4);
end

procedure Node008 begin
   Reply(137);
   NOption(138, NodeMakeBreak, 1);
   NOption(139, Node999, 1);
   if (local_var(LVAR_Did_Sabotage) == 0 and v13_status(FLG_SABOTAGE)) then begin
      set_local_var(LVAR_Did_Sabotage, 1);
      NLowOption(141, Node010); // Seraph's Fix: NLowOption only takes 2 args. Thanks Rob!
      NOption(142, Node010, 4);
   end
end

procedure Node009 begin
   float_msg(self_obj, mstr(140), FLOAT_MSG_RED);
   slvar(LVAR_Hostile, 2);
end

procedure Node010 begin
   Reply(144);
   NOption(145, Node011, 1);
end

procedure Node011 begin
   Reply(146);
   NLowOption(147, Node012);
   NOption(148, Node012, 4);
end

procedure Node012 begin
   Reply(149);
   NLowOption(150, Node999);
   BLowOption(151, Node998); //edit by killap - was NLowOption
   NLowOption(152, Node008);
   NOption(153, Node999, 4);
   BOption(154, Node998, 4); //edit by killap - was NOption
   NOption(155, Node008, 4);
end

//xxxxxxxxxxxxxxxxxxxx