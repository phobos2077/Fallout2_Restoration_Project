/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Jul Vault 13
        Location: Vault 13
        Description: Deathclaw Guarding the mother deathclaw

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define NPC_REACTION_TYPE       REACTION_TG
#define NPC_REACTION_VAR        8 /* same as thief variable */

#define SCRIPT_REALNAME "ocjul"
#include "../headers/define.h"
#include "../headers/vault13.h"

#define NAME                    SCRIPT_OCJUL
#define TOWN_REP_VAR            GVAR_TOWN_REP_VAULT_13

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/v13.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure push_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;

#define TILE_HATCHERY 21091
#define HATCHERY_BACK_POS 22092

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Transported                (7)
#define LVAR_Caught_Thief               (8)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable doTrans := 0;

procedure start begin
   if (VAULT13_SEQ) then begin
      DO_KILL_SELF
   end
end

procedure push_p_proc begin
   script_overrides;
end

procedure timed_event_p_proc begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_VAULT13);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_VAULT_DEATHCLAW);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if (((local_var(LVAR_Hostile) == 2) or map_var(MVAR_Vault13_Enemy)) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       set_map_var(MVAR_Vault13_Enemy, 1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_map_var(MVAR_Vault13_Enemy, 1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION6;

   CheckKarma;

   GetReaction;

   if (local_var(LVAR_Transported) == 1 and local_var(LVAR_Hostile) == 0) then begin
      fadeout(1);
      move_to(dude_obj, HATCHERY_BACK_POS, elevation(dude_obj));
      set_local_var(LVAR_Transported, 0);
      fadein(1);
   end else if (local_var(LVAR_Hostile) == 1) then begin
      call Node009;
   end else if (local_var(LVAR_Herebefore) == 0) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
   end else if (local_var(LVAR_Herebefore) == 1) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node008;
      gSay_End;
      end_dialogue;
   end
   set_local_var(LVAR_Herebefore, 1);

   if (doTrans == 1) then begin
      doTrans := 0;
      fadeout(1);
      move_to(dude_obj, TILE_HATCHERY, elevation(dude_obj));
      set_local_var(LVAR_Transported, 1);
      fadein(1);
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(103);
   NLowOption(104, Node999);
   NLowOption(105, Node002);
   NLowOption(106, Node003);
   NOption(107, Node999, 4);
   BOption(108, Node007, 4);
   NOption(109, Node002, 4);
   NOption(110, Node003, 4);
end

procedure Node002 begin
   Reply(111);
   NLowOption(112, Node004);
   NLowOption(113, Node003);
   NLowOption(114, Node999);
   NOption(115, Node999, 4);
   NOption(116, Node003, 4);
   NOption(117, Node004, 4);
end

procedure Node003 begin
   Reply(118);
   NLowOption(mstr(119) + " " + dude_name + mstr(146), Node004);
   NLowOption(120, Node999);
   GLowOption(121, Node006);
   NOption(mstr(122) + " " + dude_name + mstr(147), Node004, 4);
   GOption(123, Node006, 4);
   NOption(124, Node999, 4);
end

procedure Node004 begin
   Reply(125);
   NOption(126, Node005, 1);
end

procedure nodeTrans begin
   doTrans := 1;
   call Node999;
end

procedure Node005 begin
   fadeout(1);
   game_time_advance(ONE_GAME_MINUTE);
   fadein(1);
   if (party_size == 1) then
      Reply(127);
   else
      Reply(128);
   NOption(129, NodeTrans, 1);
end

procedure Node006 begin
   Reply(130);
   NLowOption(131, Node999);
   NLowOption(132, Node004);
   NOption(133, Node999, 4);
   NOption(134, Node004, 4);
end

procedure Node007 begin
   Reply(135);
   GOption(136, Node999, 4);
   BOption(137, Node998, 4);
end

procedure Node008 begin
   Reply(138);
   NLowOption(139, Node999);
   NLowOption(140, Node003);
   NLowOption(141, Node002);

   NOption(142, Node999, 4);
   NOption(143, Node002, 4);
   NOption(144, Node003, 4);
end


procedure Node009 begin
   float_msg(self_obj, mstr(145), FLOAT_MSG_RED);
   call Node998;
end

//xxxxxxxxxxxxxxxxxxxx