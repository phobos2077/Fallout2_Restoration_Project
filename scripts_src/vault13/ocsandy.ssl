/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Sandy
        Location: Vault 13
        Description: She is an NPC you can talk too.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define NPC_REACTION_TYPE       REACTION_C
#define NPC_REACTION_VAR        8 /* same as thief variable */

#define SCRIPT_REALNAME "ocsandy"
#include "../headers/define.h"
#include "../headers/vault13.h"

#define NAME                    SCRIPT_OCSANDY
#define TOWN_REP_VAR            GVAR_TOWN_REP_VAULT_13

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/v13.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure push_p_proc;
procedure timed_event_p_proc;
procedure combat_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;

#define SANDY_HAPPY    1
#define SANDY_CONFUSED 2
#define SANDY_SCARED   3

#define TIMER_MOVE 1
#define TIMER_ATTACK_PLAYER 3

/*

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool default from the prototype, and needs
// to be set in scripts.
 */



/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_State                      (7)
#define LVAR_Caught_Thief               (8)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable i_ariel_obj;

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable begin
   q1 := 0;
   q2 := 0;
   q3 := 0;
end

procedure start begin
   if (VAULT13_SEQ) then begin
      DO_KILL_SELF
   end
end

procedure combat_p_proc begin
   if (not critter_is_fleeing(self_obj)) then begin
      critter_set_flee_state(self_obj, 1);
   end
end

procedure push_p_proc begin
   Flee_From_Dude
end

procedure timed_event_p_proc begin
   variable tile := 0;

   if (fixed_param == TIMER_MOVE) then begin
      if (i_ariel_obj != -1) then begin
         if (not anim_busy(self_obj)) then begin
            tile := tile_num_in_direction(tile_num(i_ariel_obj), Random(0, 5), Random(2, 4));
            reg_anim_clear(self_obj);
            reg_anim_begin();
            if (Random(0, 1) == 0) then begin
               self_run_to_tile(tile);
            end else begin
               self_walk_to_tile(tile);
            end
            reg_anim_end();
         end
         add_timer_event(self_obj, game_ticks(Random(1, 5)), TIMER_MOVE);
      end
   end
end

procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_VAULT13);
   set_self_ai(AI_CHILD);
   add_timer_event(self_obj, game_ticks(1), TIMER_MOVE);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if (((local_var(LVAR_Hostile) == 2) and (self_can_see_dude) or map_var(MVAR_Vault13_Enemy))) then begin
       set_local_var(LVAR_Hostile,1);
       set_map_var(MVAR_Vault13_Enemy, 1);
       Flee_From_Dude
//       attack(dude_obj);
   end

   if (lvar(LVAR_State) == SANDY_SCARED) then begin
      if (not anim_busy(self_obj)) then begin
         if (i_ariel_obj != -1) then begin
            if (self_tile != tile_behind_obj(i_ariel_obj)) then begin
               add_timer_event(i_ariel_obj, 0, TIMER_ATTACK_PLAYER);
               reg_anim_clear(self_obj);
               reg_anim_begin();
               self_run_to_tile(tile_behind_obj(i_ariel_obj));
               reg_anim_end();
            end
         end else begin
            Flee_From_Dude
         end
      end
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_map_var(MVAR_Vault13_Enemy, 1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION6;

   CheckKarma;
   GetReaction;

   if (lvar(LVAR_Hostile) == 1) then begin
      call Node008;
   end else if (lvar(LVAR_Herebefore) == 0) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
   end else begin
      call Node007;
   end
   set_local_var(LVAR_Herebefore, 1);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node002a begin
   slvar(LVAR_State, SANDY_HAPPY);
   call Node002;
end
procedure Node002b begin
   slvar(LVAR_State, SANDY_CONFUSED);
   call Node002;
end
procedure Node002c begin
   slvar(LVAR_State, SANDY_SCARED);
   call Node002;
end

procedure Node001 begin
   Reply(103);
   NLowOption(104, Node002a);
   NLowOption(105, Node002b);
   NLowOption(106, Node999);
   BOption(107, Node002c, 4);
   GOption(108, Node002a, 4);
   NOption(109, NOde999, 4);
end

procedure Node002 begin
   if (lvar(LVAR_State) == SANDY_CONFUSED) then begin
      Reply(110);
      NLowOption(113, Node003);
      NLowOption(114, Node999);
      NOption(115, Node003, 4);
      NOption(116, Node999, 4);
   end
   else if (lvar(LVAR_State) == SANDY_HAPPY) then begin
      Reply(112);
      NLowOption(113, Node003);
      NLowOption(114, Node999);
      NOption(115, Node003, 4);
      NOption(116, Node999, 4);
   end
   else if (lvar(LVAR_State) == SANDY_SCARED) then begin
      Reply(111);
      ENDOPTION;
   end
end

procedure Node004a begin
   q1 := 1;
   call Node004;
end
procedure Node005a begin
   q2 := 1;
   call Node005;
end
procedure Node006a begin
   q3 := 1;
   call Node006;
end

procedure Node003 begin
   Reply(117);
   if (q1 == 0) then begin
      NLowOption(118, Node004a);
      NOption(122, Node004a, 4);
   end
   if (q2 == 0) then begin
      NLowOption(119, Node005a);
      NOption(123, Node005a, 4);
   end
   if (q3 == 0) then begin
      NLowOption(120, Node006a);
      NOption(124, Node006a, 4);
   end
   NLowOption(121, Node999);
   NOption(125, Node999, 4);
end

procedure Node004 begin
   Reply(126);
   NLowOption(127, Node003);
   NLowOption(128, Node999);
   NOption(129, Node003, 4);
   NOption(130, Node999, 4);
end

procedure Node005 begin
   Reply(131);
   NLowOption(132, Node003);
   NLowOption(133, Node999);
   NOption(134, Node003, 4);
   NOption(135, Node999, 4);
end

procedure Node006 begin
   Reply(136);
   NLowOption(137, Node999);
   NOption(138, Node999, 4);
end

procedure Node007 begin
   float_msg(self_obj, mstr(139), FLOAT_MSG_NORMAL);
end

procedure Node008 begin
   float_msg(self_obj, mstr(140), FLOAT_MSG_NORMAL);
end


//xxxxxxxxxxxxxxxxxxxx
