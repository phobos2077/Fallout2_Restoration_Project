/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Enlightened One
        Location: NCR 1
        Description: This is the enlightend one.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "../headers/define.h"
#include "../headers/ncr1.h"

#define NAME                    SCRIPT_SCENLONE
#define TOWN_REP_VAR         GVAR_TOWN_REP_NCR

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/ncr.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
procedure Node023;
procedure Node024;
procedure Node025;
procedure Node026;
procedure Node027;
procedure Node028;
procedure Node029;
procedure Node030;
procedure Node031;
procedure Node032;
procedure Node033;
procedure Node034;
procedure Node035;
procedure Node036;
procedure Node037;

procedure giveLetter;
procedure warpToPos;
procedure warpBackToPos;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Freeibe                    (7)
#define LVAR_OZ_Letter                  (8)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once := 0;
variable Node8Rep  := 0;
variable Node16Rep := 0;
variable doAdjust  := 0;
variable begin
   q1 := 0;
   q2 := 0;
   q3 := 0;
end

variable warpBack := 0;
variable pwarpBack := 0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NCR);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_PEASANT);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION3;

   CheckKarma;

   GetReaction;

   if (local_var(LVAR_Hostile) != 0) then begin
      call Node033;
   end else if (not night and global_var(GVAR_NCR_ELRON_ADJUST)) then begin
      call Node036;
   end else if (night) then begin
      call Node037;
   end else if ((not night) and (local_var(LVAR_Herebefore) > 0) and (not global_var(GVAR_NCR_ELRON_ADJUST))) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node031;
       gSay_End;
       end_dialogue;
   end else if (not night and local_var(LVAR_Herebefore) == 0) then begin
      set_local_var(LVAR_Herebefore, 1);
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
   end
   if (doAdjust == 1) then begin
      doAdjust := 0;
      warpBack := self_tile;
      pWarpBack := dude_tile;
      call warpToPos;
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node034;
      gSay_End;
      end_dialogue;
      call warpBackToPos;
   end

   q1 := 0;
   q2 := 0;
   q3 := 0;
   Node16Rep := 0; //added by killap - expansion
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
   set_mss(MSS_ELRON_DEAD);
   set_global_var(GVAR_NCR_KILL_ELRON_QST, 2);
   //give_exp_points(EXP_MERK_HUB_DEAD);
   //display_msg(mstr(218) + EXP_MERK_HUB_DEAD + mstr(219));

   //set_merk_status(MERK_STATUS_PLEASED);

   if (merk_seed_status(MSS_KILL_ELRON)) then begin //edit by killap
      set_merk_status(MERK_STATUS_PLEASED);
   end
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(103);
   NLowOption(104, Node002);
   BOption(105, Node004, 4);
   NOption(106, Node005, 4);
   NOption(107, Node006, 4);
   if (merk_seed_status(MSS_KILL_ELRON)) then
      BOption(108, Node007, 4);
   GOption(109, Node008, 4);
   NOption(110, Node006, 4);
   NOption(111, NOde999, 4);
end

procedure Node002 begin
   Reply(112);
   NOption(113, Node003, 1);
   NOption(114, NOde009, 1);
end

procedure Node003 begin
   Reply(115);
   NOption(116, Node010, 1);
   NOption(117, Node011, 1);
end

procedure Node004 begin
   set_local_var(LVAR_Hostile, 1);
   Reply(118);
   ENDOPTION;  //added by killap
end

procedure Node005 begin
   Reply(119);
   NOption(120, Node012, 6);
   NOption(121, Node013, 4);
   NOption(122, Node014, 4);
   NOption(123, Node006, 4);
   BOption(124, Node004, 4); //edit by killap - was NOption
end

procedure Node999a begin
   DownReactMinor;
   call Node999;
end

procedure Node006 begin
   Reply(125);
   if (dude_looking_for_geck) then
      NOption(126, Node015, 4);
   NOption(127, Node016, 4);
   BOption(128, Node004, 4);
   BOption(129, Node999a, 4);
end

procedure Node007 begin
   Reply(130);
   BOption(131, Node998, 4);
   NOption(132, Node032, 4);
end

procedure Node224a begin
   if ( skill_success(dude_obj, SKILL_CONVERSANT, 0) ) then
      call Node022;
   else
      call Node004;
end

procedure Node008 begin
   Node8Rep += 1;
   if (Node8Rep == 1) then
      if (dude_is_male) then
         Reply(133);
      else
         Reply(216);
   else
      Reply(134);
   if (Node8Rep == 1) then
      NOption(135, Node008, 4);
   if (dude_iq >= 8 or is_skill_tagged(SKILL_CONVERSANT)) then
      NOption(136, Node019, 4);  //137 --edit by killap - had an int requirement of 8 before.
   //unlocked by killap - expansion
   if (local_var(LVAR_Freeibe) == 0) then begin
      NOption(mstr(138) + " " + dude_level + mstr(217), Node020, 4);
   end
   //end
   NOption(139, Node224a, 4);
   NOption(140, Node023, 4);
end

procedure Node009 begin
   Reply(141);
   ENDOPTION;
end

procedure Node010 begin
   doAdjust := 1;
   Reply(142);
   ENDOPTION;
end

procedure Node011 begin
   if (dude_iq >= 4) then
      Reply(143);
   else
      Reply(144);
    ENDOPTION;
end

procedure Node012 begin
   Reply(145);
   NOption(146, Node024, 6);
   NOption(147, Node025, 6);
   NOption(148, Node026, 6);
end

procedure Node013 begin
   DownReactMinor;
   Reply(149);
   ENDOPTION;
end

procedure Node014 begin
   Reply(150);
   GOption(151, Node018, 4);
   NOption(152, NOde011, 4);
end

procedure Node015 begin
   Reply(153);
   ENDOPTION;
end

procedure Node0161 begin
   Node16Rep := 1;
   q1 := 1;
   call Node016;
end

procedure Node0162 begin
   Node16Rep := 2;
   q2 := 1;
   call Node016;
end

procedure Node0163 begin
   Node16Rep := 3;
   q3 := 1;
   call Node016;
end

procedure Node016 begin
   if (Node16Rep == 0) then
      Reply(154);
   else if (Node16Rep == 1) then
      Reply(155);
   else if (Node16Rep == 2) then
      Reply(156);
   else if (Node16Rep == 3) then
      Reply(157);
   if ( (not q1) and (Node16Rep == 3) ) then //edit by killap (expansion) - added Node16Rep == 3
      NOption(158, Node0161, 4);
   if (not q3) then
      NOption(159, Node0163, 4);
   if (not q2) then
      NOption(160, Node0162, 4);
   NOption(161, Node017, 4);
end

procedure Node017 begin
   Reply(162);
   GOption(163, Node010, 4);
   NOption(164, Node011, 4);
end

procedure Node018 begin
   Reply(165);
   NOption(166, Node016, 4);
   BOption(167, NOde004, 4); //edit by killap - was NOption
end

procedure Node019 begin
   Reply(168);
   NOption(169, Node010, 4);
   if (not V13KNOWN) then
      NOption(170, Node027, 4);
   if (has_skill(dude_obj, SKILL_CONVERSANT) >= 65) then
      NOption(171, Node028, 4);
   NOption(172, Node999, 4);
end

procedure Node021a begin
   //give_xp(EXP_TALK_FALLOUT2);
   give_xp(100);
   inc_general_rep(REP_BONUS_NCR_TALK_FALLOUT2);
   set_local_var(LVAR_Freeibe, 1);
   call Node021;
end

procedure Node020 begin
   Reply(173);
   NOption(174, Node021a, 1);
end

procedure Node021 begin
   Node8Rep -= 1; //added by killap (expansion)
   Reply(175);
   MOREOPTION(Node008);
end

procedure Node022 begin
   Reply(176);
   MOREOPTION(Node028);
   //177
end

procedure Node023 begin
   Reply(178);
   ENDOPTION;
end

procedure Node024 begin
   Reply(179);
   NOption(180, Node018, 4);
   BOption(181, Node011, 4);
end

procedure Node025 begin
   Reply(182);
   NOption(183, Node018, 4);
   NOption(184, Node011, 4);
end

procedure Node026 begin
   Reply(185);
   NOption(186, Node018, 4);
   NOption(187, Node011, 4);
end

procedure Node027 begin
   Reply(188);
   NOption(189, Node010, 4);
   NOption(190, Node999, 4);
end

procedure Node028 begin
   Reply(191);
   NOption(192, Node029, 4);
   BOption(193, Node030, 4);
end

procedure Node029 begin
   call giveLetter;
   Reply(194);
   NOption(195, Node010, 4);
   NOption(196, Node999, 4);
end

procedure Node030 begin
   DownReactMinor;
   Reply(197);
   ENDOPTION;
end

procedure Node031 begin
   Reply(198);
   NLowOption(199, Node002);
   if (merk_seed_status(MSS_KILL_ELRON)) then
      BOption(200, Node007, 4);
   NOption(201, Node006, 4);
   NOption(202, Node018, 4);
   NOption(203, Node999, 1);
end

procedure Node998a begin
   call giveLetter;
end

procedure Node032 begin
   Reply(204);
   NOption(205, Node999, 4); //edit by killap - was Node998a. Just didn't make sense to randomly get the quest with no explanation
   BOption(206, Node998, 4); //edit by killap: was NOption
end

procedure Node033 begin
   float_msg(self_obj, mstr(207 + Random(0, 2)), FLOAT_MSG_NORMAL);
end

procedure Node35a begin
   variable val;
   fadeout(1);
   val := Random(0, 1);


   if (val == 0) then begin
      if (dude_luck < 10) then begin
         if (dude_luck == 9) then
            set_critter_stat(dude_obj, STAT_lu, 1);
         else
            set_critter_stat(dude_obj, STAT_lu, 2);
      end
      ndebug("Added to luck");
   end else if (val == 1) then begin
      set_critter_stat(dude_obj, STAT_lu, -1);
      ndebug("Subtracked from luck");
   end
   set_global_var(GVAR_NCR_ELRON_ADJUST, 1);

   fadein(1);
   call Node035;
end

procedure Node034 begin
   Reply(210);
   NOption(MORESTRING, Node35a, 1);
end

procedure Node035 begin
   Reply(211);
   ENDOPTION;
end

procedure Node036 begin
   float_msg(self_obj, mstr(212), FLOAT_MSG_NORMAL);
end

procedure Node037 begin
   float_msg(self_obj, mstr(213 + Random(0, 2)), FLOAT_MSG_NORMAL);
end

procedure giveLetter begin
   if (local_var(LVAR_OZ_Letter) == 0) then begin
      set_global_var(GVAR_NCR_ENLONE_LETTER_QST, 1);
      add_obj_to_inven(dude_obj, create_object(OZ_LETTER_PID, 0, 0));
      set_local_var(LVAR_OZ_Letter, 1);
   end
end

procedure warpToPos begin
   fadeout(1);
   move_to(self_obj, POS_ADJUST, 0);
   move_to(dude_obj, POS_ADJUST_PLAYER, 0);
   animate_rotation(2); // Seraph's Fix: animate_rotation only takes 1 arg, and just affects self_obj. Thanks Rob!
   anim(dude_obj, ANIMATE_ROTATION, 5); // Seraph's Fix: animate_rotation only takes 1 arg, and just affects self_obj. Thanks Rob!
   fadein(1);
end

procedure warpBackToPos begin
   fadeout(1);
   move_to(self_obj, warpBack, 0);
   move_to(dude_obj, pwarpBack, 0);
   animate_rotation(3); // Seraph's Fix: animate_rotation only takes 1 arg, and just affects self_obj. Thanks Rob!
   anim(dude_obj, ANIMATE_ROTATION, 0); // Seraph's Fix: animate_rotation only takes 1 arg, and just affects self_obj. Thanks Rob!
   fadein(1);
end


//xxxxxxxxxxxxxxxxxxxx