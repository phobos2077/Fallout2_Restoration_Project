/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Cattle Buyer
        Location: Redding entrance map only if the player did a cattle drive.
        Description: He pays off the player, this is just a copy of hal's script on another guy
                     there are a few minor changes

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "scdrvpay"
#include "../headers/define.h"
//#include "../headers/ncr3.h"

#define NAME                    SCRIPT_SCHAL
//uses the hal msg file
#define TOWN_REP_VAR            GVAR_TOWN_REP_NCR

#include "../headers/command.h"
#include "../headers/modreact.h"
#include "../headers/caravan.h"

#include "../headers/ncr.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node006;
procedure Node007;

procedure checkLoadConditions;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Vis                        (7)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Node1Rep := 0;

#define TIMER_APPEAR    1
#define TIMER_DISAPPEAR 2
#define TIMER_DESTROY   3

procedure start begin
end

procedure timed_event_p_proc begin
   if (fixed_param == TIMER_APPEAR) then begin
      if (game_time >= global_var(GVAR_NCR_CATTLE_TIME_MIN) and game_time <= global_var(GVAR_NCR_CATTLE_TIME_MAX)) then begin
         set_obj_visibility(self_obj, OBJ_VIS);
         set_local_var(LVAR_Vis, 1);
         add_timer_event(self_obj, game_ticks(60), TIMER_DISAPPEAR);
      end else begin
         if (not cattle_drive(CATTLE_FINISHED) and cattle_drive(CATTLE_ACCEPT)) then
            add_timer_event(self_obj, game_ticks(60), TIMER_APPEAR);
      end
   end else if (fixed_param == TIMER_DISAPPEAR) then begin
      if (game_time > global_var(GVAR_NCR_CATTLE_TIME_MAX)) then begin
         set_obj_visibility(self_obj, OBJ_INVIS);
         set_local_var(LVAR_Vis, 0);
         if (not cattle_drive(CATTLE_ON1) or not cattle_drive(CATTLE_FINISHED)) then
            set_cattle_drive(CATTLE_MISSED);
      end
   end else if (fixed_param == TIMER_DESTROY) then begin
      destroy_object(self_obj);
   end
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   //critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_PLAYER);
   //edits by killap
   if (cur_map_index == MAP_NCR_DOWNTOWN) then
      set_self_team(TEAM_NCR);
   else if (cur_map_index == MAP_NCR_BAZAAR) then
      set_self_team(TEAM_NCR_ENTRANCE);
   else if (cur_town == AREA_REDDING) then
      set_self_team(TEAM_REDDING);
   else
      set_self_team(TEAM_PLAYER);
   //end edits
   set_self_ai(AI_TOUGH_GUARD);
   if (is_loading_game == FALSE) then
      call checkLoadConditions;
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       self_attack_dude;
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION2;

   CheckKarma;

   GetReaction;


   if (not cattle_drive(CATTLE_FINISHED)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node006;
      gSay_End;
      end_dialogue;
   end else begin
      //added by killap
      if (cattle_drive(CATTLE_ABANDON)) then
         float_msg(self_obj, mstr(112), FLOAT_MSG_RED);
      //end added
      else
         call Node007;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node006 begin
   variable amount;
   variable number;

   set_global_var(GVAR_NCR_BRAHMN_QST, 2);

   number := caravan_brahmin_left;

   ndebug("caravan_brahmin_left: " + caravan_brahmin_left);

   //added by killap
   if (cattle_drive(CATTLE_FAIL)) then begin
      set_cattle_drive(CATTLE_ABANDON);
   //end added
   end else if (number == 0) then begin
      ndebug("in 0 case number: " + number);
      set_cattle_drive(CATTLE_SCREWED);
   end else if (caravan_brahmin_left < 4) then begin
      ndebug("in < 4 case number: " + number);
      set_cattle_drive(CATTLE_FAIL);
   end else if (caravan_brahmin_left < 6) then begin
      ndebug("in < 6 case number: " + number);
      set_cattle_drive(CATTLE_AVERAGE);
   end else begin
      ndebug("in else case number: " + number);
      set_cattle_drive(CATTLE_SUCCESS);
   end
   ndebug("caravan_brahmin_left: " + caravan_brahmin_left);

   //added by killap
   if (cattle_drive(CATTLE_ABANDON)) then begin
      amount := 0;
      Reply(118);
   //end added
   end else if (cattle_drive(CATTLE_SCREWED)) then begin
      amount := 0;
      Reply(113);
   end else if (cattle_drive(CATTLE_FAIL)) then begin
      amount := 1000;
      Reply(114);
   end else if (cattle_drive(CATTLE_AVERAGE)) then begin
      amount := 2000;
      Reply(115);
   end else if (cattle_drive(CATTLE_SUCCESS)) then begin
      amount := 3000;
      Reply(116);
   end
   set_cattle_drive(CATTLE_FINISHED);
   dude_caps_adjust(amount);
   ENDOPTION;
end

procedure Node007 begin
   float_msg(self_obj, mstr(117), FLOAT_MSG_NORMAL);
end

procedure checkLoadConditions begin
   if (not cattle_drive(CATTLE_FINISHED) and cattle_drive(CATTLE_ON1)) then begin
      set_obj_visibility(self_obj, OBJ_VIS);
      set_local_var(LVAR_Vis, 1);
      dialogue_system_enter;
   end else if (cattle_drive(CATTLE_FINISHED)) then begin
      set_obj_visibility(self_obj, OBJ_INVIS);
      set_local_var(LVAR_Vis, 0);
   end
end
//xxxxxxxxxxxxxxxxxxxx
