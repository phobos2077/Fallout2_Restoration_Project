/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Elise
        Location: NCR Map 1
        Description: Ranger leader

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "scelise"
#include "../headers/define.h"
#include "../headers/ncr1.h"

#define NAME                    SCRIPT_SCELISE
#define TOWN_REP_VAR         GVAR_TOWN_REP_NCR

#include "../headers/command.h"
#include "../headers/modreact.h"
#include "../headers/ncr.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
procedure Node023;
procedure Node024;
procedure Node025;
procedure Node026;
procedure Node027;
procedure Node028;
procedure Node029;
procedure Node030;
procedure Node031;
//added by killap - expansion
procedure Node032;
procedure Node033;
procedure Node034;
procedure Node035;
procedure Node036;
procedure Node037;
procedure Node038;
procedure Node039;
procedure Node040;
procedure Node041;
procedure Node042;
procedure Node043;
procedure Node044;
procedure Node045;
procedure Node046;
procedure Node047;
procedure Node048;
procedure Node049;
procedure Node050;

procedure Node990;
procedure Node990a;
procedure Node991;
procedure Node992;
procedure Node993;
procedure Node994;
procedure Node995;
procedure Node996;
procedure Node997;
//end added
//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Starttile                  (7)
#define LVAR_State                      (8)
#define LVAR_Prepare_For_Attack         (9) //added by killap - expansion

#define STATE_GOND_MSG  bit_1

#define TIMER_ATTACK 1

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable i_elise_obj;

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Node021Rep := 0;

procedure start begin
end

procedure timed_event_p_proc begin
   if (fixed_param == TIMER_ATTACK) then begin
      if (obj_can_see_obj(self_obj, dude_obj)) then begin
         set_local_var(LVAR_Hostile, 2);
      end
   end
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NCR_RANGER);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_GUARD);

   if (not local_var(LVAR_Starttile)) then begin
      set_local_var(LVAR_Starttile, self_tile);
   end

   i_elise_obj := self_obj;
   ndebug("i_elise_obj: " + i_elise_obj);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   // edit by killap - added (global_var(GVAR_NCR_VORTIS_QUEST_STATE) == VORTIS_QUEST2_DONE)
   if ( ((global_var(GVAR_NCR_VORTIS_QUEST_STATE) == VORTIS_QUEST2_DONE) or (local_var(LVAR_Hostile) != 0)) and (obj_can_see_obj(self_obj,dude_obj)) ) then begin
       set_local_var(LVAR_Hostile,1);
       self_attack_dude;
   end

   if (ncr_flag(NCR_ELISE_TAMPER_FLAG)) then begin
      unset_ncr_flag(NCR_ELISE_TAMPER_FLAG);
      call Node031;
      add_timer_event(self_obj, game_ticks(5), TIMER_ATTACK);
   end

   if (not critter_is_fleeing(self_obj)) then begin
      if (self_tile != local_var(LVAR_Starttile) and not anim_busy(self_obj)) then begin
         reg_anim_clear(self_obj);
         reg_anim_begin();
         animate_move_to_tile(local_var(LVAR_Starttile));
         reg_anim_end();
      end

      if (ncr_flag(NCR_KILLED_GOND_SERUM_FLAG)) then begin
         if (obj_can_see_obj(self_obj, dude_obj)) then begin
            if (not lvar_bit(LVAR_State, STATE_GOND_MSG)) then begin
               float_msg(self_obj, mstr(182), FLOAT_MSG_RED);
               set_local_var(LVAR_Hostile, 2);
            end
         end
      end
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (obj_in_party(source_obj)) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   ReactToLevel; //added by killap

   if (has_rep_slaver) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node029;
      gSay_End;
      end_dialogue;
   //added by killap - expansion
   end else if (global_var(GVAR_WIPED_OUT_SLAVER_CAMP) == 1) then begin
      set_local_var(LVAR_Prepare_For_Attack,0);
      if (local_var(LVAR_Herebefore) == 0) then begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node044;
         gSay_End;
         end_dialogue;
      end else begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node040;
         gSay_End;
         end_dialogue;
      end
   end else if (local_var(LVAR_Prepare_For_Attack) == 1) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node038;
      gSay_End;
      end_dialogue;
   end else if (global_var(GVAR_NCR_ATTACK_SLAVER_CAMP) == 1) then begin
      floater(235);
   //end added
   end else if (dude_is_ranger) then begin
      if ( (town_known(AREA_SLAVER_CAMP) != MARK_STATE_UNKNOWN) and (global_var(GVAR_WIPED_OUT_SLAVER_CAMP) == 0) ) then begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node050;
         gSay_End;
         end_dialogue;
      end else begin
         call Node024;
      end
   end else if (get_elise_seed == ELISE_REJECTED) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node030;
      gSay_End;
      end_dialogue;
   end else if (ncr_global_state(NCR_SLAVES_FREE) /*and get_elise_seed == ELISE_TEST*/) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node020;
       gSay_End;
       end_dialogue;
   end else if (get_elise_seed == ELISE_TEST) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node019;
       gSay_End;
       end_dialogue;
   end else if (get_elise_seed == ELISE_OFFERED) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node018;
       gSay_End;
       end_dialogue;
   end else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node001;
       gSay_End;
       end_dialogue;
   end
   set_local_var(LVAR_Herebefore, 1);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
   i_elise_obj := -1;
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node0306a begin
   if (not bad_critter_reaction) then
      call Node003;
   else
      call Node006;
end

procedure Node001 begin
   if (local_var(LVAR_Herebefore) == 0) then
      Reply(103);
   else
      Reply(104);
   if (ncr_global_state(NCR_KARL_REF_RANGERS)) then
      GOption(105, Node002, 4);
   if (ncr_global_state(NCR_DOC_REF_RANGERS)) then
      GOption(106, Node002, 4);
   NOption(107, Node0306a, 4);
   NLowOption(108, Node004);
   if (global_var(GVAR_NCR_RANGERS_KNOWN) bwand RANGERS_KNOWN) then
      NOption(109, Node005, 4);

   if (dude_has_car) then
      NOption(110, Node007, 4);
   NOption(111, Node008, 4);
end

procedure Node002 begin
   Reply(112);
   NOption(113, Node009, 4);
   NOption(114, Node010, 4);
end

procedure Node003 begin
   Reply(115);
   NOption(116, Node013, 4);
   NOption(117, Node009, 4);
   if (dude_has_car) then
      NOption(118, Node007, 4);
end

procedure Node004 begin
   Reply(119);
   ENDOPTION;
end

procedure Node1411a begin
   if (not bad_critter_reaction) then
      call Node014;
   else
      call Node011;
end

procedure Node005 begin
   Reply(120);
   NOption(121, Node1411a, 4);
   BOption(122, Node016, 4);
   NOption(123, Node012, 4);
end

procedure Node006 begin
   Reply(124);
   ENDOPTION;
end

procedure Node007 begin
   Reply(125);
   ENDOPTION;
end

procedure Node008 begin
   Reply(126);
   ENDOPTION;
end

procedure Node2611a begin
   if (not bad_critter_reaction) then
      call Node026;
   else
      call Node011;
end

procedure Node009 begin
   Reply(127);
   NOption(128, Node2611a, 4);
   NOption(129, Node017, 4);
   NOption(130, Node028, 4);
end

procedure Node010 begin
   Reply(131);
   NOption(132, Node2611a, 4);
   NOption(133, Node009, 4);
   NOption(134, Node012, 4);
end

procedure Node011 begin
   Reply(135);
   ENDOPTION;
end

procedure Node012 begin
   Reply(136);
   ENDOPTION;
end

procedure Node013 begin
   Reply(137);
   NOption(138, Node009, 4);
end

procedure Node014 begin
   set_elise_seed(ELISE_OFFERED);
   Reply(139);
   NOption(140, Node015, 4);
   NOption(141, Node015, 4);
end

procedure NodeAccept begin
   set_elise_seed(ELISE_TEST);
   set_global_var(GVAR_NCR_FREE_SLAVES_QST, 1);
   call Node999;
end

procedure Node015 begin
   Reply(142);
   NOption(143, NodeAccept, 4);
   NOption(144, Node027, 4);
end

procedure Node016 begin
   Reply(145);
   MOREOPTION(Node998);
end

procedure Node017 begin
   Reply(146);
   ENDOPTION;//147 used end
end

procedure Node018 begin
   Reply(148);
   NOption(149, NodeAccept, 4);
   NOption(150, Node015, 4);
end

procedure Node019 begin
   Reply(151);
   ENDOPTION;
end

procedure Node020 begin
   if (get_elise_seed == ELISE_TEST) then begin
      Reply(152);
      NOption(153, Node022, 4);
      NOption(154, Node025, 4);
   end else begin
      Reply(155);
      NOption(156, Node999, 4);
   end
end

procedure Node021 begin
   Node021Rep += 1;
   if (Node021Rep == 0) then begin
      Reply(157);
      MOREOPTION(Node021);
   end
   else begin
      Reply(178);
      NOption(158, Node014, 4);
      NOption(159, Node025, 4);
   end
end

procedure Node022 begin
   Reply(160);
   GOption(161, Node023, 4);
   BOption(162, Node025, 4);
end

procedure Node023 begin
   set_global_var(GVAR_NCR_PLAYER_RANGER, 1);
   add_obj_to_inven(dude_obj, create_object(RANGER_PIN_PID, self_tile, 0));
   inc_general_rep(REP_BONUS_NCR_BECAME_RANGER);
   inc_ncr_rep(REP_HERO_NCR_BECAME_RANGER);
   give_xp(EXP_BECAME_RANGER);
   set_global_var(GVAR_NCR_FREE_SLAVES_QST, 2);

   Reply(200); //edit by killap - expansion
   //Reply(163);

   //added by killap - expansion
   if ( (town_known(AREA_SLAVER_CAMP) != MARK_STATE_UNKNOWN) and (global_var(GVAR_WIPED_OUT_SLAVER_CAMP) == 0) ) then begin
      NOption(201, Node032, 4);
      NOption(240, Node999, 4);
   end else begin
   //end added
      ENDOPTION;
   end
end

procedure Node024 begin
   float_msg(self_obj, mstr(164), FLOAT_MSG_NORMAL);
end

procedure Node999a begin
   set_elise_seed(ELISE_REJECTED);
   call Node999;
end

procedure Node01422a begin
   if (get_elise_seed < ELISE_TEST) then
      call Node014;
   else
      call Node022;
end

procedure Node025 begin
   Reply(165);
   NOption(166, Node999a, 4);
   NOption(167, Node01422a, 4);
end

procedure Node026 begin
   Reply(168);
   NOption(169, Node014, 4);
   NOption(170, Node021, 4);
end

procedure Node027 begin
   Reply(171);
   NOption(172, Node999, 4);
end

procedure Node028 begin
   Reply(173);
   NOption(174, Node999, 4);
end

procedure Node029 begin
   Reply(175);
   NOption(176, Node999, 4);
   BOption(177, Node998, 4); //edit by killap: was NOption
end

procedure Node030 begin
   Reply(179);
   if (ncr_global_state(NCR_SLAVES_FREE)) then
      NOption(161, Node022, 4);
   else
      NOption(161, Node014, 4);
   NOption(180, Node999, 4);
end

procedure Node031 begin
   float_msg(self_obj, mstr(181), FLOAT_MSG_RED);
end

//added by killap - expansion pack
procedure Node032 begin
   Reply(202);
   NOption(203, Node033, 4);
end

procedure Node033 begin
   Reply(204);
   if (global_var(GVAR_WIPED_OUT_SLAVER_CAMP) == 1) then
      NOption(205, Node034, 4);
   else if (town_known(AREA_SLAVER_CAMP) == MARK_STATE_VISITED) then
      NOption(206, Node035, 4);
   else
      NOption(207, Node035, 4);
end

procedure Node034 begin
   Reply(208);
   GOption(209, Node991, 4);
   BOption(210, Node037, 4);
end

procedure Node035 begin
   Reply(211);
   NOption(213, Node036, 4);
   NOption(212, Node997, 4);
end

procedure Node036 begin
   Reply(214);

   if (has_skill(dude_obj,SKILL_CONVERSANT) >= 80) then
      NOption(219, Node992, 4);
   //NOption(215, Node033, 4);
   NOption(216, Node990, 4);
   //NOption(217, Node033, 4);
   NOption(218, Node997, 4);
end

procedure Node037 begin
   display_msg(mstr(236));
   item_caps_adjust( dude_obj, 1000 );
   set_global_var(GVAR_NCR_ATTACK_SLAVER_CAMP,2);
   set_global_var(GVAR_WIPED_OUT_SLAVER_CAMP,2);
   Reply(227);
   NOption(228, Node994, 4);
end

procedure Node038 begin
   Reply(229);
   NOption(230, Node999, 4);
   NOption(231, Node039, 4);
end

procedure Node039 begin
   Reply(232);
   NOption(233, Node990a, 4);
end

procedure Node040 begin
   Reply(234);
   GOption(209, Node991, 4);
   BOption(210, Node037, 4);
end

procedure Node041 begin
   Reply(224);
   NOption(225, Node993, 4);
   NOption(226, Node039, 4);
   NOption(217, Node039, 4);
end

procedure Node042 begin
   Reply(220);
   NOption(221, Node990, 4);
   NOption(223, Node997, 4);
end

procedure Node990 begin
   set_global_var(GVAR_NCR_ATTACK_SLAVER_CAMP,1);
   set_local_var(LVAR_Prepare_For_Attack,0);
end

procedure Node990a begin
   set_global_var(GVAR_NCR_ATTACK_SLAVER_CAMP,1);
   set_local_var(LVAR_Prepare_For_Attack,0);
   load_map(MAP_SLAVER_CAMP, 5);
end

procedure Node991 begin
   display_msg(mstr(236));
   give_xp(800);
   set_global_var(GVAR_NCR_ATTACK_SLAVER_CAMP,2);
   set_global_var(GVAR_WIPED_OUT_SLAVER_CAMP,2);
   call Node994;
end

procedure Node992 begin
   if( is_success( roll_vs_skill( dude_obj, SKILL_CONVERSANT, -10 ) ) ) then
      call Node041;
   else
      call Node042;
end

procedure Node993 begin
   set_local_var(LVAR_Prepare_For_Attack,1);
end

procedure Node994 begin
      //if (ncr_global_state(NCR_SLAVES_FREE)) then
   if (global_var(GVAR_NCR_FREE_SLAVES_QST) == 2) then
      call Node999;
   else
      call Node043;
end

procedure Node043 begin
   Reply(250);
   NOption(251, Node015, 4);
   NOption(252, Node995, 4);
end

procedure Node044 begin
   Reply(260);
   NOption(261, Node045, 4);
   NOption(262, Node046, 4);
   NOption(263, Node047, 4);
   NOption(264, Node999, 4);
end

procedure Node045 begin
   Reply(270);
   NOption(273, Node048, 4);
   NOption(274, Node996, 4);
end

procedure Node046 begin
   Reply(271);
   NOption(273, Node048, 4);
   NOption(274, Node996, 4);
end

procedure Node047 begin
   Reply(272);
   NOption(273, Node048, 4);
   NOption(274, Node996, 4);
end

procedure Node048 begin
   display_msg(mstr(236));
   item_caps_adjust( dude_obj, 1000 );
   set_global_var(GVAR_NCR_ATTACK_SLAVER_CAMP,2);
   set_global_var(GVAR_WIPED_OUT_SLAVER_CAMP,2);

   Reply(280);
   NOption(281, Node999, 4);
   NOption(282, Node049, 4);
end

procedure Node049 begin
   Reply(290);
   NOption(291, Node015, 4);
   NOption(292, Node999, 4);
end

procedure Node050 begin
   Reply(300);
   NOption(201, Node032, 4);
   NOption(180, Node999, 4);
end

procedure Node995 begin
   set_elise_seed(ELISE_OFFERED);
end

procedure Node996 begin
   display_msg(mstr(236));
   give_xp(800);
   set_global_var(GVAR_NCR_ATTACK_SLAVER_CAMP,2);
   set_global_var(GVAR_WIPED_OUT_SLAVER_CAMP,2);
   call Node049;
end

procedure Node997 begin
   set_global_var(GVAR_WIPED_OUT_SLAVER_CAMP,3);
end

//end added

//xxxxxxxxxxxxxxxxxxxx