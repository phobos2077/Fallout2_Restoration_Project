/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Slaves
        Location: Ncr entrance
        Description: The floats for the slaves that are penned up.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "scslaves"
#include "../headers/define.h"
#include "../headers/ncrent.h"

#define NAME                    SCRIPT_SCSLAVES
#define TOWN_REP_VAR         GVAR_TOWN_REP_NCR

#include "../headers/command.h"
#include "../headers/modreact.h"
#include "../headers/ncr.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure RunForIt;
procedure checkRunTile;

#define TIMER_RUN 1
#define TIMER_DESTROY 2

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Tile                       (7)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Run_For_It := 0;
variable Deleteable := 0;

procedure start begin
end

procedure timed_event_p_proc begin
   if (fixed_param == TIMER_RUN) then begin
      call checkRunTile;
      if (local_var(LVAR_Tile) != -1) then begin
         if (Deleteable) then begin
            if (not obj_on_screen(self_obj)) then begin
               add_timer_event(self_obj, 0, TIMER_DESTROY);
            end
         end
      end
      if (local_var(LVAR_Tile) != -1) then begin
         add_timer_event(self_obj, 2, TIMER_RUN);
      end
   end else if (fixed_param == TIMER_DESTROY) then begin
      set_local_var(LVAR_Tile, -1);
      destroy_object(self_obj);
      ndebug("destroyed in timer event");
   end
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_SLAVES);
   set_self_ai(AI_WIMPY_PEASANT);
   if (map_var(MVAR_NCR_ENT_SLAVES_FREED) == 1 and is_loading_game == FALSE) then
      call RunForIt;
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
//       attack(dude_obj);
   end
   if (map_var(MVAR_NCR_ENT_SLAVES_FREED) == 1) then
      if not (Run_For_It) then begin
         call RunForIt;
      end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if (has_rep_slaver) then
      call Node002;
   else if (map_var(MVAR_NCR_ENT_SLAVES_FREED) == 0) then
      call Node001;
   else
      call Node003;

end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));

   set_local_var(LVAR_Herebefore, 1);
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE
procedure Node001 begin
   float_msg(self_obj, mstr(103 + Random(0, 7)), FLOAT_MSG_NORMAL);
end

procedure Node002 begin
   float_msg(self_obj, mstr(111 + Random(0, 6)), FLOAT_MSG_NORMAL);
end

procedure Node003 begin
   float_msg(self_obj, mstr(118 + Random(0, 6)), FLOAT_MSG_NORMAL);
end

procedure RunForIt begin
   variable val;

   Run_For_It := 1;

   if (local_var(LVAR_Tile) == 0) then begin
      set_local_var(LVAR_Tile, SLV_RUN_TILE_1);
   end

   add_timer_event(self_obj, 2, TIMER_RUN);
end

procedure checkRunTile begin
   if (self_tile == local_var(LVAR_Tile)) then begin
      if (local_var(LVAR_Tile) == SLV_RUN_TILE_1) then begin
         Deleteable := 1;
         set_local_var(LVAR_Tile, SLV_RUN_TILE_2);
      end else if (local_var(LVAR_Tile) == SLV_RUN_TILE_2) then begin
         Deleteable := 1;
         set_local_var(LVAR_Tile, SLV_RUN_TILE_3);
      end else if (local_var(LVAR_Tile) == SLV_RUN_TILE_3) then begin
         Deleteable := 1;
         set_local_var(LVAR_Tile, SLV_RUN_TILE_4);
      end else if (local_var(LVAR_Tile) == SLV_RUN_TILE_4) then begin
         set_local_var(LVAR_Tile, -1);
         destroy_object(self_obj);
      end
   end

   if (local_var(LVAR_Tile) != -1) then begin
      if ( not anim_busy(self_obj) and (not combat_is_initialized) ) then begin
         if (self_tile != local_var(LVAR_Tile)) then begin
            reg_anim_clear(self_obj);
            reg_anim_begin();
            self_run_to_tile(local_var(LVAR_Tile));
            reg_anim_end();
         end
      end
   end
end

//xxxxxxxxxxxxxxxxxxxx
