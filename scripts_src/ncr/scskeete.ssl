/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Skeeter
        Location: Ncr Entrance
        Description: He will watch your car..

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: April 13, 1998

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "../headers/define.h"
#include "../headers/ncrent.h"

#define NAME                    SCRIPT_SCSKEETE
#define TOWN_REP_VAR         GVAR_TOWN_REP_NCR

#include "../headers/command.h"
#include "../headers/ModReact.h"
#include "../headers/ncr.h"

#define TIMER_TALK 1
#define TIMER_MOVE 2
#define TIMER_STEAL_STUFF 3

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Node3_Count                (7)
#define LVAR_stole_today                (8) //added by killap - expansion
#define LVAR_paid                       (9) //added by killap - expansion

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


import variable playerTmpBox2;   //added

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable Skeeter_High := 0;
variable hasCar := 0;
variable repeatN1 := 0;
variable repeatN3 := 0;
variable repeatN7 := 0;
variable repeatN11 := 0;
variable from_node011Take := 0; //added by killap
//variable stole_today := 0; //added by killap - expansion

//Movement vars
variable startTileNum;
//Blah
#define CAROBJ PID_BEER

procedure timed_event_p_proc begin
   variable which;
   variable tileNum;
   variable info;
   variable Inven_Roll; //added
   variable item; //added
   variable count;   //added

//begin killap's code - expansion
   if (fixed_param == TIMER_STEAL_STUFF) then begin
      if ( (global_var(GVAR_NCR_CAR_JACKED) == 1) and (local_var(LVAR_stole_today) == 0) and (game_time_hour > 100) and (game_time_hour < 700) ) then begin
           Inven_Roll := random(0, (inven_count(Trunk_Ptr)-1));
           item := inven_ptr(Trunk_Ptr, Inven_Roll);
           count := rm_mult_objs_from_inven(Trunk_Ptr, item, 1);
           add_mult_objs_to_inven(playerTmpBox2, item, 1); //changed from self_obj
           set_local_var(LVAR_stole_today,1);
      end else begin
           add_timer_event(self_obj, ONE_GAME_HOUR*7, TIMER_STEAL_STUFF);
      end
   end
//end killap's code

   if (fixed_param == TIMER_TALK) then begin
      if not(combat_is_initialized) then begin
         if (Skeeter_High) then begin
            which := Random(1, 10);
            float_msg(self_obj, mstr(176 + which), Random(4, 7));
         end else begin
            if (obj_can_see_obj(self_obj, dude_obj)) then begin
               float_msg(self_obj, mstr(176), FLOAT_MSG_GREEN);
            end
         end
      end
      if (Skeeter_High) then
         add_timer_event(self_obj, game_ticks(5), TIMER_TALK);
      else
         add_timer_event(self_obj, game_ticks(15), TIMER_TALK);
   end else if (fixed_param == TIMER_MOVE) then begin
      if not(combat_is_initialized) then begin
         info := Random(0, 5);
         if (Skeeter_High) then
            tileNum := tile_num_in_direction(startTileNum, info, 10);
         else
            tileNum := tile_num_in_direction(startTileNum, info, 5);

         reg_anim_clear(self_obj);
         reg_anim_begin();
            if (Skeeter_High) then begin
               if (Random(0, 1)) then begin
                  animate_run_to_tile(tileNum);
               end else begin
                  animate_move_to_tile(tileNum);
               end
            end else begin
               animate_move_to_tile(tileNum);
            end
         reg_anim_end();
      end
      if (Skeeter_High) then
         add_timer_event(self_obj, game_ticks(2), TIMER_MOVE);
      else
         add_timer_event(self_obj, game_ticks(20), TIMER_MOVE);
   end
end

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NCR_SKEETER);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_PEASANT);

   Skeeter_High := Random(0, 1);

   startTileNum := tile_num(self_obj);
   add_timer_event(self_obj, 1, TIMER_TALK);
   add_timer_event(self_obj, 15, TIMER_MOVE);


   // Fix for Chosen One having the car but leaving it behind and arriving in NCR by joining a caravan - killap
   if ((global_var(GVAR_PLAYER_GOT_CAR)) and (metarule(30, 0) == 10)) then
     hasCar := global_var(GVAR_PLAYER_GOT_CAR);
   else
     hasCar := 0;

   if (local_var(LVAR_Herebefore) == 0) then
      Skeeter_High := 0;

   if (obj_is_carrying_obj_pid(self_obj, PID_BRASS_KNUCKLES)) then begin
      wield_obj(obj_carrying_pid_obj(self_obj, PID_BRASS_KNUCKLES));
   end
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
//Temp
variable tmpLCar := 0;
//Temp

procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */
   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=1;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if (Skeeter_High) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node002;
      gSay_End;
      end_dialogue;
   end else begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node003;
      gSay_End;
      end_dialogue;
   end
   //set_local_var(LVAR_Herebefore, 1); moved by killap
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/

   //inc_good_critter
   //inc_evil_critter --comment out

   inc_neutral_critter // added by killap

   //added by killap
   if (merk_seed_status(MSS_KILL_SKEETER)) then begin
      set_merk_status(MERK_STATUS_PLEASED);
   end
   //end

   set_mss(MSS_SKEETER_DEAD); //added by killap

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001Inc begin
   repeatN1 := 1;
   //if (repeatN1 > 2) then repeatN1 := 2;
   call Node001;
end

procedure Node0506a begin
   if (dude_caps > 4) then begin
      item_caps_adjust(dude_obj, -5);
      item_caps_adjust(self_obj, 5);
      call Node005;
   end else begin
      call Node006;
   end
end

procedure Node001 begin
   if (hasCar and local_var(LVAR_Herebefore) == 0 and repeatN1 == 0) then begin
      Reply(103);
   end else begin
      if (repeatN1 == 0) then
         Reply(104);
      else if (repeatN1 == 1) then begin
         Reply(105);
         repeatN1:=0;
      end
      else if (repeatN1 == 2) then begin
         Reply(106);
         repeatN1:=0;
      end
   end
   if (hasCar) then begin
      NLowOption(107, Node0506a);
      NLowOption(108, Node007);
   end
   if (dude_is_female) then
      NOption(109, Node001Inc, 4);
   NOption(110, Node009, 4);
   if ((hasCar) and (local_var(LVAR_paid) == 0)) then begin
      NOption(111, Node0506a, 4);
      NOption(112, Node007, 4); //killap's fix: changed from 999 to 007
   end
   NOption(138, Node999, 4); //added by killap
end

procedure Node002 begin
   Reply(113);
   NLowOption(114, Node010);
   if (dude_is_female) then
      NOption(115, Node008, 4);
   NOption(116, Node008, 4);
   if (has_trait(TRAIT_PERK, dude_obj, PERK_speaker)) then begin
     // added by killap
     if (repeatN11 >= 5) then
       repeatN11 := 0;
     NOption(117, Node011, 4);
   end
   BOption(118, Node999, 4);
end

procedure Node003inc begin
   set_local_var(LVAR_Node3_Count,1); //killap
   //inc_local_var(LVAR_Node3_Count);
   call Node003;
end

procedure Node003 begin
   if ((global_var(GVAR_NCR_CAR_JACKED) != 1) and hasCar and (local_var(LVAR_Herebefore) == 0)) then
      call Node001;
   else if ((global_var(GVAR_NCR_CAR_JACKED) == 1) and hasCar and (local_var(LVAR_stole_today) == 1)) then
      call Node004;
   else begin
      if (repeatN3 == 0) then begin
         if (local_var(LVAR_Node3_Count) == 0) then begin
            Reply(119);
         end else begin
            Reply(121); //was 120 - killap
            set_local_var(LVAR_Node3_Count,0); //killap
         end
      end else
         Reply(119); //was 121 - killap
      NLowOption(122, Node012);
      if (dude_is_female) then
         NOption(123, Node003inc, 4);
      NOption(124, Node001, 4);
      NOption(125, Node009, 4);
      NOption(126, Node999, 4);
   end
   set_local_var(LVAR_Herebefore, 1); //moved here by killap
end

procedure Node004 begin
   Reply(127);
   NLowOption(128, Node012);
   NOption(129, Node013, 4);
   NOption(130, Node999, 4);
end

procedure Node005 begin
   set_global_var(GVAR_NCR_CAR_JACKED, 0); //killap's fix: changed from -1
   set_local_var(LVAR_paid, 1); //added by killap

   Reply(131);
   NOption(132, Node009, 4);
   NOption(133, Node999, 1);
end

procedure Node006 begin
   Reply(134);
   NOption(ENDSTRING, Node999, 1);
end

procedure Node007Inc begin
   repeatN7 += 1;
   call Node007;
end

procedure Node007 begin
   if (repeatN7 == 0) then begin
      set_global_var(GVAR_NCR_CAR_JACKED, 1); //killap's fix: changed fron -2
      add_timer_event(self_obj, ONE_GAME_HOUR*7, TIMER_STEAL_STUFF); //added by killap - expansion
      Reply(135);
      NOption(137, Node007Inc, 4);
      NOption(138, Node999, 4);
      NLowOption(139, Node999);
   end
   else begin
      Reply(136);
      NOption(ENDSTRING, Node999, 4);
   end
end

procedure Node008 begin
   Reply(140);
   GOption(141, Node014, 4);
   NOption(142, Node015, 4);
   NOption(143, Node999, 4);
end

procedure Node001Stub begin
   repeatN1 := 2;
   call Node001;
end

procedure Node009 begin
   Reply(144);

   if ((hasCar) and (local_var(LVAR_paid) == 0)) then begin
      NOption(145, Node001, 4);
      repeatN1 := 2;
   end

   NOption(146, Node999, 4);
end

procedure Node010 begin
   Reply(147);
   BLowOption(148, Node012);
   GLowOption(149, Node014);
end

procedure Node011Take begin
   //repeatN11 += 1; // comment out by killap
   from_node011Take := 1; // added by killap
   move_obj_inven_to_obj(self_obj, dude_obj);
   Reply(187);
   NOption(MORESTRING, Node011, 1);
end
procedure Node011Repeat begin
   repeatN11 += 1;
   call Node011;
end

procedure Node011 begin
   if (repeatN11 == 0 or from_node011Take) then // added from_node011Take - killap
      Reply(150);
   else if (repeatN11 == 1) then
      Reply(151);
   else if (repeatN11 == 2) then
      Reply(152);
   else if (repeatN11 == 3) then
      Reply(153);
   else if (repeatN11 == 4) then
      Reply(154);
   else if (repeatN11 >= 5) then
      Reply(155);

   NOption(156, Node011Take, 4);
   NOption(157, Node011Repeat, 4);
   NOption(158, Node999, 4);
   from_node011Take := 0;
end

procedure Node012 begin
   Reply(159);
   NOption(160, Node999, 4);
end

procedure Node01606a begin
   if (dude_caps > 49) then begin
      item_caps_adjust(dude_obj, -50);
      item_caps_adjust(self_obj, 50);
      call Node016;
   end else
      call Node006;
end

procedure Node016017a begin
   if (dude_strength >= 8) then
      call Node016;
   else
      call Node017;
end

procedure Node013 begin
   Reply(161);
   GOption(162, Node01606a, 4);
   BOption(163, Node016017a, 4);
   NOption(164, Node999, 4);
end

procedure Node014 begin
   set_global_var(GVAR_NCR_MERK_WORK, 1);
   Reply(165);
   NOption(166, Node999, 1);
end

procedure Node015 begin
   Reply(167);
   NOption(168, Node999, 1);
end

procedure Node016 begin
   Reply(169);
   NLowOption(170, Node999);
   NOption(171, Node999, 4);
end

procedure Node017 begin
   Reply(174); //killap's fix: changed from 172
  // GOption(173, Node01606a, 4);
  // NLowOption(175, Node999);
  // NOption(176, Node999, 4);
   call Node998; //added by killap
end
//xxxxxxxxxxxxxxxxxxxx