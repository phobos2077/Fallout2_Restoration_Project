/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Felix
        Location: NCR 3
        Description: He is the forman in NCR 3 area

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "../headers/define.h"
#include "../headers/ncr3.h"

#define NAME                    SCRIPT_SCFELIX
#define TOWN_REP_VAR         GVAR_TOWN_REP_NCR

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/ncr.h"
#include "../headers/v13.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;

#define WARN_THRESH 1
//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Warn_Count                 (7)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable i_felix_obj;

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable teleport := 0;


procedure timed_event_p_proc begin
   if (fixed_param == NCR_TIMER_PLAYER_WARN) then begin
      if (local_var(LVAR_Warn_Count) < WARN_THRESH) then begin
         float_msg(self_obj, mstr(500 + local_var(LVAR_Warn_Count)), FLOAT_MSG_RED);
         inc_local_var(LVAR_Warn_Count);
      end else begin
         set_local_var(LVAR_Hostile, 2);
      end
   end
end

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NCR_WESTIN);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_GUARD);
   i_felix_obj := self_obj;
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) != 0 or ncr_flag(NCR_WESTIN_ENEMY)) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       self_attack_dude;
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (obj_in_party(source_obj)) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_ncr_flag(NCR_WESTIN_ENEMY);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_INTOLERANT;
   Karma_Perception:=KARMA_PERCEPTION6;

   CheckKarma;

   GetReaction;

   debug_msg("Felix_sent_by_westin: " + get_felix_seed(FELIX_SENT_BY_WESTIN) + " Felix_critters_accepty " + get_felix_seed(FELIX_CRITTERS_ACCEPT));
   if (get_westin_flag(WESTIN_SNUFF_KNOWN)) then begin
      set_local_var(LVAR_Hostile, 2);
   end else if ( get_felix_seed(FELIX_SENT_BY_WESTIN) and not get_felix_seed(FELIX_CRITTERS_ACCEPT)) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node010;
       gSay_End;
       end_dialogue;
   end else if (get_felix_seed(FELIX_SENT_BY_WESTIN) and get_felix_seed(FELIX_CRITTERS_ACCEPT) and not get_felix_seed(FELIX_CRITTERS_DEAD)) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node012;
       gSay_End;
       end_dialogue;
   end else if (get_felix_seed(FELIX_CRITTERS_DEAD) and not get_felix_seed(FELIX_CRITTERS_DONE)) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node015;
       gSay_End;
       end_dialogue;
   end else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node001;
       gSay_End;
       end_dialogue;
   end
   set_local_var(LVAR_Herebefore, 1);
   if (teleport) then begin
      set_global_var(GVAR_NCR_BRAHMIN_PROTECT, 1);
      load_map(MAPSTR_NCR4, 1);
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
   set_map_var(MVAR_FlxDor_Open, 1);
   i_felix_obj := -1;
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   if (local_var(LVAR_Herebefore) == 0) then
      Reply(103);
   else
      Reply(104);
   NLowOption(105, Node002);
   if (get_felix_seed(FELIX_DUMONT_REF) and not get_felix_seed(FELIX_CRITTERS_DONE)) then begin
      GOption(106, Node005, 4);
   end
   if (get_bishop_flag(BISHOP_DEAD) and get_bishop_flag(BISHOP_ACCEPT_SNUFF) and not get_bishop_flag(BISHOP_COMPLETE)) then
      NOption(107, Node013, 4);
   if (not get_felix_seed(FELIX_CRITTERS_ACCEPT)) then
      NOption(108, Node006, 4);
   if (not V13KNOWN) then
      NOption(109, Node007, 4);
   if (dude_item(PID_BISHOPS_HOLODISK) and global_var(GVAR_VAULT_DELIVER_HOLODISK) == HOLODISK_ACCEPTED) then
      NOption(147, Node013, 4);

   NOption(110, Node004, 4);
end

procedure Node002 begin
   Reply(111);
   NOption(112, Node003, 1);
   NOption(113, Node004, 1);
end

procedure Node003 begin
   if (day) then begin
      set_map_var(MVAR_FlxDor_Open, 1);
      set_felix_seed(FELIX_FRIENDLY);
      Reply(114);
      ENDOPTION;
   end else begin
      Reply(115);
      ENDOPTION;
   end
end

procedure Node004 begin
   Reply(116);
   ENDOPTION;
end

procedure Node005 begin
   set_map_var(MVAR_FlxDor_Open, 1);

   set_felix_seed(FELIX_FRIENDLY);
   if (GETDSTATUS == GDCOMPLETE) then begin
      Reply(146);
      ENDOPTION;
   end else begin
      Reply(117);
      ENDOPTION;
   end
end

procedure Node009a begin
   set_westin_flag(WESTIN_SNUFF_KNOWN);
   call Node009;
end

procedure Node006 begin
   Reply(118);
   NOption(119, Node007, 4);
   if (get_westin_flag(WESTIN_ACCEPT_SNUFF)) then
      NOption(120, Node009a, 4);
   NOption(121, Node004, 4);
end

procedure Node007 begin
   Reply(122);
   if (map_var(MVAR_Salt_Beef_Known) == 0) then
      NOption(123, Node008, 4);
   NOption(124, Node999, 4);
end

procedure Node008 begin
   Reply(125);
   NOption(126, Node999, 4);
end

procedure Node009 begin
   Reply(127);
   ENDHOSTILE;
end

procedure Node010 begin
   Reply(128);
   NLowOption(129, Node011);
   NLowOption(130, NOde999);
   NOption(141, Node011, 4);
   NOption(142, Node999, 4);
end

procedure NodeTele begin
   teleport := 1;
   set_felix_seed(FELIX_CRITTERS_ACCEPT);
   call Node999;
end

procedure Node004a begin
   rem_felix_seed(FELIX_SENT_BY_WESTIN);
   set_felix_seed(FELIX_CRITTERS_ACCEPT);

   call Node004;
end

procedure Node011 begin
   if (dude_iq <= 3) then
      Reply(131);
   else
      Reply(143);
   NOption(132, NodeTele, 1);
   NOption(144, Node004a, 4);
   NLowOption(133, Node004a); // Seraph's Fix: NLowOption only takes 2 args. Thanks Rob!
end

procedure Node012 begin
   Reply(134);
   if (get_felix_seed(FELIX_CRITTERS_DEAD)) then
      NOption(135, Node003, 1);
   NOption(136, Node004, 1);
end

procedure Node013 begin
   set_felix_seed(FELIX_FRIENDLY);
   set_map_var(MVAR_FlxDor_Open, 1);
   Reply(137);
   ENDOPTION;
end

procedure Node014 begin
   Reply(138);
   NOption(139, Node001, 1);
   BOption(140, Node998, 1);
end

procedure Node015 begin
   Reply(145);
   ENDOPTION;
end



//xxxxxxxxxxxxxxxxxxxx
