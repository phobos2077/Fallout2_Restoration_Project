/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Police Officer in NCR
        Location: Everywhere
        Description: Police officer

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define NPC_REACTION_TYPE       REACTION_TG
#define NPC_REACTION_VAR        7 /* same as thief variable */
#include "../headers/define.h"
//#include "../headers/"

#define NAME                    SCRIPT_SCCOP
#define TOWN_REP_VAR         GVAR_TOWN_REP_NCR

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/ncr.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure combat_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

#define TIMER_ATTACK 1
#define TIMER_LOOK   2

procedure Node001;
procedure Node002;
procedure Node003;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Caught_Thief               (7)
#define LVAR_Warn_Time                  (8)
#define LVAR_Last_Check_Obj             (9)
#define LVAR_Home_Tile                  (10)
#define LVAR_Home_Rot                   (11)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable i_locker_tamper;


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable waiting := 0;
variable last_locker_tamper;

procedure getArmedPartyMember begin
   if (Is_Armed(dude_obj)) then
      return dude_obj;
   if (Vic_In_Party) then
      if (Is_Armed(Vic_Ptr)) then
            return Vic_Ptr;
   if (Myron_In_Party) then
      if (Is_Armed(Myron_Ptr)) then
            return Myron_Ptr;
   if (Marcus_In_Party) then
      if (Is_Armed(Marcus_Ptr)) then
            return Marcus_Ptr;
   if (MacRae_In_Party) then
      if (Is_Armed(MacRae_Ptr)) then
            return MacRae_Ptr;
   if (Sulik_In_Party) then
      if (Is_Armed(Sulik_Ptr)) then
            return Sulik_Ptr;
   if (Lenny_In_Party) then
      if (Is_Armed(Lenny_Ptr)) then
            return Lenny_Ptr;
   if (Cyberdog_In_Party) then
      if (Is_Armed(Cyberdog_Ptr)) then
            return Cyberdog_Ptr;
   if (Doc_In_Party) then
      if (Is_Armed(Doc_Ptr)) then
            return Doc_Ptr;
   if (Goris_In_Party) then
      if (Is_Armed(Goris_Ptr)) then
            return Goris_Ptr;
   if (Davin_In_Party) then
      if (Is_Armed(Davin_Ptr)) then
            return Davin_Ptr;
   if (Miria_In_Party) then
      if (Is_Armed(Miria_Ptr)) then
            return Miria_Ptr;
   //if (Laddie_In_Party) then --dumb since even if laddie was armed there is no way to fix it.
   //   if (Is_Armed(Laddie_Ptr)) then
   //         return Laddie_Ptr;
   if (Robobrain_In_Party) then
      if (Is_Armed(Robobrain_Ptr)) then
            return Robobrain_Ptr;
   //added by killap
   if (Robobrain_Human_In_Party) then
      if (Is_Armed(Robobrain_Human_Ptr)) then
            return Robobrain_Human_Ptr;
   if (Robobrain_Abnormal_In_Party) then
      if (Is_Armed(Robobrain_Abnormal_Ptr)) then
            return Robobrain_Abnormal_Ptr;
   if (Robobrain_Chimp_In_Party) then
      if (Is_Armed(Robobrain_Chimp_Ptr)) then
            return Robobrain_Chimp_Ptr;
   //end added

   return 0;
end

#define Get_Armed_Party_Member    getArmedPartyMember


procedure start begin
end

procedure combat_p_proc begin
   if (obj_can_see_obj(self_obj, dude_obj) and ncr_flag(NCR_GUARD_CHECK_HOSTILE) and not local_var(LVAR_Personal_Enemy)) then begin
      debug_msg("SCCOP: should be attacking dude");
      set_ncr_flag(NCR_GUARD_ENEMY);
      set_local_var(LVAR_Personal_Enemy, 1);
      self_attack_dude;
   end
end

procedure combat_is_over_p_proc begin
   if (obj_can_see_obj(self_obj, dude_obj) and ncr_flag(NCR_GUARD_CHECK_HOSTILE) and local_var(LVAR_Personal_Enemy)) then begin
      debug_msg("SCCOP: should be re-attacking dude");
      self_attack_dude;
   end else begin
      if (tile_distance_objs(self_obj, dude_obj) < 15) then begin
         debug_msg("SCCOP: obj_can_see_obj: " + obj_can_see_obj(self_obj, dude_obj));
         debug_msg("SCCOP: ncr: " + ncr_flag(NCR_GUARD_CHECK_HOSTILE));
         debug_msg("SCCOP: should be clearing attacking dude.");
      end
      unset_ncr_flag(NCR_GUARD_CHECK_HOSTILE);
      set_local_var(LVAR_Personal_Enemy, 0);
   end
end

procedure timed_event_p_proc begin
   if (fixed_param == TIMER_ATTACK) then begin
      if (game_time - local_var(LVAR_Warn_Time) >= ONE_GAME_SECOND * 10) then begin
         set_local_var(LVAR_Warn_Time, 0);
         waiting := 0;
         If_Party_Is_Armed then begin
            set_local_var(LVAR_Hostile, 2);
         end
      end else begin
         add_timer_event(self_obj, game_ticks(2), TIMER_ATTACK);
      end
   end else if (fixed_param == TIMER_LOOK) then begin
      If_Party_Is_Armed then begin
      end
      if (obj_can_see_obj(self_obj, dude_obj) and How_Many_Party_Members_Armed and tile_distance_objs(self_obj, dude_obj) <= POLICE_WARN_DIST) then
         if (waiting == 0) then
            call talk_p_proc;
      add_timer_event(self_obj, game_ticks(2), TIMER_LOOK);
   end
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NCR);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_GUARD);
   if (not is_loading_game) then begin
      add_timer_event(self_obj, game_ticks(2), TIMER_LOOK);
      if (not local_var(LVAR_Home_Tile)) then begin
         set_local_var(LVAR_Home_Tile, self_tile);
      end
      if (not local_var(LVAR_Home_Rot)) then begin
         set_local_var(LVAR_Home_Rot, obj_get_rot(self_obj));
      end
   end
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if (ncr_flag(NCR_GUARD_ENEMY)) then begin
      set_local_var(LVAR_Hostile, 2);
   end

   if (last_locker_tamper != i_locker_tamper) then begin
      last_locker_tamper := i_locker_tamper;
      if (obj_can_see_obj(self_obj, dude_obj)) then begin
         if (Random(1, 5 + dude_luck) <= last_locker_tamper) then begin
            set_local_var(LVAR_Hostile, 2);
         end
         else begin
            float_msg(self_obj, mstr(112 + Random(0, 3)), FLOAT_MSG_RED);
         end
      end
   end

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       If_Party_Is_Armed then begin
          attack(Get_Armed_Party_Member);
       end else
          attack(dude_obj);
   end

   if (self_tile != local_var(LVAR_Home_Tile)) then begin
      if (not anim_busy(self_obj)) then begin
         reg_anim_clear(self_obj);
         reg_anim_begin();
         animate_move_to_tile(local_var(LVAR_Home_Tile));
         reg_anim_end();
      end
   end else if (obj_get_rot(self_obj) != local_var(LVAR_Home_Rot)) then begin
      reg_anim_clear(self_obj);
      reg_anim_begin();
      animate_rotation(local_var(LVAR_Home_Rot));
      reg_anim_end();
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
      set_ncr_flag(NCR_GUARD_ENEMY);

   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_INTOLERANT;
   Karma_Perception:=KARMA_PERCEPTION2;

   CheckKarma;

   GetReaction;

   If_Party_Is_Armed then begin
   end

   if (How_Many_Party_Members_Armed and waiting == 0) then begin
      call Node001;
      set_local_var(LVAR_Warn_Time, game_time);
      add_timer_event(self_obj, game_ticks(10), TIMER_ATTACK);
      waiting := 1;
   end
   else if (night  and waiting == 0) then
      call Node002;
   else if (waiting == 0) then
      call Node003;

   set_local_var(LVAR_Herebefore, 1);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   float_msg(self_obj, mstr(103 + Random(0, 2)), FLOAT_MSG_RED); //edit by killap - was FLOAT_MSG_NORMAL
end

procedure Node002 begin
   float_msg(self_obj, mstr(106 + Random(0, 2)), FLOAT_MSG_NORMAL);
end

procedure Node003 begin
   float_msg(self_obj, mstr(109 + Random(0, 2)), FLOAT_MSG_NORMAL);
end
//xxxxxxxxxxxxxxxxxxxx
