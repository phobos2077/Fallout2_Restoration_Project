/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Dusty
        Location: NCR 1
        Description: Bartender

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "scdusty"
#include "../headers/define.h"
#include "../headers/ncr1.h"

#define NAME                    SCRIPT_SCDUSTY
#define TOWN_REP_VAR         GVAR_TOWN_REP_NCR

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/ncr.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Restock_Time               (7)
//added by killap
#define LVAR_Freebie                    (8)
#define LVAR_Restock_Time_Fix           (9) //added by killap

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable from_node:=0; //added by killap

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   //added by killap
   variable tmp;
   variable special_item;
   //end added

   Only_Once:=0;
   set_self_team(TEAM_NCR);
   set_self_ai(AI_TOUGH_CITIZEN);

   restock_fix
   if (local_var(LVAR_Restock_Time) < game_time) then begin
      ndebug("Should have restocked");
      check_restock_item(PID_BOOZE, 10, 20, 100)
      check_restock_item(PID_BEER, 20, 30, 100)
      check_restock_item(PID_NUKA_COLA, 10, 20, 100)
      check_restock_item(PID_BOTTLE_CAPS, 30, 60, 100)
      check_restock_item(PID_CIGARETTE, 1, 1, 100) //added by killap - RP
      set_local_var(LVAR_Restock_Time, (random(1,4) * ONE_GAME_DAY) + game_time);
   end
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_INTOLERANT;
   Karma_Perception:=KARMA_PERCEPTION4;

   CheckKarma;

   GetReaction;
   from_node := 0; //added by killap

   if ( (global_var(GVAR_NCR_BEAT_HOSS) == 1) and (local_var(LVAR_Freebie) == 0) ) then begin //added check for free drink - killap
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node007;
      gSay_End;
      end_dialogue;
   end else begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure NodeBarter begin
   gdialog_barter;
   ENDOPTION;
end

procedure Node001 begin
   if (jack_state == JACK_SURRENDER) then
      Reply(104);
   else if (beat_hoss) then
      if (from_node == 7) then
         Reply(105);
      else
         Reply(131); //added by killap
   else
      Reply(103);

   NOption(106, NodeBarter, 1);
   if (map_var(MVAR_Hoss_State) == 0) then
      NOption(107, Node002, 4);
   if (global_var(GVAR_NCR_TANDI_JOB_ACCEPT) != TJOB_DONE) then
      NOption(108, Node003, 4);
   NOption(109, Node999, 1);
end

procedure Node002 begin
   if not (beat_hoss) then begin
      Reply(110);
      set_map_var(MVAR_Hoss_State, HOSS_KNOWN);
   end else
      Reply(111);
   NOption(112, Node004, 4);
   NOption(113, Node006, 4);
end

procedure Node003 begin
   set_global_var(GVAR_NCR_TANDI_WORK, 1);
   Reply(114);
   NOption(115, Node005, 4);
   NOption(116, Node004, 4);
   NOption(117, Node006, 4);
end

procedure Node004 begin
   Reply(118);
   if (global_var(GVAR_NCR_TANDI_WORK) == 1 and global_var(GVAR_NCR_TANDI_JOB_ACCEPT) != TJOB_DONE) then
      NOption(119, Node005, 4);
   if (map_var(MVAR_Hoss_State) == 0) then
      NOption(120, Node002, 4);
   NOption(121, Node006, 4);
end

procedure Node005 begin
   Reply(122);
   if (map_var(MVAR_Hoss_State) == 0) then
      NOption(123, Node002, 4);
   NOption(124, Node006, 4);
end

procedure Node006 begin
   Reply(125);
   NOption(126, NodeBarter, 1);
   NOption(127, Node999, 1);
end

procedure Node007 begin
   from_node := 7;
   add_obj_to_inven(dude_obj, create_object(PID_BOOZE, self_tile, 0));
   set_local_var(LVAR_Freebie,1); //added by killap
   Reply(128);
   NOption(129, Node001, 4);
   NOption(130, Node999, 1);
end

//xxxxxxxxxxxxxxxxxxxx
