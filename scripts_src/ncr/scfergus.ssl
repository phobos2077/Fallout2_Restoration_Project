/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Feargus
        Location: NCR 1
        Description: He is the receptionist for Tandi and such.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "scfergus"
#include "../headers/define.h"
#include "../headers/ncr2.h"

#define NAME                    SCRIPT_SCFERGUS
#define TOWN_REP_VAR            GVAR_TOWN_REP_NCR

#include "../headers/command.h"
#include "../headers/modreact.h"
#include "../headers/ncr.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;

procedure checkFergusState;
procedure leaveMap;
procedure anim_end_call;
procedure doWalk;

#define TIMER_TALK_PLAYER 1

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Walk_Tile                  (7)
#define LVAR_Warn_Count                 (8)

#define WARN_THRESH 3

#define FERG_LEAVE1   19490
#define FERG_LEAVE2   13895

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable i_fergus_obj;

/* Local variables which do not need to be saved between map changes. */
variable Only_Once  := 0;
variable Node1Count := 0;
variable anim_flag  := 0;


procedure timed_event_p_proc begin
   if (fixed_param == NCR_TIMER_PLAYER_WARN) then begin
      if (map_var(MVAR_Fergus_Knows) == 0) then begin
         if (local_var(LVAR_Warn_Count) < WARN_THRESH) then begin
            float_msg(self_obj, mstr(500 + local_var(LVAR_Warn_Count)), FLOAT_MSG_RED);
            inc_local_var(LVAR_Warn_Count);
         end else begin
            float_msg(self_obj, mstr(503), FLOAT_MSG_NORMAL);
            set_ncr_flag(NCR_GUARD_ENEMY);
         end
      end
   end
end

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_VAULT15);
   set_self_ai(AI_TOUGH_CITIZEN);
   i_fergus_obj := self_obj;

   if (not is_loading_game) then begin
      if (not map_var(MVAR_Fergus_Knows)) then
         set_map_var(MVAR_FergDor_Open, 0);
      set_local_var(LVAR_Warn_Count, 0);
      call checkFergusState;
   end
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if (map_var(MVAR_Fergus_Knows) == 1) then begin
      i_fergus_obj := -1;
      set_map_var(MVAR_FergDor_Open, 1);
      call leaveMap;
   end

   if ((local_var(LVAR_Hostile) != 0) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       self_attack_dude;
   end
   do_self_anim_callback

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
      SET_NCR_ENEMY;
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   if (not NCR_ENEMY) then begin
      CheckKarma;

      GetReaction;

      if (map_var(MVAR_Fergus_Knows) == 1) then begin
         float_msg(self_obj, mstr(141), FLOAT_MSG_YELLOW);
      end else begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node001;
         gSay_End;
         end_dialogue;
      end

      if (map_var(MVAR_Fergus_Knows) == 1) then begin
         i_fergus_obj := -1;
         set_map_var(MVAR_FergDor_Open, 1);
         call leaveMap;
      end
   end else set_local_var(LVAR_Hostile, 2);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
   i_fergus_obj := -1;

   set_map_var(MVAR_FergDor_Open, 1);
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Node1Count += 1;
   if (Node1Count == 1) then begin
      if (local_var(LVAR_Herebefore) == 0) then
         Reply(103);
       else
         Reply(104);
   end else
      Reply(105);
   NLowOption(106, Node002);
   if ((global_var(GVAR_NCR_TANDI_WORK) or global_var(GVAR_NCR_V15_DARION_DEAL) == 1) and global_var(GVAR_NCR_TANDI_JOB_ACCEPT) != TJOB_DONE) then begin
      NLowOption(107, Node003); //108
      NOption(109, Node003, 4);
   end
   if (not V13KNOWN) then
      NOption(110, Node007, 4);
   if (dude_looking_for_geck) then
      NOption(111, Node008, 4);
   if (global_var(GVAR_NCR_SPY_KNOWN) or dude_item(PID_SPY_HOLO)) then
      NOption(112, Node004, 4);
   //113, 114 above
   NOption(115, Node005, 4);
   NOption(116, Node006, 4);
   NOption(117, Node999, 4);
   NLowOption(118, Node999);
   if (global_var(GVAR_NCR_V15_DARION_DEAD) and global_var(GVAR_NCR_TANDI_JOB_ACCEPT) == TJOB_ACCEPT) then begin
      NLowOption(149, Node016);
      NOption(150, Node017, 4);
   end
end

procedure Node002 begin
   Reply(119);
   ENDOPTION;
end

procedure Node003 begin
   if (dude_iq >= 4) then
      Reply(120);
   else
      Reply(121);

   if (global_var(GVAR_NCR_TANDI_WORK) and global_var(GVAR_NCR_V15_DARION_DEAL) != 1) then
      NLowOption(122, Node009);
   if (global_var(GVAR_NCR_V15_DARION_DEAL) == 1) then
      NLowOption(123, Node010);
   if (global_var(GVAR_NCR_TANDI_WORK) and global_var(GVAR_NCR_V15_DARION_DEAL) != 1) then
      NOption(124, Node009, 4);
   if (global_var(GVAR_NCR_V15_DARION_DEAL) == 1) then
      NOption(125, Node010, 4);
//   if (V13KNOWN) then
//      NOption(126, Node011, 4);
end

procedure Node004 begin
   Reply(127);
   NOption(128, Node012, 4);
   NOption(129, Node013, 4);
end

procedure Node005 begin
   Reply(130);
   ENDOPTION;
end

procedure Node006 begin
   Reply(131);
   MOREOPTION(Node001);//132
end

procedure Node007 begin
   Reply(133);
   NOption(134, Node014, 4);
   NOption(135, Node015, 4);
end

procedure Node008 begin
   Reply(136);
   MOREOPTION(Node001);//137
end

procedure Node009 begin
   set_map_var(MVAR_FergDor_Open, 1);
   Reply(138);
   ENDOPTION;
end

procedure Node010 begin
   set_map_var(MVAR_FergDor_Open, 1);
   Reply(139);
   ENDOPTION;
end

procedure Node011 begin
   set_map_var(MVAR_FergDor_Open, 1);
   Reply(140);
   ENDOPTION;
end

procedure Node012 begin
   Reply(141);
   set_map_var(MVAR_Fergus_Knows, 1);
   ENDOPTION;
end

procedure Node013 begin
   Reply(142);
   BOption(143, Node012, 4);
   NOption(144, Node011, 4);
end

procedure Node014 begin
   Reply(145);
   MOREOPTION(Node001);//146
end

procedure Node015 begin
   Reply(147);
   MOREOPTION(Node001);
end

procedure Node016 begin
   set_map_var(MVAR_Fergus_Knows, 1);
   Reply(151);
   ENDOPTION;
end

//begin comment out by killap
//procedure Node012a begin
//   set_map_var(MVAR_Fergus_Knows, 1);
//   call Node012;
//end
//end killap comment out

procedure Node017 begin
   Reply(152);
   NOption(153, Node018, 4);
   if (dude_item(PID_SPY_HOLO)) then
      NOption(154, Node012, 4);  //killap's fix: changed from 012a
end

procedure Node018 begin
   Reply(155);
   ENDOPTION;
end

procedure checkFergusState begin
   if (map_var(MVAR_Fergus_Knows)) then
      destroy_object(self_obj);
end

procedure anim_end_call begin
   if (map_var(MVAR_Fergus_Knows) == 1) then //added by killap
   call leaveMap;
end

procedure leaveMap begin
   if (not anim_busy(self_obj)) then begin
      if (local_var(LVAR_Walk_Tile) == 0) then begin
         set_local_var(LVAR_Walk_Tile, FERG_LEAVE1);
         call doWalk;
      end else if (local_var(LVAR_Walk_Tile) == FERG_LEAVE1) then begin
         set_local_var(LVAR_Walk_Tile, FERG_LEAVE2);
         call doWalk;
      end else if (local_var(LVAR_Walk_Tile) == FERG_LEAVE2) then begin
         destroy_object(self_obj);
         i_fergus_obj := -1;
      end
   end
end

procedure doWalk begin
   reg_anim_clear(self_obj);
   reg_anim_begin();
   self_walk_to_tile(local_var(LVAR_Walk_Tile));
   reg_anim_end();
end

//xxxxxxxxxxxxxxxxxxxx
