/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Saltbeef Bob
        Location: NCR 3
        Description: He is an old dude who tells you where Vault 13 is

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "scsalbob"
#include "../headers/define.h"
#include "../headers/ncr3.h"

#define NAME                    SCRIPT_SCSALBOB
#define TOWN_REP_VAR         GVAR_TOWN_REP_NCR

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/ncr.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Bob_State                  (7)

#define RILED   1

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable node8repeat;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NCR);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_CITIZEN);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if (local_var(LVAR_Bob_State) == RILED) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node015;
       gSay_End;
       end_dialogue;
   end
   else if (local_var(LVAR_Herebefore == 0)) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node001;
       gSay_End;
       end_dialogue;
   end else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node002;
       gSay_End;
       end_dialogue;
   end
   set_local_var(LVAR_Herebefore, 1);
   set_map_var(MVAR_Salt_Beef_Known, 1);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(103);
   NLowOption(104, Node005);
   NOption(105, Node002, 4);
end

procedure Node002 begin
   if (local_var(LVAR_Herebefore) == 0) then
      Reply(106);
   else
      Reply(107);
   NOption(dude_name + ".", Node003, 4);
   NOption(109, Node010, 4);
   NOption(110, Node014, 4);
end

procedure removeAlchol begin
   variable obj;
   variable numobj;

   if (dude_has_booze) then begin
      numobj := dude_has_booze;
      ndebug("numObj: " + numObj);
      obj := PID_BOOZE;
      ndebug("dude_booze_ptr: " + dude_booze_ptr);
      destroy_object(dude_booze_ptr);
   end else if (dude_has_beer) then begin
      numobj := dude_has_beer;
      obj := PID_BEER;
      destroy_object(dude_beer_ptr);
   end else if (dude_has_rum) then begin
      numobj := dude_has_rum;
      obj := PID_ROENTGEN_RUM;
      destroy_object(dude_rum_ptr);
   end else if (dude_has_gulp_beer) then begin
      numobj := dude_has_gulp_beer;
      obj := PID_GAMMA_GULP_BEER;
      destroy_object(dude_gulp_beer_ptr);
   end

   if (numobj - 1 > 0) then begin
      ndebug("atting booze back minus 1");
      add_mult_objs_to_inven(dude_obj, create_object(obj, self_tile, 0), numobj - 1);
   end
end

procedure Node007a begin
   call removeAlchol;
   call Node007;
end

procedure Node003 begin
   Reply(111);
   if (dude_has_alcohol) then
      NOption(112, Node007a, 4);
   NOption(113, Node006, 4);
   NOption(114, Node004, 4);
end

procedure Node004 begin
   Reply(115);
   ENDOPTION;
end

procedure Node005 begin
   Reply(116);
   if (dude_has_alcohol) then
      NOption(117, Node007a, 1);
   NOption(118, Node006, 1);
end

procedure Node006 begin
   Reply(119);
   ENDOPTION;
end

procedure Node007 begin
   set_local_var(LVAR_Bob_State, 0);
   Reply(120);
   NOption(121, Node008, 1);
   NOption(122, Node013, 4);
   NLowOption(123, Node006);
end

procedure Node008 begin
   node8repeat += 1;
   if (node8repeat == 1) then begin
      Reply(124);
      MOREOPTION(Node008);
   end
   else if (node8repeat == 2) then begin
      Reply(125);
      MOREOPTION(Node008);
   end
   else if (node8repeat == 3) then begin
      Reply(126);
      MOREOPTION(Node008);
   end
   else if (node8repeat == 4) then begin
      Reply(127);
      MOREOPTION(Node008);
   end
   else if (node8repeat == 5) then begin
      Reply(128);
      MOREOPTION(Node008);
   end
   else if (node8repeat == 6) then begin
      ncr_set_global_state(NCR_SALBOB_REF_DOCTOR);
      Reply(129);
      node8repeat := 0;
      ENDOPTION;
   end
end

procedure Node009 begin
//doesn't seem to exist
end

procedure Node010 begin
   Reply(131);
   NOption(132, Node011, 4);
   BOption(133, Node012, 4); //edit by killap - was NOption
end

procedure Node011 begin
   set_local_var(LVAR_Bob_State, RILED);
   Reply(134);
   ENDOPTION;
end

procedure Node012 begin
   Reply(135);
   if (dude_has_alcohol) then
      NOption(136, Node007a, 4);
   NOption(137, Node006, 4);
   BOption(138, Node004, 4); //edit by killap - was NOption
end

procedure Node013 begin
   Reply(139);
   ENDOPTION;
end

procedure Node014 begin
   Reply(140);
   NOption(141, Node010, 4);
   NOption(mstr(142) + " " + dude_name + mstr(150), Node003, 4);
end

procedure Node015 begin
   Reply(143);
   NOption(144, Node012, 4);
   NOption(145, Node016, 4);
end

procedure Node016 begin
   Reply(146);
   if (dude_has_alcohol) then
      NOption(147, Node007a, 4);
   NOption(148, Node006, 4);
   NOption(149, Node004, 4);
end



//xxxxxxxxxxxxxxxxxxxx