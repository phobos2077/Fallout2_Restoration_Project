/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Raider
        Location: Random Encounters
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "ecguard"
#include "../headers/define.h"
#include "../headers/bhrnddst.h"

#define NAME                    SCRIPT_ECGUARD

#include "../headers/command.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


#define LVAR_Gave_Warning                               (0)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable In_Timed_Event;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_CARAVAN);
   set_self_ai(AI_GENERIC_GUARDS);
end


procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */


   if ((self_can_see_dude) and (tile_distance_objs(self_obj,dude_obj) < 8)) then begin
       if ((local_var(LVAR_Gave_Warning) == 0) and (dude_is_armed)) then begin
           set_local_var(LVAR_Gave_Warning,1);
           floater(random(200,203));
       end
       else if ((dude_is_armed) and (In_Timed_Event == 0)) then begin
           In_Timed_Event:=1;
           add_timer_event(self_obj,game_ticks(15),1);
       end
       else if ((dude_is_armed) and (In_Timed_Event == 2)) then begin
           floater(250);
           attack(dude_obj);
       end
   end

   else if ((tile_distance_objs(self_obj,dude_obj) < 4) and (In_Timed_Event == 2) and (dude_is_armed)) then begin
       if (not(dude_is_sneaking)) then begin
           floater(250);
           attack(dude_obj);
       end
       else if (not(skill_success(dude_obj,SKILL_SNEAK,-15))) then begin
           floater(250);
           attack(dude_obj);
       end
   end
end

procedure timed_event_p_proc begin

   if (fixed_param == 1) then begin
       In_Timed_Event:=2;

       if (dude_is_armed) then begin
           if (tile_distance_objs(self_obj,dude_obj) < 5) then begin
               floater(250);
               attack(dude_obj);
           end
           else if (self_can_see_dude) then begin
               floater(250);
               attack(dude_obj);
           end
       end
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */

   attack(source_obj);

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   attack(source_obj);
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin

   call Node001;

end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter


   if (cur_map_index == MAP_BH_RND_DESERT) then begin
       dec_map_var(MVAR_Hostile_Total);
   end
/* Set global_variable for Enemy status*/
end


/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   floater(random(300,303));
end

//xxxxxxxxxxxxxxxxxxxx
