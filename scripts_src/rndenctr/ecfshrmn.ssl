/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Fisher Men
        Location: Random Encounter
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "ecfshrmn"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_ECFSHRMN
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_)

#include "../headers/command.h"
#include "../headers/modreact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure combat_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE
#define TAUNT_CHANCE 20

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;

procedure NodeFleeing;
procedure NodeFighting;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Choice                     (7)

#define CH_FRUIT  1
#define CH_BROC   2
#define CH_XANDER 3
#define CH_SPEARS 4

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

procedure combat_p_proc begin
   if (critter_is_fleeing(self_obj)) then begin
      if (Random(1, 100) <= TAUNT_CHANCE) then begin
         call NodeFleeing;
      end
   end else begin
      if (Random(1, 100) <= TAUNT_CHANCE) then begin
         call NodeFighting;
      end
   end
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_RND_FISHERMEN);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_GENERIC_GUARDS);
   set_local_var(LVAR_Choice, Random(CH_FRUIT, CH_SPEARS));

   if (local_var(LVAR_Choice) == CH_FRUIT) then begin
      check_restock_item(PID_MUTATED_FRUIT, 3, 5, 100)
   end else if (local_var(LVAR_Choice) == CH_BROC) then begin
      check_restock_item(PID_BROC_FLOWER, 3, 5, 100)
   end else if (local_var(LVAR_Choice) == CH_XANDER) then begin
      check_restock_item(PID_XANDER_ROOT, 3, 5, 100)
   end else if (local_var(LVAR_Choice) == CH_SPEARS) then begin
      check_restock_item(PID_SHARPENED_POLE, 3, 5, 100)
   end
   check_restock_item(PID_FROZEN_DINNER, 1, 1, 50)
   check_restock_item(PID_LASER_PISTOL, 1, 1, 1)
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   start_gdialog(NAME,self_obj,4,-1,-1);
   gSay_Start;
   call Node001;
   gSay_End;
   end_dialogue;
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(101);
   NOption(102, Node002, 1);
end

procedure Node002 begin
   if (local_var(LVAR_Choice) == CH_FRUIT) then
      Reply(104);
   else if (local_var(LVAR_Choice) == CH_BROC) then
      Reply(105);
   else if (local_var(LVAR_Choice) == CH_XANDER) then
      Reply(106);
   else if (local_var(LVAR_Choice) == CH_SPEARS) then
      Reply(107);
   NOption(108, Node003, 4);
   NLowOption(109, Node004);
end

procedure NodeBuyJunk begin
   variable obj;
   if (dude_caps >= 10) then begin
      obj := self_item(PID_FROZEN_DINNER);
      rm_obj_from_inven(self_obj, obj);
      add_obj_to_inven(dude_obj, obj);
      item_caps_adjust(dude_obj, -10);
      item_caps_adjust(self_obj, 10);
      call Node005;
   end else begin
      call Node006;
   end
end

procedure NodeBuyLaser begin
   variable obj;
   if (dude_caps >= 10000) then begin
      obj := self_item(PID_LASER_PISTOL);
      rm_obj_from_inven(self_obj, obj);
      add_obj_to_inven(dude_obj, obj);
      item_caps_adjust(dude_obj, -10000);
      item_caps_adjust(self_obj, 10000);
      call Node005;
   end else begin
      call Node006;
   end
end

procedure NodeBuyPole begin
   variable obj;
   if (dude_caps >= 40) then begin
      obj := self_item(PID_SHARPENED_POLE);
      rm_obj_from_inven(self_obj, obj);
      add_obj_to_inven(dude_obj, obj);
      item_caps_adjust(dude_obj, -40);
      item_caps_adjust(self_obj, 40);
      call Node005;
   end else begin
      call Node006;
   end
end

procedure NodeBuyFruit begin
   variable obj;
   if (dude_caps >= 5) then begin
      obj := self_item(PID_MUTATED_FRUIT);
      rm_obj_from_inven(self_obj, obj);
      add_obj_to_inven(dude_obj, obj);
      item_caps_adjust(dude_obj, -5);
      item_caps_adjust(self_obj, 5);
      call Node005;
   end else begin
      call Node006;
   end
end

procedure NodeBuyXander begin
   variable obj;
   if (dude_caps >= 20) then begin
      obj := self_item(PID_XANDER_ROOT);
      rm_obj_from_inven(self_obj, obj);
      add_obj_to_inven(dude_obj, obj);
      item_caps_adjust(dude_obj, -20);
      item_caps_adjust(self_obj, 20);
      call Node005;
   end else begin
      call Node006;
   end
end

procedure NodeBuyBroc begin
   variable obj;
   if (dude_caps >= 10) then begin
      obj := self_item(PID_BROC_FLOWER);
      rm_obj_from_inven(self_obj, obj);
      add_obj_to_inven(dude_obj, obj);
      item_caps_adjust(dude_obj, -10);
      item_caps_adjust(self_obj, 10);
      call Node005;
   end else begin
      call Node006;
   end
end

procedure Node003 begin
   Reply(110);
   if (self_item(PID_FROZEN_DINNER)) then
      NOption(111, NodeBuyJunk, 4);
   if (self_item(PID_LASER_PISTOL)) then
      NOption(112, NodeBuyLaser, 4);
   if (self_item(PID_SHARPENED_POLE)) then
      NOption(113, NodeBuyPole, 4);
   if (self_item(PID_MUTATED_FRUIT)) then
      NOption(114, NodeBuyFruit, 4);
   if (self_item(PID_XANDER_ROOT)) then
      NOption(115, NodeBuyXander, 4);
   if (self_item(PID_BROC_FLOWER)) then
      NOption(116, NodeBuyBroc, 4);
   NOption(117, Node999, 4);
end

procedure Node004 begin
   Reply(118);
   NLowOption(119, Node999);
end

procedure Node005 begin
   Reply(120);
   NOption(121, Node999, 4);
end

procedure Node006 begin
   Reply(122);
   NOption(123, Node999, 4);
end



procedure NodeFleeing begin
end

procedure NodeFighting begin
end
//xxxxxxxxxxxxxxxxxxxx