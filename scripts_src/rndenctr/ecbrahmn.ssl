/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/* Random Encounter scripts. These will be filled in more once they
   have been designed.
*/

procedure start;

#define SCRIPT_REALNAME "ecbrahmn"
#include "../headers/define.h"

#define NAME                    SCRIPT_ECBRAHMN

#include "../headers/command.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure push_p_proc;

#define br_mstr(x)                              message_str(SCRIPT_ZCBRAHMN,x)
#define br_floater(x)                           float_msg(self_obj, br_mstr(x), FLOAT_COLOR_NORMAL)

#define TIMER_STAND_UP 4

procedure push_p_proc begin
   if (obj_in_party(source_obj)) then begin
      if (stat_success(source_obj, STAT_st, 0)) then begin
         if ((critter_state(self_obj) bwand DAM_KNOCKED_DOWN) == FALSE) then begin
            reg_anim_clear(self_obj);
            reg_anim_begin();
               reg_anim_animate(self_obj,ANIM_hit_from_front,-1);
               reg_anim_animate(self_obj,ANIM_fall_back,5);
               reg_anim_animate(self_obj,ANIM_fall_back_sf,-1);
            reg_anim_end();
            critter_injure(self_obj,DAM_KNOCKED_DOWN);
            add_timer_event(self_obj, game_ticks(Random(10, 30)), TIMER_STAND_UP);
            ndebug("ahh Cow TIPPED you get +10 Hick Experience Points.");
         end
      end
   end
end

procedure timed_event_p_proc begin
variable temp_poo;
   if (fixed_param == 1) then begin
      if ((critter_state(self_obj) bwand DAM_KNOCKED_DOWN) == FALSE) then begin
         self_walk_to_tile(tile_num_in_direction(self_tile,random(0,5),random(1,3)));
      end
      flush_add_timer_event_sec(self_obj, random(45, 90), 1);
   end if (fixed_param == 2) then begin
      br_floater(random(201, 205));
      flush_add_timer_event_sec(self_obj, random(30, 45), 2);
   end else if (fixed_param == 3) then begin
         br_floater(206);
         if (((critter_state(self_obj) bwand DAM_KNOCKED_DOWN) == FALSE) and (not(combat_is_initialized))) then begin
            reg_anim_clear(self_obj);
            reg_anim_begin();
            if (self_tile / 200 > 131) then
               self_walk_to_tile(tile_num_in_direction(self_tile,5,4));
            else if (self_tile / 200 < 108) then
               self_walk_to_tile(tile_num_in_direction(self_tile,2,4));
            else if (self_tile % 200 < 80) then
               self_walk_to_tile(tile_num_in_direction(self_tile,3,4));
            else if (self_tile % 200 > 122) then
               self_walk_to_tile(tile_num_in_direction(self_tile,0,4));
            else
               self_walk_to_tile(tile_num_in_direction(self_tile,random(0,7),3));
            reg_anim_end();
         end
         flush_add_timer_event_sec(self_obj, random(10, 20), 3);
   end else if (fixed_param == TIMER_STAND_UP) then begin
      reg_anim_clear(self_obj);
      reg_anim_begin();
      reg_anim_animate_reverse(self_obj, ANIM_fall_back, -1);
      reg_anim_end();
      critter_uninjure(self_obj, DAM_KNOCKED_DOWN);
   end
end

procedure start begin
end

procedure critter_p_proc begin
end

procedure pickup_p_proc begin
end
procedure talk_p_proc begin
end

procedure destroy_p_proc begin
end

procedure damage_p_proc begin
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure description_p_proc begin
   script_overrides;
   display_msg(br_mstr(105));
end

procedure use_obj_on_p_proc begin
   // if you give a Brahmin beer or booze, it'll fall down and take damage. This was pulled from the original
   //    fallout script
   if ((obj_pid(obj_being_used_with) == PID_BEER)
    or (obj_pid(obj_being_used_with) == PID_BOOZE)) then begin
      variable item := 0;
      script_overrides;
      item := obj_being_used_with;
      rm_obj_from_inven(source_obj,obj_being_used_with);
      destroy_object(item);
      if ((critter_state(self_obj) bwand DAM_KNOCKED_DOWN) == FALSE) then begin
         reg_anim_clear(self_obj);
         reg_anim_begin();
            reg_anim_animate(self_obj,ANIM_hit_from_front,-1);
            reg_anim_animate(self_obj,ANIM_fall_back,5);
            reg_anim_animate(self_obj,ANIM_fall_back_sf,-1);
         reg_anim_end();
         critter_injure(self_obj,DAM_KNOCKED_DOWN);
      end
   end
end

procedure use_skill_on_p_proc begin
   // a good science skill check will give you a little more info about the brahmin.
   if (action_being_used == SKILL_SCIENCE) then begin
      script_overrides;
      if (is_success(roll_vs_skill(dude_obj,action_being_used,0))) then
         display_msg(br_mstr(101));
      else
         display_msg(br_mstr(102));
   end else if (action_being_used == SKILL_STEAL) then
      script_overrides;
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
      if (map_first_run) then begin
         set_self_team(TEAM_RND_BRAHMIN);
         set_self_ai(AI_GENERIC_BRAHMIN);
      end
      flush_add_timer_event_sec(self_obj, random(15, 90), 1);
      flush_add_timer_event_sec(self_obj, random(5, 15), 2);
      flush_add_timer_event_sec(self_obj, random(5, 10), 3);
   end
end
