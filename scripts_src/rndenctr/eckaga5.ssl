/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Kaga Encounter 5
        Location: Random Encounter
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "eckaga5"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_ECKAGA5
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_)

#include "../headers/command.h"
#include "../headers/modreact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure do_setup;
procedure map_enter_p_proc;
procedure combat_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE
#define TAUNT_CHANCE 50

procedure NodeFleeing;
procedure NodeFighting;
procedure doSequence;
procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6) //Also used to as a run flag

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable sequence:=0;
variable animated:=0;
#define TIMER_SEQUENCE 1
#define ATTACK 2
#define DEFEAT 3

procedure start begin
end

procedure timed_event_p_proc begin
   if (fixed_param == TIMER_SEQUENCE) then begin
      call doSequence;
   end
end

procedure combat_p_proc begin
   if (Random(1, 100) <= TAUNT_CHANCE) then
      call NodeFighting;
end


procedure map_exit_p_proc begin
   set_kaga_met;
end

procedure do_setup begin
   variable item;
   variable Critter;
   variable Critter_Rotation;
   variable Critter_Tile;

   ndebug("do_setup");

   Only_Once:=0;
   set_self_team(TEAM_RND_KAGA);
   set_self_ai(AI_KAGA);
   set_kaga_1;
   set_kaga_2;
   set_kaga_3;
   set_kaga_4;
   set_kaga_5;
   check_restock_item(PID_PLASMA_GRENADE, 3, 3, 100)
   check_restock_item(PID_STIMPAK, 5, 5, 100)
   check_restock_item(PID_MICRO_FUSION_CELL, 3, 3, 100)
   check_restock_item(PID_BOTTLE_CAPS, 5000, 5000, 100)
   check_restock_item(PID_GATLING_LASER, 1, 1, 100)

   wield_obj_critter(self_obj,self_item(PID_GATLING_LASER));

   Critter:=create_object_sid(PID_MALE_MERCENARY ,0,0,SCRIPT_ECMERCS);
   add_obj_to_inven(Critter, create_object(PID_PLASMA_PISTOL,0,0));
   add_mult_objs_to_inven(Critter, create_object(PID_BOTTLE_CAPS,0,0),random(0,50));
   add_mult_objs_to_inven(Critter, create_object(PID_SMALL_ENERGY_CELL,0,0),2);
   wield_obj_critter(Critter,get_item(Critter, PID_PLASMA_PISTOL));
   set_team(Critter,TEAM_RND_KAGA);
   set_ai(Critter,AI_GENERIC_GUARDS);
   Critter_Rotation:=dude_inv_rot;
   Critter_Tile:=tile_num_in_direction(self_tile,Critter_Rotation,random(3,4));
   critter_attempt_placement(Critter,Critter_Tile,0);
   Face_Critter(dude_obj,Critter);

   Critter:=create_object_sid(PID_MALE_MERCENARY ,0,0,SCRIPT_ECMERCS);
   add_obj_to_inven(Critter, create_object(PID_NEEDLER_PISTOL,0,0));
   add_mult_objs_to_inven(Critter, create_object(PID_BOTTLE_CAPS,0,0),random(0,50));
   add_mult_objs_to_inven(Critter, create_object(PID_HN_NEEDLER_CARTRIDGE,0,0),2);
   wield_obj_critter(Critter,get_item(Critter, PID_NEEDLER_PISTOL));
   set_team(Critter,TEAM_RND_KAGA);
   set_ai(Critter,AI_GENERIC_GUARDS);
   Critter_Rotation:=dude_inv_rot;
   Critter_Tile:=tile_num_in_direction(self_tile,Critter_Rotation,random(3,4));
   critter_attempt_placement(Critter,Critter_Tile,0);
   Face_Critter(dude_obj,Critter);

   Critter:=create_object_sid(PID_FEMALE_MERCENARY ,0,0,SCRIPT_ECMERCS);
   add_obj_to_inven(Critter, create_object(PID_RIPPER,0,0));
   add_mult_objs_to_inven(Critter, create_object(PID_BOTTLE_CAPS,0,0),random(0,50));
   add_mult_objs_to_inven(Critter, create_object(PID_SMALL_ENERGY_CELL,0,0),2);
   item:=create_object(PID_PLASMA_GRENADE,0,0);
   add_mult_objs_to_inven(Critter,item,random(4,6));
   wield_obj_critter(Critter,get_item(Critter, PID_PLASMA_GRENADE));
   set_team(Critter,TEAM_RND_KAGA);
   set_ai(Critter,AI_GENERIC_GUARDS);
   Critter_Rotation:=dude_inv_rot;
   Critter_Tile:=tile_num_in_direction(self_tile,Critter_Rotation,random(3,4));
   critter_attempt_placement(Critter,Critter_Tile,0);
   Face_Critter(dude_obj,Critter);

   Critter:=create_object_sid(PID_FEMALE_MERCENARY ,0,0,SCRIPT_ECMERCS);
   add_obj_to_inven(Critter, create_object(PID_RIPPER,0,0));
   add_mult_objs_to_inven(Critter, create_object(PID_BOTTLE_CAPS,0,0),random(0,50));
   add_mult_objs_to_inven(Critter, create_object(PID_SMALL_ENERGY_CELL,0,0),2);
   wield_obj_critter(Critter,get_item(Critter, PID_RIPPER));
   set_team(Critter,TEAM_RND_KAGA);
   set_ai(Critter,AI_GENERIC_GUARDS);
   Critter_Rotation:=dude_inv_rot;
   Critter_Tile:=tile_num_in_direction(self_tile,Critter_Rotation,random(3,4));
   critter_attempt_placement(Critter,Critter_Tile,0);
   Face_Critter(dude_obj,Critter);

   if (kaga_met) then begin
      float_msg(dude_obj, mstr(120), FLOAT_MSG_GREEN);
      set_local_var(LVAR_Herebefore, 1);
      set_local_var(LVAR_Hostile,2);
   end else begin
      add_timer_event(self_obj, 1, TIMER_SEQUENCE);
   end
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   if( not( is_loading_game ) ) then begin
      call do_setup;
   end
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */
   if (not( is_loading_game ) and not(kaga_5)) then begin
      call do_setup;
   end else if ( not(combat_is_initialized) and (local_var(LVAR_Hostile) == 2) and (self_can_see_dude)) then begin
       //set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_evil_critter

/* Set global_variable for Enemy status*/
   set_kaga_dead;
   set_kaga_met;
   give_xp(1000);
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then begin
      display_msg(mstr(100));
   end else begin
      display_msg(mstr(101));
   end
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
   attack(dude_obj);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure NodeFleeing begin
end

procedure NodeFighting begin
   variable rnd := 0;

   rnd := Random(103, 111);

   if ((rnd == 103 or rnd == 104) and not kaga_met) then rnd := 105;

   if (rnd == 108) then begin
      CheckKarma;
      if not (has_rep_shield) then begin
         rnd := 110;
      end
   end

   if (rnd == 109) then begin
      CheckKarma;
      if not (has_rep_sword) then begin
         rnd := 110;
      end
   end

   float_msg(self_obj, mstr(rnd), FLOAT_MSG_RED);


end

procedure doSequence begin

   Face_Critter(dude_obj, self_obj);
   Face_Critter(self_obj, dude_obj);

   start_gdialog(NAME,self_obj,4,-1,-1);
   gSay_Start;
      call Node001;
   gSay_End;
   end_dialogue;

end

procedure Node001 begin
   set_local_var(LVAR_Herebefore, 1);
   Reply(150);
   NOption(151,Node002,004);
   NOption(152,Node003,004);
   NLowOption(200,Node004);
end


procedure Node002 begin
   Reply(160);
   NOption(g_mstr(350),Node998,004);
   //attack
end


procedure Node003 begin
   Reply(161);
   NOption(g_mstr(350),Node998,004);
   //attack
end


procedure Node004 begin
   Reply(201);
   NLowOption(202,Node005);
end


procedure Node005 begin
   Reply(203);
   NLowOption(g_mstr(350),Node998);
end

//xxxxxxxxxxxxxxxxxxxx
