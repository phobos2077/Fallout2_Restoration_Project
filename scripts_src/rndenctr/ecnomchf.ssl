/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/* Random Encounter scripts. These will be filled in more once they
   have been designed.
*/


#define SCRIPT_REALNAME "ecnomchf"
#include "../headers/define.h"

#define NAME                    SCRIPT_ECNOMCHF

#include "../headers/command.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure combat_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

#define LVAR_Herebefore                 (0)
#define LVAR_Hostile                    (1)
#define LVAR_Personal_Enemy             (2)


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE
#define TAUNT_CHANCE 40

#define On_Tent_Map                 (cur_map_index == MAP_RND_MOUNTAIN_4) or   \
                                    (cur_map_index == MAP_RND_MOUNTAIN_5) or   \
                                    (cur_map_index == MAP_RND_DESERT4) or      \
                                    (cur_map_index == MAP_RND_DESERT5) or      \
                                    (cur_map_index == MAP_RND_DESERT_9) or     \
                                    (cur_map_index == MAP_RND_COAST7) or       \
                                    (cur_map_index == MAP_RND_COAST8) or       \
                                    (cur_map_index == MAP_RND_COAST9) or       \
                                    (cur_map_index == MAP_RND_COAST_10) or     \
                                    (cur_map_index == MAP_RND_COAST_11) or     \
                                    (cur_map_index == MAP_RND_COAST_12)


procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node004a;
procedure Node005;

procedure NodeFleeing;
procedure NodeFighting;


procedure start begin
end

procedure combat_p_proc begin
   if (critter_is_fleeing(self_obj)) then begin
      if (Random(1, 100) <= TAUNT_CHANCE) then begin
         call NodeFleeing;
      end
   end else begin
      if (Random(1, 100) <= TAUNT_CHANCE) then begin
         call NodeFighting;
      end
   end
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   if (cur_map_index == MAP_RND_MOUNTAIN_4) then
      move_to(self_obj, 17278, 0);
   else if (cur_map_index == MAP_RND_MOUNTAIN_5) then
      move_to(self_obj, 17891, 0);
   else if (cur_map_index == MAP_RND_DESERT4) then
      move_to(self_obj, 18897, 0);
   else if (cur_map_index == MAP_RND_DESERT5) then
      move_to(self_obj, 19101, 0);
   else if (cur_map_index == MAP_RND_DESERT_9) then
      move_to(self_obj, 18293, 0);
   else if (cur_map_index == MAP_RND_COAST7) then
      move_to(self_obj, 20702, 0);
   else if (cur_map_index == MAP_RND_COAST8) then
      move_to(self_obj, 18306, 0);
   else if (cur_map_index == MAP_RND_COAST9) then
      move_to(self_obj, 17490, 0);
   else if (cur_map_index == MAP_RND_COAST_10) then
      move_to(self_obj, 16706, 0);
   else if (cur_map_index == MAP_RND_COAST_11) then
      move_to(self_obj, 18293, 0);
   else if (cur_map_index == MAP_RND_COAST_12) then
      move_to(self_obj, 19708, 0);


   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_RND_NOMAD);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_CITIZEN);

   check_restock_item(PID_SPEAR, 2, 5, 80)
   check_restock_item(PID_CLUB, 5, 7, 80)
   check_restock_item(PID_KNIFE, 3, 5, 80)
   check_restock_item(PID_SHARPENED_POLE, 4, 6, 80)
   //check_restock_item(PID_RADSCORPION_PARTS, 5, 6, 80)
   check_restock_item(PID_GECKO_PELT, 5, 6, 80)
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end


end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   start_gdialog(NAME,self_obj,4,-1,-1);
   gSay_Start;
   call Node001;
   gSay_End;
   end_dialogue;

   set_local_var(LVAR_Herebefore, 1);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure NodeBarter begin
   gdialog_barter;
   Reply(203);
   NOption(204,Node999,004);
end

procedure Node001 begin
   if (On_Tent_Map) then begin
      Reply(103);
      NOption(104, Node002, 4);
      NOption(105, Node003, 4);
      NLowOption(106, Node005);
   end else begin
      Reply(200);
      NOption(201, Node002, 4);
      NOption(105, Node003, 4);
      NLowOption(106, Node005);
   end
end

procedure Node002 begin
   Reply(107);
   NOption(108, NodeBarter, 4);
   if (On_Tent_Map) then
      NOption(109, Node004, 4);
   else
      NOption(109, Node004a, 4);
end

procedure Node003 begin
   Reply(110);
   NOption(111, Node999, 4);
end

procedure Node004 begin
   Reply(112);
   NOption(113, Node999, 4);
end

procedure Node004a begin
   Reply(202);
   NOption(113, Node999, 4);
end

procedure Node005 begin
   Reply(114);
   NLowOption(115, Node999);
end

procedure NodeFleeing begin
   float_msg(self_obj, mstr(Random(120, 122)), FLOAT_MSG_YELLOW);
end

procedure NodeFighting begin
   float_msg(self_obj, mstr(Random(116, 119)), FLOAT_MSG_RED);
end


//xxxxxxxxxxxxxxxxxxxx