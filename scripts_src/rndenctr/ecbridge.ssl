/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Robed Figure
        Location: Special Encounter map
        Description: He likes to ask you questions

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "ecbridge"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_ECBRIDGE
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_)

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/specenc.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure combat_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure map_exit_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;

procedure killPlayer;
procedure killSelf;
procedure selfLeave;
procedure Do_cow;

#define KILL_PLAYER 1
#define KILL_SELF   2
#define SELF_LEAVE  3
#define LEAVE_TILE   20105
#define LEAVE_TILE1  15693

#define TIMER_NODE011 1
#define TIMER_SELF_LEAVE 2
//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Cow_Obj                    (7)
#define LVAR_Leave_Tile                 (8)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable talkCount := 0;
variable doWhat := 0;
variable makeCow := 0;
variable leaveCount := 0;

procedure map_exit_p_proc begin
   if (doWhat == SELF_LEAVE) then begin
      destroy_object(self_obj);
   end

end

procedure start begin
   ndebug("in the start");
end

procedure timed_event_p_proc begin
   if (fixed_param == TIMER_NODE011) then begin
      call Node011;
   end else if (fixed_param == TIMER_SELF_LEAVE) then begin
      call selfLeave;
   end
end

procedure combat_p_proc begin
   script_overrides;

   if (critter_state(local_var(LVAR_Cow_Obj)) == CRITTER_IS_DEAD) then begin
      call Do_Cow;
      terminate_combat;
   end
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_RND_BRIDGE);
   set_self_ai(AI_PEASANT);
   add_timer_event(self_obj, 0, TIMER_NODE011);
   ndebug("in the map enter");
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */


 /*  if (tile_distance_objs(self_obj, dude_obj) < 5 and self_can_see_dude) then begin
      if (talkCount == 0) then begin
         talkCount := 500000;
         dialogue_system_enter;
      end else
         talkCount -= 1;
   end*/
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

//   CheckKarma;

//   GetReaction;

   if (not local_var(LVAR_Leave_Tile)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
      set_local_var(LVAR_Herebefore, 0);
      if (doWhat == KILL_PLAYER) then begin
         call killPlayer;
      end else if (doWhat == KILL_SELF) then begin
         call killSelf;
      end else if (doWhat == SELF_LEAVE) then begin
         set_local_var(LVAR_Leave_Tile, LEAVE_TILE);
         call selfLeave;
      end

      talkCount := 50;
      doWhat := 0;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(101));
   else
      display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(104);
   NLowOption(105, Node002);
   NLowOption(106, Node999);
   NOption(107, Node002, 4);
   NOption(108, Node002, 4);
   NOption(109, Node999, 4);
end

procedure Node002 begin
   Reply(110);
   NLowOption(111, Node009);
   NLowOption(dude_name + mstr(112), Node004);
   NLowOption(mstr(113) + " " + dude_name + " " + mstr(172), Node008);
   NLowOption(114, Node999);

   NOption(dude_name + " " + mstr(115), Node003, 4);
   NOption(116, Node009, 4);
   NOption(117, Node009, 4);
   NOption(118, Node009, 4);
   NOption(119, Node009, 4);
   NOption(120, Node009, 4);
   NOption(121, Node003, 4);
   NOption(122, Node999, 4);
end

procedure NodeRand begin
   variable val;

   val := Random(1, 3);

   if (val == 1) then
      call Node004;
   else if (val == 2) then
      call Node005;
   else if (val == 3) then
      call Node006;
end

procedure Node003 begin
   Reply(123);
   NLowOption(124, Node009);
   NLowOption(125, Node004);
   NLowOption(126, Node999);
   if (not dude_found_geck) then
      NOption(127, NodeRand, 4);
   if (not Fallout2_tribe_kidnapped) then
      NOption(128, NodeRand, 4);
   if (Fallout2_tribe_kidnapped) then
      NOption(129, NodeRand, 4);
   if (Fallout2_fix_tanker) or (Fallout2_tanker_known) then //edit by killap  - added Fallout2_tanker_known
      NOption(130, NodeRand, 4);
   NOption(131, Node009, 4);
   NOption(132, Node999, 4);
end

procedure Node004 begin
   if (dude_iq < 4) then
      Reply(133);
   else
      Reply(134);
   NLowOption(135, Node008); //edit by killap - was 007
   NLowOption(136, Node009);
   NLowOption(137, Node999);
   NOption(138, Node009, 4);
   NOption(139, Node007, 4); //edit by killap - was 009 but since Ian is in the RP, this needs to be valid
   NOption(140, Node007, 4);
   NOption(141, Node007, 4);
   NOption(142, Node009, 4);
   NOption(143, Node007, 4);
   if (dude_luck == 10) then
      NOption(144, Node008, 7);
   else
      NOption(145, Node008, 7);
end

procedure Node005 begin
   Reply(146);
   NOption(147, Node009, 4);
   NOption(148, Node009, 4);
   NOption(149, Node007, 4);
   NOption(150, Node009, 4);
   NOption(151, Node009, 4);
   NOption(152, Node009, 4);
   NOption(153, Node009, 4);
   NOption(154, Node008, 7);
end

procedure Node006 begin
   Reply(155);
   NOption(156, Node009, 4);
   NOption(157, Node009, 4);
   NOption(158, Node007, 4);
   NOption(159, Node009, 4);
   NOption(160, Node007, 4);
   NOption(161, Node009, 4);
   NOption(162, Node009, 4);
   NOption(163, Node008, 4);
end

procedure Node007 begin
   doWhat := SELF_LEAVE;
   Reply(164);
   NLowOption(165, Node999);
   NLowOption(166, Node999);
   NOption(167, Node999, 4);
end

procedure Node008 begin
   give_xp(EXP_DEAD_ROBE_MAN);
   doWhat := KILL_SELF;
   call Node999;
end

procedure Node009 begin
   doWhat := KILL_PLAYER;
   call Node999;
end

procedure Node010 begin
end

procedure Node011 begin
   float_msg(dude_obj, mstr(171 + (1000*(dude_is_stupid))), FLOAT_MSG_NORMAL); //edit by killap - added (10*(dude_is_stupid))
end

procedure killPlayer begin
   float_msg(self_obj, mstr(170), FLOAT_MSG_NORMAL);
   float_msg(dude_obj, mstr(200), FLOAT_MSG_RED);
   critter_damage(dude_obj, 10000);
end

procedure killSelf begin
   float_msg(self_obj, mstr(200), FLOAT_MSG_RED);
   critter_damage(self_obj, 10000);
end

procedure do_cow begin
   variable obj;
   variable tile;

   tile := tile_num_in_direction(self_tile, 2, 1);

   obj := create_object_sid(PID_BRAHMIN, tile, 0, SCRIPT_ECGCWBMB);
   critter_attempt_placement(obj, tile, 0);
   set_local_var(LVAR_Cow_Obj, obj);
end

procedure selfLeave begin
   ndebug("should be leaving going to tile: " + local_var(LVAR_Leave_Tile));
   if (local_var(LVAR_Leave_Tile) == LEAVE_TILE and self_tile == LEAVE_TILE) then begin
      set_local_var(LVAR_Leave_Tile, LEAVE_TILE1);
   end

   if (self_tile != local_var(LVAR_Leave_Tile)) then begin
      if (not anim_busy(self_obj)) then begin
         reg_anim_begin();
         self_walk_to_tile(local_var(LVAR_Leave_Tile));
         reg_anim_end();
      end else begin
         if (game_time > talkCount) then begin
            float_msg(self_obj, mstr(173 + leaveCount), FLOAT_MSG_NORMAL);
            leaveCount += 1;
            if (leaveCount > 5) then leaveCount := 1;
            talkCount := game_time + (ONE_GAME_SECOND * 3);
         end
      end
   end else begin
      destroy_object(self_obj);
   end

   add_timer_event(self_obj, game_ticks(2), TIMER_SELF_LEAVE);
end

//xxxxxxxxxxxxxxxxxxxx
