/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Grant
        Location: Ncr 2 and 3
        Description: He opens the gate for you.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define NPC_REACTION_TYPE       REACTION_TG
#define NPC_REACTION_VAR        7 /* same as thief variable */
#include "..\headers\define.h"
#include "..\headers\ncr.h"
#include "..\headers\ncr1.h"  //added by killap
#include "..\headers\ncr3.h"  //added by killap

#define NAME                    SCRIPT_SCWESGRD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_NCR)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"



/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;

procedure FieldOff;
procedure Walk;
procedure checkNotify;
procedure UseComputer;
procedure UseEmitter;
#define FIELD_TILE_RANCH    22454
#define FIELD_TILE_DOWNTOWN 19562

#define DOWNTOWN_START      21163
#define WESTIN_START        23656

#define WESTIN_ROT          3
#define DOWNTOWN_ROT        0

#define TIMER_FIELD_OFF 1
#define TIMER_WALK      2
#define TIMER_PLAYER_USE_COMPUTER_SUCCESS 3
#define TIMER_PLAYER_USE_COMPUTER_FAIL    4
#define TIMER_PLAYER_USE_EMITTER          5


//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Caught_Thief               (7)
#define LVAR_Turn_Field_Off             (8)
#define LVAR_Annoyance                  (9)
#define LVAR_Let_In                     (10)
#define LVAR_Warned                     (11)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable i_westin_emitter_obj;
import variable i_westin_guard_obj;


/* Local variables which do not need to be saved between map changes. */
variable Only_Once := 0;
variable starttile := 0;
variable startrot  := 0;

procedure start begin
end

procedure timed_event_p_proc begin
   if (fixed_param == TIMER_FIELD_OFF) then begin
      call FieldOff;
   end else if (fixed_param == TIMER_WALK) then begin
      call Walk;
   end else if (fixed_param == TIMER_PLAYER_USE_COMPUTER_SUCCESS) then begin
      call UseComputer;
   end else if (fixed_param == TIMER_PLAYER_USE_COMPUTER_FAIL) then begin
      call UseComputer;
   end else if (fixed_param == TIMER_PLAYER_USE_EMITTER) then begin
      call UseEmitter;
   end
end


/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NCR_WESTIN);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_GUARD);
   set_local_var(LVAR_Turn_Field_Off,0); //added by killap
   if (global_var(GVAR_NCR_GRANT_HOSTILE) == 5) then begin
      destroy_object(self_obj);
      i_westin_guard_obj := -1;
   end else begin
      if (cur_map_index == MAP_NCR_DOWNTOWN) then begin
         starttile := DOWNTOWN_START;
         startrot := DOWNTOWN_ROT;
      end else begin
         starttile := WESTIN_START;
         startrot := WESTIN_ROT;
      end
      i_westin_guard_obj := self_obj;

      //if (not is_loading_game) then begin
         if (self_tile != starttile) then begin
            move_to(self_obj, starttile, 0);
            animate_rotation(startrot); // Seraph's Fix: animate_rotation always affects self_obj and only takes 1 arg. Thanks Rob!
         end
      //end
   end
end

//added by killap
procedure map_update_p_proc begin
   i_westin_guard_obj := self_obj; 
end
//end added

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((global_var(GVAR_NCR_GRANT_HOSTILE) != 0 or ncr_flag(NCR_WESTIN_ENEMY)) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_global_var(GVAR_NCR_GRANT_HOSTILE, 1);
       self_attack_dude;
   end

   if (global_var(GVAR_NCR_WFIELD_NOTIFY) < 4) then
      call checkNotify;

   if (cur_map_index == MAP_NCR_DOWNTOWN) then begin
      if (global_var(GVAR_NCR_WFIELD_NOTIFY) == 4 or global_var(GVAR_NCR_POWER_ON) != POWER_ON) then begin
         if (obj_can_see_obj(self_obj, dude_obj) and tile_distance_objs(self_obj, dude_obj) <= 5) then begin
            if (not ncr_flag(NCR_GRANT_ACCESS) and not local_var(LVAR_Warned)) then begin
               set_local_var(LVAR_Warned, 1);
               reg_anim_clear(dude_obj);
               call Node009;
            end
         end
      end
   end else begin
      if (not ncr_flag(NCR_GRANT_ACCESS) and obj_can_see_obj(self_obj, dude_obj)) then
         set_global_var(GVAR_NCR_GRANT_HOSTILE, 2);
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_global_var(GVAR_NCR_GRANT_HOSTILE, 2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_INTOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if (local_var(LVAR_Turn_Field_Off) == 0 and not anim_busy(self_obj)) then begin
      if (global_var(GVAR_NCR_GRANT_HOSTILE) == 0) then begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node001;
         gSay_End;
         end_dialogue;
      end else begin
         set_global_var(GVAR_NCR_GRANT_HOSTILE, 2);
      end

      if (local_var(LVAR_Turn_Field_Off)) then begin
         add_timer_event(self_obj, 0, TIMER_FIELD_OFF);
      end
   end else begin
      call Node007;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
   set_global_var(GVAR_NCR_GRANT_HOSTILE, 5);

   i_westin_guard_obj := -1;
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_global_var(GVAR_NCR_GRANT_HOSTILE, 2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure NodeFieldOff begin
   set_local_var(LVAR_Let_In, 1);
   set_local_var(LVAR_Turn_Field_Off, 1);
   set_ncr_flag(NCR_GRANT_ACCESS);

   call Node999;
end

procedure Node001 begin
   if (cur_map_index == MAP_NCR_DOWNTOWN) then begin
      //added by killap - expansion
      if (get_westin_flag(WESTIN_DEAD)) then begin
        Reply(150);
        NOption(g_mstr(350), Node999, 1);
      //end added
      end else if (local_var(LVAR_Let_In)) then begin
        Reply(136);
        NOption(137, NodeFieldOff, 1);
        NOption(138, Node999, 1);
      end else begin
         Reply(103);
         NLowOption(104, Node002); // Seraph's Fix: NLowOption only takes 2 args. Thanks Rob!
         if ((global_var( GVAR_VAULT_DELIVER_HOLODISK ) == HOLODISK_ACCEPTED) and (obj_is_carrying_obj_pid(dude_obj, PID_BISHOPS_HOLODISK))) then
            NLowOption(139, Node008);
         NLowOption(105, Node999); // Seraph's Fix: NLowOption only takes 2 args. Thanks Rob!
         NOption(106, Node002, 4);
         if ((global_var( GVAR_VAULT_DELIVER_HOLODISK ) == HOLODISK_ACCEPTED) and (obj_is_carrying_obj_pid(dude_obj, PID_BISHOPS_HOLODISK))) then
            NOption(140, Node008, 4);
         NOption(107, Node999, 4);
      end
   end else begin
      Reply(108);
      NLowOption(109, NodeFieldOff);
      NLowOption(110, Node999);
      NOption(111, NodeFieldOff, 4);
      NOption(112, Node999, 4);
   end
end

procedure Node002 begin
   Reply(113);
   NLowOption(114, Node003); // Seraph's Fix: NLowOption only takes 2 args. Thanks Rob!
   NLowOption(115, Node999); // Seraph's Fix: NLowOption only takes 2 args. Thanks Rob!
   NOption(116, Node003, 4);
   NOption(117, Node999, 4);
end

procedure Node003 begin
   Reply(118);
   if (get_felix_seed(FELIX_DUMONT_REF)) then
      NLowOption(119, Node004);
   if ((global_var( GVAR_VAULT_DELIVER_HOLODISK ) == HOLODISK_ACCEPTED) and (obj_is_carrying_obj_pid(dude_obj, PID_BISHOPS_HOLODISK))) then
      NLowOption(139, Node008);
   NLowOption(120, Node005);
   NLowOption(121, Node006);
   NLowOption(122, Node999);
   if (get_felix_seed(FELIX_DUMONT_REF)) then
      NOption(123, Node004, 4);
   if ((global_var( GVAR_VAULT_DELIVER_HOLODISK ) == HOLODISK_ACCEPTED) and (obj_is_carrying_obj_pid(dude_obj, PID_BISHOPS_HOLODISK))) then
      NOption(140, Node008, 4);
   NOption(124, Node005, 4);
   NOption(125, Node999, 4);
end

procedure Node004 begin
   Reply(126);
   NLowOption(127, NodeFieldOff);
   NOption(128, NodeFieldOff, 4);
end

procedure Node005 begin
   Reply(129);
   NOption(130, Node999, 1);
end

procedure Node006 begin
   Reply(131);
   NLowOption(132, NodeFieldOff);
end

procedure Node007 begin
   float_msg(self_obj, mstr(133), FLOAT_MSG_NORMAL);
end

procedure Node008 begin
   Reply(141);
   NLowOption(127, NodeFieldOff);
   NOption(128, NodeFieldOff, 4);
end

procedure Node009 begin
   float_msg(self_obj, mstr(142), FLOAT_MSG_NORMAL);
end


procedure FieldOff begin
   variable tile;

   debug_msg("SCWESGRD: i_westin_emitter_obj: " + i_westin_emitter_obj);
   if (i_westin_emitter_obj != -1 and global_var(GVAR_NCR_POWER_ON) == POWER_ON) then begin
      if (cur_map_index == MAP_NCR_DOWNTOWN) then
         tile := FIELD_TILE_DOWNTOWN;
      else
         tile := FIELD_TILE_RANCH;

      reg_anim_clear(self_obj);
      reg_anim_begin();
      animate_move_to_tile(tile);
      reg_anim_end();

      add_timer_event(self_obj, 4, TIMER_WALK);
   end else begin
      set_local_var(LVAR_Turn_Field_Off, 0);
   end
end

#define EMITTER_MAP_DELAY(x) set_map_var(MVAR_Emitter_Wes_Control, EMITTER_CONTROL_DELAY)

procedure Walk begin
   variable tile;

   if (cur_map_index == MAP_NCR_DOWNTOWN) then
      tile := FIELD_TILE_DOWNTOWN;
   else
      tile := FIELD_TILE_RANCH;

   if (local_var(LVAR_Turn_Field_Off)) then begin
      if (self_tile == tile) then begin
         if (i_westin_emitter_obj != -1) then begin
            EMITTER_MAP_DELAY(MVAR_Emitter_Wes_Control); //edit by killap
         end
         reg_anim_clear(self_obj);
         reg_anim_begin();
         animate_move_to_tile(starttile);
         animate_rotation(self_obj, startrot); //added by killap
         reg_anim_end();
         set_local_var(LVAR_Turn_Field_Off, 0);
      end else begin
         if (not anim_busy(self_obj)) then begin
            reg_anim_clear(self_obj);
            reg_anim_begin();
            animate_move_to_tile(tile);
            reg_anim_end();
         end
      end
      add_timer_event(self_obj, 4, TIMER_WALK);
   end else begin
      if (self_tile != starttile) then begin
         if (not anim_busy(self_obj)) then begin
            reg_anim_clear(self_obj);
            reg_anim_begin();
            animate_move_to_tile(starttile);
            animate_rotation(self_obj, startrot); //added by killap
            reg_anim_end();
         end
         add_timer_event(self_obj, 4, TIMER_WALK);
      end
   end
end

procedure checkNotify begin
   if (obj_can_see_obj(self_obj, dude_obj)) then begin
      if (global_var(GVAR_NCR_WFIELD_NOTIFY) == 3) then begin
         set_global_var(GVAR_NCR_WFIELD_NOTIFY, 0);
         float_msg(self_obj, mstr(134), FLOAT_MSG_RED);
      end else if (global_var(GVAR_NCR_WFIELD_NOTIFY) == 2 or global_var(GVAR_NCR_WFIELD_NOTIFY) == 1) then begin
         set_global_var(GVAR_NCR_WFIELD_NOTIFY, 0);
         float_msg(self_obj, mstr(135), FLOAT_MSG_RED);
         set_global_var(GVAR_NCR_GRANT_HOSTILE, 2);
      end
   end else begin
      if (global_var(GVAR_NCR_WFIELD_NOTIFY) < 5) then
         set_global_var(GVAR_NCR_WFIELD_NOTIFY, 0);
   end
end

procedure UseComputer begin
   float_msg(self_obj, mstr(135), FLOAT_MSG_RED);
   inc_local_var(LVAR_Annoyance);

   if (local_var(LVAR_Annoyance) == 2) then
      set_global_var(GVAR_NCR_GRANT_HOSTILE, 2);
end

procedure UseEmitter begin
   float_msg(self_obj, mstr(135), FLOAT_MSG_RED);
   inc_local_var(LVAR_Annoyance);
end
//xxxxxxxxxxxxxxxxxxxx