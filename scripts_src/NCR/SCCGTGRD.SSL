/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Carlson's gate guard
        Location: NCR map 2
        Description: Guards the gate.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define NPC_REACTION_TYPE       REACTION_TG
#define NPC_REACTION_VAR        7 /* same as thief variable */
#include "..\headers\define.h"
#include "..\headers\ncr2.h"

#define NAME                    SCRIPT_SCCGTGRD
#define TOWN_REP_VAR         GVAR_TOWN_REP_NCR

#include "..\headers\command.h"
#include "..\headers\ModReact.h"
#include "..\headers\ncr.h"

import variable i_emitter_carlson_obj;

//added by killap
import variable merk_obj;
import variable merk_visible;
//end added

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure use_obj_on_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005; //added by killap
procedure Node006; //added by killap

procedure use_obj_on_p_proc;
procedure LowerGate;
procedure checkGateAttack;

#define GATE_TILE 19514

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Caught_Thief               (7)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable Field1a_Ptr;

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable imCaptain := 0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   if (self_tile == CARLSON_GATE_GUARD_CAPTAIN_TILE) then
      imCaptain := 1;
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NCR_CARLSON);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_GUARD);
end

// added by killap - expansion
procedure map_exit_p_proc begin
   if (get_carlson_flag(CARLSON_DEAD)) then
      set_carlson_flag(CARLSON_GATE_CLOSED);
end
// end added

//added by killap
procedure map_update_p_proc begin
   if (self_tile == CARLSON_GATE_GUARD_CAPTAIN_TILE) then
      imCaptain := 1;
end
//end added

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

   if (map_var(MVAR_Carlson_Team_Attack) == 1) then begin
      set_local_var(LVAR_Hostile, 2);
   end

   if (imCaptain) then
      call checkGateAttack;

   //added by killap
   if ( (imCaptain) and (merk_visible) ) then begin
      if (tile_distance(tile_num(merk_obj), 19513) < 6) then begin
         call Node005;
         call LowerGate;
      end
   end
   //end added
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
      SET_NCR_ENEMY;
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION4;

   CheckKarma;

   GetReaction;
   if (imCaptain) then begin
      // added by killap - expansion
      if (get_carlson_flag(CARLSON_GATE_CLOSED)) then begin
         call Node006;
      // end added
      end else if (map_var(MVAR_Player_has_pass)) then begin
         call Node003;
         call LowerGate;
      end
      else if (get_carlson_flag(CARLSON_SECURITY_DUTY)) then begin
         call Node002;
         call LowerGate;
      end
      else
         call Node001;
   end else begin
      call Node004;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));

end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

procedure use_obj_on_p_proc begin
   variable tool;

   tool:=obj_pid(obj_being_used_with);

   if (imCaptain) then begin
      if (tool == OBJ_PRES_PASS) then begin
         script_overrides;
         set_map_var(MVAR_Player_has_pass, 1);
         call talk_p_proc;
      end
   end
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   float_msg(self_obj, mstr(103 + Random(0, 2)), FLOAT_MSG_NORMAL);
end

procedure Node002 begin
   variable num;
   if (dude_is_male) then num := 106;
   else num := 109;

   float_msg(self_obj, mstr(num) , FLOAT_MSG_NORMAL);
end

procedure Node003 begin
   if (global_var(GVAR_NCR_POWER_ON) == POWER_ON) then
      float_msg(self_obj, mstr(108), FLOAT_MSG_NORMAL);
   else
      float_msg(self_obj, mstr(107), FLOAT_MSG_NORMAL);
end

procedure Node004 begin
   float_msg(self_obj, mstr(Random(110, 112)), FLOAT_MSG_NORMAL);
end

//added by killap
procedure Node005 begin
   float_msg(self_obj, mstr(113), FLOAT_MSG_NORMAL);
end

// expansion
procedure Node006 begin
   float_msg(self_obj, mstr(114), FLOAT_MSG_NORMAL);
end
//end added

#define EMITTER_MAP_DELAY(x) set_map_var(MVAR_Carlson_Gate, EMITTER_CONTROL_DELAY)

procedure LowerGate begin
    EMITTER_MAP_DELAY(MVAR_Carlson_Gate);
end

procedure checkGateAttack begin
   if (map_var(MVAR_Player_has_pass) == 0 and not get_carlson_flag(CARLSON_SECURITY_DUTY)) then begin
      if (obj_can_see_obj(self_obj, dude_obj)) then begin
         if (dude_tile == GATE_TILE) then begin
//         if (tile_distance(dude_tile, GATE_CHECK_TILE) < GATE_CHECK_DIST) then begin
            call LowerGate;
            set_map_var(MVAR_Carlson_Team_Attack, 1);
         end
      end
  end
end
//xxxxxxxxxxxxxxxxxxxx