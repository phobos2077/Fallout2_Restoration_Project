/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: NCR Gate Guard
        Location: NCR Entrance
        Description: Talks to the player.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "..\headers\define.h"
//#include "..\headers\ncrent.h"

#define NAME                    SCRIPT_SCGTEGRD
#define TOWN_REP_VAR         GVAR_TOWN_REP_NCR

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

#include "..\headers\ncr.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;

procedure canSeePlayer;

#define TIMER_SET_CHECKWEAPONATTACK 1
#define TIMER_SET_CHECKBADATTACK    2

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once         :=0;
variable checkBadAttack    := 0;
variable checkWeaponAttack := 0;
variable didFloat          := 0;
variable successCount      := 0;

procedure start begin
end

procedure timed_event_p_proc begin
   if (fixed_param == TIMER_SET_CHECKWEAPONATTACK) then begin
      debug_msg("SCGTEGRD: setting check weapon attack");
      checkWeaponAttack := 1;
   end else if (fixed_param == TIMER_SET_CHECKBADATTACK) then begin
      debug_msg("SCGTEGRD: setting bad weapon attack");
      checkBadAttack := 1;
   end
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NCR_GUARD);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_GENERIC_GUARDS);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin
   variable dist;

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end else if (didFloat == 0 and global_var(GVAR_NCR_GTEGRD_ATTACK) == 0) then begin
      dist := canSeePlayer;
      if (dist <= GUARDLOOKDIST and dist != 0) then begin
         reg_anim_clear(dude_obj);
         didFloat := 1;
         call talk_p_proc;
         //Not using start_dialog because there is no real dialog
      end
   end else if (global_var(GVAR_NCR_GTEGRD_ATTACK) == 0) then begin
      if (checkWeaponAttack or checkBadAttack) then begin
         dist := canSeePlayer;
         if (dist <= GUARDATTACKDIST and dist != 0) then begin
            If_Party_Is_Armed then begin
            end

            if (How_Many_Party_Members_Armed > 0) then begin
               set_global_var(GVAR_NCR_GTEGRD_ATTACK, 1);
            end else if (checkBadAttack) then begin
               set_global_var(GVAR_NCR_GTEGRD_ATTACK, 1);
            end
         end
      end
   end else if (global_var(GVAR_NCR_GTEGRD_ATTACK) == 1) then
      if not combat_is_initialized  and canSeePlayer then
         set_local_var(LVAR_Hostile, 2);
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_global_var(GVAR_NCR_GTEGRD_ATTACK, 1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure spewArmed begin
   if (Is_Armed(dude_obj)) then
            debug_msg("SCGTEGRD: dude armed");
   if (Vic_In_Party) then
      if (Is_Armed(Vic_Ptr)) then
            debug_msg("SCGTEGRD: Vic armed");
   if (Myron_In_Party) then
      if (Is_Armed(Myron_Ptr)) then
            debug_msg("SCGTEGRD: Myron armed");
   if (Marcus_In_Party) then
      if (Is_Armed(Marcus_Ptr)) then
            debug_msg("SCGTEGRD: Marcus armed");
   if (MacRae_In_Party) then
      if (Is_Armed(MacRae_Ptr)) then
            debug_msg("SCGTEGRD: MacRae armed");
   if (Sulik_In_Party) then
      if (Is_Armed(Sulik_Ptr)) then
            debug_msg("SCGTEGRD: Sulik armed");
   if (Lenny_In_Party) then
      if (Is_Armed(Lenny_Ptr)) then
            debug_msg("SCGTEGRD: Lenny armed");
   if (Cyberdog_In_Party) then
      if (Is_Armed(Cyberdog_Ptr)) then
            debug_msg("SCGTEGRD: CyberDoc armed");
   if (Doc_In_Party) then
      if (Is_Armed(Doc_Ptr)) then
            debug_msg("SCGTEGRD: Doc armed");
   if (Davin_In_Party) then
      if (Is_Armed(Davin_Ptr)) then
            debug_msg("SCGTEGRD: Davin armed");
   if (Miria_In_Party) then
      if (Is_Armed(Miria_Ptr)) then
            debug_msg("SCGTEGRD: Mira armed");
   if (Laddie_In_Party) then
      if (Is_Armed(Laddie_Ptr)) then
            debug_msg("SCGTEGRD: Laddie armed");
   if (Robobrain_In_Party) then
      if (Is_Armed(Robobrain_Ptr)) then
            debug_msg("SCGTEGRD: Robobrain armed");
end

procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_INTOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;
   
   ReactToLevel; //added by killap
   
   If_Party_Is_Armed then begin
   end

   if (bad_critter_reaction) then begin
      checkbadattack := 0;
      call Node004;
      add_timer_event(self_obj, game_ticks(10), TIMER_SET_CHECKBADATTACK);
   end else if (How_Many_Party_Members_Armed > 0) then begin
      checkweaponattack := 0;
      call Node002;
      call spewArmed;


      add_timer_event(self_obj, game_ticks(10), TIMER_SET_CHECKWEAPONATTACK);
   end else if (not IS_DAYTIME and global_var(GVAR_NCR_GATE_NIGHT) == 0) then
      call Node003;
   else
      call Node001;
   set_local_var(LVAR_Herebefore , 1);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   float_msg(self_obj, mstr(103 + Random(0, 3)), FLOAT_MSG_NORMAL);
end

procedure Node002 begin
   float_msg(self_obj, mstr(107 + Random(0, 3)), FLOAT_MSG_NORMAL);
end

procedure Node003 begin
   float_msg(self_obj, mstr(111 + Random(0, 2)), FLOAT_MSG_NORMAL);
end

procedure Node004 begin
   float_msg(self_obj, mstr(114 + Random(0, 3)), FLOAT_MSG_NORMAL);
end

procedure canSeePlayer begin
   variable retval := 0;

   if not (successCount) then begin
      if (obj_can_see_obj(self_obj, dude_obj)) then begin
         retval := 1;
      end
      else begin
         successCount := SUCCESSNOCHECK;
         retval := 0;
      end
   end else begin
      successCount -= 1;
      retval := 0;
   end

   if (retval) then
      retval := tile_distance_objs(self_obj, dude_obj);
   return retval;
end

/*procedure canSeePlayer begin
   variable retval := 0;

   if (obj_can_see_obj(self_obj, dude_obj)) then
      if (dude_is_sneaking) then begin
         if (is_success(roll_vs_skill(dude_obj, SKILL_SNEAK, 0))) then begin
            successCount := SUCCESSNOCHECK;
         end
         if (successCount) then begin
            successCount -= 1;
            retval := 0;
         end else begin
            retval := 1;
         end
      end
      else begin
         retval := 1;
      end
   if (retval) then
      retval := tile_distance_objs(self_obj, dude_obj);
   return retval;
end*/
//xxxxxxxxxxxxxxxxxxxx