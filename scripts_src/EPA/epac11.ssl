/*
  ***   Script conwerted to Interplay format by Fallout Script Edytor (by Jargo)  ***
*/

#include "..\HEADERS\define.h"
#include "..\headers\EPAMAIN.h"
#include "..\headers\NEWR1.h" //added by killap
#include "..\headers\EPASL.h"

#define NAME                    SCRIPT_EPAC11          //Script name from scripts.h
#define TOWN_REP_VAR            GVAR_TOWN_REP_           //Global variable name for this Town

#include "..\HEADERS\command.h"
#include "..\HEADERS\ModReact.h"
#include "..\HEADERS\partybkg.h"

#define PARTY_NODE_X                   Node1000

#define cat_jules_party_member_options                                        \
         if (local_var(LVAR_Told_Past) == 0) then                       \
            NOption(300,Node014,004);                                   \
         if (local_var(LVAR_Teach) == 0) then                           \
            NOption(310,Node017,004);                                   \
         party_member_default_options

#define cat_jules_joins_party             party_add_self;                                                   \
                                          critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_PLAYER)

#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_WAITING                    (7)
#define LVAR_FOLLOW_DISTANCE            (8)
#define LVAR_TEAM                       (9)
#define LVAR_Joined                     (10)
#define LVAR_Wait                       (11)
#define LVAR_Teach                      (12)
#define LVAR_Told_Past                  (13)
#define LVAR_Kill_NPC                   (14)

procedure start;
procedure critter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure use_obj_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure talk_p_proc;
procedure kill_p_proc;

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;

procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;

procedure Node990;
procedure Node999;

procedure Node1000;
procedure Node1001;
procedure Node1002;
procedure Node1003;
procedure Node1004;
procedure Node1005;
procedure Node1006;
procedure Node1007;
procedure Node1008;
procedure Node1009;
procedure Node1010;
procedure Node1100;

procedure Node2000;
procedure Node2001;
procedure Node2002;
procedure Node2003;

procedure Node3000;
procedure Node3001;
procedure Node3002;
procedure Node3003;

procedure Node3500;

procedure Node5000;
procedure Node5001;
procedure Node5002;
procedure Node5003;
procedure Node5004;

variable Only_Once := 0;
variable Perform_Critter_Float := 0;
variable Delete_Self := 0;
variable chamber := 0;
variable waiting := 0;

#define CATJULES_HIB_HEX         (17096)
#define CATJULES_LOCKER_HEX      (15700)

procedure timed_event_p_proc;

procedure start begin
   //critter_add_trait(self_obj, TRAIT_OBJECT, OBJECT_AI_PACKET, AI_TOUGH_GUARD);
end

procedure push_p_proc begin

end

procedure critter_p_proc begin
   if cur_map_index == MAP_NEW_RENO_1 then begin
      if self_obj == party_member_obj(PID_CAT_JULES)
         and map_var(MVAR_Kitty_Drug) == party_member_obj(PID_CAT_JULES)
      then begin
         set_map_var(MVAR_Kitty_Drug, 0);
         waiting := 1;
         if get_critter_stat(self_obj, STAT_st) < 10 then begin
            use_obj_on_obj(create_object_sid(PID_KITTY_SEX_DRUG_STRENGTH, 0, 0, -1), self_obj);
         end
      end
   end

   if( Perform_Critter_Float == 1 ) then begin
      Perform_Critter_Float := 0;
      // Float an appropriate message based on where we are.
      if( cur_map_index == MAP_NEW_RENO_1 ) then
         call Node2000;
      else if( cur_map_index == MAP_MILITARY_BASE_ENTRANCE ) then
         call Node2001;
      else if( cur_map_index == MAP_REDDING_DOWNTOWN  ) then
         call Node2002;
      else if( cur_map_index == MAP_EPA_SUBLEVELS_1 or cur_map_index == MAP_EPA_SUBLEVELS_2) then
         call Node2003;
   end

   if (local_var(LVAR_Herebefore) == 0) then begin
      call talk_p_proc;
   end else if (Cat_Jules_In_Party) then begin
      if (party_is_waiting == false) then begin
         if (waiting != 1) then begin //check added by killap - needed for "hacky fix" for kitty drug... :(
            party_member_follow_dude
         end
      end
   end

end


procedure look_at_p_proc begin
  script_overrides;
  display_msg(mstr(100));
end

procedure description_p_proc begin
  script_overrides;
  display_msg(mstr(101));
end

procedure use_skill_on_p_proc
begin
end

procedure use_obj_on_p_proc begin

   if ( (obj_pid(obj_being_used_with) == PID_MICRO_FUSION_CELL) or (obj_pid(obj_being_used_with) == PID_SMALL_ENERGY_CELL) ) then begin
      script_overrides;
      display_msg(g_mstr(20000));
   end

end

procedure damage_p_proc
begin
end

procedure map_enter_p_proc begin
   //critter_add_trait(self_obj, TRAIT_OBJECT, OBJECT_AI_PACKET, AI_TOUGH_GUARD);
   party_member_map_enter;
   if( Cat_Jules_Ptr != 0 ) then
      Perform_Critter_Float := 1;
   else
      Perform_Critter_Float := 0;

   flush_add_timer_event_sec(self_obj, random(25, 40), 1);
end

procedure map_update_p_proc begin
   if (combat_is_initialized == false) then begin
      if (anim_busy(self_obj) == false) then begin
         if (party_member_obj(obj_pid(self_obj)) != 0) then begin
            if (chem_use_level(self_obj) == 1) then begin
               if ((100 * self_cur_hits / self_max_hits) <= 60) then begin
                  global_temp := 1;
                  obj_heal_no_skill(self_obj)
               end
            end else if (chem_use_level(self_obj) == 2) then begin
               if ((100 * self_cur_hits / self_max_hits) <= 30) then begin
                  global_temp := 1;
                  obj_heal_no_skill(self_obj)
               end
            end else if (chem_use_level(self_obj) == 3) then begin
               if ((100 * self_cur_hits / self_max_hits) <= 50) then begin
                  global_temp := 1;
                  obj_heal_no_skill(self_obj)
               end
            end else if (chem_use_level(self_obj) == 4) then begin
               if ((100 * self_cur_hits / self_max_hits) <= 50) then begin
                  global_temp := 1;
                  obj_heal_no_skill(self_obj)
               end
            end
         end
      end
   end

   //added by killap - needed for "hacky fix" for kitty drug... :(
   if (waiting == 1) then begin
      waiting := 0;
   end
   //end added

end

procedure kill_p_proc begin
   float_msg(dude_obj, message_str(NAME,5002), 8);
   critter_dmg(self_obj,10000,DMG_plasma);
end

procedure talk_p_proc begin
   //B-team mod code
   /*variable loc1;
   variable loc2;
   loc1:=critter_inven_obj(self_obj,0);*/
   //end B-team mod code

  if (local_var(LVAR_Herebefore) == 0) then begin
      set_local_var(LVAR_Herebefore,1);
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
  end else begin
      if ((Cat_Jules_In_Party) or (party_is_waiting)) then begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node1000;
         gSay_End;
         end_dialogue;
      end else begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node013;
         gSay_End;
         end_dialogue;
      end
  end

  if (Delete_Self == 1) then
   destroy_object(self_obj);

  if (local_var(LVAR_Kill_NPC) == 1) then begin
      set_obj_visibility(self_obj,0); //visi
      call kill_p_proc;
  end

//B-team mod code
/*loc2:= critter_inven_obj(self_obj, 2);
if (loc2 == 0) then begin
   loc2 := critter_inven_obj(self_obj, 1);
end
if  (critter_inven_obj(self_obj,0)!=loc1) then
 begin
  if (critter_inven_obj(self_obj,0)== 0 ) then
  begin
  //no armor
  metarule3(107,self_obj,FID_NMPRMB,0);
  end
  else
  if ((obj_pid(critter_inven_obj(self_obj,0))== 1) or (obj_pid(critter_inven_obj(self_obj,0))==379)) then
  begin
  // Ustawianie grafiki dla Lether Armor
  metarule3(107,self_obj,FID_NMLTBB,0);
  end
  else
  if ((obj_pid(critter_inven_obj(self_obj,0))== 232) or (obj_pid(critter_inven_obj(self_obj,0))== 3 )) then
  begin
  // Ustawianie grafiki dla Power Armor
  metarule3(107,self_obj,FID_HAPOWR,0);
  end
  else
  if ((obj_pid(critter_inven_obj(self_obj,0))==349) or (obj_pid(critter_inven_obj(self_obj,0))==348 )) then
  begin
  // Ustawianie grafiki dla Advenced Power Armor
  metarule3(107,self_obj,FID_HANPWR,0);
  end
  else
  if ((obj_pid(critter_inven_obj(self_obj,0))==240) or (obj_pid(critter_inven_obj(self_obj,0))==380) or (obj_pid(critter_inven_obj(self_obj,0))==2 )) then
  begin
  // Ustawianie grafiki dla Metal Armor
  metarule3(107,self_obj,FID_NMMETB,0);
  end
  else
  if ((obj_pid(critter_inven_obj(self_obj,0))==74) or (obj_pid(critter_inven_obj(self_obj,0))==265)) then
  begin
  // Ustawianie grafiki dla Lether Jacket
  metarule3(107,self_obj,FID_NMMAXZ,0);
  end
  else
  if ((obj_pid(critter_inven_obj(self_obj,0))==239) or (obj_pid(critter_inven_obj(self_obj,0))==17) or (obj_pid(critter_inven_obj(self_obj,0))==381 )) then
  begin
  // Ustawianie grafiki dla Combat Armor
  metarule3(107,self_obj,FID_HMCMBT,0);
  end
  else
  if ((obj_pid(critter_inven_obj(self_obj,0))==524) or (obj_pid(critter_inven_obj(self_obj,0))==113)) then
  begin
  // Ustawianie grafiki dla habitu
  metarule3(107,self_obj,FID_HAROBE,0);
  end
  restock_obj := critter_inven_obj(self_obj, 1);
  debug_msg("armour pid == " + obj_pid(restock_obj));
  rm_obj_from_inven(self_obj, restock_obj);
  add_obj_to_inven(self_obj, restock_obj);
  if (loc2 != 0) then begin
   wield_obj_critter(self_obj, loc2);
  end
 end*/
//end B-team mod code

end

procedure Node001 begin
   Reply(200);
   NOption(201,Node002,004);
end

procedure Node002 begin
   if (map_var(MVAR_Cat_Jules_Free) == 2) then begin
      Reply(5000);
	   NOption(5001,Node999,004);
	   set_local_var(LVAR_Kill_NPC,1);
	   destroy_object(tile_contains_pid_obj(CATJULES_HIB_HEX,dude_elevation,33556341));
      chamber:=create_object_sid(33556343,CATJULES_HIB_HEX,dude_elevation,-1);
   end else begin
      if (dude_is_male) then begin
         Reply(210);
         NOption(mstr(212) + dude_name + mstr(213),Node003,004);
      end else begin
            Reply(211);
         NOption(214,Node005,004);
         NOption(215,Node005,004);
      end

      //if ( (global_var(GVAR_FETCH_GECKOPELTS) >= 1) or (global_var(GVAR_EPALIGHTING_GEN) >= 1) ) then
      //   NOption(216,Node004,004);
   end
end

procedure Node003 begin
   Reply(220);
   NOption(221,Node005,004);
end

procedure Node004 begin
   Reply(230);
   NOption(231,Node005,004);
   NOption(232,Node005,004);
end

procedure Node005 begin
   Reply(240);
   NOption(241,Node006,004);
end

procedure Node006 begin
   Reply(250);
   NOption(251,Node007,004);
end

procedure Node007 begin
   Reply(260);
   NOption(261,Node008,004);
   NOption(262,Node009,004);
end

procedure Node008 begin
   Reply(270);
   NOption(272,Node011,004);
   NOption(273,Node010,004);
end

procedure Node009 begin
   Reply(271);
   NOption(272,Node011,004);
   NOption(273,Node010,004);
end

procedure Node010 begin
   Reply(290);
   NOption(291,Node012,004);
end

procedure Node011 begin
   Reply(280);
   NOption(281,Node990,004);
   NOption(282,Node990,004);
end

//NPC does not join and leaves for good.
procedure Node012 begin
   destroy_object(tile_contains_pid_obj(CATJULES_HIB_HEX,dude_elevation,33556341));
   chamber:=create_object_sid(33556343,CATJULES_HIB_HEX,dude_elevation,-1);
   Delete_Self := 1;
end

//party was full when first tried to recruit
procedure Node013 begin
   Reply(390);
   NOption(391,Node1100,004);
   NOption(392,Node999,004);
end

procedure Node014 begin
   Reply(301);
   NOption(302,Node015,004);
end

procedure Node015 begin
   Reply(303);
   NOption(304,Node016,004);
end

procedure Node016 begin
   set_local_var(LVAR_Told_Past,1);
   Reply(305);
   NOption(306,Node1000,004);
end

procedure Node017 begin
   Reply(311);
   NOption(312,Node018,004);
end

procedure Node018 begin
   set_local_var(LVAR_Teach,1);
   critter_mod_skill(dude_obj, SKILL_SMALL_GUNS, 10);
   critter_mod_skill(dude_obj, SKILL_ENERGY_WEAPONS, 10);
   critter_mod_skill(dude_obj, SKILL_BIG_GUNS, 10);
   game_time_advance(2*ONE_GAME_HOUR);

   Reply(313);
   NOption(314,Node1000,004);
end

procedure Node999 begin

end

procedure Node990 begin
   variable locker := 0;

   if (dude_charisma <= 1) then begin
      Reply(490+dude_is_female);
      NOption((g_mstr(352)), Node012, 4);
   end else if (dude_at_max_party_size) then begin
      Reply(501);
      set_local_var(LVAR_Wait,1);
      NOption(g_mstr(10007),Node999,001);
   end else begin
      Reply(2900);
      NOption(2901,Node999,004);
      cat_jules_joins_party;
      set_local_var(LVAR_Joined,1);
   end

   locker:=tile_contains_pid_obj(CATJULES_LOCKER_HEX,dude_elevation,PID_LOCKER_CLEAN_LEFT);
   move_obj_inven_to_obj(locker, self_obj);
   obj_open( locker );
   set_obj_visibility(self_obj,0); //visi
   destroy_object(tile_contains_pid_obj(CATJULES_HIB_HEX,dude_elevation,33556341));
   chamber:=create_object_sid(33556343,CATJULES_HIB_HEX,dude_elevation,-1);
end

//party member stuff
procedure Node1000 begin
   if (party_is_waiting) then begin
      Reply(random(440,441));
      party_member_default_options;
   end else begin
      Reply(random(400,402));
      cat_jules_party_member_options;
   end
end

procedure Node1001 begin
   // heal yourself
   obj_heal(self_obj)
   global_temp := 0;
   if (party_healed_max) then begin
      Reply(480);
   end else if (party_healed_good) then begin
      Reply(481);
   end else if (party_healed_hurt) then begin
      Reply(482);
   end else begin
      Reply(483);
   end
   cat_jules_party_member_options;
end

procedure Node1002 begin
   // wait here
   set_party_waiting;
   Reply(random(430,431));
   party_member_default_options;
end

procedure Node1003 begin
   // put your weapon away
   inven_unwield(self_obj);
   Reply(470);
   cat_jules_party_member_options;
end

procedure Node1004 begin
   // follow close
   set_follow_close;
   Reply(422);
   cat_jules_party_member_options;
end

procedure Node1005 begin
   // follow medium
   set_follow_medium;
   Reply(423);
   cat_jules_party_member_options;
end

procedure Node1006 begin
   // follow far
   set_follow_far;
   Reply(424);
   cat_jules_party_member_options;
end

procedure Node1007 begin
   // distance
   //Reply("TEMP MESSAGE: DISTANCE");
   Reply(421);
   party_member_def_follow_options;
end

procedure Node1008 begin
   // gear
   //Reply("TEMP MESSAGE: GEAR");
   Reply(410);
   party_member_def_gear_options;
end

procedure Node1009 begin
   // remove armor
   //NEED TO FIND OUT WHY THIS DOESNT WORK!!! - NBK
   remove_armor(self_obj)
   //Reply("TEMP MESSAGE: REMOVE ARMOR");
   Reply(460);
   cat_jules_party_member_options;
end

procedure Node1010 begin
   // weapons that can be used
   //Reply("TEMP MESSAGE: CAN USE...");
   Reply(411);
   cat_jules_party_member_options;
end

procedure Node1100 begin
   // join me
   if (dude_charisma <= 1) then begin
      set_self_abandon_party;
      Reply(490+dude_is_female);
      NOption((g_mstr(352)), Node012, 4);
   end else if (dude_at_max_party_size) then begin
      set_self_abandon_party;
      Reply(500);
      NOption(g_mstr(10007),Node999,001);
   end else begin
      end_party_waiting;
      Reply(random(450,451));
      cat_jules_party_member_options;
   end
end

//area floats

//new reno
procedure Node2000 begin
   floater(700);
end

//military base
procedure Node2001 begin
   floater(710);
end

//redding
procedure Node2002 begin
   floater(720);
end

//EPA
procedure Node2003 begin
   floater(730);
end


//NPC floats
procedure Node3000 begin
   floater(800);
end

procedure Node3001 begin
   floater(810);
end

procedure Node3002 begin
   floater(820);
end

procedure Node3003 begin
   floater(830);
end

//generic floats
procedure Node3500 begin
   floater(random(600,602));
end

//hurt
procedure Node5000 begin
   floater(random(483,484));
end

//poisoned
procedure Node5001 begin
   floater(510);
end

//radiated
procedure Node5002 begin
   floater(520);
end

//broken limb
procedure Node5003 begin
   floater(30);
end

//blind
procedure Node5004 begin
   floater(540);
end

procedure timed_event_p_proc begin
 if (obj_is_visible_flag(self_obj)) then begin
   if (combat_is_initialized == false) then begin
      if (Cat_Jules_In_Party == false) then begin
      //blah
      end else begin
         if (party_healed_bad) then begin
            call Node5000;
         end else if (self_is_poisoned) then begin
            call Node5001;
         end else if (get_critter_stat(self_obj, STAT_current_rad) > 0) then begin
            call Node5002;
         end else if (Is_Crippled(self_obj)) then begin
            call Node5003;
         end else if (Is_Blind(self_obj)) then begin
            call Node5004;
         end else if ( (Myron_In_Party) and (random(1,2) == 2) ) then begin
            call Node3000;
         end else if ( (Sulik_In_Party) and (random(1,2) == 2) ) then begin
            call Node3001;
         end else if ( (Lenny_In_Party) and (random(1,2) == 2) ) then begin
            call Node3002;
         end else if ( (Marcus_In_Party) and (random(1,2) == 2) ) then begin
            call Node3003;
         end else begin
            call Node3500;
         end
      end
   end
 end
 flush_add_timer_event_sec(self_obj, random(90, 120), 1);
end