/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Townsfolk
        Location: Gecko
        Description: Generic worker in Gecko.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: May 5, 1998
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "gcfolk"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_GCFOLK
#define TOWN_REP_VAR            (GVAR_TOWN_REP_GECKO)

#include "../headers/command.h"
#include "../headers/gecko.h"
#include "../headers/gecksetl.h"
#include "../headers/modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure combat_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure gobar;
procedure banter;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Hostile                    (4)
#define LVAR_Personal_Enemy             (5)

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable pick;
variable seq;
variable In_Timed_Event:=0;
variable I_Am_In_Battle:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   //CHANGE THESE -nbk
   set_self_team(TEAM_GECKO);
   set_self_ai(AI_TOUGH_CITIZEN);
   seq:=random(0,1);
end

procedure combat_p_proc begin
   I_Am_In_Battle:=1;
end

procedure timed_event_p_proc begin
   if (I_Am_In_Battle == 0) then begin
      if (fixed_param == 1) then begin
          call gobar;
          //floater(random(700,714));
          In_Timed_Event:=0;
      end
   end
   else begin
      In_Timed_Event:=0;
   end
end

/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin
   I_Am_In_Battle:=0;
   seq := 1 - seq; //GCFolk Critter Proc only run every other time to optimize speed
   if (seq) then begin
      if (self_can_see_dude) then begin
         if (global_var(GVAR_GECKO_MELTDOWN) == 1) then begin
            set_local_var(LVAR_Hostile,1);
            attack(dude_obj);
         end
         else if ( (local_var(LVAR_Hostile) == 2) or (local_var(LVAR_Personal_Enemy) == 1) or (global_var(GVAR_ENEMY_GECKO)) ) then begin //edit by killap - added GVAR_ENEMY_GECKO
            set_local_var(LVAR_Hostile,1);
            attack(dude_obj);
         end
         //BAR GOER THAT CAN SEE DUDE
         else if (tile_distance(self_tile, TAVERN) < 8) then begin
            if (map_var(MVAR_Bought_Another) == 1) then begin
               set_map_var(MVAR_Bought_Another,0);
               call gobar;
            end
            else begin
               call banter;
            end
         end

      end//can see dude
 /*   //BAR GOER THAT CURRENTLY CANNOT SEE DUDE -- all this dialogue involves actually seeing the dude, so pull this out - killap
      else if (tile_distance(self_tile, TAVERN) < 8) then begin
         if (map_var(MVAR_Bought_Another) == 1) then begin
            set_map_var(MVAR_Bought_Another,0);
            call gobar;
         end
         else begin
            call banter;
         end
      end*/


   end
end

procedure banter begin
   if (tile_distance(self_tile, TAVERN) < 8) then begin
      if ((random(0,100) == 1) and (In_Timed_Event == 0)) then begin
         In_Timed_Event:=1;
         if (map_var(MVAR_Round_Of_Drinks) == 0) then
            add_timer_event(self_obj,game_ticks(random(20,40)),1);
         else if (map_var(MVAR_Round_Of_Drinks) == 1) then
            add_timer_event(self_obj,game_ticks(random(18,36)),1);
         else if (map_var(MVAR_Round_Of_Drinks) == 2) then
            add_timer_event(self_obj,game_ticks(random(15,30)),1);
         else if (map_var(MVAR_Round_Of_Drinks) > 2) then
            add_timer_event(self_obj,game_ticks(random(10,20)),1);
       end
   end
end


/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_global_var(GVAR_ENEMY_GECKO,1);
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   //GetReaction;

   if ((REP_ENEMY_GECKO) or (local_var(LVAR_Personal_Enemy) == 1)) then begin
       call Node998;
   end
   else if (global_var(GVAR_VAULT_PLANT_STATUS) == PLANT_DESTROYED) then begin
       call Node998;
   end
   //else if (PC wounded > 75% and Lenny not in party) then begin
   //   call Node012;
   //end
   else if (cur_map_index == MAP_GECKO_SETTLEMENT) then begin
      if ( (random(1,10) == 1) and (global_var(GVAR_GECKO_ANKH) == ANKH_GIVEN) ) then begin
          call Node011;
      end
      else if (tile_distance(self_tile, GARDEN_1) < 8) then begin
         call Node004;
      end
      else if (tile_distance(self_tile, GARDEN_2) < 8) then begin
         call Node004;
      end
      else if (tile_distance(self_tile, BRAHMA_PEN_1) < 8) then begin
         call Node005;
      end
      else if (tile_distance(self_tile, TAVERN) < 8) then begin
         call gobar;
      end
      else if (global_var(GVAR_VAULT_PLANT_STATUS) == PLANT_OPTIMIZED) then begin
         call Node010;
      end
      else if (global_var(GVAR_VAULT_PLANT_STATUS) == PLANT_REPAIRED) then begin
         call Node001;
      end
      else if town_rep_better_than(9) then begin
         call Node002;
      end
      else begin
         call Node003;
      end
   end //not on settlement map
   else if (cur_map_index == MAP_GECKO_JUNKYARD) then begin
      if ( (random(1,10) == 1) and (global_var(GVAR_GECKO_ANKH) == ANKH_GIVEN) ) then begin
          call Node011;
      end
      else if (tile_distance(self_tile, GARDEN_3) < 8) then begin
         call Node004;
      end
      else if (tile_distance(self_tile, BRAHMA_PEN_2) < 8) then begin
         call Node005;
      end
      else if (tile_distance(self_tile, BRAHMA_PEN_3) < 8) then begin
         call Node005;
      end
      else if (global_var(GVAR_VAULT_PLANT_STATUS) == PLANT_OPTIMIZED) then begin
         call Node010;
      end
      else if (global_var(GVAR_VAULT_PLANT_STATUS) == PLANT_REPAIRED) then begin
         call Node001;
      end
      else if town_rep_better_than(9) then begin
         call Node002;
      end
      else begin
         call Node003;
      end
   end //not on junkyard map

end

procedure gobar begin
   if ( (REP_ENEMY_GECKO) or
        (global_var(GVAR_GECKO_MELTDOWN) == 1) or
        (global_var(GVAR_VAULT_PLANT_STATUS) == PLANT_DESTROYED) or
        (local_var(LVAR_Hostile) > 0) or
        (local_var(LVAR_Personal_Enemy) == 1) )then begin
        set_map_var(MVAR_Round_Of_Drinks,0);
        //DO NOTHING
        //BAR GOERS SHOULD NEVER FLOAT DIALOGUE IF PLAYER IS KILLING THEM!!!
   end
   else begin
      if (map_var(MVAR_Round_Of_Drinks) == 0) then begin
         call Node006;
      end
      else if (map_var(MVAR_Round_Of_Drinks) == 1) then begin
         call Node007;
      end
      else if (map_var(MVAR_Round_Of_Drinks) == 2) then begin
         call Node008;
      end
      else if (map_var(MVAR_Round_Of_Drinks) > 2) then begin
         call Node009;
      end
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
/* Increment the aligned critter counter*/
   inc_good_critter
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
   //if (action_being_used == SKILL_STEAL) then begin
      //if (is_success(roll_vs_skill(dude_obj,action_being_used,0))) then begin
      //end
   //end
end

procedure Node001 begin
   floater(random(120,132));
end
procedure Node002 begin
   floater(random(140,151));
end
procedure Node003 begin
   floater(random(160,169));
end
procedure Node004 begin
   floater(random(180,189));
end
procedure Node005 begin
   floater(random(200,209));
end
procedure Node006 begin
   floater(random(220,229));
end
procedure Node007 begin
   floater(random(240,249));
end
procedure Node008 begin
   floater(random(260,269));
end
procedure Node009 begin
   floater(random(280,291));
end
procedure Node010 begin
   floater(random(300,309));
end
procedure Node011 begin
   floater(random(320,329));
end
procedure Node012 begin
   floater(random(340,347));
end

procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end
/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end
