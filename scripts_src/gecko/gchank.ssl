/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Hank
        Location: Gecko
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: January 14, 1998

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "../headers/define.h"
#include "../headers/gecko.h"

#define NAME                    SCRIPT_GCHANK
#define TOWN_REP_VAR            (GVAR_TOWN_REP_GECKO)
#define floater(x)		float_msg(self_obj, message_str(NAME,x),FLOAT_MSG_RED)
//#define PID_COOLANT_REPORT      10 //need to change to proper pid
#define PID_REACTOR_REPAIR_PART 20 //need to change to proper pid

#include "../headers/command.h"
#include "../headers/modreact.h"

#define Vault_Known             (1)     // Temp Until get command from Jesse
#define Visited_City            (1)     // Temp Until get command from Jesse

#define WAYPOINTA     18878
#define WAYPOINTB     17887
#define WAYPOINTC     14087
#define WAYPOINTD     12876
#define WAYPOINTE     13864
#define WAYPOINTF     16265
#define WAYPOINTG     13864
#define WAYPOINTH     12876
#define WAYPOINTI     14087
#define WAYPOINTJ     17887
#define WAYPOINTK     18878

#define HOME          WAYPOINTA

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node910;
//procedure Node920;
procedure Node995;
procedure Node996;
procedure Node997;
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Give                       (7)
#define LVAR_01_before                  (8)
#define LVAR_convinced                  (9)
#define LVAR_SEQ                        (10)
#define LVAR_THEX                       (11)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable k;
variable coolant;
variable my_hex;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_GECKO);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_CITIZEN);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */
   if ( ( (global_var(GVAR_GECKO_MELTDOWN) == 1) or (global_var(GVAR_ENEMY_GECKO)) ) and (obj_can_see_obj(self_obj,dude_obj))) then begin
      set_local_var(LVAR_Hostile,1);
      attack(dude_obj);
   end
   else if ( (local_var(LVAR_Hostile) == 2) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end
   else if ( (local_var(LVAR_SEQ) < 11) and (local_var(LVAR_convinced) == 1) and (local_var(LVAR_Hostile) == 0) ) then begin
         my_hex := tile_num(self_obj);
         if (my_hex == local_var(LVAR_THEX)) then
            set_local_var(LVAR_SEQ,local_var(LVAR_SEQ)+1);
         if (local_var(LVAR_SEQ) == 0) then set_local_var(LVAR_THEX,WAYPOINTA);
         else if (local_var(LVAR_SEQ) == 1) then set_local_var(LVAR_THEX,WAYPOINTB);
         else if (local_var(LVAR_SEQ) == 2) then set_local_var(LVAR_THEX,WAYPOINTC);
         else if (local_var(LVAR_SEQ) == 3) then set_local_var(LVAR_THEX,WAYPOINTD);
         else if (local_var(LVAR_SEQ) == 4) then set_local_var(LVAR_THEX,WAYPOINTE);
         else if (local_var(LVAR_SEQ) == 5) then set_local_var(LVAR_THEX,WAYPOINTF);
         else if (local_var(LVAR_SEQ) == 6) then set_local_var(LVAR_THEX,WAYPOINTG);
         else if (local_var(LVAR_SEQ) == 7) then set_local_var(LVAR_THEX,WAYPOINTH);
         else if (local_var(LVAR_SEQ) == 8) then set_local_var(LVAR_THEX,WAYPOINTI);
         else if (local_var(LVAR_SEQ) == 9) then set_local_var(LVAR_THEX,WAYPOINTJ);
         else if (local_var(LVAR_SEQ) == 10) then set_local_var(LVAR_THEX,WAYPOINTK);
         if (local_var(LVAR_SEQ) == 6) then begin
            if (global_var(GVAR_GECKO_MELTDOWN) == 0) then begin //once only
               set_gvar_bit_off(GVAR_GECKO_WORKING_ON_PLANT,1);
               set_global_var(GVAR_GECKO_MELTDOWN,1);
               set_global_var(GVAR_ENEMY_GECKO,1);
               display_msg(mstr(300));
               give_xp(EXP_CLEVER_DESTROY_BONUS);
               inc_general_rep(REP_BONUS_GC_TRICK_HANK);
            end
         end
            //GC_REP_ADJUST(-75)
            //inc_general_rep(REP_BONUS_GC_PLANT_DESTROYED);
         //meltdown should signal to do damage to everything on map
         //can increment it over time (doing damage) until it gets to a value that
         //indicates the meltdown is over - slag the door and kill anything
         //not dead already.
         if (local_var(LVAR_SEQ) < 11) then
            animate_move_to_tile(local_var(LVAR_THEX));
   end //of convinced
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_local_var(LVAR_Hostile,2);
       set_global_var(GVAR_ENEMY_GECKO,1);
       //if i was the one working on the plant now it is not scheduled for change
       if (gvar_bit(GVAR_GECKO_WORKING_ON_PLANT,1)) then begin
          set_global_var(GVAR_PLANT_SCHEDULED_FOR_CHANGE, 0);
       end
       set_gvar_bit_off(GVAR_GECKO_WORKING_ON_PLANT,1); //if you attack hank i must allow other methods to fix power plant
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   GetReaction;

   // CHANGE -nbk (Aldo doesn't care if your an enemy of the either side in Klamath)
   //             -he just wants a drink
   if (local_var(LVAR_Personal_Enemy) == 1) then begin
       call Node998;
   end

   else if (global_var(GVAR_GECKO_WORKING_ON_PLANT) > 0) then begin
      floater(900);
   end

   else if (global_var(GVAR_VAULT_PLANT_STATUS) == PLANT_REPAIRED) then begin
      call Node011;
   end

   else if (global_var(GVAR_VAULT_PLANT_STATUS) == PLANT_OPTIMIZED) then begin
      call Node012;
   end

   else if ((get_critter_stat(dude_obj,STAT_iq) <= 3)) then begin
      call Node013;
   end


   else if (obj_is_carrying_obj_pid(dude_obj, PID_HY_MAG_PART)) then begin

      if (local_var(LVAR_Herebefore) == 0) then begin
//else if ((local_var(LVAR_Herebefore) == 0) and (get_critter_stat(dude_obj,STAT_iq) <= (0-LOW_IQ))) then begin
       set_local_var(LVAR_Herebefore,1);
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node005;
       gSay_End;
       end_dialogue;
      end

      else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node007;
       gSay_End;
       end_dialogue;
      end

   end

   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
          call Node001;
       gSay_End;
       end_dialogue;
   end

end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
   //if i was the one working on the plant now it is not scheduled for change
   if (gvar_bit(GVAR_GECKO_WORKING_ON_PLANT,1)) then begin
      set_global_var(GVAR_PLANT_SCHEDULED_FOR_CHANGE, 0);
   end
  set_gvar_bit_off(GVAR_GECKO_WORKING_ON_PLANT,1);
/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

procedure Node910 begin
   if (global_var(GVAR_VAULT_PLANT_STATUS) == PLANT_REPAIRED) then
      call Node008;
   else
      call Node007;
end

procedure Node920 begin
  coolant:=obj_carrying_pid_obj(dude_obj, PID_CLIPBOARD);
  rm_obj_from_inven(dude_obj, coolant);
  destroy_object(coolant); //added by killap
  set_local_var(LVAR_convinced,1);
  set_global_var(GVAR_PLANT_SCHEDULED_FOR_CHANGE, SCHEDULED_FOR_DESTRUCTION);
  set_gvar_bit_on(GVAR_GECKO_WORKING_ON_PLANT,1);
end

procedure Node995 begin
   if (get_critter_stat(dude_obj,STAT_ch) >= 7) then begin
       TopReact;
   end
   else begin
       BottomReact;
   end
end

procedure Node996 begin
   BottomReact;
end

procedure Node997 begin
   if (get_critter_stat(dude_obj,STAT_ch) >= 8) then
       call Node004;
   else
       call Node005;
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end

// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
if (local_var(LVAR_01_before) == 0) then begin
   set_local_var(LVAR_01_before, 1);
   Reply(110);
end
else begin
   Reply(111);
end
   NOption(112,Node002,004);
   if (obj_is_carrying_obj_pid(dude_obj,PID_CLIPBOARD) > 0) then
      NOption(113,Node003,004);
   NOption(114,Node999,004);
end

procedure Node002 begin
   Reply(120);
   NOption(121,Node004,004);
   NOption(122,Node999,004);
end

procedure Node003 begin
   Reply(130);
   NOption(132,Node920,004);
   NOption(133,Node999,004);
end

procedure Node004 begin
   Reply(140);
   NOption(141,Node010,004);
   NOption(142,Node999,004);
end

procedure Node005 begin
   Reply(150);
   NOption(151,Node006,004);
   NOption(152,Node999,004);
end

procedure Node006 begin
   Reply(160);
   NOption(161,Node008,004);
   NOption(162,Node009,004);
   NOption(163,Node999,004);
end

procedure Node007 begin
   Reply(170);
   NOption(171,Node008,004);
   NOption(172,Node009,004);
   NOption(173,Node999,004);
end

procedure Node008 begin
   Reply(180);
   NOption(181,Node999,004);
end

procedure Node009 begin
   Reply(190);
   NOption(191,Node004,004);
   NOption(192,Node002,004);
end

procedure Node010 begin
   Reply(200);
   NOption(201,Node999,004);
end

procedure Node011 begin
   /*k := random(1,3);
   if (k == 1) then floater(210);
   else if (k == 2) then floater(211);
   else if (k == 3) then floater(212);*/

   float_msg(self_obj, message_str(NAME,random(210,212)),FLOAT_MSG_NORMAL); //edit by killap
end

procedure Node012 begin
   /*k := random(1,3);
   if (k == 1) then floater(220);
   else if (k == 2) then floater(221);
   else if (k == 3) then floater(222);*/

   float_msg(self_obj, message_str(NAME,random(220,222)),FLOAT_MSG_NORMAL); //edit by killap

end

procedure Node013 begin
   //floater(random(230,232));

   float_msg(self_obj, message_str(NAME,random(230,232)),FLOAT_MSG_NORMAL); //edit by killap

end
//xxxxxxxxxxxxxxxxxxxx