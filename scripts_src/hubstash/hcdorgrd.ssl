/*
	Script for critter tmplete
*/




/* Include Files */
#define SCRIPT_REALNAME "hcdorgrd"
#include "../headers/define.h"

#define NAME                    SCRIPT_HCDORGRD

#include "../headers/command.h"
#include "../headers/sanfran.h"
#include "../headers/hubstash.h"

/* Standard Script Procedures */
procedure start;
procedure use_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure use_obj_on_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure talk_p_proc;

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node990;
procedure Node991;
procedure Node992;
procedure Node993;
procedure Node994;
procedure Node997;
procedure Node998;
procedure Node999;

/*****************************************************************
   Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_
*****************************************************************/

#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Warned                     (7)

/*******************************************************************
   Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_)
*******************************************************************/
import variable stashdoor_obj;
/*******************************************************************
   Local variables which do not need to be saved between map changes.
*******************************************************************/


/*******************************************************************
*******                         PROCEDURES                   *******
*******************************************************************/


/*******************************************************************
   The start procedure is the first procedure called when the map is
   first entered. Any initial information that needs to be set up
   should be placed in here.
*******************************************************************/

procedure start begin
end

procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */
   if ( ((san_fran_flag(SF_ELRON_ENEMY)) or (map_var(MVAR_Attack_Player) == 1)) and (obj_can_see_obj(self_obj,dude_obj)) ) then begin
       attack(dude_obj);
   end

   else if( not( is_loading_game ) ) then begin
      if ( (map_var(MVAR_Stash_Enter) == 0) and (local_var(LVAR_Warned) == 0) and (obj_can_see_obj(self_obj,dude_obj)) ) then begin
         call talk_p_proc;
      end
   end

end

procedure timed_event_p_proc begin
end

/********************************************************************
********************************************************************/

procedure use_p_proc begin
end

/***************************************************************************
   This is cursory glance description that the player will receive should
   he just pass the Action Cursor over. Examines which give more information
   need to be in the description_p_proc procedure.
***************************************************************************/

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end


procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end



procedure use_skill_on_p_proc begin
end


procedure use_obj_on_p_proc begin
end


/******************************************************************************************
   IF it gets damaged
******************************************************************************************/

procedure damage_p_proc begin
   if (local_var(LVAR_Hostile) == 0 ) then begin
      set_map_var(MVAR_Attack_Player,1);
      set_local_var(LVAR_Hostile,1);
   end
end

procedure destroy_p_proc begin

   //set_map_var(MVAR_Stash_Enter,1);
   //obj_unlock(stashdoor_obj);
/* Increment the aligned critter counter*/
   inc_evil_critter

/* Set global_variable for Enemy status*/
end

/***************************************************************************************
   Whenever the map is first entered, this procedure will be called.
***************************************************************************************/

procedure map_enter_p_proc begin
   critter_add_trait(self_obj, TRAIT_OBJECT, OBJECT_TEAM_NUM, TEAM_HUBOLOGIST_STASH);
end


/**************************************************************************************
   This procedure gets called roughly every 30 seconds of real time.
**************************************************************************************/

procedure map_update_p_proc begin
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_map_var(MVAR_Attack_Player,1);
   end
end


/**************************************************************************************
   This is used for any dialogue that may need to occur with the player.
**************************************************************************************/

procedure talk_p_proc begin
   if (local_var(LVAR_Warned) > 0) then begin
      if (local_var(LVAR_Warned) == 1) then begin
         floater(430);
         set_local_var(LVAR_Warned,local_var(LVAR_Warned)+1);
      end else begin
         floater(431);
         call Node998;
      end
   end else if (map_var(MVAR_Stash_Enter) == 0) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
   end else begin
      floater(random(300,301));
   end
end

procedure Node999 begin

end

procedure Node998 begin
   set_map_var(MVAR_Attack_Player,1);
   set_local_var(LVAR_Hostile,1);
end

procedure Node001 begin
   Reply(200);
   BOption(201, Node002, 4);
   NOption(202, Node003, 4);
   NLowOption(400,Node013);
end

procedure Node002 begin
   Reply(204);
   NOption(g_mstr(350), Node998, 4);
end

procedure Node003 begin
   Reply(203);
   NOption(205, Node004, 4);
   BOption(206, Node998, 4);
end

procedure Node004 begin
   variable tmp;
   tmp := random(1,3);
   if (tmp == 1) then begin
      Reply(220);
      BOption(230, Node005, 4);
      GOption(231, Node006, 4);
      BOption(232, Node005, 4);
      BOption(233, Node005, 4);
      BOption(234, Node005, 4);
   end else if (tmp == 2) then begin
      Reply(221);
      BOption(240, Node005, 4);
      BOption(241, Node005, 4);
      GOption(242, Node006, 4);
      BOption(243, Node005, 4);
      BOption(244, Node005, 4);
   end else begin
      Reply(222);
      BOption(250, Node005, 4);
      BOption(251, Node005, 4);
      BOption(252, Node005, 4);
      GOption(253, Node006, 4);
      BOption(254, Node005, 4);
   end
end

procedure Node005 begin
   Reply(261);
   NOption(g_mstr(350), Node998, 4);
end

procedure Node006 begin
   Reply(260);
   NOption(270, Node990, 4);
   NOption(271, Node010, 4);
end

procedure Node990 begin
   if( is_success( roll_vs_skill( dude_obj, SKILL_CONVERSANT, -50 ) ) ) then
      call Node007;
   else
      call Node008;
end

procedure Node007 begin
   Reply(280);
   NOption(282, Node997, 4);
end

procedure Node008 begin
   Reply(281);
   NOption(283, Node991, 4);
end

procedure Node991 begin
   if( is_success( roll_vs_skill( dude_obj, SKILL_CONVERSANT, -30 ) ) ) then
      call Node007;
   else
      call Node009;
end

procedure Node009 begin
   Reply(284);
   NOption(g_mstr(350), Node998, 4);
end

procedure Node010 begin
   Reply(290);
   NOption(291, Node992, 4);
end

procedure Node992 begin
   if( is_success( roll_vs_skill( dude_obj, SKILL_CONVERSANT, -50 ) ) ) then
      call Node011;
   else
      call Node012;
end

procedure Node011 begin
   Reply(293);
   NOption(282, Node997, 4);
end

procedure Node012 begin
   Reply(292);
   NOption(294, Node993, 4);
end

procedure Node013 begin
   Reply(410);
   NLowOption(411,Node014);
   NLowOption(412,Node014);
end

procedure Node014 begin
   Reply(420);
   NLowOption(421,Node994);
   BLowOption(422,Node998);
end

procedure Node993 begin
   if( is_success( roll_vs_skill( dude_obj, SKILL_CONVERSANT, -30 ) ) ) then
      call Node011;
   else
      call Node009;
end

procedure Node994 begin
   set_local_var(LVAR_Warned,1);
end

procedure Node997 begin
   set_map_var(MVAR_Stash_Enter,1);
   obj_unlock(stashdoor_obj);
end