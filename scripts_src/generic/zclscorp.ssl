/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Generic Lesser Small Scorpions
        Location: Arroyo
        Description: Generic lesser small scorpion.

        Log:

           Created: December 02, 1997

           Updated:
*/

/* Include Files */

#define SCRIPT_REALNAME "zclscorp"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_ZCLSCORP

#include "../headers/command.h"
//#include "../headers/modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure combat_p_proc;

/* Local variables which do not need to be saved between map changes. */

procedure start begin
end

procedure timed_event_p_proc begin
   if ((combat_is_initialized == false) and ((critter_state(self_obj) bwand DAM_KNOCKED_OUT) == FALSE)) then begin
      reg_anim_clear(self_obj);
      reg_anim_begin();
         if (((cur_map_index == MAP_ARROYO_CAVES) and (dude_elevation != self_elevation))) then
           ndebug("I'm in Arroyo Caves and not on the same elevation as the dude.");
         else
           self_walk_to_tile(tile_num_in_direction(self_tile,random(0,5),random(1,3)));
      reg_anim_end();
   end
   add_timer_event(self_obj,game_ticks(random(3,5)),0);
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      attack(dude_obj);
   end
end

procedure destroy_p_proc begin
   variable tail := 0;

   // Small scorpions now drop small tails. Makes much more sense - killap (expansion)
   if ((self_pid == PID_SMALL_RADSCORPION) or (self_pid == PID_BLACK_SCORPION)) then
      tail := create_object(PID_SMALL_SCORPION_TAIL,0,0);
   else
      tail := create_object(PID_SCORPION_TAIL,0,0);

   add_obj_to_inven(self_obj,tail);

   //inc_evil_critter - comment out by killap (expansion)
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      attack(dude_obj);
   end
end

procedure combat_p_proc begin
   if (fixed_param == COMBAT_SUBTYPE_HIT_SUCCEEDED) then begin
      if (target_obj == dude_obj) then begin
         if (not(is_success(do_check(dude_obj,STAT_lu,-1)))) then begin
            poison(target_obj,random(1,6));
         end
      end
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure description_p_proc begin
end

procedure use_obj_on_p_proc begin
end

procedure use_skill_on_p_proc begin
   if (action_being_used == SKILL_STEAL) then
      script_overrides;
end

procedure map_enter_p_proc begin
   set_self_team(TEAM_SCORPION);
   set_self_ai(AI_GENERIC_SCORPION);
   add_timer_event(self_obj,game_ticks(random(1,5)),0);
end
